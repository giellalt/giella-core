# Divvun & Giellatekno - open source grammars for Sámi and other languages
# Copyright © 2000-2013 The University of Tromsø
# http://giellatekno.uit.no & http://divvun.no
#
# This program is free software; you can redistribute and/or modify
# this file under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version. The GNU General Public License
# is found at http://www.gnu.org/licenses/gpl.html. It is
# also available in the file $GTHOME/LICENSE.txt.
#
# Other licensing options are available upon request, please contact
# giellatekno@hum.uit.no or divvun@hum.uit.no

# ==================================================================== #
#    N O R T H E R N  S Á M I  D I S A M B I G U A T O R
# ==================================================================== #
# ====================================================================

#  Sámi language technology project 2003-2013, University of Tromsø #



      # =========================================== #
      #      Short table of contents                #
      # =========================================== #

      # Delimiters
      # Tags and sets
      # Disambiguation rules
       # One-cohort disambiguation - cycle 0
       # Local disambiguation - cycles 1 and 2
       # Cycle 1a: Special cases
       # Cycle 1b: Cleaning up after the special cases
       # Cycle 2: Other local disambiguation
       # Mapping of COMP-CS<, CC and CS
       # More diambiguation
       # Verb mappings
       # Disambiguating nouns
       # Mainly mapping-rules
       # Disambiguating nouns
  	   # Case disambiguation
       # Cycle 3: Global disambiguation
	   # Cycle 4: Syntactic disambiguation
	   # Cycle 5: Post-syntactic morphological disambiguation



# =============== #
#!! !!!DELIMITERS
# =============== #

DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>";
#!! Sentence delimiters are the following: <.> <!> <?> <...> <¶>

#PARENTHESES = "<$(>" "<$)>" "<$[>" "<$]>" "<${>" "<$}>" "<$«>" "<$»>" ;

# ================== #
#!! !!!TAGS AND SETS
# ================== #

SETS


#!! !!Tags


#!! This section lists all the tags inherited from the fst, and used as tags
#!! in the syntactic analysis. The next section, __Sets__, contains sets defined
#!! on the basis of the tags listed here, those set names are not visible in the output.



# Tags declared as single-membered LISTs
# ======================================

#!! !Beginning and end of sentence
LIST BOS = (>>>) (<s>) ;  #!! BOS
LIST EOS = (<<<) (</s>) ; #!! EOS

# We define end of clause and beginning of clause in a way so that the file
# may be read both by the CG-2 and the vislcg formalisms.
# CG3 doesn´t function without >>> and <<< !


#!! !Parts of speech tags

LIST N = N ;		    #!! N
LIST A = A ;		    #!! A
LIST Adv = Adv ;	    #!! Adv
LIST V = V ;		    #!! V
LIST Pron = Pron ;	    #!! Pron
LIST CS = CS ;		    #!! CS
LIST CC = CC ;		    #!! CC
SET CC-CS = CC OR CS ;
LIST Po = Po ;		    #!! Po
LIST Pr = Pr ;		    #!! Pr
LIST Pcle = Pcle ;	    #!! Pcle
LIST Num = Num ;	    #!! Num
LIST Interj = Interj ;  #!! Interj
LIST ABBR = ABBR ;	    #!! ABBR
LIST ACR = ACR ;	    #!! ACR
LIST CLB = CLB ;	    #!! CLB
LIST LEFT = LEFT ;	    #!! LEFT
LIST RIGHT = RIGHT ;    #!! RIGHT
LIST WEB = WEB ;	    #!! WEB
LIST QMARK = """ ; # "
LIST PPUNCT = PUNCT ;   #!! PUNCT
SET PUNCT = PPUNCT - QMARK ;

LIST COMMA = "," ;
LIST ¶ = ¶;

#LIST V* = V* ;


#!! !Tags for POS sub-categories

LIST Pers = Pers ;	    #!! Pers
LIST Dem = Dem ;	    #!! Dem
LIST Interr = Interr ;  #!! Interr
LIST Indef = Indef ;    #!! Indef
LIST Recipr = Recipr ;  #!! Recipr
LIST Refl = Refl ;	    #!! Refl
LIST Rel = Rel ;	    #!! Rel
LIST Coll = Coll ;	    #!! Coll
LIST NomAg = NomAg ;    #!! NomAg
LIST G3 = G3 ;		    #!! G3
LIST Prop = Prop ;	    #!! Prop



#!! !Tags for morphosyntactic properties

LIST Nom = Nom ; #!! Nom
LIST Acc = Acc ; #!! Acc
LIST Gen = Gen ; #!! Gen
LIST Ill = Ill ; #!! Ill
LIST Loc = Loc ; #!! Loc
LIST Com = Com ; #!! Com
LIST Ess = Ess ; #!! Ess
LIST Sg = Sg ;   #!! Sg
LIST Du = Du ;   #!! Du
LIST Pl = Pl ;   #!! Pl
LIST RCmpnd = RCmpnd ;	   #!! RCmpnd
LIST Cmpnd = Cmpnd ;	   #!! Cmpnd
LIST SgNomCmp = SgNomCmp ; #!! SgNomCmp
LIST SgGenCmp = SgGenCmp ; #!! SgGenCmp
LIST ShCmp = ShCmp ;	   #!! ShCmp
LIST PxSg1 = PxSg1 ;	   #!! PxSg1
LIST PxSg2 = PxSg2 ;	   #!! PxSg2
LIST PxSg3 = PxSg3 ;	   #!! PxSg3
LIST PxDu1 = PxDu1 ;	   #!! PxDu1
LIST PxDu2 = PxDu2 ;	   #!! PxDu2
LIST PxDu3 = PxDu3 ;	   #!! PxDu3
LIST PxPl1 = PxPl1 ;	   #!! PxPl1
LIST PxPl2 = PxPl2 ;	   #!! PxPl2
LIST PxPl3 = PxPl3 ;	   #!! PxPl3
LIST Comp = Comp ;		   #!! Comp
LIST Superl = Superl ;	   #!! Superl
LIST Attr = Attr ;		   #!! Attr
LIST Ord = Ord ;		   #!! Ord
LIST Qst = Qst ;		   #!! Qst
LIST IV = IV ; #!! IV
LIST TV = TV ; #!! TV
LIST Prt = Prt;				   #!! Prt
LIST Prs = Prs ;			   #!! Prs
LIST Ind = Ind ;			   #!! Ind
LIST Pot = Pot ;			   #!! Pot
LIST Cond = Cond ;			   #!! Cond
LIST Imprt = Imprt ;		   #!! Imprt
LIST ImprtII = ImprtII ;	   #!! ImprtII
LIST Sg1 = Sg1 ;			   #!! Sg1
LIST Sg2 = Sg2 ;			   #!! Sg2
LIST Sg3 = Sg3 ;			   #!! Sg3
LIST Du1 = Du1 ;			   #!! Du1
LIST Du2 = Du2 ;			   #!! Du2
LIST Du3 = Du3 ;			   #!! Du3
LIST Pl1 = Pl1 ;			   #!! Pl1
LIST Pl2 = Pl2 ;			   #!! Pl2
LIST Pl3 = Pl3 ;			   #!! Pl3
LIST Inf = Inf ;			   #!! Inf
LIST ConNeg = ConNeg ;		   #!! ConNeg
LIST Neg = Neg ;			   #!! Neg
LIST PrfPrc = PrfPrc ;		   #!! PrfPrc
LIST VGen = VGen ;			   #!! VGen
LIST PrsPrc = PrsPrc ;		   #!! PrsPrc
LIST Ger = Ger ;			   #!! Ger
LIST Sup = Sup ;			   #!! Sup
LIST Actio = Actio ;		   #!! Actio
LIST VAbess = VAbess ;		   #!!
# The ("ge" Pcle) etc. are used in Apertium

#!! !Tags for clitic particles
LIST Foc/ge = Foc/ge ("ge" Pcle) ;	   #!! Foc/ge
LIST Foc/gen = Foc/gen ("gen" Pcle) ;  #!! Foc/gen
LIST Foc/ges = Foc/ges ("ges" Pcle) ;  #!! Foc/ges
LIST Foc/gis = Foc/gis ("gis" Pcle) ;  #!! Foc/gis
LIST Foc/naj = Foc/naj ("naj" Pcle) ;  #!! Foc/naj
LIST Foc/ba = Foc/ba ("ba" Pcle) ;	   #!! Foc/ba
LIST Foc/be = Foc/be ("be" Pcle) ;	   #!! Foc/be
LIST Foc/hal = Foc/hal ("hal" Pcle) ;  #!! Foc/hal
LIST Foc/han = Foc/han ("han" Pcle) ;  #!! Foc/han
LIST Foc/bat = Foc/bat ("bat" Pcle) ;  #!! Foc/bat
LIST Foc/son = Foc/son ("son" Pcle) ;  #!! Foc/son


#!! !Derivation tags

LIST Der/PassL = Der/PassL ;
LIST Der/Pass = Der/PassL Der/PassS ; #!! Der/PassL Der/PassS
LIST Der/NomAg = Der/NomAg ;   #!! Der/NomAg
LIST Actor = NomAg Der/NomAg ; #!! NomAg
LIST Der/adda = Der/adda ;		 #!! Der/adda
LIST Der/ahtti = Der/ahtti ;	 #!! Der/ahtti
LIST Der/alla = Der/alla ;		 #!! Der/alla
LIST Der/asti = Der/asti ;		 #!! Der/asti
LIST Der/easti = Der/easti ;	 #!! Der/easti
LIST Der/d = Der/d ;			 #!! Der/d
LIST Der/eaddji = Der/eaddji ;	 #!! Der/eaddji
LIST Der/eamoš = Der/eamoš ;	 #!! Der/eamoš
LIST Der/amoš = Der/amoš ;		 #!! Der/amoš
LIST Der/geahtes = Der/geahtes ; #!! Der/geahtes
LIST Der/gielat = Der/gielat ;	 #!! Der/gielat
LIST Der/NuA = Der/NuA ;         #!! Der/NuA (what is NuA?)
LIST Der/h = Der/h ;				 #!! Der/h
LIST Der/heapmi = Der/heapmi ;		 #!! Der/heapmi
LIST Der/hudda = Der/hudda ;		 #!! Der/hudda
LIST Der/huhtti = Der/huhtti ;		 #!! Der/huhtti
LIST Der/huvva = Der/huvva ;		 #!! Der/huvva
LIST Der/halla = Der/halla ;		 #!! Der/halla
LIST Der/j = Der/j ;				 #!! Der/j
LIST Der/l = Der/l ;				 #!! Der/l
LIST Der/laš = Der/laš ;			 #!! Der/laš
LIST Der/las = Der/las ;			 #!! Der/las
LIST Der/hat = Der/hat ;			 #!! Der/hat
LIST Der/meahttun = Der/meahttun ;	 #!! Der/meahttun
LIST Der/muš = Der/muš ;			 #!! Der/muš
LIST Der/NomAct = Der/NomAct ;		 #!! Der/NomAct
LIST Der/st = Der/st ;				 #!! Der/st
LIST Der/stuvva = Der/stuvva ;		 #!! Der/stuvva
LIST Der/upmi = Der/upmi ;			 #!! Der/upmi
LIST Der/supmi = Der/supmi ;		 #!! Der/supmi
LIST Der/vuohta = Der/vuohta ;		 #!! Der/vuohta
LIST Der/goahti = Der/goahti ;		 #!! Der/goahti
LIST Der/lágan = Der/lágan ;		 #!! Der/lágan
LIST Der/lágán = Der/lágán ;		 #!! Der/lágán
LIST Der/lágaš = Der/lágaš ;		 #!! Der/lágaš
LIST Der/jagáš = Der/jagáš ;		 #!! Der/jagáš
LIST Der/jahkásaš = Der/jahkásaš ;	 #!! Der/jahkásaš
LIST Der/diibmosaš = Der/diibmosaš ; #!! Der/diibmosaš
LIST Der/dássásaš = Der/dássásaš ;	 #!! Der/dássásaš
LIST Der/A = Der/A ;				 #!! Der/A
LIST Der/Dimin = Der/Dimin ;		 #!! Der/Dimin
LIST Der/viđá = Der/viđá ;			 #!! Der/viđá
LIST Der/viđi = Der/viđi ;			 #!! Der/viđi
LIST Der/veara = Der/veara ;		 #!! Der/veara
LIST Der/duohke = Der/duohke ;		 #!! Der/duohke
LIST Der/duohkai = Der/duohkai ;	 #!! Der/duohkai
LIST Der/vuolle = Der/vuolle ;		 #!! Der/vuolle
LIST Der/vuollai = Der/vuollai ;	 #!! Der/vuollai
LIST Der/vuolde = Der/vuolde ;		 #!! Der/vuolde
LIST <vdic> = <vdic> ;				 #!! <vdic>


#!! !Semantic tags

LIST Sem/Ani = Sem/Ani ;	 #!! Sem/Ani
LIST Sem/Fem = Sem/Fem ;	 #!! Sem/Fem
LIST Sem/Mal = Sem/Mal ;	 #!! Sem/Mal
LIST Sem/Obj = Sem/Obj ;	 #!! Sem/Obj
LIST Sem/Org = Sem/Org ;	 #!! Sem/Org
LIST Sem/Plc = Sem/Plc ;	 #!! Sem/Plc
LIST Sem/Sur = Sem/Sur ;	 #!! Sem/Sur
LIST Sem/Time = Sem/Time ;	 #!! Sem/Time
LIST Sem/Hum = Sem/Hum ;	 #!! Sem/Hum
LIST Sem/Date = Sem/Date ;	 #!! Sem/Date
LIST Sem/Group = Sem/Group ; #!! Sem/Group
LIST Sem/Route = Sem/Route ; #!! Sem/Route
LIST Sem/Build = Sem/Build ; #!! Sem/Build
LIST Sem/Place = Sem/Place ; #!! Sem/Place
LIST Sem/Food = Sem/Food ;	 #!! Sem/Food
SET FIRSTNAME = (Prop Sem/Fem) OR (Prop Sem/Mal) ;


#!! ! Syntactic tags

LIST @+FAUXV = @+FAUXV ;	         #!! @+FAUXV
LIST @+FMAINV = @+FMAINV ;	         #!! @+FMAINV
LIST @-FAUXV = @-FAUXV ;	         #!! @-FAUXV
LIST @-FMAINV = @-FMAINV ;	         #!! @-FMAINV
LIST @-FSUBJ> = @-FSUBJ> ;	         #!! @-FSUBJ>
LIST @-F<OBJ = @-F<OBJ ;	         #!! @-F<OBJ
LIST @-FOBJ> = @-FOBJ> ;	         #!! @-FOBJ>
LIST @SPRED<OBJ = @SPRED<OBJ ;       #!! @-FSPRED<OBJ
LIST @-F<ADVL = @-F<ADVL ;	         #!! @-F<ADVL
LIST @-FADVL> = @-FADVL> ;	         #!! @-FADVL>
LIST @-F<SPRED = @-F<SPRED ;         #!! @-F<SPRED
LIST @-F<OPRED = @-F<OPRED ;         #!! @-F<OPRED
LIST @-FSPRED> = @-FSPRED> ;         #!! @-FSPRED>
LIST @-FOPRED> = @-FOPRED> ;         #!! @-FOPRED>
SET FOBJ = @-F<OBJ OR @-FOBJ> ;
SET FMAINV = @-FMAINV OR @+FMAINV ;
SET FAUXV = @-FAUXV OR @+FAUXV ;
LIST @>ADVL = @>ADVL ;               #!! @>ADVL
LIST @ADVL< = @ADVL< ;               #!! @ADVL<
LIST @<ADVL = @<ADVL ;               #!! @<ADVL
LIST @ADVL> = @ADVL> ;               #!! @ADVL>
LIST ADVL = @ADVL @ADVL> @ADVL< @<ADVL @>ADVL @-F<ADVL @-FADVL> ;  #!! @ADVL
LIST @HAB> = @HAB> ;                    #!! @HAB>
LIST @<HAB = @<HAB ;                    #!! @<HAB
SET @HAB = @<HAB OR @HAB> ;
LIST @>N = @>N ;		      #!! @>N
LIST @>Interj = @>Interj ;    #!! @>Interj
LIST @N< = @N< ;		      #!! @N<
LIST @>A = @>A ;		      #!! @>A
LIST @P< = @P< ;		      #!! @P<
LIST @>P = @>P ;		      #!! @>P
LIST @HNOUN = @HNOUN ;	      #!! @HNOUN
LIST @INTERJ = @INTERJ ;      #!! @INTERJ
LIST @>Num = @>Num;		      #!! @>Num
LIST @Pron< = @Pron< ;	      #!! @Pron<
LIST @>Pron = @>Pron ;	      #!! @>Pron
LIST @Num< = @Num< ;	      #!! @Num<
LIST @OBJ = @OBJ ;			  #!! @OBJ
LIST @<OBJ = @<OBJ ;		  #!! @<OBJ
LIST @OBJ> = @OBJ> ;		  #!! @OBJ>
LIST @OPRED = @OPRED ;		  #!! @OPRED
LIST @<OPRED = @<OPRED ;	  #!! @<OPRED
LIST @OPRED> = @OPRED> ;	  #!! @OPRED>
LIST @PCLE = @PCLE ;		  #!! @PCLE
LIST @COMP-CS< = @COMP-CS< ;  #!! @COMP-CS<
LIST @SPRED = @SPRED ;		  #!! @SPRED
LIST @<SPRED = @<SPRED ;	  #!! @<SPRED
LIST @SPRED> = @SPRED> ;	  #!! @SPRED>
LIST @SUBJ = @SUBJ ;		  #!! @SUBJ
LIST @<SUBJ = @<SUBJ ;		  #!! @<SUBJ
LIST @SUBJ> = @SUBJ> ;		  #!! @SUBJ>
SET SUBJ = @<SUBJ OR @SUBJ> OR @SUBJ ;
SET SPRED = @<SPRED OR @SPRED> OR @SPRED ;
SET OPRED = @<OPRED OR @OPRED> OR @OPRED ;
LIST @PPRED = @PPRED ;        #!! @PPRED
LIST @APP = @APP ;			   #!! @APP
LIST @APP-N< = @APP-N< ;	   #!! @APP-N<
LIST @APP-Pron< = @APP-Pron< ; #!! @APP-Pron<
LIST @APP>Pron = @APP>Pron ;   #!! @APP>Pron
LIST @APP-Num< = @APP-Num< ;   #!! @APP-Num<
LIST @APP-ADVL< = @APP-ADVL< ; #!! @APP-ADVL<
LIST @VOC = @VOC ;			   #!! @VOC
LIST @CVP = @CVP ;			   #!! @CVP
LIST @CNP = @CNP ;			   #!! @CNP
SET OBJ = (@<OBJ) OR (@OBJ>) OR (@OBJ) OR (@-F<OBJ) OR (@-FOBJ>) ;
LIST <OBJ = @-F<OBJ @<OBJ ;
LIST OBJ> = @OBJ> @-FOBJ> ;
SET <OBJ-OTHERS = <OBJ OR (Gen) OR (Nom) OR (Ess) OR (Loc) OR (Adv) ;
SET OBJ>-OTHERS = OBJ> OR (Gen) OR (Nom) OR (Ess) OR (Loc) OR (Adv) ;
SET NOT-FAUXV = FMAINV OR OBJ + V OR ADVL + V ;
# Works after the mapping rules for verbs.
SET SYN-V = FMAINV OR FAUXV OR V + SUBJ OR OBJ + V OR ADVL + V OR (V @>N) OR (V @N<) OR (V @A<) ;
LIST @X = @X ;    #!! @X



# ========
  SETS
# ========


#!! !!Sets containing sets of lists and tags

#!! This part of the file lists a large number of sets based partly upon the tags defined above, and
#!! partly upon lexemes drawn from the lexicon.
#!! See the sourcefile itself to inspect the sets, what follows here is an overview of the set types.



#!! !Sets for Single-word sets

#!! OKTA and go, and the set INITIAL for initial letters
LIST OKTA = "akta" "okta";
LIST go = "go" ;
LIST INITIAL = "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m"
        "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z"
        "á" "æ" "ø" "å" "ö" "ä" ;


#!! !Sets for word or not

LIST WORD = N A Adv V Pron CS CC Po Pr Interj Pcle Num ABBR ACR  \? ; #!! WORD
 # any word
SET REAL-WORD = WORD - Num - Ord ; #!! REAL-WORD
SET REAL-WORD-NOT-ABBR = WORD - Num - Ord - (ABBR N) ; # This is former REALWORD-NOTABBR #!! REAL-WORD-NOT-ABBR
SET WORD-NOT-de = WORD - ("de") ; #!! WORD-NOT-de
SET NOT-COMMA = WORD - COMMA ;  #!! NOT-COMMA
 # Note! We also have CLB_NOT-COMMA


#!! !Derivational affixes

LIST DER-V = Der/adda Der/alla Der/asti Der/d Der/g Der/h Der/halla Der/hudda Der/huhtti Der/huvva Der/j Der/l Der/ahtti Der/st Der/stuvva Der/goahti ;

LIST DER-N = Der/Dimin Der/eamoš  Der/heapmi Der/NomAct Der/š Der/muš Der/upmi Der/us Der/vuohta Der/NomAg Der/NomAct ;

LIST DER-A1 = Der/diibmosaš Der/dássásaš Der/eaddji Der/geahtes Der/keahtes Der/heapmi Der/jahkásaš Der/lágan Der/lágaš Der/laš Der/meahttun Der/lágán Der/gielat Der/kultuvrrat (V* A) Der/A Der/A* ;

SET DER-A = DER-A1 - Adv ;

LIST A-V = (V* A) ;

SET A-NOT-V = A - A-V ;

#!! !Case sets
# ---------

LIST ADVLCASE = Ill Loc Com Ess ;

# Sg Nom - Sg Nom | Pl Nom - Pl Nom
# Sg Acc - Sg Acc | Pl Acc - Pl Acc
# Sg Gen - Sg Gen | Pl Gen - Pl Gen
# Sg Gen - Sg Ill | Pl Gen/Ill - Pl Ill
# Sg Gen - Sg Loc | Pl Loc - Pl Loc
# Sg Com - Sg Com | Pl Gen/Com - Pl Com
# Ess - Ess

LIST CASE-HALFAGREEMENT = Ill (Sg Loc) (Pl Com) Ess ;
LIST CASE-AGREEMENT = Nom Acc Gen (Pl Ill) Loc Com Ess ;
LIST CASE = Nom Acc Gen Ill Loc Com Ess ;

SET NOT-NOM = CASE - Nom ;
SET NOT-GEN = CASE - Gen ;
SET NOT-ACC = CASE - Acc ;

#!! !Verb sets
# ---------

# Verbs and their complements
# - - - - - - - - - - - - - -

SET NOT-V = WORD - V ;

#!! !Sets for finiteness and mood
# - - - - - - - - - -

SET REAL-NEG = Neg - Sup ;

SET MOOD-V = Ind OR Pot OR Imprt OR ImprtII OR Cond OR (Neg Sup) ;

LIST GC = ("gč") ;

SET VFIN = GC OR MOOD-V - ConNeg ;

SET VFIN-POS = MOOD-V - ConNeg - Neg ;

SET VFIN-NOT-IMPRT = VFIN - Imprt ;

SET VFIN-NOT-NEG = VFIN - Neg ;
  # this might be to strict, besides, 'iige' can be written 'ii ge'

SET NOT-PRFPRC = WORD - PrfPrc ;


#!! !Sets for person
# - - - -

LIST SG1-V = (V Ind Prs Sg1)  (V Ind Prt Sg1) (V Cond Prs Sg1)
             (V Cond Prt Sg1) (V Pot Prs Sg1) (V Neg Ind Sg1);

LIST SG2-V = (V Ind Prs Sg2)  (V Ind Prt Sg2) (V Cond Prs Sg2)
             (V Cond Prt Sg2) (V Pot Prs Sg2) (V Neg Ind Sg2);

LIST SG3-V = (V Ind Prs Sg3)  (V Ind Prt Sg3) (V Cond Prs Sg3)
             (V Cond Prt Sg3) (V Pot Prs Sg3) (V Neg Ind Sg3);

LIST DU1-V = (V Ind Prs Du1)  (V Ind Prt Du1) (V Cond Prs Du1)
     	     (V Cond Prt Du1) (V Pot Prs Du1) (V Neg Ind Du1);

LIST DU2-V = (V Ind Prs Du2)  (V Ind Prt Du2) (V Cond Prs Du2)
	         (V Cond Prt Du2) (V Pot Prs Du2) (V Neg Ind Du2);

LIST DU3-V = (V Ind Prs Du3)  (V Ind Prt Du3) (V Cond Prs Du3)
	         (V Cond Prt Du3) (V Pot Prs Du3) (V Neg Ind Du3);

LIST PL1-V = (V Ind Prs Pl1)  (V Ind Prt Pl1) (V Cond Prs Pl1)
             (V Cond Prt Pl1) (V Pot Prs Pl1) (V Neg Ind Pl1);

LIST PL2-V = (V Ind Prs Pl2)  (V Ind Prt Pl2) (V Cond Prs Pl2)
             (V Cond Prt Pl2) (V Pot Prs Pl2) (V Neg Ind Pl2);

LIST PL3-V = (V Ind Prs Pl3)  (V Ind Prt Pl3) (V Cond Prs Pl3)
             (V Cond Prt Pl3) (V Pot Prs Pl3) (V Neg Ind Pl3);

SET POSITIVE-V = SG1-V OR SG2-V OR SG3-V OR DU1-V OR DU2-V OR DU3-V OR PL1-V OR PL2-V OR PL3-V ;

 # Note that imperative verbs are not included in these sets!

# Some subsets of the VFIN sets
# - - - - - - - - - - - - - - -
SET SG-V = SG1-V OR SG2-V OR SG3-V ;
SET DU-V = DU1-V OR DU2-V OR DU3-V ;
SET PL-V = PL1-V OR PL2-V OR PL3-V ;

SET DU-PL-V = DU1-V OR DU2-V OR DU3-V OR PL1-V OR PL2-V OR PL3-V ;

SET 1-2-V = SG1-V OR SG2-V OR DU1-V OR DU2-V OR PL1-V OR PL2-V ;

#!! !Sets consisting of forms of "leat" (these ones need to be rewritten)
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
LIST LEAN = ("leat" V Ind Prs Sg1) ("leat" V Prt ConNeg);
LIST LEAT = ("leat" V Ind Prs Pl1) ("leat" V Ind Prs Pl3)
      ("leat" V Ind Prs Sg2) ("leat" V Inf) ("leat" V Ind Prs ConNeg);

LIST LEAT-FIN-NOT-IMPRT = ("leat" V Ind) ("leat" V Cond) ("leat" V Pot);

LIST PRFPRC-V = (V PrfPrc) ;
LIST LEATPRFPRC = ("leat" PrfPrc) ;
SET PRC-NOT-LEAT = PRFPRC-V - LEATPRFPRC ;
    # In order to distinguish between real leat-constructions and participles of other verbs
LIST HABEO-SG3 = ("leat" Sg3) (V Neg Sg3) ("šaddat" Sg3) ;

LIST HABEO-PL3 = ("leat" Pl3) (V Neg Pl3) ("šaddat" Pl3) ;

#!! !Pronoun sets
# ------------

LIST MUN = (Pron Pers Sg1 Nom);
LIST DON = (Pron Pers Sg2 Nom);
LIST SON = ("son" Pron Pers Sg3 Nom);
LIST MOAI = (Pron Pers Du1 Nom);
LIST DOAI = (Pron Pers Du2 Nom);
LIST SOAI = (Pron Pers Du3 Nom);
LIST MII-PERS = (Pron Pers Pl1 Nom);
LIST DII = (Pron Pers Pl2 Nom);
LIST SII = ("son" Pron Pers Pl3 Nom);

SET PPRON-NOM-NOT-DAT = MUN OR DON OR SON OR MOAI OR DOAI OR SOAI OR MII-PERS OR DII OR SII ;

SET PPRON-NOT-DAT = (Pron Pers) - ("dat") ;

SET PPRON-DU-PL = MOAI OR DOAI OR SOAI OR MII-PERS OR DII OR SII ;
SET PPRON-PL = MII-PERS OR DII OR SII ;

SET PRON-DU = MOAI OR DOAI OR SOAI ;

SET PPRON-NOT-SII = MUN OR DON OR SON OR MOAI OR DOAI OR SOAI OR MII-PERS OR DII ;

LIST PPRON-GEN = (Sg1 Gen) (Sg2 Gen) (Sg3 Gen) (Du1 Gen) (Du2 Gen) (Du3 Gen)
	(Pl1 Gen) (Pl2 Gen) (Pl3 Gen);

SET PPRON-NOT-GEN = (Pron Pers) - PPRON-GEN ;

LIST SG-DEM = (Pron Dem Sg Nom);
LIST PL-DEM = (Pron Dem Pl Nom);
SET NOT-DEM = WORD - Dem ;

LIST SG-PRON = (Pron Sg1) (Pron Sg2) (Pron Sg3) (Pron Sg) (Pron PxSg1) (Pron PxSg2) (Pron PxSg3);
LIST DU-PRON = (Pron Du1) (Pron Du2) (Pron Du3)  (Pron PxDu1) (Pron PxDu2) (Pron PxDu3);
LIST PL-PRON = (Pron Pl1) (Pron Pl2) (Pron Pl3) (Pron Pl) (Pron PxPl1) (Pron PxPl2) (Pron PxPl3);
LIST DU-PRON-NOTPX = (Pron Du1) (Pron Du2) (Pron Du3) ;

LIST DU-NR = Du1 Du2 Du3 ;

SET NOT-SG-PRON = DU-PRON OR PL-PRON ;

LIST DAT-PRON = ("dat") ("dát") ("diet") ("duot") ("dot") ;

LIST QUANT-PRON = "ollu" "olu" "unnán" "váháš" "veaháš" "veháš" ;
# This set is for choosing between Adv and Pron Indef.


#!! !Adjectival sets and their complements
# -------------------------------------
SET LEX-A = A - DER-A ;

SET A-CASE = A - Attr - Adv ;

LIST A-CC = A CC ;

SET NOT-A = WORD - A ; # This is former NOT-ADJ
SET NOT-A-COMMA = WORD - A - COMMA ;
SET NOT-Attr = WORD - Attr ;
SET NOT-A-PCLE = WORD - A - Pcle ;
SET NOT-A-CC = WORD - A-CC ;

# This set was removed, for a good reason?
SET NOT-A-ADV = WORD - A - Adv OR ("maid") ;


LIST NOMINAL-A = "guoktilaš" "lámis" "oasálaš" ("suddu" Der/laš) "viissis";

LIST OLLU = "ollu" "olu" ;

 # and many others

#!! !Adverbial sets and their complements
# ------------------------------------
SET LEX-ADV = Adv - (A*) ;

SET NOT-ADV-DE            = WORD - Adv ;
SET NOT-ADV            = NOT-ADV-DE OR ("de" Adv) OR CLB ;
SET NOT-ADV-N          = NOT-ADV - N;
SET NOT-ADV-PCLE       = NOT-ADV - Pcle ;
SET NOT-ADV-INDEF      = NOT-ADV - Indef ;
SET NOT-ADV-PCLE-ILL-LOC-COM = WORD - Adv - Pcle - Ill - Loc - Com;
SET NOT-A-ADV-PCLE = WORD - Pcle - A - Adv OR ("maid") ; #

LIST MOVT = "govt" ("got" Adv) "mo" "mot" "mov" "movt" ;

LIST MO-MANge = "danne" "goas" "gokko" "gos" "gosa" "govt" ("got" Adv) "makkár" "man" "manne" "mo" "mot" "mov" "movt" ("nugo" @CVP) (V Qst);

SET MO = MO-MANge - Foc/ge ;

 # Introduce finite clauses.

LIST PLACE-ADV =  "davábealde" "lagabus" "máttabealde" "nuorttabealde" "oarjjábealde" "oarjjabealde" "olggobeale" ;
# There will usually be a Gen in front.

LIST DOPPE = "badjin" "bajil" "dakko" "dá" "dákko" "dáppe" "diekko" "dieppe" "do" "dokko" "doppe" "duo" "duokko" "duoppe" "olgun" ;
# Adverbs with a locativic form, but don´t get Loc as a tag.

LIST DOHKO = "bajás" "deike" "diehke" "diehko" "dohko" "duohko" "lulás" "olggos" "ruoktot" "sisa" "vuovdimassii" ;
# Adverbs with a illativic form, but they don´t get Ill as a tag.


#!! !Sets for coordinators
# ------------
SET Foc = Foc/ge  OR Foc/gen  OR Foc/ges  OR Foc/gis  OR Foc/naj OR Foc/ba  OR Foc/be  OR Foc/hal  OR Foc/han  OR Foc/bat OR Foc/son ;

LIST NEGFOC = (Neg Foc/ge) ;

LIST XGO = "dego" "dugo" "nugo" "seammaládjego" "seammaláhkáigo" ;
  # Compounds
LIST SEAMMAX = "seamma_ládje" "seamma_láhkái" ;
  # Those combine with go

LIST MADE = "mađe" "mađi" ;
LIST DADE = "dađe" "dađi" ;

SET CRD = COMMA OR CC OR NEGFOC OR XGO OR ("/") OR ("-") OR DADE ;
	# coordinators

SET NOT-CRD = WORD - CRD ;

SET LCRD = @CNP OR ("asdfasdf") ;
    # local coordinator
SET GCRD = @CVP OR ("sadfasdf") ;
    # global coordinator

SET CRD2 = (@CNP) OR COMMA OR NEGFOC OR XGO OR ("/") OR ("-") OR DADE ;
    # AFTER LCRD vs. GCRD disambiguation

#!! !Sets for adverbs that have lookalikes
# -------------------------------------
 #!! Here come some adverbs that have identical twins in other POS.
 #!! If these are found in Adv contexts, we treat them as adverbs.

LIST LACCAT-ADV = "absoluhtalaš" "agálaččat" "aitosaččat" "aktiivvalaš" "almmolaš" "analogalaš" "automáhtalaš" "álgoálgosaš" "álgovuolggalaš" "árbevirolaš" "beaivválaččat" "biologalaš" ("buhttenriekti" Der/laš) ("bustávva" Der/laš) "čálalaččat" "čearddalaš" "dábálaš" "duođalaččat" ("dutkanpolitihkka" Der/laš) "eahpedábálaš" "eahpeformálalaš" "eahpekritihkalaš" "eahpevirggálaš" "eaktodáhtolaččat" "ekologalaš" "ekologálaš" "ekonomálaš" "eksplisihtalaš" "elektrovnnalaš" ("eahpidit" VAbess) "etihkalaš" "etymologalaš" "fágalaš" "fonehtalaš" "fonologalaš" "fonotávttalaš" "formálalaš" ("fysiologa" Der/laš) "gaskamearálaš" "geavatlaš" "gelbbolaš" "geográfalaš" "gielalaš" ("girkoriekti" Der/laš) "globálalaččat" ("gorri" Der/laš) "govttolaš" "hálddahuslaš" "historjjálaš" "ideologalaš" "iešheanálaš" "implisihtalaččat" "jahkásaččat" "jeavddalaččat" ("jietnadatoahppa" Der/laš) "juridihkalaš" "konkrehtalaš" "kritihkalaš" "kronologalaš" "kulturhistorjjálaš" "kulturpolitihkalaš" "kultuvrralaš" "logihkalaš" ("luohtu" Der/laš) "máŋggafágalaš" "mekánalaš" ("metoda" Der/laš) "moralalaš" "morálalaš" "morfologalaš" "našuvnnalaš" "negatiivvalaš" "njálmmálaččat" "njuolgguslaččat" "oaiveáššálaš" "obbalaš" "obbalohkái" "oktasaččat" "oktilaš" "ollislaččat" "oppalaččat" "optimalalaččat" "ovttabealálaš" "ovttajienalaš" "ovttamielalaš" "pedagogalaš" "politihkalaš" "praktihkalaš" "prinsihpalaš" ("priváhtariekti" Der/laš) "profehtalaš" "psykologalaš" "ráfálaččat" ("reálapolitihkka" Der/laš) "riektehistorjjálaš" "rievttálaš" "riikkaidgaskasaš" "sámegielalaš" "riikkagottálaš" "ruđalaš" "rumašlaš" "sámepolitihkalaš" "sekundáralaččat" "semantihkalaš" "servodatávkkálaš" ("siviilariekti" Der/laš) "sosiálalaš" ("stáhtapolitihkka" Der/laš) "strategalaš" "teknihkalaš" "teorehtalaš" "topográfalaš" ("váldoášši" Der/laš) "virggálaš" "virolaš" ;

LIST MOD-NP-ADV = "áibbas" "dušše" "erenoamážit" "goitge" "man" "measta" "meastui" "oalle" "oba" "oktiibuot" "oppa" "sullii" ;

LIST MOD-ADV-ADV = ("áibbas" Adv) ("eanet" Adv) ("erenoamáš" Adv) ("erenoamáš" Adv) ("hirbmat" Adv) "hui" "man" ("mealgat" Adv) "menddo" "oalle" "nu" "oba" "oppa" ;

LIST EASKKA = "easkka" "easka" ;

LIST NOT-ARG-ADV = "aiddo" "ain" "aivve" "albma" "aŋkke" "álggos" "báifáhkka" "beanta" "beare" ;

#LIST NOT-ARG-ADV2 = "aiddo" "ain" "aivve" "albma" "aŋkke" "álggos" "álgobáliid" "álo" "báifáhkka" "beanta" "beare" "dakkaviđe" "dal" "dan_dihte" "danne" "dasto" "dattetge" ("dábálaš" Adv) "dávjá" "de" "dieđinge" "dieđusge" "duođaid" "easkka" "fal" "fargga" "fas" "fárrolaga" "gal" "goas" "goasse" "gos" "gosa" "gostege" "govt" "gusto" "hedi" "juo" "justa" "loahpas" "maiddái" "manin" "manne" "nuppes" "suige" "šat" "vel" "viimmat" "vuos" ;


LIST ADV-NOT-V = "dušše" "viimmat" ;

LIST ADV-NOT-N = "easka" "várra" ;


LIST ADV-NOT-PROP = "Ain" "Anne" "Diego" "Dieppe" "Enge" "Galle" "Haga"
           "Joba" "Johan" "Liikka" "Mai" "Mannes" "Mo" "Naba"
           "Nan" "Oktan" "Sierra" "Sokka" "Villa" ;

SET ADV-NOT-OTHER-POS = ADV-NOT-V OR ADV-NOT-N OR ADV-NOT-PROP ;

LIST MANNEL = "maŋŋel" "maŋŋil" "maŋŋá"  ;


#!! !Sets of elements with common syntactic behaviour
# ================================================

SETS

#!! !Sets for verbs
# -----

LIST NOT-REAL-V = (Actio Nom) (Actio Gen) (Actio Loc) (Actio Com) PrsPrc ;

SET REAL-V = V - NOT-REAL-V  ;
  #!! ** V is all readings with a V tag in them, REAL-V should
  #!! be the ones without an N tag following the V.
  #!! The REAL-V set thus awaits a fix to the preprocess V ... N bug.

SET LEX-V = V - DER-V ;
       # VERB but not DER-V

 # The set REAL is smaller than COPULAS, made for verbs with PrfPrc complements: Seammás REAL-COPULAS son dovdan iežas...
LIST REAL-COPULAS = "dáidit" "leat" "soaitit" "veadjit" ;

#!! * The set COPULAS is for predicative constructions
LIST COPULAS = "dáidit" "gártat" "leat" "soaitit" "šaddat" "orrut" "veadjit" ;
 #  "bissut" ?
 # 'Dáidit' can appear without 'leat'.

SET NOT-COP-V = V - COPULAS ;

LIST MOD-ASP = "áigut" "bállet" "berret" "dáidit" "fertet" "galgat" "gillet" "lávet" "sáhttit" "seahtit" "soaitit" "viggat" ;

 # Verbs that never have arguments of their own - so we leave out "áigut", for ex.

LIST AUX-OR-MAIN = "áigut" "álgit" "beassat" "galgat" "háliidit" "máhttit" "nagadit" "nagodit" "ollit" "orrut" "sihtat" "veadjit";

 # These verbs can take arguments, so they do not belong in the AUX group,
 # but they are nevertheless mapped to (@+FAUXV).
 # Added "álgit" to the group (Lene) - Made rules for "boahtit" which  sometimes can be AUX
 # Magga (1982: 120): áigut, dáidit, deaivat, fertet, galgat, lávet, máhttit, oažžut, sáhttit, soaitit, veadjit - in some of the meanings
 # Halvhjelpeverb: álgit, berret, dohkket, gártat, heivet, liigut, šaddat - in some of the meanings

SET AUX = COPULAS OR Neg OR MOD-ASP OR AUX-OR-MAIN ;


SET NOT-AUX-V = V - AUX ; # This is former V-NOT-AUX
 # The set of verbs that cannot be auxiliaries.
			  # Does not include AUX-OR-MAIN.
SET VFIN-NOT-AUX = VFIN - AUX ;

SET MAIN-V = V - COPULAS - Neg - MOD-ASP OR ("orrut") ;

# All active verbs with a TV tag, including AUX-OR-MAIN.

LIST V-TRANS = (V TV) (IV Der/ahtti) (IV Der/h) ;


SET TRANS-ACT-NOT-AUX-V =  V-TRANS - Der/Pass - AUX ;

#!! TRANS-V is the set for verbs really taking objects


SET TRANS-V = V-TRANS - Der/Pass + REAL-V ;

#SET TRANS-V = V-TRANS - Der/Pass - (Actio Nom) - (Actio Gen)  - (Actio Loc) -  (Actio Com) - PrsPrc ;

#!! * Sets for verbs choosing oblique objects or adverbials
# - - - - - - - - - - - - - - - - - - - - - - - - - - -
# bargat "čoallut" "borrat" lohkat "gazzat" vuoššat
# This is the list of strictly transitive verbs. In the rules, refer not to STVLIST, but to the set STV defined below.
LIST STVLIST = (IV Der/ahtti) (IV Der/h) "addit" "amašit" "ásahit" "atnit" "ánssášit" "árvalit" "árvidit" "árvvoštallat" "ásahit" "ávžžuhit" "bajidit" "bassat" "bearrat" "biebmat" "bidjat" "bilidit" "billistit" "bissehit" "bisuhit" "boaldit" "boktit" "borahit" "buhtadit" "buktit" "buoridit" "caggat" "coggalit" "cealkit" "cegget" "čađahit" "čatnat" "čielggadit" "čoaggit" "čoavdit" "čohkket" "čuoččuhit" "čuohppat" "čuollat" "čuovvulit" "dadjat" "dagahit" "dahkat" "dáhkidit" "dáhttut" "dárbbahit" "dárbbašit" "dárkkistit" "dávistit" "deaivat" "deattuhit" "deavdit" "diehtit" "dikšut" "diktit" "doahttalit" "doaimmahit" "doalahit" "doallat" "doalvut" "doarjut" "dohkkehit" "dovdat" "dovddahit" "duođaštit" "eahpidit" "eaiggáduššat" "eaktudit" "ealáskahttit" "ealihit" "eavttuhit" "evttohit" "fállat" "fidnet" "fievrridit" "fuobmát" "fuolahit" "fuomášit" "garvit" "gaskkustit" "gádjut" "gáhttet" "gáibidit" "gáržžidit" "gávdnat" ("gávnnadit" Sg1) ("gávnnadit" Sg2) ("gávnnadit" Sg3) "geahčadit" "geahččat" "geassit" "geatnegahttit" "geavahit" "gieđahallat" "giddet" "goddit" "gohččut" "gohčodit" "goikadit" "gokčat" "goldit" "gozihit" "guoddalit" "guođđit" "guođohit" "guorahallat" "guorrat" "guoskkahit" "hábmet" "háhkat" "hálddašit" "heaittihit" "heivehit" "hilgut" "ipmirdit" "iskat" "iskkadit" "jeahkit" "jođihit" "juksat" "juohkit" "juolludit" "kártet" "lágidit" "láhčit" "láigohit" "loahpahit" "lohpidit" "loktet" "luoikat" "luoikkahit" "luoitit" "massit" "mearkkašit" "mielddisbuktit" "movttiidahttit" "muddet" "muitalit" "muitit" "muittuhit" "namahit" "nammadit" "namuhit" "nannet" "nanosmahttit" "nuppástuhttit" "oahppat" "oaidnit" "oastit" "oaivvildit" "oamastit" "oažžut" "ođasmahttit" "ođastit" "ollašuhttit" "organiseret" "ovdanbuktit" "ovdánahttit" "ovddastit" "ovddidit" "prioriteret" "ráddjet" "ráđđet" "ráfáidahttit" "ráfáiduhttit" "ráhkadit" "rávkat" "rávvet" "rievdadit" "rihkkut" "sakŋadit" "sáddet" "sávvat" "seaguhit" "seailluhit" "sihkkarastit" "sihtat" "siskkildit" "sisttisdoallat" "suodjalit" "suovvat" "unnidit" "vađastit" "vahágahttit" "váillahit" "váldit" "válljet" "várret" "vásihit" "váttásmahttit" "viiddidit" "vuhtiiváldit" "vuođđudit" "vuohttit" "vuoitit" "vuojuhit" "vuoruhit" "vuostáiváldit" "vuosttaldit" "vuovdit" ;

	# Strictly transitiv-verbs - a start. This set is ment as a help choosing Acc or Gen. Example: "Mun borren eatni kafeás." - there is Gen, no Acc, even if the verb is transitive. That´s why "borrat" is not in this set.
	# These verbs: "addit", "fidnet" and "oažžut" can, when it´s about sex, be without object: "Son oaččui gonagasa bártnis."

SET STRICT-TV-V = STVLIST + V - Der/Pass ;

#!! STRICT-TRANS-V is the set for verbs which don't let a GenAcc be a modifier of anything else than an object, e.g. Mun organiseren eatni gievkkanis. - eatni wants to be the object

SET STRICT-TRANS-V = STRICT-TV-V + REAL-V ;

LIST LOC-V = "ávkkástallat" "ballat" "beassat" "beroštit" "biehttalit" "bihtit" "ceavzit" "dinet" "dolkat" "eastadallat" "eastadit" "fitnat" "fuolahit" "fuollat" "geargat" "heaitit" "hehttet" "ilbmat" "jearrat" "jearralit" "luohpat" "máinnašit" "nohkkot" "oassálastit" "spiehkastit" "váibat" "váruhit" "vástidit" ;

		#Note that "fuolahit" obviously has two homonymous variants:
		#fuolahit1+Acc, fuolahit2+Loc

LIST COM-V = "árvalit" "árvvohuššat" "ávkašuvvat" "bálddalastit" "bártašuvvat" "birgehallat" "birget" "bivvat" "buohtastahttit" "deaivvadit" "gulahallat" "gulaskuddat" "hilbošit" "joatkit" "leaikkastallat" "lohpádaddat" "lohpádallat" "lonuhit" "meannudit" "malssidit" "molssodit" "náitalit" "ovttasbargat" "ovttastahttit" "riidalit" "servvoštallat" "servvoštaddat" "soabadit" "soahpat" "šiehtadallat" "šiehtadit" "veardidit" "vrd" ;

		#Note that "leaikkastallat" has two homonymous variants:
		#leaikkastallat1 (IV), leaikkastallat2+Com

LIST ILL-V = "áibbašit" "álgit" "ásaiduvvat" "báitit" "bahkket" "beassat" "čohkkedit" "čujuhit" "čuohcit" "deaivat" "doaškut" "dorvvastit" "došket" "duhtat" "gullat" "guoskat" "gustot" "hárjánit" "heivet" "irggástallat" "jáhkkit" "liikot" "luohttit" "mannat" "máhccat" "mieđihit" "miehtat" "njiedjat" "oahpásmuvvat" "oahpásnuvvat" "ollet" "oskut" "riepmat" "ráhkkanit" "soahpat" "searvat" "suhttat" "váikkuhit" "vástidit" "vuolgit" ;
###"geahččat" Lene: fjerna denne


LIST TRANSL-V = "jápmit" "duššat" "hávkat" "náitalit" "gaskkalduvvat" "nohkat" "goarránit" "heahpanit" "boakčánit" "šlundut" "ráigánit" "vuollánit" "gopmánit" "fuotnánit" "roahppánit" "reakčanit" "bieđganit" "luoddanit" "guvggodit" "liegganit" "suorganit" "báhkkanit" "cuovkanit" "hedjonit" "lohppet" "goikat" "galbmot" "goallut" "ruostut" "buoidut" "guvggodit" "suvrut" "suddat" ;

  # Taken from Helander (2001) p. 89, might be useful in the future as those verbs typically take humans and places in illative case.

LIST REKS-V = "áibbašit" "deaivat" "dorvvastit" "duhtat" "gustot" "hárjánit" "jáhkkit" "liikot" "mieđihit" "ollet" "ráhkkanit" "soahpat" "searvat" "vuolgit" "beroštit" "biehttalit" "bihtit" "ceavzit" "dinet" "dolkat" "eastadallat" "eastadit" "fuolahit" "fuollat" "geargat" "heaitit" "luohpat" "oassálastit" "váikkuhit" "váruhit" ;
# This set is for giving infitives @ADVL after the verb.

LIST ACC-LOC-V = "biehttalit" "čuovvut" "earuhit" "gieldit" "jearrat" ;

LIST ACC-ILL-V = "addit" "bidjat" "mearkkašit";
    # Two-place predicates that take a direct object and an illative adverbial.

LIST MIELDE-V = "addit" "bidjat" "dáidit" "leat" "oastit" "oažžut" "váldit" ;
    # Verbs that take mielde as their complement.

# Son attii girjji mielde.

LIST TIME-ADVL-V = "ádjánit" "ássat" "bargat" "bistit" "bivdit" "eallit" "fievrridit" "guohtut" "joatkit" "láddet" "oahpahit" "ođasmahttit" ;

    # Do not take members of the time set as an object

LIST TIMEOBJECT-V = "addit" "atnit" "árvalit" "bálvalit" "čuovvut" "deavdit" "diŋgot" "evttohit" "fátmmastit" "gáibidit" "gávdnat" "geavahit" "gierdat" "guhkidit" "hábmet" "jorgalit" "juolludit" "lasihit" "liigudit" "loahpahit" "loaktit" "markeret" "mearridit" "molsut" "oanidit" "oažžut" "soahpat" "šiehttat" "várret" "viiddidit" ;

    # might take time as an object - verbs which are not members of PLACE-V:

LIST DURATION-V = "ádjánit" "bargat" "bistit" "eallit" "gollat" "guohtut" "leat" "vázzit";

LIST POINT-IN-TIME-V = "árrit" "bissehit" "caggat" "cakkadit" "cakkastallat" "doalahit" "doallat" "duostut" "dustet" "eastadit" "gieldit" "goahcat" "heađuštit" "hehttet" ;

LIST ROUTE-ADVL-V = "boahtit" "čuoigat" ;

    # bevegelse langsmed noe, innafor et bestemt område
LIST ROUTEOBJECT-V = "ásahit" "bidjat" "botnjat" "basuhit" "buoridit" "čielggadit" "čuovvulit" "čilget" "čuorvut" "čurvet" "dieđihit" "duođaštit" "duorggastit" "geavahit" "geahpedit" "geahpidit" "gudnejahttit" "guorahallat" "gieđahallat" "guođđit" "gilljut" "heahtit" "huikit" "huikkádit" "juoigat" "lávlut" "lohkat" "loahpahit" "mearridit" "máidnut" "nannet" "njulget" "oahpahit" "ráhkadit" "sivdnidit" "suokkardit" "unnidit" "válmmaštit" "vurket" ;

LIST HAB-V = "boahtit" "leat" "goallut" "heaŋgát" "jápmit" "šaddat" ;
	# To be used in habitative-sentences.

#!! !Valency sets
# - - - - - - -

LIST INF-V = "astat" "ádjánit" "áigut" "álgit" "ásahit" "bágget" "bávččagit" "beassat" "berret" "bivdit" "bivvat" "bistit" "boahtit" "bovdet" "čohkkát" "čohkkedit" "čohkánit" "čuoččahit" "čuoččastit" "čuorvut" "čurggodit" ("dadjat" Der/PassL) ("dadjat" Der/PassS) "dagahit" "dáhttut" "dáidit" "dárbbašit" "defineret" "diktit" "doaivut" "doapmat" "duostat" "fertet" "fuobmát" "fuolahit" "galgat" "gáibidit" "gárrut" "gártat" "geahččalit" "geargat" "geatnegahttit" "gierdat" "gillet" "gohččut" "háhppehit" "háliidit" "hárjánit" "heivet" "lávet" "lihkkat" "mannat" "máhttit" "máššat" "movttiidahttit" "muitit" "nagodit" "navdit" "oahpahit" "oahpástuvvat" "oahppat" "oažžut" "ollet" "ribahit" "riepmat" "sáhttit" "seahtit" "sihtat" "soaitit" "suovvat" "šaddat" "stađđat" "veadjit" "viggat" "viššat" "vuogáiduvvat" "vuolgit" "vuollánit" "vuordit";


LIST ACC-INF-V = "bivdit" "čuorvut" "dagahit" "diktit" "gohččut" "movttiidahttit" "sihtat" "suovvat" ;

 # These are verbs taking accusative objects and infinitives at the same time;

 # a subset of INF-V.
 	#$ Ii suova nieiddas náitalit.

LIST OPRED-V = "alidit" "atnit" "álggahit" "bargat" "bidjat" "ceavzit" "dadjat" "dahkat" "dulkot" "geavahit" "geahččat" "gohčodit" "gohččut" "málet" "meroštallat" "meroštallojuvvot" "navdit" "oaivvildit" "sihkkarastit" "váldit" "válljet" "rábmot" "vihahit" ;

 # Verbs that occur with objects that have secondary predicates.

LIST OKTII-V = ("boallu" Der/huvva) "goallostit" "beaškkehit" "bidjat" "čatnat" "suddat" "heivet" "bidjat" "časkit" "geassit" "gullat" "rehkenastit" "ordnet" "soahpat" "gávnnadit" "heivehit" "doaibmat" "iskat" "laktit" "seaguhit" "sorrot" ;

 # Danger: This list could be too long, in certain respects.
 # TODO: Think of counter examples, i.e. sentences where we have these verbs, but where oktii is not an adverbial.

LIST VGEN-V = "bassit" "bivdit" "boahtit" "čierrut" "čuoigat" "čuoiggadit" "čuovvut" "fárret" "fitnat" "garrudit" "gálašit" "gállit" "gilljut" "girdit" "johtit" "jorgut" "luoibmat" "mannat" "mátkkoštit" "njahkalit" "njoammut" "riidet" "ruohttat" "sihkkelastit" "soabbut" "šloahtat" "váccašit" "vádjolit" "vájaldit" "vájuldit" "vánddardit" "vázzit" "viehkat" "viežžat" "vuodjat" "vuodjit" "vuojadit" "vuojašit" "vuolgit" "čilget" "čuorvut" "čurvet" "dadjat" "dárostit" "sámástit" "suomastit" "digáštallat" "hállat" "hoallat" "huikit" "hupmat" "juoigat" "lávlut" "logaldallat" "máidnut" "máinnašit" "muitalit" "savkalit" "skierbmut" "vástidit" "njurgut" "veahkehit" "vuoitit" "vuoššat" ;


LIST SOURCE-V = "gáibidit" ;


LIST MOVEMENT-V = "ádjit" "beassat" "boahtit" "čuoigat" "čuoiggadit" "fárret" "finadit" "finihit" "fitnat" "gálašit" "gállit" "girdit" "johtit" "mannat" "mátkkoštit" "njoammut" "ollit" "riidet" "ruohttat" "sáhtostit" "sihkkelastit" "soabbulit" "soabbut" "šloahtat" "váccašit" "vádjolit" "vájaldit" "vájuldit" "vánddardit" "vázzit" "viehkalit" "viehkat" "viežžat" "vuodjalit" "vuodjat" "vuodjit" "vuojadit" "vuojašit" "vuolgit" ;
 # These verbs typically combine with place adverbials in the genitive case @ADVL
 # (i.e. not accusative) I am not sure if the verbs in the second line do that too. They denote movement anyway.
 # These verbs take GEOGRAPHICAL-PLACE in Acc @OBJ.

LIST JOHTIT = "čuoigat" "čuoiggadit" "gálašit" "gállit" "girdit" "johtit" "mátkkoštit" "njoammut" "riidet" "ruohttat" "sihkkelastit" "soabbulit" "soabbut" "šloahtat" "váccašit" "vádjolit" "vájaldit" "vájuldit" "vánddardit" "vázzit" "viehkalit" "viehkat" "vuodjalit" "vuodjat" "vuodjit" "vuojadit" "vuojašit" ;


LIST HUMAN-ACTIVITY-V = "áitit" "bassit" "bealuštit" "beaskidit" "beastit" "bivdit" "boaldit" "botket" "buktit" "buvttadit" "časkit" "čatnat" "čájehit" "čállit" "čoaggit" "čoavdit" "čugget" "čuohppat" "dearvvahit" "deavdit" "devdnet" "dikšut" "divvut" "doarrádallat" "doidit" "dovddastit" "dubmet" "duddjot" "duvdilit" "ferdnet" "gáhkket" "gárvodit" "gásttašit" "gávkat" "geahččat" "gilvit" "goarrut" "goddit" "gođđit" "gohččut" "goikadit" "goivet" "gokčat" "gottihit" "guldalit" "guolástit" "hábmet" "hervet" "hoigat" "hoitát" "hukset" "huškut" "jugahit" "juhkat" "juksat" "juogadit" "juohkit" "láibut" "láktadit" "loaiddastit" "luddet" "merket" "mihtidit" "murret" "nannet" "náitalit" "njuoskadit" "oahpahit" "oastit" "ohcat" "olahit" "rahpat" "ráhkadit" "ráidnet" "rátkit" "registreret" "rekruteret" "ruohkkat" "sáddet" "sáltet" "sárgut" "sihkkut" "skihtardit" "skoalkalit" "tevdnet" "vadjat" "veahkehit" "vikšet" "vuođđudit" "vuosehit" "vuoidat" "vuoiddadit" "vuoitit" "vuostáiváldit" "vuoššat" "vuovdit" "vurket" ;

  # in contrast to abstract "activities" and static verbs.
  # According to the rules choosing Loc or Com, all activity-verbs do not select TOOL, ABSTR-TOOL, CONCEPT or HUMAN in (Pl Loc).

LIST BODY-ACTIVITY-V = "borrat" "čiekčat" "čujuhit" "čuorbmat" "gullat" "guoskkahit" "lihkahit" "lihkastahttit" "loktet" "njávkkadit" "njávkkastit" "oaidnit" "seavvit" ;

    # These verbs can also have BODY as a tool. The verb-set belongs to the superset of dynamic-verbs, so they will also work with TOOL and ABSTR-TOOL in Com. "Giehta" is in both the TOOL-set and the BODY-set because it selects in Com to a lot of verbs.

LIST PLACING-ACTIVITY-V = "bidjat" "buonjostit" ;

LIST WRITING-ACTIVITY-V = "čállit" ;

LIST HANDICRAFT-ACTIVITY-V = "buddet" "botnit" "goarrut" "hervet" "vadjat" "vikšet" ;

SET ACTIVITY-V = BODY-ACTIVITY-V OR WRITING-ACTIVITY-V OR PLACING-ACTIVITY-V OR HUMAN-ACTIVITY-V OR HANDICRAFT-ACTIVITY-V ;

LIST DADJAT = "čuoččuhit" "dadjat" "lohkat" "muitalit" ;
# This set is for  cealkkavástta: DADJAT + Acc + Inf or PrfPrc

LIST HUPMAT = "dáikit" "dárostit" "digáštallat" "digaštit" "duoldat" "háladit" "háleštit" "hállat" "hoallat" "humadit" "hupmat" "logaldallat" "máidnut" "máinnašit" "muitaladdat" "njurgut" "oaivvildit" "savkalit" "ságastaddat" "ságastallat" "ságastit" "sámástit" "sámistit" "sárdnidit" "sárdnut" "sártnodit" "solžat" "suomastit" ;
# These should not take HUMAN as object

LIST VERBAL-ACTIVITY = "árvalit" "cealkit" "čilget" "čuoččuhit" "basuhit" "bealkit" "cuoigut" "čuorvut" "čurvet" "dadjat" "gielistit" "gilljut" "gopmulit" "háladit" "háleštit" "hállat" "hoallat" "huikit" "huikkádit" "jearrat" "juoigat" "kommenteret" "lávlut" "lohkat" "muitalit" "namahit" "namuhit" "rávvet" "sivdnidit" "váruhit" "vástidit" ;

SET VERBAL-ACTIVITY-V = VERBAL-ACTIVITY OR HUPMAT ;

#Under construction! (as most of the other LISTs)
    # According to the rules choosing Loc or Com, the these verbs select TOOL, ABSTR-TOOL, CONCEPT or HUMAN in (Pl Loc).
    # SATNI in Com

LIST HUMAN-AGENT-V = "áŋgiruššat" "ásahit" "beahttit" "buoridit" "čielggadit" "čuovvulit" "dássidit" "deattastit" "deattuhit" "dieđihit" "dievvat" "duođaštit" "geahpedit" "geahpidit" "gudnejahttit" "guorahallat" "illudit" "lassánit" "liekkadallat" "loahpahit" "luohpat" "mearridit" "moivašuhttit" "namuhit" "nannet" "oassálastit" "suokkardit" "unnidit" "váikkuhit" "válmmaštit" "verrošit" "vurket" ;

    # According to the rules choosing Loc or Com, the these verbs do not select TOOL, ABSTR-TOOL, CONCEPT, SATNI or HUMAN in (Pl Loc).

LIST NOT-HUMAN-OBJECT-V = "bargat" "borrat" "čállit" "lohkat" "vuodjit" ;
    # transitive verbs that typically do not have humans as their object and usually have an intransitive reading

LIST USUALLY-IV =  "boradit" ;
	# TV verbs which usually are used intransitively

#LIST ABSTR-EVENT-V = "lassánit" ;

    # In contrast to the HUMAN-AGENT-V set, this one presupposes no human or human-like actor who initiates the action.

LIST INTELLECTUAL-ACTIVITY-V = "dovdat" "fuobmát" "fuomášit" "huobmát" "jurddahit" "jurddašit" "smiehttat" ;

    # According to the rules choosing Loc or Com, the these verbs do not select TOOL, ABSTR-TOOL, CONCEPT or HUMAN in (Pl Loc).

LIST VEHICLE-PLACE-V = "addit" "bearrat" "gaikkihit" "gáibidit" "geiget" "oastit" "oažžut" "suoládit" "váldit"  ;

    # According to the rules choosing Loc or Com, these verbs do not select TOOL, ABSTR-TOOL and CONCEPT in (Pl Loc), but HUMAN or ACTOR in (Pl Loc).
    # HUMAN is source and locative, VEHICLE is locative

LIST VEHICLE-TOOL-V = "buktit" "doalvut" "fievrridit" "vuolggahit" ;

    # HUMAN is source and locative, VEHICLE is comitative

SET HUMAN-SOURCE-VEHICLE-V = VEHICLE-PLACE-V OR VEHICLE-TOOL-V ;

    # The two subsets differ in their choice of case for members of VEHICLE, but both choose locative for members of the set HUMAN (with the thematic role source)

LIST ABSTR-ENTITY-COM-V = "investeret" "lihkosmuvvat" "lihkosnuvvat" "lihkostuvvat" "lihkustuvvat" "ruhtadit";

    # According to the rules choosing Loc or Com, these verbs do not select TOOL, ABSTR-TOOL, CONCEPT, ABSTR-ENTITY and ACTOR in (Pl Loc).

LIST ONLY-PLACE-LOC-V = "álggahit" "bártidit" "bargat" "čohkkedit" "dahkat" "geahččalit" "olahit" "veahkehit" ;

    # According to the rules choosing Loc or Com, these verbs only select (Pl Loc) if PLACE or ORGANIZATION.

LIST HUMAN-LOC-V = "atnit" "báhcit" "geavahit" "gohčodit" "namuhit" ;

    # According to the rules choosing Loc or Com, these verbs choose only (Sg Com) if CONCEPT or HUMAN or "sátni".
    # Abstract

#!! * __PLACE-V__ Those get only not locative if the target is a member TOOL, ABSTR-TOOL or ANIMATE or CONCEPT. Selects more locatives than ONLY-PLACE-LOC-V
LIST PLACE-V = "ássat" "buolvvastallat" "buolvvastit" "čippostallat" "čohkkát" "čohkahit" "čuožžut" "čuččodit" "morihit" "gullát" "morránit" "orrut" "orodit" "stuorrut" "veallát" "veallahit" ;

LIST ABSTR-PLACE-V = "váilut" "heivet" "hiehpat" "sisttisdoallat" "soahpat" "šiehtat";
    # Verbs without agent

  # TIME/AIGODAT are locative

LIST STATE-V = "eallit" ;


LIST REM-WITH-PARTS = "gaikkihit" ;

    # Belongs to the DYNAMIC-V. They also take PARTS in comitative case.


LIST PERCEPTION-V = "áicat" "dovdat" "ealvit" "fuomášit" "fuobmát" "gullat" "haksit" "huobmát" "huomihit" "oaidnit" ;

LIST ILLNESS-V = "buohcat" "dearvvašmahttit" "dearvvašmuvvat" "dearvvašnuvvat" "dearvvaštuvvat" ;


# HIERARCHY:

#SET STATIC-V = PLACE-V OR EMOTION-V OR PERCEPTION-V ;

SET DYNAMIC-V = ACTIVITY-V OR MOVEMENT-V OR VERBAL-ACTIVITY-V OR HUMAN-AGENT-V OR INTELLECTUAL-ACTIVITY-V OR ABSTR-ENTITY-COM-V OR HUMAN-SOURCE-VEHICLE-V OR REM-WITH-PARTS ;

    # According to the rules choosing Loc or Com, dynamic-verbs do not select TOOL, ABSTR-TOOL or CONCEPT in (Pl Loc).
    # Human becomes either Loc or Com depending on its subset membership.

SET N-TIMEOBJECT-V = MOVEMENT-V OR ACTIVITY-V OR VERBAL-ACTIVITY-V OR INTELLECTUAL-ACTIVITY-V OR PLACE-V ;

    # Those verb sets in addition to TIMEADVERBIAL-V do not take TIME expressions as objects

#!! !Adverb sets
# -------

# Adverbs as predicates
# - - - - - - - - - - -
    # in combination with verbs

LIST COM-ADV = "fárrolaga" "oktan" "oktanaga" "ovttamielas" "ovttas" "ovttasráđiid" "searválagaid" "searválaga" ;

    # Could also be perceived as particles within complexs verbs.

# others: vuhti combines with váldit - Com

# Other adverb sets
# - - - - - - - - -

LIST GRADE-ADV =  "áibbas" "beare" ("duođaid" Adv) ("eanet" Adv) "erenoamán" "erenoamáš" "hirbmat" "hui" "ila" "issoras" ("man" Adv) ("mealgat" Adv) ("mearkkašahtti" Adv) "measta" "menddo" "muhtun_muddui" "nu" "oalle" "oba" ("oppa" Adv) "sakka" "seammá" ("veaháš" Adv) ("uhccán" Adv) ("unnán" Adv) "vehá" "veháš" "veahá" "veaháš" "viehka" ;

LIST PLURALIZER = "buot" "buotlágan" "buotlágaš" "earálágan" "iežálágan" "gait" "gaitlágan" "goappeš" "goappašat" "máŋggalágan" "moattelágan" ;
    # Singular nouns can have a plural form combined with those

LIST BUOT = "buot" "gait" "gaitin" "gaitdivnnat" ;

#!! !Adjective sets
# ----------

# Lexical valency sets
# - - - - - - - - - - -
    # here we have adjectives according to their semantic properties

LIST NUMAG-A = "čeahppi" "viššal" "láiki" ;
# This set for vuovdi etc., not for nisson etc.
# Here thus not ?stuorra vuovdi, etc,

# in periphrastic constructions (together with "leat")
# these subcategorize for comitative, locative etc. depending on
# the noun's semantic properties

LIST COM-A = "duhtavaš" "ollis" "oppa" ;

LIST ILL-A = "gullevaš" "bahá";
	## Ja danin son lei šaddan nu bahá tálaide ja bustávaide.

LIST ACTIVITY-A = "hilbat" "oskkáldas" ;

    # denote som kind of activity, might take HUMAN comitative?

LIST DEHALAS = "dábálaš" "dehálaš" "suohtas" ;
	# When Attr = pred.form
 	#$ Lea suohtas kurssas. # not Attr
 	#$ Lea čuovvovaš kurssas. # Attr

# Nickel: predicative adjectives (take a complement) "fargat" "heavnnas" "sáiggas" "suhtas"

# Other adjective sets
# - - - - - - - - - - -

LIST A-N = "buoidi" "čeahppi" "headju" "heittot" "jalla" "neavri" "nuorra" "oahpis" "oarbbis" "ráhkis" "rikkis" "váivváš" "vuoras";

    # nouns that have very specific semantics and are derived from adjectives
    # those nouns are animate

LIST A-N-CASE = "varas" ;
    # adjectives which are also nouns in other cases than nominative

LIST NON-GRADE-A = "sápmelaš" "alternatiiva" ;
    # gradable under certain circumstances, for example if we write "nu" "hirbmat" "hui" "oalle" "oppa" "veháš"
    ## mon dagahallan NU sápmelažžan = jeg later som jeg er SÅÅ same/samisk?
    ## mon anán du VIEHKA sápmelažžan

LIST GRADE-A = "fávru" "headju";
    # The ones that have a strong adjective meaning, and are inclined to be adjectives rather than nouns in Ess constructions

LIST A-LEX-AS-NOUN = "bassi" "guovddáš" "nuorra" "oahpis" "váivi" "oainnus";
    # Those are quite strongly lexicalized as nouns. NBNB Earlier LEX-A, but this was another set

LIST TIME-A = "boahtte" "čuovvovaš" "maŋemus" "maŋimus" "ovddit" ("mannat" PrfPrc) ("vássit" PrfPrc)   ;

LIST DURATION-A = "heila" "eanas" "eanet" "geažo" "geažos" "guhkki" "obba" "oba" "ollis" "oppa" "visot" ;

LIST POINT-IN-TIME-SPEC = "dološ" "eará" "nubbi" ;



#!! !NP sets defined according to their morphosyntactic features
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

SET NORM-LOC = Loc - (South Loc) ;

LIST N-SG-NOM = (N Sg Nom);

SET LEX-N = N - DER-N;

LIST PROP = (N Prop);

SET COMMON-N = N - Prop;

SET HEAD-N = N - RCmpnd ;

SET HEAD-N-NOM = (N Nom) - RCmpnd ;

SET SUBJECTHEAD = N OR A OR Pron - Refl ; # These, can be subject heads

SET NP = N OR A ;                      # anything that can take except numerals
SET NP-HEAD        = Pron OR HEAD-N - ("buorre") ;
SET NP-HEAD-SG     = SG-PRON OR (N Sg) OR (A Sg) - RCmpnd  - Dem - ("buorre");
SET NP-HEAD-PL     = PL-PRON OR (N Pl) OR (A Pl) - RCmpnd  - Dem - ("buorre");
SET NP-HEAD-SG-NOM = SG-PRON + Nom OR (N Sg Nom) OR (A Sg Nom) - ("buorre") - RCmpnd ;
SET NP-HEAD-PL-NOM = PL-PRON + Nom OR (N Pl Nom) OR (A Pl Nom) - RCmpnd  - ("buorre");
SET NP-HEAD-NOM    = NP-HEAD-SG-NOM OR NP-HEAD-PL-NOM ;
SET NP-HEAD-ACC    = (Pron Acc) OR (N Acc) OR (A Acc) - RCmpnd - (Dem Attr) - ("buorre");
SET NP-HEAD-GEN    = (Pron Gen) OR (N Gen) OR (A Gen) - Der/NomAct - RCmpnd - (Dem Attr) - ("buorre")   ;

#!! !The PRE-NP-HEAD family of sets

#!! These sets model noun phrases (NPs). The idea is to first define whatever can
#!! occur in front of the head of the NP, and thereafter negate that with the
#!! expression __WORD - premodifiers__.


SET PRE-NP-HEAD = (Prop Attr) OR (Prop @>N) OR (A Attr) OR (ABBR Attr) OR ("buorre")
         OR (Pron Pers Gen) OR (N Gen) OR BUOT OR
         Num OR RCmpnd OR CC OR (Pron Dem) OR (Pron Refl Gen) OR (Indef Attr) OR
         (PrfPrc @>N) OR PrsPrc OR (A Ord) ;
         # The strict version of items that can only be premodifiers, not parts of the predicate

SET PRE-NP-V = PrfPrc OR PrsPrc OR Der/NomAg OR Actio OR Der/NomAct OR (V A) OR (Ind Prs Sg3) OR (Ind Prs Sg1) OR (Imprt Du2) ;
    # to be used together with PRE-NP-HEAD before @>N is disambiguated

SET NP-MEMBER   = PRE-NP-HEAD OR N ;

SET PRE-A-N     = (Pron Pers Gen) OR (Pron Pers Acc) OR (Pron Indef) OR Num OR (A Ord) OR (Pron Dem) OR (Pron Refl Gen) OR (Pron Refl Acc) ; # Acc pga av manglende disambiguering tidlig i fila

SET NOT-PRE-A-N = WORD - PRE-A-N ;

LIST PUNCT-LEFT = (PUNCT LEFT) ;
LIST PUNCT-RIGHT = (PUNCT RIGHT) ;

SET PRE-APP   = COMMA OR PUNCT-LEFT OR PRE-NP-HEAD ;
 # This set ist not only for what can
                            # stand in front of appositions but also
                            # postmodifiers.



#!! The set __NOT-NPMOD__ is used to find barriers between NPs.
#!! Typical usage: ... (*1 N BARRIER NPT-NPMOD) ...
#!! meaning: Scan to the first noun, ignoring anything that can be
#!! part of the noun phrase of that noun (i.e., "scan to the next NP head")

SET NOT-NPMOD     = WORD - PRE-NP-HEAD OR ABBR ;
# This is the previous NPNH (npnh) set.
# NOT-NPMOD  = "NOT-PRE-NP-HEAD"

SET NOT-NPMOD-ACC     = NOT-NPMOD - Acc OR ABBR ;
SET NOT-NPMOD-ACC-ADV     = NOT-NPMOD - Acc - Adv OR ABBR ;
# To be used in mappingrules, before the disambuation of Acc and Gen.

SET NOT-NPMODADV    = WORD - PRE-NP-HEAD - Adv OR MO;
# NOT-NPMODADV = "NOT-PRE-NP-HEAD-OR-ADV"
SET NOT-NPMODADV-INDEF   = WORD - PRE-NP-HEAD - Adv - Indef ;
## NOT-NPMODADVI = "     ...-OR-INDEF"
SET NOT-NPMODADVII  = WORD - PRE-NP-HEAD - Adv - Indef - Ill ;
# Illative indir.obj.
SET NOT-NPMODADVIIP = WORD - PRE-NP-HEAD - Adv - Indef - Ill - Pcle ;
# <== is this our NOT-NPMOD set?
SET NOT-NPMODCC   = WORD - PRE-NP-HEAD - COMMA - LCRD ;
SET NAPP     = WORD - PRE-APP ;

# SET TOLERANT-PRENPP = V ; (everything non-NP-head!)
# ... Most sloppy BARRIERs

# Set for NP with participle premodifier
# ... IF *-1 X BARRIER NOT-NPMOD OR
# LocComIll are ok, but only with a PrfPcc to the right

#!! !Other negatively defined morphosyntactic noun sets


SET NOT-N = WORD - N ;

#SET NOT-A = WORD - A ;

SET NOT-N-A = WORD - N - A ;

SET NOT-NAPrfCC = WORD - N - A - CC - PrfPrc ;

SET NOT-NP = VFIN-NOT-NEG OR ConNeg OR Inf OR Pcle OR Interj OR CS ;
 # The set NP-BOUNDARY is bigger.

SET NPROP = N - PROP ;

SET CLB_NOT-COMMA = CLB - COMMA OR PUNCT-LEFT OR PUNCT-RIGHT ;

SET NOT-QMARK = WORD OR CLB OR PUNCT - QMARK ;

SET W-NOT-ACC-GEN = WORD - Acc - Gen ;



#!! !Noun sets

#!! Nominal sets defined according to their morphophonological properties
#!! Sets for lexeme homonymy (most of them are moved to where the actual rules are.)


#!! The words in the set __N-PO__ can be both N and Po, the set takes that into account.
#LIST N-PO = "áigi" "gaskan" "gaskka" "gaskkas" "guovddáš" "ovddas" "ráji" "vuostá" "guvlui" ;
LIST N-PO = "áigi" "guovddáš" "vuostá" ;


#!! !The LAHKA set family

LIST LAGA = (".*bargo#lága"r) "spábbačiekčan#lága" ;

LIST LAGALAHKA = (".*bargo#láhka"r) "spábbačiekčan#láhka" "sierraláhka" ;

SET LAHKA = (".*láhka"r) - LAGALAHKA ;


LIST LAHKA-MOD = "almennet" "almmolašvuohta" "apotehka" "arkiiva" "áhpenuohtástit" "álbmotoadju" "bargi" "bargobiras" "báikenamma" "bákti" "bátnedearvvašvuođabálvalus" "bivdu" "boazodoallu" "čázádat" "dálkkas" "dásseárvu" "deatnu" "dearvvašvuođabargi" "dearvvašvuohta" "dearvvašvuođafitnodat" "dearvvašvuođabargiidláhka" "dearvvašvuođaregisttar" "divššohasvuoigatvuohta" "doaibmi" "eana" "eana#suodjaleapmi" "eanavuovdin" "elliidsuodjaleapmi" "energiija" "fápmudus" "Finnmárku" "friddjasuohkan" "fuođđu" "gávpot" "geaidnu" "gearggusvuohta" "gieldda#dearvvašvuođabálvalus" "gielda" "gili#oppalašeana" "girku" "golli" "guollebiebman" "háhkan#konsešuvdna" "hálddašeapmi" "heaggadieđut" "huksen" "industrikonsešuvdna" "jávrebivdu" "joatkkaoahpahus" "johtolat" "konsešuvdna" "kulturmuitu" "lavdnjeloggut" "lotnolasealáhusabargi" "luodda" "luonddusuodjalus" "luopmu" "luossa" "mánáidgárdi" "mánáidsuodjalusbálvalus" "mearraguolásteapmi" "mearraresursa" "minerála" "mohtorjohtolat" "muddet" "namma" "Norga" "norga" "oahpahallat" "oahpahus" "oahppu" "oassálastinláhka" "olgunastin" "olmmošvuoigatvuohta" "oktasašsápmi" "pasieantavuoigatvuohta" ("ráŋggáštit" Der/NomAct) "resursahálddašeapmi" "riekteveahkki" "sáivabivdu" "sápmi" "searvan" "sierra" "spesialistadearvvašvuođabálvalus" "stáhtaborgárriekti" "stáhtafitnodat" "tuollu" "uhccimusmihttonjuolggadus" "varas#guolli" "várri" "vearro#dahku" "vearru" "vuođđu" "vuođđoskuvla" "vuovdedoallu" "vuovdegáhttet" "vuovdi" "1789" "1842" "1863" "1888" "1899-#bivdu" "1899" "1920-#várri" "1933" "1965" "1975-#várri" ;

# This list is for lahka compounds written with two single words

LIST LAHKA-A = "boaris" "dálá" "dáláš" "gustojeaddji" "gustovaš" "našunála" "ovdalaš" "sorjjasmeahttun" ("suodjalit" Der/PassL PrfPrc) ;

LIST LAHKA-V = "addit" "áimmahuššat" "árvalit" "ásahit" "čállit" "čuovvulit" "čuovvut" "dohkkehit" "gáibidit" "geatnegahttit" "geavahit" "gieđahallat" "jorgalit" "kriminaliseret" "mearridit" "muhttit" "ovddidit" "ráŋggáštit" "viiddidit" ;

LIST LAHKA-V-PASS = "atnit" "ásahit" "báhkkodit" "báhkkot" "čállit" "čuoldit" "dadjat" "dahkat" "dohkkehit" "duššadit" "eaktudit" "fámuhuhttit" "geatnegahttit" "jorgalit" "jurddahit" "jurddašit" "kriminaliseret" "mearridit" "muddet" "njulget" "oaivvildit" "rievdadit" "ráŋggáštit" ;

# This one works in combination with others, f.in. LAHKA-POST.
LIST LAHKA-N = "§" "almmolašvuohta" "eanagirji" "eanangirji" "eatnangirji" "čađaheapmi" "fápmudus" "fápmuiboahtin" "gáibádus" "geatnegasvuohta" "geavaheapmi" "gonagas" "hálddašangeavat" "juksanmearri" "kap" "kapihtal" "konvenšuvdna" "láhkaásahus" "láhkaovdabargu" "láigohanvuoigatvuohta" "mearrádus" "njuolggadus" "nr" "paragráfa" "riekteáddejupmi" "riektegáldu" "riektegeavat" "riektesubjeakta" "sámekonvenšuvdna" "vuoigatvuođabargu" "vuoigatvuohta" ;

# This one does it more likely to be "láhka" in combination with f.i. LAHKA-V:
LIST LAHKA-SUBJ-N = "eiseváldi" "guovddášeiseváldi" "stáhta" ;

LIST LAHKA-GEN-N = "§" "buohkaidvuoigatvuohta" "cealkka" ("fápmu" Ill)"hápmi" "johtolatvuoigatvuohta" "kapihtal" "láhkavuođđu" "ovdabargu" "rámma" "ráhkkananbargu" "ráidu" "sátnemearkkašupmi" "ulbmilparagráfa" "vuođđu" "vuođđoeaktu" "vuolit" "V";

# "vuolit" is an adj. but suit pragmatically to the set.

LIST LAHKA-ASAHUS = "alimusriekti" "Odeldiggi" "Stuoradiggi" "Stuorradiggi" "stuoradiggi" "stuorradiggi" "vuolleriekti" ;

LIST LAHKA-POST = "bokte" "olis" "mielde" "vuođul" ;


LIST GIELDA = "fylkkagielda" "gielda" "guovddášgielda" "hálddašangielda" "riddogielda" "ruovttugielda" "sámegielda" "vuotnagielda" ;

  # We need this set because there is ambiguity between "gielda"+Gen/Acc and "gieldda"+Nom.

LIST GIELDDA = "gieldda" ;


#!! !Nominal sets defined according to their semantical properties
# -------------------------------------------------------------

# Here we will write what sets we have and how we divide them.

#!! * Spatial noun sets. These nouns behave like postpositions
LIST SPATIAL-N = "guovlu" "osku" "sadji" "vuođđu" ;

# These nouns in essiv get @ADVL:
LIST ESS-ADVL-N = "arvedálki" "arvi" "árga" "baika" "bajándálki" ("bassi" N) ("báhkka" N) "beaivvádat" "beaivvadat" "biegga" "bievla" ("buolaš" N) "borga" "čakčaseavdnjat" "čáppadálki" "čuoika" "čuovga" "cuoŋu"  (".*dálki"r) "dulvi" "fiertu" "goalki" "guoldu" "mánoheahpi" "mierká" "muohta" "njáhcu" "obbadálki" ("seavdnjat" N) "varra" "veaigi";

# These nouns/adjectives in essiv can get @ADVL:
LIST ESS-PROB-ADVL-N = ("bassi" N) "boaris" ("nuorra" N) ;


#!! * Time sets
# - - - - -

LIST MANNU = "ođđajagemánnu" "guovvamánnu" "njukčamánnu" "njuvččamánnu" "mársmánnu" "cuoŋománnu" "cuoŋosmánnu" "cuoŋumánnu" "gáranasmánnu" "miessemánnu" "máimánnu" "geassemánnu" "suoidnemánnu" "borgemánnu" "šnjilčamánnu" "šnjilžamánnu" "čakčamánnu" "golggotmánnu" "skábmamánnu" "novembermánnu" "ritnemánnu" "juovlamánnu" ;

# Duomma:ii

LIST AIGODAT = "áigi" (".*áigodat"r) "álgoáigi" "bargoáigi" "soahteáigi" "soahtegaskaáigi" ;

# Duomma:lea

LIST VAHKKU-DUR = "álgojahki" "árrageassi" "beaivi" "jándor" "bodda" "čakča" "čakčageassi" "čakčaseavdnjat" "čuohtejahki" "dálvi" "diibmu" "eahketbodda" "geassi" "giđđa" "idja" "iđitbodda" "jahki" "jahkebealle" "jahkečuohti" "kaleanddarjahki" "loahppajahki" ("[0-9]*-#lohku"r) "maŋŋe#giđđa" "mánnu" "minuhtta" "minukta" "njealjádasjahki" "skuvlajahki" "tiibma" "vahkkoloahppa" "vahkku" ;
# these are periodes and can be Acc

LIST VAHKKU-TIME = "b" "beassážat" "biidnovahkku" "bievlajahki" "bivdoáigi" (".*beaivi"r) "d-beaivi" "dienasjahki" "doaibmajahki" "eamiálbmotjahki" "earrejahki" "gaskaidja" "geardi" "juovllat" "juovlabassi" "luomejahki" "luopmojahki" "mihcamárat" "muorjejahki" "nissonjahki" "stuorradiggeáigodat" "vuoitojahki" "vuođđoskuvla#vahkku" ("[0-9]*-#jahki"r) ("[0-9]*-#jahki"r) "[0-9]*-[0-9]*-#jahki" "[0-9]*-[0-9]*-#lohku" ("[0-9]*-#lohku"r) ("[0-9]*-#lohku"r) "eahket" "iđit" ;

 # The quoteless jahki here is since the preprocessor is not fine-tuned
 # yet, it still gives '"1870-" # jahki N Sg Com'. # lets see if "dbm" works
 # Duomma:lea

SET VAHKKU = VAHKKU-DUR OR VAHKKU-TIME ;

LIST JAHKI-NUM = "jahki" "skuvlajahki" ;

    # Years which can be numerated

LIST PARTTIME = "čavčča#bealle" ;
	# This set is for finding Gen in expressions like PARTTIME OR TIME Gen.

LIST AIGI = "áigi" (".*náigi"r) ;

LIST SP-BEAIVI = "albmáimannan#beaivi" "bargočoahkkin#beaivi" "beassášbeaivi" "mánnodat" "vuossárga" "disdat" "maŋŋebárga" "gaskavahkku" "duorastat" "bearjadat" "juovlabeaivi" "lávvardat" "lávvordat" "sotnabeaivi" "sábbát" "skilleduorastat" ;

LIST GEN-BEAIVI = "álbmotbeaivi" "árgabeaivi" "bargobeaivi" "beaivi" "duopmobeaivi" "riegádanbeaivi" "skuvlabeaivi"  ;
# General-beaivi can have Gen modifier, like "skuvlla árgabeaivi".

#LIST DATE1 = ("[0-3][0-9]\.[0-1][0-9]\.[0-3][0-9][0-9][0-9]"r) ("[0-3][0-9]\.[0-1][0-9]\.[0-9][0-9]"r)  ("[0-9]\.[0-1][0-9]\.[0-3][0-9][0-9][0-9]"r) ("[0-3][0-9]\.[0-9]\.[0-3][0-9][0-9][0-9]"r) ("[0-9]\.[0-9]\.[0-3][0-9][0-9][0-9]"r) ("[0-3][0-9]\.[0-3][0-9]"r) ;

#SET DATE = DATE1 - ("[0-9]*"r) ; # En litt klosset måte å gjøre det på, fikk ikke regex i DATE1 til å fungere som de skulle

SET BEAIVI-REF = SP-BEAIVI OR GEN-BEAIVI OR Sem/Date ;

LIST BEAIVI = "beaivi" "b" ;

LIST YEAR = ("[0-2][0-9][0-9][0-9]"r) ;


SET TIME = MANNU OR VAHKKU OR BEAIVI-REF OR AIGI OR YEAR ;

  # Do not add AIGODAT, it has some other characteristics in some cases.
  # But fix!!
  # "Sámediggeráđđi   lea áigodagas doallan   5 čoahkkima."
  # Today believed to be obj.

SET NOTIME = N - TIME ;

LIST GEASSET = "dálvet" "dálvit" "giđđat" "geasset" ("geassit" Adv) "čakčat" ;
 # What is special here?

LIST PAST-MARKER = ("mánná" Ess) "dalle" "diibmá" "dolin" "dološ" "dovle" "duvle" "eske" "gieskat" "ikte" ("nuorra" Ess) "ovdal" ("ovdditbeaivi" Gen) ("vássánáigi" Loc) ("boaresáigi" Gen) ;

LIST PRESENT-MARKER = "dál" "ihttin" "odne" "otne" ("boahtteáigi" Loc) ("boahtteáigi" Gen)  ;

SET TIME-MARKER = PAST-MARKER OR PRESENT-MARKER ;

LIST OTHER-TIME-ADV =  "árabuš" "árrat" "dál" "diibmá" "easkka" "easka" "guhká" ("guhkit" Adv) "maŋemustá" "maŋŋel" "maŋŋil" "maŋŋá" "maŋŋelaš" "maŋŋeleappos" "maŋŋit" "muhtumin" "ovdal" ;

SET TIME-ADV = TIME-MARKER OR OTHER-TIME-ADV OR GEASSET;

LIST PLACE-PO = "duohken" ;

#!! * Amount sets

LIST VOLUME = "cl" "lihtar" (".*lihtter"r) "dl" "lihtter" "ml"  ;

LIST WEIGHT = "mg" "kg" "kilo" "kilu" "gilo" "tonna" ;

LIST LENGTH = "čuolda#badji" "dumá" "fierbme#badji" "gaska" "miila" (".*mehtar"r) (".*mehter"r) "mehtar" "mehter" "sente" "cm" "tumá" "állan" "salla" "gaska" ;

LIST TEMPERATURE = "buolašceahkki" "buolašgráda" "lieggaceahkki" "lieggagráda" ;

LIST AMOUNT = "uhcánaš" "unnánaš" ;

LIST TIME-UNIT = (".*jahki"r) "minuhtta" "minukta" "sekunda" "vahkku" "mánnu" "diibmu" "beaivi" ;
	# For use for rules hitting "maŋŋel" as Adv instead of Po. Also in MEASURE-set.

SET MEASURE = VOLUME OR WEIGHT OR LENGTH OR AMOUNT OR TEMPERATURE OR TIME-UNIT ;

LIST CURRENCY = "denara" "dollár" "euro" "kruvdnu" "kr" "ru" "rubel" "ruvdno" "ruvdnu" "¢" "€" "$";

SET CURRENCY-GEN = CURRENCY + Gen ;


LIST NUMUNIT = "čuohti" "duhát" "miljovdna" "mill" "milj" "miljárda" "miljon" "mrd";

LIST KIND = (".*šládja"r)  ;

LIST COUNTABLE = "oassi" "kapihtal" "paragráfa" ;

LIST NUMMAR = "identifiserennummar" "njuolggotlf" "nr" "nummar" "nummir" "tabeallanummar" "vuorkááššenr" ;

LIST KLASS = "ášši" "bálkáceahkki" "čuokkis" "s" "siidu" "§" "§§" "paragráfa" "oassi" "kap" "kapihtal";

LIST PEOPLE-NUM = "guovttis" "golmmas" "njealjis" "viđas" "guđas" "čiežas" "gávccis" "ovccis" "logis" ;

#!! * Sets for nouns with morpho-syntactic preferences
# ----------------------------------------

#!! * Number-related sets


LIST PL-N = "áiru" "beaivváščalbmeláset" "beassážat" "biddu" "čalbmeláset" "čázet" "fáhcca" "gállot" "gálssot" "gáma" "gistta" "goikket" "gummestevvel" "heajat" "hellodagat" "juovllat" "mállásat" "nagir" "njealljejuvllat" "njuorjjot" "nuhtat" "nuvttat" "nuvttot" "sabet" "sáppat" "sisttet" "skárrit" "skierat" "skuovva" "stevvel" "suohkku" "suorbmafáhcca" "suorpmahat" "ullofáhcca" "ullosuohkku" "vuotta" "viljjit" ("[0-9]*-#jahki"r) ("[0-9]*-[0-9]*-#jahki"r) ("[0-9]*-#jahki"r) ;

 # Nominals that appear with plural numerals.

LIST SG-WORD = ("addit" Der/upmi) "almmolašvuohta" "anistupmi" "arvi" "áigemearri" "álgu" "áŋgirvuohta" "ávvu" "ballu" "boazodoallu" "borakeahttáivuohta" "buorredáhtolašvuohta" "doaivu" "doarjja" "dárogiella" "Finnmárkoláhka" "fuolalašvuohta" "gaskkamuddu" "gávvilvuohta" "geargatvuohta" "geardun" "geavaheapmi" "gelbbolašvuohta" "giellageavaheapmi" "giitevašvuohta" "gudnejahttin" "fápmu" "guossoheapmi" "guovttegielalašvuohta" "hearvásvuohta" "iešdovdu" "illu" "ipmárdus" "isitvuohta" "juonalašvuohta" "kulturduogáš" "kulturgelbbolašvuohta" "láhkavuođđu" luondduvuođu" "loahppa" "máŋggabealatvuohta" "mielki" "miella" "miellasuvvan" "moraš" "muohta" "musihkka" "oamedovdu" "olahahttivuohta" "ovddasvástádus" "ovttasbargu" "ovttadássásašvuohta"  (".*politihkka"r) "ráffi" "ráfi" "ráhkisvuohta" "riska" "roahkkatvuohta" "sámegiella" "sámegielmáhttu" "sámekultuvra" "ustitvuohta" "vanhurskkisvuohta" "vásáhusduogáš" "veahkaválddálašvuohta" "vuohki" ("vuordnut" Der/NomAct)  ;

# Words that are used in singular unless their plurality is stressed by words such as "goappeš", "goappašat", "earálágan", "máŋgalágan" or Num or Ord.

#!! * Sets for case, possessive, etc.


LIST LOC-N = (".*dilálašvuohta"r) "dáhpáhus" "ipmárdus"  ;

  # Those nouns appear in locative unless a head asks for comitative.

LIST VUOHKI = (".*vuohki"r)  ;
# Usually in comitative, see rule

LIST N-NOPXLIST = "atnu" "almmolašvuohta" "áibmu" "áigemearri" "áigi" "álbmotjienasteapmi" "álgoálgu" "álgostávval" "álgovuorru" "álgu" "álki" ("árvidit" Der/NomAct) "bargoáigi" "báhkka" ("beaivádit" Der/NomAct)  "beaivi" "bievlajahki" "boahtteáigi" "boahtu" "boazodoallu" "boraspire" ("buorre" Comp) "cealkka" "čakčageassi" "čáppagirjjálašvuohta" "čiegus" "čiehča" "dálvi" "dievasčoahkkin" "dilli" "divregiksi" "dollagáddi" "dološ" "eahket" "Finnmárkkuopmodat" "geatnegas" "geavat" "gaskkamuddo" "gárvi" "geassi" "genitiivahápmi" "giđđa" "giellageavaheapmi" "girkosiida" "hálddašanguovlu" "historjá" "jahki" "japma" "jođán" "johgáddi" "johtu" "kilo" "láhka" "leansmánneárvvošteapmi" "loahppa" "logaheapmi" "lohku" "lohpi" "luondu" "maŋŋi" "máŋggaidlohku" "meallu" "meannudeapmi" "mielladearvvašvuođa#dikšu" "njunuš" "nuoraidskuvladássi" "nuppádas" "nuppát" ("oahpahit" Der/NomAct) "ođas" "ođđajahki" "oktavuohta" "olámuddu" "olle" "olmmoš" "ortnet" "ovttaidlohku" "ovttasbargu" "predikatiiva" "ráfi" "rávásmanahki" "riektegeavat" "riikkaarkivára" "sápmelaš" "sisriika" "sivva" "Skjerstad-ášši" "smávvaskuvladássi" "soaittáhat" "stáhtaráđđi" "unnitlogugiella" "vahkku" "valljodat" "viiddis" "viso" "vuokta" ("[0-9]*-#lohku"r) Coll ;

 # Nominals that probably do not have a Px ('1600-logus').

LIST N-PX = "áhčči" "bárdni" "biigá" "boadnji" "eadni" "eammi" "gáibmi" "guoibmi" "hearrá" "nieida" "oabbá" "olmmái" "ráhkis" "reaŋga" "skibir" "skihpár" "ustibaš" "ustit" "viellja" ;

 # Nominals that well may have a Px.

LIST ALU = "alu" "gasu" "govddu" "guhku" "losu" "stuoru" "viiddu" "čieŋu" ;

	  # Those are words that only appear in one form, which Nickel calls A/G.
    # They are only in a predicative position.

# These are ABBR:
LIST REAL-TITLE =  "prof" "cand.mag" "cand.oecon" "cand.philol" "cand.real" "cand.scient" "cand.theol" "cand" "dr.art" "eksp.hoavd" "eksp.sj" "fenr" "fig" "fil.tri" "gen.lt" "genr" "genr.lt" "h.r.adv" "hr" "kapt" "korp" "lekt" "lic" "ltn" "mag.art" "mag" "merc" "miss" "Mr" "mr" "Mrs" "mrs" "odont" "oecon" "ordf" "ossod.dir" "stud.phil." "res.kap" "stud.philol" "vit.ass" "dr" "dr.med" "dr.philos" "dr.theol" ;

LIST TITLE-LIKE = "gásttašeaddji" "justitiarius" "majestehta" "miss" "ránnjá" ;
# Ránnjá Ole Vuolab imašta manne ....

#!! * Sets for nouns as pred


# subcategorizing for a particular case

LIST LOC-COMPL-N = "ávki" "árvalus" "dovdu" "ipmárdus" "ovdamearka" "sáhka" ;

  # Nouns that take a locative
  # There is also an idiomatic use of "ovdamearka" in the constructions "ovdamearkka dihte" and "ovdamearka" (Ess).


LIST COM-COMPL-N = "bargu" "čoahkkin" "gaskavuohta" "lihkku" "oktavuohta" "ovttaoaivilis" "ovttasbargočoahkkin" "ovttasbargu" "ovttasbargošiehtadus" "šiehtadus" ;

SET LOC-HEAD = LOC-V OR LOC-COMPL-N ;

SET COM-HEAD = COM-V OR OKTII-V OR COM-COMPL-N ;


LIST PREGEN = "álggogeahčen" "beallemuttus" "beallemuddu" ("bealli" N Sg Loc) "gaskamuddu" "gaskkamuddo" "gaskkamuddu" ("gaskavuohta" N Sg Loc) "loahpageahčen" "oktavuohta" "ovttasbargu" ;

 # Nouns and adjectives that take a preceding genitive.



# Possessor hierarchy
#                          +            P  o  s  s  e  s  s  e  d		    |
# Possible possessors      +  FAMILY    FAMILY-ALSO-ABSTRACT    NON-FAMILY  +
# ------------------------ +  --------    -----------------   ------------  |
# Prop Sem/Plc                 +                      x               x		    |
# HUMAN-INDIVIDUAL         +      x               x               x		    |
# FAMILY pronouns	       + 									   		    |
# HUMAN-GROUP              +                                      x		    |
# PROFESSION               +      x               x               x		    |
# OFFICE                   +      x               x               x		    |
# HUM-FUNCTION             +      x               x               x		    |
# ABSTR-TEXT               +      x               x               x		    |
# PLACE (not NATURE-PLACE) +                      x               x		    |
# NATION                   +                      x               x		    |
# ANIMAL                   +                      x			   			    |
# EDUCATION                +                      x               x		    |
# ORGANIZATION             +                      x               x         |
# INSTITUTION              +                      x               x		    |
# INDUSTRY                 +                                      x		    |

LIST FAMILY-ONLY-HUMAN = (Prop Sem/Mal) (Prop Sem/Fem) (Prop Sem/Sur) "áddjut" "áhkká" "áhkkut" "bárdnemánná" "boadnji" "čeahci" "čeahcit" "dálueamit" "eahki" "eamit" "eammi" "eanu" "goaski" "guoibmi" "ipmi" "irgi" "isit" "lunta" "mánáidmánná" "mannji" "máhka" "máttaráddjá" "máttaráhkku" "moarsi" "muoŧŧá" "muoŧŧal" "náittosguoibmi" "neahpi" "oabbá" "oabbábealle" "oambealle" "oappáš" "oarpmealle" "osku#guoibmi" "ristváhnen" (".*sássa"r) "siessal" "siessá" "vieljaš" "viellja" "vielljabealle" "vilbealle" "vuohppa" "vuonáhkku" "vuoni" ;
# These can only be possessed by HUMAN-INDIVIDUAL, HUMAN-GROUP, PROFESSION, OFFICE, HUM-FUNCTION, ABSTR-TEXT

LIST FAMILY-ALSO-ABSTRACT = "áhčči" "áddjá" "áhkku" "bearaš" "eadni" "oapmahaš" "sohka" "sohkagoddi" "váhnen" "veahka" ;
# These can be possessed by HUMAN-INDIVIDUAL, HUMAN-GROUP, PROFESSION, OFFICE, HUM-FUNCTION, ABSTR-TEXT, PLACE (minus NATURE-PLACE), Prop Sem/Plc, NATION, ANIMAL, EDUCATION, ORGANIZATION and INSTITUTION. Can also be used as metaphors.
# Giellaplánaid eadni.

SET FAMILY = FAMILY-ONLY-HUMAN OR FAMILY-ALSO-ABSTRACT ;

LIST NON-FAMILY = "bárdni" "bártnáš" "divššohas" "gánda" (".*guoibmi"r) "guossi" "kránnjá" "lagamuš" (".*mánná"r) "mánáš" (".*nieida"r) "nieiddaš" (".*nisson"r) (".*nisu"r) "nuorra" "oskkuviellja" "olmmái" (".*olmmoš"r) "ránnjá" "rumaš" "sámenuorra" "sápmelaš" "skibir" "skihpár" "smávvagánda" (".*ustit"r) "verdde" "viissis" ;
# These can be possessed by HUMAN-INDIVIDUAL, HUMAN-GROUP, PROFESSION, OFFICE, HUM-FUNCTION, ABSTR-TEXT, EDUCATION, PLACE (minus NATURE-PLACE), Prop Sem/Plc, NATION, EDUCATION, ORGANIZATION, INSTITUTION and INDUSTRY.

LIST HUMAN-LIKE = "beahtu" "beargalat" "eŋgel" "háldi" "hearrá" "ipmil" "stállu" "suodjalus#eŋgel" "ulda" ;

SET HUMAN-INDIVIDUAL = HUMAN-LIKE OR FAMILY OR NON-FAMILY ;

LIST HUMAN-GROUP =  "almennet" "almmolašvuohta" "álbmot" (".*álbmot"r) (".*bearaš"r) (".*čearda"r) "delegašuvdna" "gázzi" "gilialmennet" (".*guovttis"r) (".*guovttos"r) (".*joavku"r) (".*kommišuvdna"r) (".*komiteá"r) "ráidu" "ráđđi" ("sápmi" N Pl) "stáhtaalmennet" "unnitlohku" (".*veahka"r) ;
  ## group of humans that connot be a building at the same time

LIST PROFESSION1 = (".*advokáhta"r) (".*advokáhtta"r) (".*duopmár"r) "arkeologa" (".*artista"r) (".*assisteanta"r) (".*áittardeaddji"r) "ákšo#almmái" (".*bargi"r) "buorideaddji" "ámmátolmmái" "ámmátolmmoš" (".*mánni"r) (".*hoavda"r) (".*meannudeaddji"r)
(".*geahčči"r) (".*bagadalli"r) (".*bagadeaddji"r) (".*konsuleanta"r) "bargoveahka" "bassaleaddji" "bassi" "báhkár" "báhppa" "bálvaleaddji" (".*čálli"r) "bearremánni" "bibliotekára" (".*biigá"r) ("biila#divvut" Der/NomAg) (".*vuovdi"r) "birasgáhttenráđđeaddi" (".*terapevta"r)  (".*terapeuta"r) "bisma" "bismá" "bispa" (".*bivdi"r) (".*hábmejeaddji"r) "boanda" "boazodoalli" "boazosápmelaš" "boazosápmi" "bolesmeašttir" (".*vuoddji"r) "buvttadeaddji"
"čalbmeáhkku" "čalbmeeadni" "čálaoahppavaš" "čiekči" "čorgejeaddji" (".*čuojaheaddji"r)
"dáidár" "dáiddár" "dálon" "dálueamit" "dáluisit" "dárkkisteaddji" "davvisápmi" (".*dikšu"r) (".*direktevra"r) (".*dirigeanta"r) (".*divššár"r) (".*doaimmaheaddji"r) (".*doavttir"r) "dulka" "duojár" (".*duopmar"r) (".*dutki"r)
"ealáhusdoalli" "eallo#doalli" "eanadoalli" "eananhearrá" (".*sadjásaš"r) "eatnandoalli"
"fáldi" "fitnodatdoaibmi" "fitnodatoamasteaddji"
"gávpejas" "giellaráđđi" (".*veahkki"r) "girdi" "girko#bálvaleaddji" "girkodulka" "gollerávdi" "gonagas" "govvadáidár" "govvadáiddár" "govvejeaddji" "guođoheaddji" "guolásteaddji"
"hárjeheaddji" (".*teknihkar"r) (".*jođiheaddji"r) "johttisápmi" "johttisápmelaš" (".*journalista"r) "keaisár" "kirurga" "koahkka" "kuráhtor" "korefereanta" "kursaoahppi"
"lágideaddji" "láibu" "lávlu" "leanut" "logopeda" "luhkkár"
"máilmmemeašttir" "máhttájeaddji" "máttasápmi" "mearraalmmái" "mearraolmmái" "mearrasápmi" "mišuneara" "museá#bargi" "musihkkadahkki" "musihkkár"
"návetvázzi" "neavttár" "njunušjurista" "noaidi" "offiseara" (".*oahpahalli"r) (".*oahpaheaddji"r) "oahppi" ("oaivadit" Der/NomAg) "oaivámuš" "ođashoavda" "ođaslohkki" "ossodatdirektevra" "ossodatdirektøra"
"parkerenfákta" "pedagoga" "plánejeaddji" "poliisa" "politiija" ("prentet" Der/NomAg) "profehta" "professor"
"rádjefákta" "rádjesoalddát" "ráđđeaddi" "ráđđeolmmái" "reainnár" "rávdi" "reaŋga" "redaktevra" "rehketdoallodárkkisteaddji" "rehketdoalli" "rektor" "ruhttejeaddji" "ruvdnaprinsa" ("ruvvet" Der/NomAg)
"sámediggeráđđi" "sámeduojár" "sárdnideaddji" "sávzadoalli" "sealgeeadni" "sester" "silbarávdi" "snihkkár" "soalddát" "sosionoma" "spábbačiekčanhárjeheaddji" "spábbačiekči" "stipendiáhta" "studeanta" "sundi" "suohkanbáhppa" "suohkan#plánejeaddji"
"šibitdoalli" "taksivuoddji" "telefonvástideaddji" "tuollár" "turistaofelaš" "uksafákta" "universitehtalektor"
"váldodoaimmaheaddji" "viessohoiddár" "vuoktačuohppi" ;

SET PROFESSION = PROFESSION1 - Prop ;

LIST OFFICE = "áirras" "ámtamánni" "bajitbáhppa" "boazodoallohoavda" (".*čálli"r) "dekána" "farao" "fylkkaráđđi" "gonagas" "leansmánni" "minister" "ministtar" (".*ministtar"r) (".*minister"r) "nissonáirras" "politihkar" "politihkkár" "prinseassa" "ruvdnaprinseassa" "ságadoalli" "Sámedemokráhtta" "sámedikkeáirras" "Sámediggepresideanta" "sámediggepresideanta" "sátnejođiheaddji" "stivralahttu" "ovdaolmmoš" "presideanta" "stáhtaráđđi" "várrepresideanta" ;

SET TITLE = REAL-TITLE OR TITLE-LIKE OR PROFESSION OR OFFICE ;

LIST HUM-FUNCTION =  "Bergen-soalddát" "buohcci" "divššohas" "kandidáhtta" "kandidáhta" "nomáda" "oahppanolmmoš" "oktavuođaolmmoš" "olbmobárdni" "pasieanta" "ráđđealmmái" "riika#vuloš" "sámedemokráhtta" "skohtervuoddji" "soahteolmmái" "stáhtaboargár" "suttolaš" "turista" "virge#guoibmi" "virgeolmmoš" "vuorasolmmoš" ;
 #XXX bad name
 # Don´t add veahkki + Ess.

LIST HUM-ATTR =  "bassi" "divššohas" "eananeaiggát" "eaiggát" "eatnigielhálli" "fástaássi" "gávttehas" "informánta" "lestadiánalaš" "sámegielat" "suttolaš" ;
    # adjectival

LIST HUM-VERB = "ássi" "boaris" "bálvaleaddji" "bálvalanguoibmi" "boazoeaiggát" "dállodoalli" "eananeaiggát" "geavaheaddji" "gili#ássi" "muitaleaddji" "ovttasássi" "riikkaássi" "vuotnaássi" "vuostebelle" "vuostebealli" ;
    # vearbal

LIST NATIONAL-IDENTITY-NOT-SAPMI = "badjesápmelaš" "dáčča" "dánskalaš" "dáža" "duiskalaš" "Enodatsápmelaš" "Guovdageainsápmelaš" "guovdageaidnolaš" "hollándalaš" "indiána" "israellaš" "judálaš" "juvddálaš" "julevsápmelaš" "kvena" "lullisápmelaš" "maya-#indiána" "njávdánsápmelaš" "nuortalaš" "oarjesápmelaš" "olgoriikalaš" "palestinalaš" "romalaš" "romer" "ruotasuopmelaš" "ruošša-#sápmelaš" "samarialaš" "siseatnansápmelaš" "Suonjilsápmelaš" "Várjjatsápmelaš" "vuotnasápmelaš" ;

     # Also regional identity
 # Duomma:lea

LIST SUOPMA = "Davvi-Ruoŧŧa" "Davvi-Suopma" "Lulli-Ruoŧŧa" "Lulli-Suopma" "Mátta-Ruoŧŧa" "Mátta-Suopma" "Oarje-Ruoŧŧa" "Oarje-Suopma" "ruošša" "Ruošša" "ruoŧŧa" "Ruoŧŧa" "suopma" "Suopma" ;

# This set is used in Gen/Acc-rules.

LIST SAPMI = "davvisápmi" "julevsápmi" "lullisápmi" "máttasápmi" "mearrasápmi" "nuortasápmi" "oarjesápmi" "sápmi" "Sápmi" ;

# This set is used in Gen/Acc-rules.
 # Duomma:lea

SET NATIONAL-IDENTITY = NATIONAL-IDENTITY-NOT-SAPMI OR SAPMI ;

SET HUMAN = HUMAN-INDIVIDUAL OR HUMAN-GROUP OR PROFESSION OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR OFFICE  ;

SET NOT-HUMAN = N - HUMAN ;

#!! * Sets for animals

LIST GEN-ANIMAL = "ábegáhttu" "ásen" "beana" "bohkká" "bussá" "čeavrris" "ciiku" "čivga" "dámmá" "ealit" "fuođđu" "gáhttu" "gáica" "gálbi" "gihcci" "guigu" "gussa" "heasta" "horti" "láppis" "loddi" "máhtu" "njiŋŋálas" "ore" "sávza" "sávzabeana" "smávvafuođđu" "spiinnečivga" "spiidni" "vársá" "vársi" "vielppis" "vierca" "vuoksá" "vuoncávaris" "vuonccis" ;

LIST PREDATOR = "gumpe" "guovža" "bierdna" "geatki" "albbas" ;


LIST HUNT-ANIMAL = "ealga" "rievssat" ;

LIST BOAZU = "áldu" "boazu" "čearpmat" "čora" "eallu" "heargi" "miessi" "sarvvis" "vierroboazu" "vuonjal" ;

LIST FISH = "bálddis" "čuska" "dápmot" "diddi" "diksu" "dorske#guolli" "dorski" "duovvi" "gilot" "guolli" "hávga" "juksu" "luosjuolgi" "luossa" "luossaguolli" "luossanálli" "muortu" "sallit" "sáidi" "sáivaguolli" "šákša#dorski" "stábbasáidi" "stáinnir" "stuorasáidi" "rutnot" "veajet" ;

LIST BIRD = "ákta" "báiski" "bovttáš" "cagan" "čearret" "gádde#loddi" "gáiru" "mearaloddi" "skávli" "skuolfi" "šuoršu" "albatrossa" "alitbađuš" "alitgaccet" "alitoaigaccet" "bakku" "bastesuorsi" "bađošgaccet" "beahcebáhcatloddi" "beahceloddi" "bealdocihci" "bealdo#garjá" "bealdo#vuorra" "bealgeloddi" "beallje#loađgu" "bealljerásttis" "bearalskuolfi" "beasterages" "beibboš" "beibbošcihci" "beibbošcizáš" "belddoš#vizar" "beškkoš" "beštor" "bieggafálli" "biehkan" "bigán" "bihcabasčáihni" "bihcebasgáski" "bihcebasčáihni" "bissehávka" "bižus" "boaimmáš" "boallonjukča" "boađđocivkkán" "borjjastit" "borsenjunni" "bovttáš" "bulddogas" "bumbá" "buoidegaccet" "buokčat" "buokči" "bupmálaš" "bursanjunni" "bussáskuolfi" "buvvedat" "báiski" "bákteduvvá" "bárbmofálli" "bávgoš" "cagan" "cagán" "cielláš" "cihceskuolfi" "cihrrelastavizar" "civkaleivvoš" "cizášfálli" "cizopaš" "cizot" "davvelastavizar" "davveloddi" "davvevizar" "deaggačáihni" "deagge#báiski" "deaggecihci" "dearbmerásttis" "derpmoš" "dieldá" "dielkoduvvá" "dihtti" "diksobáhppa" "dirre#lihká" "doadjosuorsi" "dokta" "dovtta" "duoršu" "duottarbižus" "duottarcivkaleivvoš" "duvvá" "duvvárásttis" "dálvebeštor" "dálveruoivil" "dálvi" "dážaboaimmáš" "dážasuorsi" "fasána" "fiehta" "fiervá#vierru" "fierváviroš" "fiskesbeštor" "fiskescihci" "fiskescižáš" "fiskesgaccet" "fiskesjievžžan" "fiskesnjungáhkkor" "fiskesvizar" "fiŋka" "gaccepaš" "gaccet" "gahperbáiski" "gahpervizar" "garjá" "gassa#njunni" "gazzaloddi" "gazzebiehtár" "geađgejorgu" "geađgerásttis" "giega#biigá" "giehka" "giehpa#vuonccis" "giellavealgu" "gierdo#duvvá" "gierdorásttis" "gierdosuorsi" "gierdo#čuonjá" "gierra" "giljobaš" "gilljobaš" "gilvabeštor" "girjebáhcatloddi" "girjelivkkár" "giron" "goahcce#loddi" "goahppil" "goahppil#gálli" "goahtegoljaš" "goalsi" "goalsoruoššehas" "goalšovizar" "goarga" "goargaloddi" "goargŋohaŋŋá" "goaski" "goaskin" "gollaš" "gollebeibboš" "gollecihci" "golle#gahpir" "golli" "golláš" "gorges#hávda" "gorgeslihkká" "govuskuolfi" "gođa#loddi" "gožu#vuoražas" "guoikgáhrit" "guoikgárri" "guoksa" "guolbba#vierru" "guolbbaviroš" "guolbba#čoavžžu" "guollefálli" "guollečearret" "guorga" "guossabáhcatloddi" "guossagaccet" "guossaruossanjunni" "guossavizar" "guovssat" "guovssoloddi" "gussagoalsi" "guškil#vierru" "guškil#viroš" "guškkástat" "gáddebuvvet" "gáddeskirvi" "gádde#vierru" "gáddeviroš" "gáhkkor" "gáiru" "gállat" "gálli" "gállit" "gáranas" "gáranas#loddi" "gárggogoalsi" "gárgo#goalsi" "gávlačuonjá" "hálkaloddi" "haŋŋá" "heartavizar" "hoaššacihci" "hurrat" "hurri" "hurri#goahppil" "hurri#čukčá" "háigir" "háigir#loddi" "hálka" "hálkagonagas" "hálka#čielkkis" "háskil" "hávda" "idja#skuolfi" "jeaggefálli" "jeaggemeahkástat" "jeagge#čoavžžu" "jeaggehávut" "kanárialoddi" "keaisár#pingviidna" "jiekŋačielkkis" "jiesmi" "jievjaskuolfi" "jievželoddi" "jávresopmir" "leahttofálli" "leahttospálfu" "leaibeloddi" "leivvoš" "lidnu" "liidn-elláš" "liin-elláš" "litnut" "livkkár" "loađgu" "loddi" "loddegonagas" "loddejievži" "luhkka#loddi" "láfol" "láhtospálfu" "látteguoksa" "látteguovssat" "láttovuonccis" "láŋgá" "meahkástat" "mearragoaskin" "mearragáiru" "mearra#čuonji" "mearračuonjá" "miellespálfu" "miesttacihci" "miesttarásttis" "miesttavizar" "miesttašurrevizar" "moldecizáš" "muhkkecizáš" "muogir" "muorracivkkán" "muorragakcu" "mákreallačearret" "niehttegarjá" "niitocivkkán" "njukča" "njurggu" "njurgut" "noaideloddi" "nuortacihci" "nuortalastavizar" "nuorti" "náhkke#soadjá" "oanehisnjunčuonjá" "paradiisa#loddi" "pelikanloddi" "pingviidna" "ribeskáiti" "riddocivkaleivvoš" "riddo#vierru" "riddoviroš" "rievssat" "rievssatcižáš" "rievssatfálli" "roavgogonagas" "rohtogaccet" "rohtovizar" "rohtočoavžu" "rohtočoavžžu" "rukses#bađoš" "ruksesbeibboš" "ruksescivkaleivvoš" "ruksesfiehta" "ruksesguškil" "ruksesjuolčoavžžu" "ruksesruivil" "ruksesruoivil" "ruksesrásttis" "rukses#soadjá" "ruksessuorrehávut" "ruoivil" "ruonábeibboš" "ruonácihci" "ruonácizáš" "ruonájuolčoavžžu" "ruoná#čáihni" "ruossalas#njunni" "ruovddagas" "ruovdecihci" "ruovdecizáš" "ruođa" "ruoššaboaimmáš" "ruoššaskire" "ruošša#skirri" "ruoššaállat" "ránescihci" "ránescizáš" "ránesháigir" "ráneslivkkár" "ránesrásttis" "ránesskuolfi" "ránes#skávli" "ránessnárttal" "ránesčuonjá" "ránesčáihni" "rásttis" "rávgoš" "salletgáiru" "sallitgáiru" "seallecižáš" "siesse#mohkki" "sieđgacivkkán" "sildegáiru" "silkebuokča" "sitronbeštor" "skierru" "skilkeskálka" "skire" "skirri" "skiŋgegaccet" "skoarra" "skuolfi" "skutnjaloddi" "skáhpeloddi" "skáiti" "skárfa" "skárfa#čielkkis" "skávli" "snárttal" "soadja#njálla" "sopmir" "spoađđo#duoršu" "spálfu" "stoarbmaloddi" "stoarbmaspálfu" "stohkkegaccet" "stohkkerievdu" "stohkkespálfu" "stohpoháigir" "stohpospálfu" "struhcca" "struhccaloddi" "stuorrafiehta" "stuorragiljobaš" "stuorraguškil" "stuorraguškkástat" "stuorrameahkástat" "stuorraskáiti" "stuorraskávli" "stuorravuodja" "stuorravuojaš" "stuorračarkkus" "stuorračoavžu" "stuorračoavžžu" "stuorračuonjá" "stálobeibboš" "stárra" "suoidnesuorsi" "suoidnečiksa" "suolaskáiti" "suorrespálfu" "suorsi" "suotnjoliráš" "suotnjočoavžžu" "suovkacihci" "suvlá" "sáhcomeahkástat" "sáhpánboaimmáš" "sáhpánskuolfi" "sáiva#hávda" "sávjahávda" "sávza#loddi" "toardnagarjá" "toardnagájon" "uhcabuokča" "uhcabuvvedat" "uhca#báiski" "uhcaciellá" "uhcacihci" "uhcameahkástat" "uhca#njukča" "uhca#saddi" "uhca#vierru" "uhcaviroš" "uhca#čoavžžu" "uhca#čáihni" "ummolcizáš" "upmolcizáš" "unduláhtta" "uvlohávut" "varra#loddi" "veastareaddji" "vigláčoavžžu" "vilgesnierčuonjá" "vilges#soadja#skávli" "vilgesummolcizáš" "vilges#vierru" "vilges#viroš" "vilges#čoavžu" "vilges#čoavžžu" "vilgessealčáihni" "vintán" "vuoncáčivga" "vuoncávaris" "vizardit" "vuodja" "vuojaš" "vuoktafiehta" "vuoktagaccet" "vuoktagoalsi" "vuoktaláfol" "vuoktaskárfa" "vuonccis" "vuonccisloddi" "vuoražas" "vuorččis" "vuovdaduvvá" "vuovdecivkaleivvoš" "vuovdegaccet" "vuovde#loddi" "vuovdemeahkástat" "vuovderásttis" "vuovdevizar" "vuovde#vuorččis" "vuovde#čoavžu" "vuovdečoavžžu" "ábirievssat" "áhpedokta" "áhpedovtta" "áhpe#háskil" "áhpehávda" "áhpelihkká" "áhpespálfu" "ákta" "állat" "čarkkus" "čearret" "čiekčá" "čielkkis" "čierggesrásttis" "čiksa" "čoađgi" "čukčá" "čuohtegielas" "čuohtegiella" "čuonji" "čuonjá" "čuonjáfálli" "čáhcebávgoš" "čáhcevuonccis" "čáhppes#bađoš#guškil" "čáhppesbuokča" "čáhppessealbeštor" "čáhppesgaccet" "čáhppesrásttis" "čáhppesčoavji" "čáhppes#čoavžu" "čáhppesčoavžžu" "čáihnecizáš" "čáihnegaccet" "čáihni" "šilljocihci" ;

#LIST ANIMAL-PLANT =
    # Plants that are rather animals (lingustically)

SET ANIMAL = GEN-ANIMAL OR HUNT-ANIMAL OR BOAZU OR FISH OR PREDATOR OR BIRD ;


SET ANIMATE = HUMAN OR ANIMAL ;


#!! * Sets for things

LIST TOOL = "áibmi" "ákšu" "bargobierggas" "baste" "bastevuogga" "báddi" "báddenrusttet" "beana" "bieggamillorusttet" "bierggas" "bissu" (".*bivddus"r)  "bivttas" "boazonjuovvan#niibi" "bohtal" "borramuš" "buiku" "buođđu"
"čáhci" "čála" "čuoibmi" "datneárpu" "dáidda" "dálkkas" "dihtor" "dihtormašiidna" "doarta" "dolgevuogga" "dolla" "dorte" "duodji" "duorgu" "ettetviidni" "fierbmi"
"gáddenuohtti" "gáidánuohtti" "gáffal" "gazirdanniibi" "giehta" "giehtamihttu" "giehtaváđđu" "gievdni" "giikan" "goallunstoahku" "gohppu" "golgadat" "golle#bánno" "gurotgiehta"
"hárpa" "HV-#bissu" "jávre#vuogga" "joddu" "joddu-#buođđu" "kohppa" "kruvdnu"
"láhkki" "láibi" "liidna" "liidnesihkaldat" "luovosbeana" "luossa#vuogga" "luovos#fierbmi" "lyra"
"mašiidna" "miehkki" "mihttu" "millo" "millu" "mobiltelefovdna" "mohtorsahá" "muorječoaggi" "muorravuogga" "nállu" "niibi" "njuovvan#niibi" "oar-#juolgi"
"oaggunstággu" "oaggunstávrá" "olgešgiehta" "olju" "roahkkefierbmi" "ruhta" "ruvdnu"
"sahá" "sáiti" "sálti" "seaila" "siibma" "sihkaldat" "sirpe" "skárbban" "skierat" "snáldu" "soabbi" "soavvelvuogga" "spánnja" "speallanmašiidna" "spihkár" "stággu" "stávrá" "stivrran" "suohpan" "suorbma"
"váđđu" "veahkkemohtor" "veaikebaste" "vealgi" "viidni" "vuogga" "vuoiddas" "vuoigŋan#mašiidna" ;

   # Those could be further subdivided into: kitchen tools, working tools, hunting tools, fishing tools, writing tools...

#LIST FISHING-TOOL = "buođđu" "fierbmi" "gádde#nuohtti" "giehta#váđđu" "goallun#stoahku" "joddu" "joddu-#buođđu" "liidna" "luovos#fierbmi" "vuogga";

#LIST HUNTING-TOOL = "bissu" "HV-#bissu" "njuolla";

#LIST KITCHEN-TOOL = "baste" "gealva" "millo" "bánno";

#LIST WORKING-TOOL = "ákšu" "goaivu" "mohtor#sahá" "nibi" "sirpe" ;

#LIST COMMUNICATION-TOOL = "mobil#telefovdna" ;

#LIST MACHINE = "bieggamillo#rusttet" "mašiidna" ;

#LIST EATING-TOOL = "gáffal" ;

#LIST MUSIC-INSTRUMENT = "hárpa" "lyra" ;

LIST WRITING-TOOL = "bliánta" "bleahkka" "dihtor" "krihtta" "liánta" "peanna";

#LIST HANDICRAFT-TOOL = "áibmi" "datne#árpu" "nálu" "suorbma#goahti" "vikšamuorra" ;

# preliminary name for the set:
LIST ABSTR-T =  "bargobearráigeahčču" "bargodearvvašvuođa#bálvalus" "bearráigeahčču" "cummá" "dánsa" "dánsu" "dearvvašvuođageahčču" "doarjja" "eallin" "fápmu" "gásta" "geahčču" "gullu" "heagga" "jahkebargu" "láhkavuođđudus" "mearka" "oahppu" "oaidninvuohki" "preassadoarjja" "prošeakta" "ruhtadoarjja" "sisabahkken" "soabadanproseassa" "sosialiserenproseassa" "suvli" "varra" "vuohki" ;

LIST SINGLE-SOUND = "fonema" "jietna" "jietnadat" "loahppa#jietnadat" "vokála" ;

    # Can be part of a word
LIST SOUND-SYSTEM = "básonšuokŋa" "čotta#juoigan" "gohččun" "lávlunvuohki" "luohti" "musihkka" "riedja" "rohkos" ;

LIST LANGUAGE =  "jiddisch" (".*giella"r) (".*suopman"r) ;

SET SOUND = SINGLE-SOUND OR SOUND-SYSTEM OR LANGUAGE ;

LIST SATNI = "adjektiiva" "advearba" "bárahisstávvalvearba" "boazosápmelaštearbma" "doaba" "kásushápmi" "kontráktavearba" "laktapartihkkal" "laktapartihkal" "namma" "partihkal" "partihkkal" "postposišuvdna" "preposišuvdna" (".*tearbma"r) "sátni" "sátnemátta" "substantiiva" "tearbma" "vearba" (".*sátni"r) (".*namma"r) ;

    ## tool for verbal activity

LIST WORD-PARTS = "akseantamearka" "bustávva" "b-bustávva" "c-bustávva" "goallossáhcu" "gravismearka" "stávval" ;

SET ABSTR-TOOL = SOUND OR ABSTR-T ;

# Premilinary dividing of the set as a help for dimambiguating "gullat" vs. "gullát".

#Those are typically comitative (instead of locative) if they go together with verbs from the LIST HUMAN-AGENT-V and probably also from INTELLECTUAL-ACTIVITY-V. The counterpart for locative is until now SITUATION, we might have to consider making a new list ABSTR-TOOL comprising SITUATION and other elements.

#!! * Sets for qualities
# - - - - -

LIST COLOUR = "alit" "čáhppat" "fiskat" "fioleahtta" "ránis" "ruoná" "ruoksat" "runta" "ruškat" "sáhppat" "vielgat" "violeahtta";

#!! * Sets for things, not necessarily tools
# - - - - - - - - - - - - - - - -

LIST MATERIAL = "ávnnas" "báhkki" (".*náhkki"r) "gámas" "gusanáhkki" "materiála" "muorra" "náhkki" "ullu" "sávzzanáhkki" "silba" "stálli" ;

LIST PLANT-PART = "lasta" "ruohtas" "upmol" ;

    # of plants etc.

LIST SHOE = "čázet" "gállot" "gálssot" "gáma" (".*gáma"r) "goikket" "gummestevvel" (".*guohpá"r) (".*loabát"r) "njuorjjot" "nuhtat" "nuvttat" "nuvttot" "sabet" "sáppat" "sisttet" "skárri" "skuovva" "stevvel" "suohkku" (".*suohkku"r) ;

    # anything that will go on feet. IV-verbs like "vázzit" can take these as objekt.

LIST BEDCLOTHES = "beaska" "gávnnit" "oađđádagat" "rátnu" "roavgu" "seaŋga";

    # anything that can be used as bedclothing. IV-verbs like "oađđit" can take these as objekt.

LIST CLOTHES = "avvi" "báidi" (".*báidi"r) "beaska" (".*bealjus"r) "bivttas" "boagán" (".*boagán"r)  (".*buksa"r) (".*buser"r) (".*busir"r) (".*bivttas"r) "čalbmeláset" (".*čuhppa"r) "čuvla" "dorka" "fáhcca" (".*gahpir"r) "gákti" "gárvu" (".*gákti"r) "gistta" (".*hámet"r) (".*liidni"r) (".*luhkka"r) "rahpot" (".*readdju"r) (".*reddjo"r) "T-báidi" (".*váhcca"r) (".*váhttu"r) (".*vuolpu"r);


LIST FOOD = (".*biebmu"r) (".*biergu"r) (".*borramuš"r)  (".*muorji"r) "fuođar" (".*guolli"r) "gordni" (".*mális"r) (".*jáffu"r) "jokŋa"  (".*láibi"r) "mális" "málli" "márfi" "niesti" "sarrit" "spelle" (".*gáhkku"r) ;


    # potential tool for political activity etc., potential place for other food, insects etc.

LIST LIQUID = "bohccomielki" "bruvsa" "buolleviidni" "buolleviidna" "buollin#viidna" "buollánviidna" "buollánviidni" "čáhci" "čáhppesmuorjesákta" "čielgamielki" "deadja" "duolvačáhci" "gáffe" "gáiccamielki" "gearra" "girkoviidna" "girkoviidni" "goden" "gusamielki" "jugus" "juhkamuš" "kirkoviidna" "kirkoviidni" "mielki" "rihppaviidna" "rihppaviidni" "ruksesviidna" "ruksesviidni" "sarritsákta" "sáivačáhci" "sákta" "sávzzamielki" "suvramielki" "viidna" "viidni" "vilgesviidna" "vilgesviidni" "vuola" "vuolla" ;

LIST PLANT = "alášbađverássi" "alitdiehpperássi" "ávža" "báhpačehporaš" "beahci" "bastilislátnjá" "beahceguolbba" "beatnatnjuovččarássi" "biehtárguhkká" "biehtárrássi" "bievlaguolbba" "boallorássi" "biello#daŋas" "biellorássi" "cizáš#vuolpu" "duopma" "duottarbusságeahpil" "fiikkonmuorra" "fiskesrássi" "gálla" "gámasuoidni" "gáiski" "gáskálas" "gieddeguolbba" "gieddemáđir" "gollerássi" "guossa" "heasttagazzarássi" "horbmá" "jámešbátni" "jeagil" "juopmu" "lastamuorra" "lageš" "leaibi" "miestta" "muotkenjárgaguolbba" "niittoguolbba" "njálanjuovčča" "oaivejeagil" "oaksi" "rásseguolbba" "reatká" "sállja" "sieđga" "skáhpi" "skierri" "soahki" "suhpi" "uvlo#rássi" "vándádivga" "vilgesmáđir" "vuodjarássi" "vuovdeguolbba" ;

    # Plants could be actors in fairytales (Alice)


LIST FURNITURE = "áltár" "beavdi" "hildu" "ildu" "seaŋga" "skáhppe" "soffá" "stuollu" ;

#!! * Sets for things such that people can be inside them:
LIST SPACIOUS-VEHICLE = "áhpeguolástanfanas" "biila" "boastabiila" "borjjasfanas" "borjjasvanca" "buohccebiila" "buohccibiila" "bussa" "busse" "čáhce#girdi" "dámpa" "drošša" "drošše" "fanas" "fievru" "gádjunhelikopter" "gálvobiila" "gávpotbussa" "gávpotbusse" "girjebusse" "geaksu" "girdi" "guollefanas" "guorbmebiila" "helikopter" "johkafanas" "láivi" "minibussa" "minibusse" "mohtorfanas" "mohtorfievru" "mohtorvanca" "njealjit" "njealljejuvllat" "olgoriikkafeastonuohttefanas" "ruvttobiila" "skuvla#biila" "skuvlabusse" "suhkkivanca" "tákse" "táksi" "toga" "traktor" "tráktor" "vanas" "vanca";

#!! * Sets for things such that people cannot be inside them:
LIST SMALL-VEHICLE = "ásen" "beana" "fievrošibit" "heargi" "heasta" "mánáid#sihkkel" "meahccesihkkel" "mohtorgielká" "mohtorsihkkel" "mopeda" "nieidamáná#sihkkel" "njealljejuvllat" "sihkkel" "skohter" "skuter" "sykkel" "njealjejuvlageassi" "čiekčansihkkel" ;

SET VEHICLE = SPACIOUS-VEHICLE OR SMALL-VEHICLE ;

LIST TEXT = (".*listu"r) (".*čálus"r) (".*girji"r) (".*-girji"r) (".*gažaldat"r) (".*aviisa"r) (".*áviisa"r) (".*bábir"r) (".*logahallan"r)
"bargobihttá" (".*prográmma"r) (".*lávlla"r) (".*bláđđi"r) (".*mearrádus"r) "bušeahttapoasta" (".*čála"r) (".*cealkámuš"r) "čuovus"
(".*teaksta"r) (".*dieđáhus"r) (".*journála"r) "dokumeanta" (".*girje"r)
"e-poasta" (".*eksamen"r) "eksamenbihttá"
"fáddá#siidu" "fáksa" "gáldu" "gullugovva" "journála#čállin" "journáladiehtu"
"kárta" "lasta" (".*láhka"r) "láđđi" "leaksu" "mátkemuitalus" "mielddus"
"neahttasiidu" "novealla" (".*raporta"r) (".*reive"r) "riektedieđa" "romána" "ruovttubargu" "ruovttusiidu" (".*konvenšuvdna"r)  "sárdni" "sitáhtta" "statistihkka" "telegrámma" "váidda" ;

  # A potential place (locative?) for sth. written.
  # Maybe one should differentiate between written and spoken texts

LIST ABSTR-TEXT = (".*plána"r) (".*mearrádus"r) (".*evttohus"r)  (".*njuolggadus"r) (".*ortnet"r) (".*árvalus"r) (".*cealkámuš"r) "cuvccas" "doaibmabidju" "eaktu" "eanačujuhus" "gažaldat" "gáhtteneaktu" "gáldokoda" "geasku" "giellagažaldat"  "jearaldat" "lappekodisilla" "láhkaásahus" "luođaleaktu" (".*mearrádus"r) "máinnas" "muitalus" "resolušuvdna" "sáme#kodisilla" (".*soahpamuš"r) ;

  # A potential place (locative?) for sth. written, spoken or abstract activity

#!! * Part-whole sets for human

LIST BODY = "alimat" "aski" "ámadadju" "bahta" "bahtačalbmi" "bahtaráigi" "bavssa" "bállosuotna" "bátni" "beađbelađas" "bealgi" "beallji" "bierggas" "biežadákti" "billu" "buđđá" "bullječalbmi" "čalbmečiehka" "čalbmedeahkki" "čalbmegolláš" "čalbmegulbmi" "čalbmeguolga" "čalbmeluopma" "čalbmi" "čatnangođđu" "čeabet" "čelččen" "čeŋkkeš" "čibbeskálžu" "čibbi" "čižžebátni" "čižži" "čielgeađa" "čielgedákti" "čielgi" "čoalli" "čoamohas" "čoarbbealdeahkki" "čoarbbeallađas" "čoarbbealli" "čoavji" "čorbma" "čorbmaoassi" "čorbmaváimmus" "čorbma#vuođđu" "čotta" "čuožžamasdeahkki" "čurti" "čuvdi" "dábba" "dáđvi" "dáktegođus" "dákti" "dávdi" "deahkkegođus" "deahkki" "erttet" "erttetdeahkki" "gahpárus" "gahperus" "ganjalčalbmi" "gardnjil" "gaskasuorbma" "gassačoalli" "gassaváhkká" "gáddu" "gáibi" "gállu" "gánis" "gátnis" "geađđi" "geahpis" "giegir" "giehta" "giehtadeahkki" "giehtagávva" "giehtaláhpi" "giehtaramas" "giehtaruohtas" "giehtasealgi" "giehtaváibmu" "giehtaváimmus" "gilga" "gipsajuolgi" "goapmirváimmus" "goaŧŧočotta" "goaŧŧu" "goikedákti" "gorut" "gullu" "guoggomas" "guoggomasráksá" "gurotgiehta" "hárdu" "heaggaroasmmoheapmi" "jarri" "juolgebealgi" "juolgečelččen" "juolgedeahkki" "juolgegávva" "juolgelađas" "juolgeláhpi" "juolgemátta" "juolgesuorbma" "juolgevuođđu" "juolgi" "lađas" "láhpelađas" "manin" "maŋŋebuoidi" "mánágoahtečotta" "mánágoahti" "mielganjunni" "monimuš" "monin" "monnejođas" "monneráksá" "morčesuotna" "morči" "muohtu" "náhpi" "nealgečoavji" "niehkki" "nierra" "niskegohpi" "niski" "njálbmi" "njiehcehas" "njunni" "njuovčča" "oaiveassi" "oaivečohkka" "oaivi" "oalgedeahkki" "oalgenorasdeahkki" "oalgenorasváhkká" "oalgi" "oalul" "obbalággá" "orbbeš" "raddi" "rápma" "rumaš" "rumaš#golggus" "rumašlahttu" "rumašnjalbi" "sáhppesuvri" "rumašlahttu" "rumašnjalbi" "salla" "sáhppesuvri" "siidu" "siskkitbeallji" "sotkenoalli" "spirralat" "suolbmudanoalli" "suorbma" "suorbmageahči" "šušmi" "váhkkádeahkki" "váibmu" "vuohččecuozza" "vuoivvas" "vuokta" "vuolit" "vuorggu";

LIST WEATHER-ENTITY = "balva" "arvi" "muohta" ;


#!! * Sets for places
LIST BUILDING = "áiti" "ámmátskuvla" "biebmorámbuvri" "barta" "badjedállu" "boasta" "buohcciidsiida" "buohcceviessu" "buvda" "buvri"
"dállu" "dálve#skuvla" "dálveviessu" "eanandoalloallaskuvla" "fáŋgaviessu" "fiervábuvri"
"geaidnogievkkan" "geardeviessu" "geasse#buvri" "goahti" "gávpi" "gieldda#skuvla" "girku" "hotealla"
"internáhttaskuvla" "inšenevraallaskuvla" "kaféa" "krámbuvda" "kulturskuvla" "láđđu" "láhtu" "lávvu" "lonuhusgávpi"
"mánáidskuvla" "mearra#buvri" "mearraskuvla" "musihkka#allaskuvla" "návet" "návetgoahti" "návsti" "návstu" "nuoraidskuvla"
"oktasašbarta" "orrunviessu" "poasta" "priváhtaskuvla" "ráidoviessu" "rámbuvri" "rámbuvra" "rásseláđđu" "riika#skuvla" "riika#buohcceviessu"
"sámeskuvla" "skuvla" "stálla" "stálle" "stállja" "stohpu" "synagoga" "šloahtta"
"tempel" "tuolloviessu" "universiteahtta" "viessu" "visti";

LIST GENERAL-PLACE = "albmi" "almmiravda" "almmiriika" "areála" "atnueana" (".*guovlu"r) "ássanbáiki"
"badjegeardi" "badjeeana" "báikegoddi" "báiki" "báktehárji" "bealdu" "biegga#báiki" "biras" "boazoguohtoneana" "buođđosadji"
"čakčaeana" "čakčaeana" "čakča#báiki" "čakčaorohat" "čiegus" "čoahkkebáiki"
"dálvebáiki" "dálveeana" "dálveguohtoneana" "dálveorohat" "davviriika" "davvisiida" "dánssa#báiki" "diggebáiki" "dimbbar#čuohppan#sadji" "dollagáddi" "duiska#eana" "eanandoallo-#eana" "eanandoalloeana" "earre#sadji" "fanasláttasadji" "fágabiras"
"gádde#báiki" "gádde#ravda" "gávpebáiki" "gávpemárkan" "gávpot" "gávpotpoarta" "geaidnoguorra" "geaidnoravda" "geassebáiki" "geasseeana" "geasseguohtoneana" "geasseorohat" "geassesadji" "gilli" "giddagas#báiki" "giđđabáiki" "gieddeeana" "gilvvagárdi" "gilvvohallanbáiki" "girkogárdi" "goahtesadji" "goikeeana" "guoddinseaŋga" "guollebáiki" "guollebivdosadji" "guohtoneana" "guohtuneana" "guottetbáiki" "hámman" "hávdeeana" "helvet" "jeaggeeana" "jeagelguohtuneana" "jienastanbáiki"
"kálka#eana" "kontineanta" "lassieana" "láigoeana" "luome#eana" "luossabáiki" "luonddubiras" "máilbme#čiehka" "máilbmi" "márkan"   "meahcceeana" "meahccegilli" "njáskan#báiki" "njunušsadji" "nuortaeana"
"oahppobáiki" "oaivegállobáiki" "oktasašeana" "oktasašorohat" "olgobáiki" "olgoriika" "orohat" "orrunbáiki" "priváhtaeana"
"rasttidanbáiki" "ránnáriika" "riddo#eana" "riika" "ruoktu" "ruovdemáđidja" "ruovttueana"
"sadji" "sáidebáiki" "sámieana" "sámeeana" "sámevearroeana" "sallju" "siida" "siseana" "skoaltabáiki" "skuvlabiras" (".*šillju"r) "stáhtaeana" "stáhtasadji"
"TV-šearbma" "váhneneana" "várdobáiki" "vearroeana" "viessosadji" "viesttarbađaeana" "virge#báiki" "vuoiŋŋastanbáiki" "vuopme#eana" ;

LIST NATURE-PLACE = "almennet" "ávži" "bákti" "beahcevuovdi" "borri" "buollán#ávži" "coagis" "čázádat" "čolpi" "darfejeaggi" "dearbmi" "duottarjávri" "duottaroassi" "eatnu"
"fielbmá" "fiervá" "gáddi" "gáisá" "gáisi" "gárggu" "gárgu" "gieddi" "gieva#jeaggi" "gohpi" "gohppi" "gorži" "guolbba" "gurra" "jávri" "jávregáddi" "jeaggi" "jiehkki" "johka" "johka#gaska" "johkagáddi" "johkaleahki" "johkanjálbmi"
"láddu" "láttu" "latnja" "lavdnjejeaggi" "leahki" "lokta" "luohkká" "luokta" "luondu" "luoppal" "luossajohka" "meahccegieddi" "mearra" "mielli" "mohkki" "muotki" "njárga" "njunni" "oaivi" "oalgejohka" "oalgi" "orda" "orrit" "rádja#čázádat"
"reašši" "riddogáddi" "riddu" "riidi" "ruovttugieddi" "soahkevuovdi" "stealli" "várrevielti" "várri" "vearroduottar" "vielti" "vuohppi" "vuotnabahta" "vuotnagáddi" ;

#LIST ELEMENT = "áibmu" "čáhci" "dolla" ;

    ## (probably often place) They are often tools - they are not probably Pl Loc.

LIST SETTLEMENT-PLACE = "ássanguovlu" "duottar" "eana" "eanaguovlu" "gáddi" "geassesiida" "Gennesaretjávregáddi" "gilli" "guovlu" "mátkeguovlu" "meahcci" "mearragáddi" "njárga" "riddu" "sámisiida" "siseana" "siseatnansiida" "sisvuotna" "suolu" (".*jávregáddi"r) "vággi" "vuotna" "vuotnabahta" "vuotnagáddi" "vuotnaguovlu" "vuovdi" ;

SET GEOGRAPHICAL-PLACE = NATURE-PLACE OR SETTLEMENT-PLACE ;

LIST GEN-POLITICAL-PLACE = "gávpot" "gilli" ".*suohkan" "fylka" "orohat" "rádji" "riika" "riikkarádji" "ruovttugávpot" "sámeeana" "Sámeeana" "siseatnangilli" "stáhta" "válgabiire" "vuotnagávpot" ;

  ## can also function as an actor

LIST FYLKA = "Finnmárku" "Finnmarku" "Romsa" "Nordlánda" "Lulli-Trøndelag" "Davvi-Trøndelag"
       "Hedmárku" "Akershus" "Buskerud" "Vestfold" "Østfold" "Aust-Agder" "Vest-Agder"
       "Rogalánda" "Hordalánda" "Opplánda" "Fjordane" "Romsdal" "Telemark" ;

LIST NORGA = "Dánmárku-#Norga" "Danmárku-#Norga" "Davvinorga" "Davvi-Norga" "Gaska-Norga" "Norga" "norga" "norggabealli" "Norga-#Ruoŧŧa" ;

LIST NATION-NOT-NORGA = "Columbia" "Duiska" "Frankriika" "Guatemala" "Israel" "Kanada" "Láhten-Amerihká" "naššuvdna" "riika" "Ruošša" "Ruoŧŧa" "Sápmi" "sápmi" "Skandinávia" "stáhta" "Suopma" "Suopma-#Ruošša" ;


SET NATION = NATION-NOT-NORGA OR NORGA ;

SET POLITICAL-PLACE = NATION OR GEN-POLITICAL-PLACE OR FYLKA OR GIELDA ;

    # Can be an actor


# This set is important for distiguishing "vuodjit" and "vuodjat", so there shouldn`t be words which can be combined with "vuodjat":
LIST CONCRETE-ROUTE = (".*máđidja"r) (".*máđii"r) (".*bálggis"r) (".*luodda"r) (".*geaidnu"r) (".*gáhtta"r) (".*guorra"r) (".*láhttu"r) (".*ráigi"r) "ráidu" "spáiddarráidu" ;
 # and others!

LIST ABSTR-ROUTE = "gaska" "mátki" "miila" "mohkki" "tuvra" ;

SET ROUTE = CONCRETE-ROUTE OR ABSTR-ROUTE ;


LIST WATER = "ája" "áhpi" "álddis" "basseaŋŋa" "čáhci" "cázádat" "Deanučáhcadat" "Deanucázádat" "eatnu" "fielbmá" "jávri" "johka" "luokta" "mearra" "sálteája" "vuodjanálddis" "vuojadanálddis" "vuotna" ;

LIST GOAHTI-PLACE = "árran" "boaššu" "loaidu" "reahpenráigi" "soggi";


LIST ROOM = "bargomárkankantuvra" "bártniidhivsset" "biergodiehtojuohkinkantuvra" "boazodoallokantuvra" "čájáhuslatnja" "ealáhuskantuvra" "eamiálbmotkantuvra" "eanajuohkinkantuvra" "eananvuovdinkantuvra" "eanavuovdinkantuvra" "feaskkir" "fylkkaeanadoallokantuvra" "gámmár" "gámmir" "garáša" "gievkkan" "guovllukantuvra" "hivsset" "informašuvdnakantuvra" "kantuvra" "klássalatnja" "leansmánnekantuvra" "lášmmohallansále" "lášmmohallansálla" "livnnetkantuvra" "lokta" "luohkkálatnja" "molssodanlatnja" "nieiddaidhivsset" "nohkkanlatnja" "oađđenlatnja" "oađđinlatnja" "oahpaheaddjilatnja" "sallju" "skuvlalatnja" "sosiálkantuvra" "stohpu" "studio" "šillju" ;

SET PLACE = GEOGRAPHICAL-PLACE OR POLITICAL-PLACE OR GENERAL-PLACE OR ROUTE OR BUILDING OR ROOM OR GOAHTI-PLACE OR Sem/Plc ;


LIST MAIN-ORGANIZATION = (Prop Sem/Org) "alimusriekti" "ámtadiggi" (".*ásahus"r) "bargokantuvra" "báhpaskuvla" "bálvalandoaibma" "bearráigeahččanorgána" (".*bellodat"r) "buohccestohpu" "buohcceviessu" "buohcciidsiida" "dearvvašvuođafitnodat" (".*departemeanta"r) "dearvvašvuođadutkanguovddáš" "diggi" (".*direktoráhta"r) "duopmostuollu"
"eananjuohkindiggi" "eiseváldi" "Finnmárkokommišuvdna" "firbmá" "fitnodat" "fylka" "fylkkagielda"
"geahččo#doaibma" "geahččoeiseváldi" "gelbbolašvuođaguovddáš" "giddagas" "gieldda#diggi" "giellaguovddáš" "giellakantuvra" "giellaráđđi" "girjerájus" "girku" "girkoráđđi" "gozihankomitea" "guovddášeiseváldi" "guovllubuohcceviessu"
"hálddašanorgána" "hállehus" "dearvvašvuođaguovddáš" "fylkkadiggi" "institušuvdna" "justiskomitea" "klinihkka" "komitea" "komišuvdna" (".*lávdegoddi"r)  (".*listu"r)  "mánáidgárdi" "media" "meieriija" "militeara" "musea"
"našunála#diggi" "oahpahusdoaibma" "oahpahuskantuvra" (".*organisašuvdna"r) "orgána" "parlameanta" "psykiatriijabuohcceviessu" "preassa" "ráđđehus" (".*ráđđi"r) "Sámediggi" "sámediggi" "sámeinstitušuvdna" "sámekomitea" "sámemišuvdna" "sámemusea" "sámesiida" "searvegoddi" (".*searvi"r) "servodat" "sierraduopmostuollu" (".*skuvla"r) "skuvlakantuvra" "stáhta" "stuorradiggi" "suodjalankomitea" "suohkan" (".*stivra"r) "synagoga"
"universiteahtta" "universitehta" "universitehtabuohcceviessu"
"dearvvašvuođastašuvdna" "gielda" "riddogielda" "ruovttugielda" "sámegielda" "vuotnagielda"  ;

LIST DEPARTMENT =  "doaibmaguovddáš" "fakultehta" "etáhtta" (".*guovddáš"r)  "hálddahus" "instituhtta" "kantuvra" "giellakantuvra" "mearridanorgána"  (".*ovttadat"r) (".*ossodat"r) ;

SET ORGANIZATION = DEPARTMENT OR MAIN-ORGANIZATION ;

#!! * Sets that can both be buildings/places and represent humans

LIST INSTITUTION =  "geahččoortnet" "guovddáš" "instituhtta" "kulturráđđi" "searvegodderáđđi" ;

# the previous items can both represent abstract processes and humans

LIST MEDIA = "media" "oahpahus#rádio" "radio" "rádio" "televišuvdna" "TV" ;

#SYMB-INSTITUTION = "njuolggadus" "vuoigatvuohta" "láhka" "ortnet" "čoahkkinortnet" ;

    # Those cannot be buildings, but 'act' , 'say' things for example..

# Two aspects: Place and institution:
# and a couple of other words that do not really have to do with that
# suits "vázzit" + Acc
LIST EDUCATION = (".*skuvla"r) (".*oahppu") "artiumkursa" "badjedássi" "báhpaskuvla" "boazodoallofága" "boazodoallosuorgi"
"dásseárvofága" "duodjekursa" "fága"
"gaskadássi" "giellafága" "giellagursa" "gursa" "joatkkakursa" "joatkkaoahppu" "kursa" "luohkká" "luonddufága" "mánáidskuvla" "musihkkadiibmu" "musihkkasuorgi" "oahppofálaldat" "oahppoinstitušuvdna" "oahppoásahus" "oahppodássi"
"sámegielfága" "sámegiel#gaskafága" "seminára" "skuvla" "stáhtainternáhtta" "stáhtainternáhta" "stuorraskuvla" "universiteahtta" "universitehta" "vuolledássi" ;

# suits to LANGUAGE Gen
LIST LESSON =  "álgogursa" "dáiddaoahpahus" "gaskafága" "gealbu" "gelbbolašvuohta" "giellaovdáneapmi" "fálaldat" "joatkkafáddá" "joatkkagursa" "lávdaoahppofálaldat" "lohkanbájioahppu" "máhttu" "musihkkadiibmu" "oahppa" "oahppofálaldat" "oahpahus" "váldofága" "vuođđofága" ;


LIST OTHER-INDUSTRY = "bálvalusealáhus" "biedjo#bivdu" "bivdu" "boazoealáhus" "boazodoallu" "Deanubivdu" "duodji" "ealáhus" "ealáhusdoaibma" "eanandoallu" "eanadoalloealáhus" "eksportaealáhus" "fuođđobivdu"
"geasse#guollebivdu" "giđđa#guollebivdu" "goddebivdu" "guollebiebmanealáhus" "guollebivdu" "industriija" "johka#luossabivdu" "lassiealáhus" "luossabivdu" "lotnolasealáhus"
"mátkeealáhus" "mátkkoštanealáhus" "njunušealáhus" "mearradoalloealáhus" "riektepolitihkka" "rievssatbivdu" "ruvkedoaibma"
"sáivaguollebivdu" "sáivabivdu" "sosiálapolitihkka" "šákša#dorske#bivdu" "sámeduodji" "sámeealáhus" "smávva#bivdu" "šibitdoallu" "tertiearaealáhus" "vuođđoealáhus" ;

# Not related to certain political verbs
 # Duomma:lea

LIST POLITICAL-INDUSTRY = (".*politihkka"r) ;
 # Related to certain political verbs
 # Duomma:lea

SET INDUSTRY = POLITICAL-INDUSTRY OR OTHER-INDUSTRY ;

LIST RIEKTEDILLI = "bajithearrávuohta" "bálvalusfálaldat" "čearddalašvuohta" "diggegirji" "dilálašvuohta" "ealáhusvuođđu" "eallindiliváttisvuohta" "eanajuohkinsuohkan" "eanadoallobuvttadeapmi" "eanavuovdinkantuvra" "hálddašeapmi" "kultuvra" "matrihkal" "meahcceriggodat" "opmodat" "rádjemearka" "rádji" "rádjá" "resursageavaheapmi" "riektedilli" "riekteáddejupmi" "riddomearraguovlu" "sierraortnet" "stáhtaeana" "suodjalanguovlu" "váldi" "váldofierpmádat" "vearro#njuolggadus" "vuoigatvuođadilálašvuohta" (".*vuoigatvuohta"r) "vuovdeatnu" "vuovde#njuolggadus" "suverenitehta" "kulturdoarjja" ;

# FYLKA in front of the set is usually in Gen.

LIST STAHTA-GEN = "IT-#doaibmaplána" "almennet" "bargiidgiehtagirji" "boazodoallohálddahus" "bálvalus" "dearvvašvuođageahčču" "dearvvašvuođaiskkadeapmi" "dearvvašvuođa#gozáhus" "doaibma" "doaibmaplána" "doarjja" "doarjjavuogádat" "dássi" "eaiggáduššanvuoigatvuohta" "eaiggátgáibádus" "eana" "eiseváldi" "gaskadilleortnetdoarjja" "geahččoortnet" "geatnegasvuohta" "geavat" "gáibádus" "háldu" "joatkkaskuvla" "kulturráđđi" "mearrabodni" "oahppodoaibma" "oaidnu" "oaivil" "orgána" "ovddasteaddji" "peršonalgiehtagirji" "ruhta" "sosiálapolitihkka" "spesialistadearvvašvuođabálvalus" "tariffašiehtadus" "vuostebealli" "áddejupmi" "ámmátolmmoš" "ásahus" ("jávri" Pl) "oassálastin" ("vuovdi" Pl) "áŋgiruššan" ;
 # "stáhta" in front of the set, is usually Gen.

LIST SAMEDIGGI-GEN = "autoritehta" "áirras" "árvokommišuvdnalahttu" "ásaheapmi" "bargu" "bušeahtta" "bušeahttamearrádus" "bušeahttavuogádat" "čielggadeapmi" "čilgehus" "čoahkkin" "čoahkkinortnet" "dievasčoahkkin" "doaibma" "doaimmahat" "ealáhusfoanda" "ekonomiijahálddašeapmi" "ekonomiijastivret" "hálddašanapparáhtta" "jahkedieđáhus" "joavku" "kulturviessobušeahtta" "lahttu" "mearrádus" "njuolggadus" "nuoraidkonferánsa" "oaidnu" "oassálastin"  "ollesáigepolitihkkár" "opposišuvdna" "ovttasbargoaktøra" "politihkkár" "rehketdoallu" "sadji" "ságajođiheaddji" "šiehtadusmálle" "ulbmil" "váldoášši" "váldohálddahus" ;

SET ACTOR = ANIMATE OR INSTITUTION OR ORGANIZATION ;

  # Should EVENT be part of this set?

# To be used in habitative-sentences:
SET HAB-ACTOR = ACTOR OR HUMAN-GROUP OR EDUCATION OR PEOPLE-NUM OR ("gii") OR Pers OR ("eará") OR ("buohkat") OR ("gaikkat") OR ("juohkehaš") OR ("goappašagat") OR ("soames") OR ("oktage") OR Sem/Hum - SAPMI ;

SET HAB-ACTOR-NOT-HUMAN =  INSTITUTION OR ORGANIZATION OR INDUSTRY OR EDUCATION ;


LIST STATE = "bahávuohta" "dearvvasvuohta" "gaskavuohta" "mielladearvvašvuohta" "ráfi" "váivi" "veahkki";


LIST OTHER-ABSTR-ENTITY = "ášši" (".*prošeakta"r) (".*plána"r) "hehttehus" "váttisvuohta" ;

  # can represent a concrete entity

LIST MONEY = "ánsu" "árvu" (".*bálká"r) (".*bušeahtta"r) (".*buhtadus"r) "dienas" "divat" (".*doarjja"r)
(".*gollu"r) "haddi" "juolludeapmi" "juolludus" ("liigudit" Der/NomAct) "máksu"
 "reantu" (".*ruhta"r) "sáhkku" "sisaboahtu" "tienas" "vealgi" "vearru" "vuoitu";

    # Ambiguous with respect to concreteness, either the symbolic amount or the concrete coins and bills

SET ABSTR-ENTITY = MONEY OR STATE OR OTHER-ABSTR-ENTITY ;


LIST GEN-CONCEPT = "anistupmi" "álgu" "áŋgirvuohta" "áigumuš" "árbevierru" "ávžžuhus"
"ballu" "bálvaleapmi" "bálvaluskvaliteahtta" "bargomálle" "boađus" "dárbu" "dássi" "doaibma" "duođaštangeatnegasvuohta" "duogáš" "eaiggátvuohta" "eallinvuohki" "erohus" "evttohus"
"fádda" "friddjavuohta" "fuolalašvuohta" "gaskamearri" "gaskaoapmi" "geargatvuohta" "geatnegasvuohta" "gelbbolašvuohta" "gudnejahttin" "gudni" "guollebivdovuoigatvuohta" "hállu" "hástalus" "isitvuohta"
"konflikta" "kontákta" "kulturduogáš" "kulturmáhttu" "kvaliteahtta" "loahppa" "loatnaetymologiija" "lohpi"
"máhttu" "máŋggabealatvuohta" "miella" "mihttomearri" "molssaeaktu" "muitu" "oamedovdu" "oktavuohta" "olahahttivuohta" "ovdamearka" "ovddasvástádus" "ovttadássásašvuohta" "perspektiiva"
"ráffi" "ráfi" "ráhkisvuohta" "rámma" "riekteprinsihppa" "rolla" "sámegielmáhttu" "sámekultuvra" "sihkkarastinstrategiija" "strategiija" "šiehtadallanfáddá" "symbolasystema"
"ulbmil" "ustitvuohta" "vanhurskkisvuohta" "váibmu" "váikkuhanfápmu" "váikkuhus" "váldosivva" "vásáhusduogáš" "vearrivuohta" "virgelohpi" "vuordámuš" "vuoigatvuohta"
"bivdovuoigatvuohta" "geavahanvuoigatvuohta" "guolástanvuoigatvuohta" "jienastanvuoigatvuohta" "eanageavahanvuoigatvuohta" "oamastanvuoigatvuohta" "mearraguollevuoigatvuohta" "teltenvuoigatvuohta" "sierravuoigatvuohta" "murjenvuoigatvuohta" "láigovuoigatvuohta" "láigohanvuoigatvuohta" "bivdinvuoigatvuohta" "guohtunvuoigatvuohta" "eananoamastanvuoigatvuohta" "eanangeavahanvuoigatvuohta" "vuođđovuoigatvuohta" ;

  # very abstract
  # Adding words to this set, consider if they suit to the SG-WORD-set as well.


LIST EMOTION = "gierisvuohta" "moraš" "ráhkisvuohta" "vašši" ;

LIST STRUCTURE = "álgu" "loahppa" "struktuvra" "maŋŋegeahči" "lasáhus" "loahppageahči" "álgogeahči" "álgo#oassi" "sisdoallu" "veršuvdna" ;

LIST PROPERTY = "burrodat" "fastivuohta" "hedjodat" "allodat" "gassodat" "kvaliteahtta" "sturrodat" "guhkkodat" "govdodat" "hápmi" "ivdni" "fearga" "dovdomearka" "iešvuohta" "viidodat" ;

LIST IDEOLOGY = "buddhisma" "geahččanguovlu" "kommunisma" "oaidninčiehka" "osku" "sosial#darwinisma" ;

SET CONCEPT = GEN-CONCEPT OR STRUCTURE OR PROPERTY OR IDEOLOGY ;


LIST ACTIVITY = "aktivitehta" "báikenammadutkamuš" "boazodoallodutkamuš" "čađaheapmi" "dearvvašvuođadutkan" "eallinbirasdutkamuš" "fuolaheapmi" "geahčastat" "gilidutkamuš" "guorahallan" "heahtebargu" "loatnadutkamuš" (".*lodden"r) "lodden" "mátki" "návetbargu" "sámedutkamuš" "sisafárren" "substráhtadutkamuš" "šiehtadallan" ;

  # semantically comparable to Actio forms + lexicalized Der/NomAct forms.
  # probably change of state in the course of time

LIST PROCESS = (".*proseassa"r) ;

    # Reminds of time sets

LIST TOPIC = "areálaášši" "áide#ášši" "boazodoalloášši" (".*-#ášši"r) "buhtadusášši" "bušeahttagáržžidanášši"
"čázádathuksenášši" "čiekŋudan#fáddá" "čoahkkin#ášši" "diggeášši" "doarjja#juohkin#ášši"
"eallin#ášši" "eanamihtádusášši" "eaŋkalášši" "eananoastinášši"
"giellaášši" "girkoášši" "gudnerihkkun#ášši" "guolle#ášši" "guovlogáhttenášši" "hálddašanášši" "huksenášši" "individuála#ášši" "juogadan#ášši" "konsešuvdnaášši"
"lassiášši" "luonddugáhttenášši"
"meroštanášši" "mudden#ášši"
"namma#logahallan#ášši" "nággoášši"
"rádja#ášši" "ráfáiduhttinášši" "rávdnje#huksenášši" "riidoášši"
"sámeášši" "sisbuksa#ášši" "suohkanstivrraášši"
"plánaášši" "riekteášši"
"sámeášši" "suopman#ášši" "váiddaášši" "váidinášši" "váldoášši" "vearroduottarášši" "vuoigatvuođaášši" ;

SET ABSTRACT = ABSTR-ENTITY OR CONCEPT OR ACTIVITY ;


LIST DILLI = (".*dilli"r) ;

  # Likely to be locative in most contexts

LIST WEATHER-SITUATION = "beaivvádat" "bievla" "buolaš" "dálki" "dálkedilálašvuohta" "fiertu" "goalki" "siivu" "jođádat" "liekkas" "linádat" "oppas" "riđđu" "muohtti" "ilbmi" "balva" "čuoika" "čoaskin" "biegga" "nuorttadat" "davádat" "oarjjádat" "luládat" "orjješbiegga" "nuortabiegga" "obbadálki" "balvadálki" "balva#ilbmi" "arvi" "savda" "savda#arvi" "liehmu" "láfubiegga" "baján" "johtti" "gállu" "jiella" "hieibma" "šlahtti" "seaŋáš" "borga" "guoldu" "dobadat" "dulvi" "moarri" "geardni" "cuoŋu" "jealahas" "jalahas" "geasádat" "dálvvádat" "jáldu" "lievhnu" "galmma" "guostta" "čođđolat" "čođđálat" "sealádat" "šearádat" "bálgu" "bálggádat" ;

SET SITUATION = DILLI OR WEATHER-SITUATION ;

SET ACTOR-ROLE = HUMAN OR ORGANIZATION OR POLITICAL-PLACE OR EDUCATION ;
 # Institution: Restricting possible actor subjects.

LIST EVENT-TOOL-ACTIVITY = Der/NomAct "addin" "dearvvašvuođaiskkadeapmi" "goddin" "hárjehallan" "jápmin" "ráđđádallan" "šiehtadallan" "vázzin" ;

LIST EVENT = "avádatčoahkkin" "álbmotčoahkkin" "ámtadigge#čoahkkin" "bargiidčoahkkin" "bajimusčoahkkin" "beassášmárkan" "bismmačoahkkin" "buohkaidčoahkkin" "boazodoallošiehtadallan" "budjeahttačoahkkin"
"cup" "čakča#márkan" "čoahkkin" "čuoigan#cup" "dievasčoahkkin" "digaštallančoahkkin" "digge#čoahkkin" "dutkančoahkkin" "erenoamáščoahkkin" "fáddáčoahkkin" "feasta" "festivála" "fitnodatčoahkkin"
"gielddaválga" "girkočoahkkin" "gittačoahkkin" "gozihančoahkkin" "gulaskuddančoahkkin" "guossečoahkkin" "gulaskuddančoahkkin"
"heajat" "heargegilvu" "jahkečoahkkin" "joavkočoahkkin" "joavko-#čoahkkin" "juovla#márkan" "juristtačoahkkin" "konferánsa" "konsearta" "kursa" "lávdegottečoahkkin" "loahppačoahkkin" "luoddačoahkkin" "lávdegoddečoahkkin"
"máilmmesoahti" "mátki" "mearridanproseassa" "ministtarčoahkkin" "miellačájehančoahkkin" "miellahttočoahkkin" "njunuščoahkkin" "nominašuvdnačoahkkin" "nášunála#čoahkkin"
"ođđa#organiseren#čoahkkin" "oktavuohtačoahkkin" "oktasaščoahkkin" "ovttasbargočoahkkin" "olgoriikaministerčoahkkin" "ollesčoahkkin" "proseassa"
"ráđđádallančoahkkin" "ráđđečoahkkin" "ráđđehusčoahkkin" "ráhkkanančoahkkin" "ráhkkananstivračoahkkin" "riektečoahkkin" "riikkačoahkkin" "ruhkos"
"ságaškuššančoahkkin" "sámečoahkkin" "sámediggeválga" "sámediggeválga" "sámemeašttirgilvu" "Sámi-meašttirgilvu" "sámemusihkkafestivála" "siida#čoahkkin" "soahti" "stivračoahkkin" "suokkardančoahkkin"
"telefončoahkkin"
"Varangerfestivála" "váldočoahkkin" "válga" "vuosttaldančoahkkin" "vuođđudančoahkkin" "váhnenčoahkkin" "válgačoahkkin" "vuosttaščájálmas" ;
 #XXX bad name, too general

SET ABSTR-PLACE = ORGANIZATION OR INSTITUTION OR EVENT OR EDUCATION OR INDUSTRY OR SITUATION ;

LIST ILLNESS = "albmasuoigi" "allergiija" "anemiija" "ástandávda" "árbedávda" "báhti" "bátnevárka" "bátnevearka" "bárku" "bárkodávda" "bearri" "boazodávda" "boahkkodávda" "boahkku" "borasdávda" "buohcu" "buohcuvuohta" "buollán" "buozalmas" "čáhcedávda" "čižžeborasdávda" "čoavjebávččas" "čoavjebonjahat" "čoavjedávda" "čottadávda" "dávda" "dohppehallan#dávda" "eksema" "fiskesdávda" "fiskes#feber" "gárrenávnnas" "gárrenávnnasváttisvuohta" "gárrendilli" "geahpesdávda" "geahppesdávda" "geahppesvihki" "golgadandávda" "golgodávda" "golgovihki" "guolledávda" "infekšuvdnadávda" "influeansa" "jápmindávda" "juhkanvuohta" "lađasdávda" "lađasleasmedávda" "leasmedávda" "leasmi" "liikedávda" "logahasdávda" "luossadávda" "luhčadávda" "mánáid#dávda" "mánodávddat" "nisson#dávda" "njoammudávda" "nuorvu" "nuoskkesdávda" "oaivvesvuohta" "obbondávda" "rabiesdávda" "ráŋka" "rohttodávda" "rohttudávda" "rohtudávda" "ruodnu" "skihpa" "časkásanváddu" "seargadávda" "sismáhtodávda" "sisváddu" "skurbbetdávda" "sogadávda" "sogaváddu" "sohkardávda" "spánskadávda" "speallandávda" "suohtta" "tuberkulosadávda" "varrasonahat" "váddu" "vátnedávda" "vihki" "virusdávda" "vuolši" "vuorri" ;

# LIST ANIMAL-ILLNESS = "luossa#dávda" "vuorri" ;

#!! * Sets denoting relations

LIST OASSI = "gonagasoassi" "riika#oassi" "riikkaoassi" "oassi" "teakstaoassi" "váldooassi" ;

	# Might be useful for partitive constructions.
	# Try to make a set of nouns wich not will be Gen in a AccGen string.

LIST HEADOFPARTS = "mearri" "nissonoassi" "oassi" "váldooassi" "bealli" "guhtemuš" "goappašat" "proseanta" "%" ;

  # This may be a better partitive set.

#!! !Miscellaneous sets
# ------------------
LIST Px = PxSg1 PxSg2 PxSg3 PxDu1 PxDu2 PxDu3 PxPl1 PxPl2 PxPl3 ;

SET N-NOPX = N-NOPXLIST OR LAHKA OR EDUCATION ;

LIST GASKAL = "gaskal" "gaskkal" "gaskii" "gaskka" "gaskkas" ;

 # p-positions that like coordination

LIST TIME-PP = "badjel" "čađa" "earret" "gaskkal" "guovddáš" "maŋŋel" "maŋŋil" "maŋŋá" "miehtá" "ovdal" ;


SET NUM = Num - OKTA ;

# this set does not contain ordinals, I am not sure if that is necessary.

SET NOT-NUM = WORD - Num ;

LIST MANGA = "máŋga" "galle" ;
 # Not referred to by any rule.

SET CARDINALS = Num - Ord - MANGA ;

SET NOT-CC = WORD - CC ;

SET NOT-PCLE = WORD - Pcle ;

LIST COMPAR = ("dávji" A Comp) "eanet" "earalágan" "eará" "earret" "seammás" "seammalágan" "seamma_láhkái" ;

 # These combine with "go" Pcle, but COMPAR stands for 'comparison' rather than
 # 'comparative'.

LIST CONTRA = "muhto" ;
 # In lean muitalan, muhto dál muitalan.

LIST PROSEANTA = "proseanta" "%" ;

SET REAL-CLB = CLB - COMMA ;


SET NOT-INITIAL-CC = WORD - INITIAL - CC ;

#!! !Border sets and their complements
# ---------------------------------

SET CP = (Pron Interr) OR (Pron Rel) OR MO ;

LIST BOUNDARYSYMBOLS =  "\;" ":" "-" "–" ;

# This set if for disambiguating rules after mapping @CVP and @CNP to all CS and CC readings, therefore we cannot have @CVP in the set
SET S-BOUNDARY-NOT-CVP = CP OR BOUNDARYSYMBOLS OR ("muhto") OR ("de" Adv) OR (Neg Sup) ;

SET S-BOUNDARY = CP OR BOUNDARYSYMBOLS OR ("muhto") OR ("de" Adv) OR (Neg Sup) OR (@CVP) OR ("vel" Adv Qst) ;
	# does not include CS, because of "go" in questions, before it is disambugated.
	# includes CP
	# this one includes @CVP, the conjunction which actually connects two sentences (each with a finite verb) to each other,
	# and not @CNP, which coordinates internal NP-/AdvP-/AP ... coordination
	# To be used only AFTER the disambiguation of @CVP and @CNP taking place in the chapter right before "Disambiguating pronouns"


SET BOC = S-BOUNDARY OR BOS ;
SET BOC-PUNCT = BOC - ("-") - ("–") ;
SET EOC = S-BOUNDARY OR EOS ;

SET NP-BOUNDARY = BOS OR EOS OR REAL-CLB OR VFIN OR Inf OR (Actio Ess) OR ConNeg OR VGen OR Sup OR PPRON-NOT-GEN OR Recipr OR Po OR Pr OR Pcle OR ("jed") OR Interj OR CS OR CP OR @CVP ;

SET APP-BOUNDARY = REAL-CLB OR VFIN OR Inf OR (Actio Ess) OR ConNeg OR VGen OR Sup OR Recipr OR Po OR Pr OR Pcle OR Interj OR CS OR CP ;
# A special barrier used with mapping of appositions.

# This set contains FMAINV with @, which means that it functions for all kind of mainverbs after the verb-mapping rules
SET SV-BOUNDARY = S-BOUNDARY OR Inf - AUX OR Sup OR FMAINV ;
 # VFIN-NOT-AUX ;
 # should be MAIN-V linked to VFIN-aux to the left. (cg-3)
# This set is ment to use in rules for disambiguating due to verbs or verbsets. It contents @.

SET SVF-BOUNDARY = S-BOUNDARY OR VFIN ;

# This set is ment to use in rules for disambiguating due to verbs or verbsets.
# Here we search for either an S-BOUNDARY or a finite verb, either aux or main.

SET CCCOMMA = CC OR COMMA ;
	# remember that those are potential sentence boundaries, too


#!! !Syntactic sets
# --------------

LIST ALLSYNTAG = (@+FAUXV) (@+FMAINV) (@-FAUXV) (@-FMAINV) (@-FSUBJ>)(@-FOBJ)(@-F<ADVL)(@-FADVL>)(@>A) (@>ADVL) (@ADVL<) (@<ADVL) (@ADVL>) (@ADVL) (@HAB>) (@<HAB) (@>N) (@APP) (@APP-N<) (@APP-Pron<) (@APP>Pron) (@APP-Num<) (@APP-ADVL<) (@VOC) (@CNP) (@CVP) (@P<) (@>P) (@HNOUN) (@INTERJ) (@Pron<) (@OBJ) (@OBJ>) (@<OBJ) (@OPRED) (@<OPRED) (@OPRED>) (@SPRED<OBJ) (@PCLE) (@COMP-CS<) (@N<) (@SPRED) (@<SPRED) (@SPRED>) (@PPRED) (@Num<) (@SUBJ) (@<SUBJ) (@SUBJ>) (@X); #!! ALLSYNTAG
SET NON-APP = ALLSYNTAG - (@APP); #!! NON-APP

#!! ''These were the set types.''



# ==========================
#!! !!!DISAMBIGUATION RULES
# ==========================
# ==========================
# ==========================


#!! !!!Section 1, The section for popular rules
# ==================================
# ==================================


BEFORE-SECTIONS

#!! !!Rule for adding Sem/Date as a tag to readings which looks like dates
SUBSTITUTE:Date1 (Num Sg) (Num Sg Sem/Date) TARGET ("<[0-3][0-9][.][0-2][0-9][.][1-2][0-9][0-9][0-9]>"r Num) ;
SUBSTITUTE:Date1 (Num Sg) (Num Sg Sem/Date) TARGET ("<[0-9][.][0-9][.][1-2][0-9][0-9][0-9]>"r Num) ;
SUBSTITUTE:Date1 (Num Sg) (Num Sg Sem/Date) TARGET ("<[0-3][0-9][.][0-2][0-9][.][0-9][0-9]>"r Num) ;

#!! * Rule for adding <vdic> to verbs denoting verbal actions like: ... dadjá Aili Kestkitalo.


LIST MUITALIT = "árvalit" "bivdit" "boagustit" "cealkit" "cuoigut" "čilget" "čurvet" "čuorvut" "dadjat" "deattuhit" "eahpidit" "geardduhit" "gielistit" "gilljut" "hállat" "hoallat" "huikit" "huikkádit" "imaštallat" "jearrat" "jievžat" "juoigat" "jurdilit" "jurddašit" "kommenteret" "lávlut" "lasihit" "loahpahit" "lohkat" "lohpidit" "máidnut" "máinnašit" "muitalit" "muittuhit" "nágget" "njávggádit" "oaivvildit" "rávvet" "savkalit" "sárdnidit" "šuohkihit" "váidalit" "váruhit" "vástidit" ;
	#$ Deaŧalaš lea sihkkarastit eamiálbmogiid ovddastusa dákkár čállingottis, dadjá Keskitalo.


SUBSTITUTE:muitalit (V) (<vdic> V) TARGET MUITALIT (0 REAL-V LINK -1 COMMA LINK 2 HUMAN-INDIVIDUAL + Nom OR (Prop Sem/Plc Nom) LINK *1 EOS BARRIER WORD - Prop) ;
	#$ Sápmelaččat eai leat goassege luohpan meara riggodagain, ja danne ii sáhte eahpidit sápmelaččaid vuoigatvuođaid mearrariggodagaide, deattuha Keskitalo.
	#$ Mii fertet maiddái geahčadit makkár lassi guoimmuhemiid galgat fállat vai bohtet vel eanet olbmot boahtte jagi, loahpaha duhtavaš beaivválaš jođiheaddji Eriksen. #Comment: Shold hit, but doesen't?



SECTION


#!! !!Lexicalised derivations
#     -----------------------


#!! * __derN__ removes DER-N if lexicalised non-essives
REMOVE:derN DER-N IF (0 LEX-N LINK NOT 0 Ess OR ("dahkat"));
	## Dat lea oahpaheaddjin doppe.
 # Since Der/NomAct was included in the DER-N set, this rule now removes
 # many Actio. This may be unfortunate. If so, the
 # Der/NomAct is removed from the DER-N set.

REMOVE:semAct Der/NomAct IF (0 (Sem/Act));
REMOVE:semNomAg Der/NomAg IF (0 NomAg);


#!! * __derNEss__ removes DER-N if lexicalised essives (revise this) - flytter denne til slutten av fila
#REMOVE:derNEss DER-N IF (0 DER-N LINK 0 Ess)(0 LEX-N LINK 0 Ess)(NEGATE 0 (NomAg Ess) OR (Der/NomAg Ess));
 	## Boarrásis leai dat vel eanaš ealáhussan ge.
 	# Removes DER-N if O is also a lexical noun, and both nouns are essive.

#!! * __derA__ removes DER-A if lexicalised A
REMOVE:derA DER-A IF (0 LEX-A);
 	## Dábálaš mearrádusat.

LIST DER-A-ADV = (Der/laš Der/at Adv) ;
SET NOT-DER-A-ADV = Adv - DER-A-ADV ;

REMOVE:derAdv DER-A-ADV (0 NOT-DER-A-ADV) ;

#!! * __derV__ removes DER-V if lexicalised V,
REMOVE:derV DER-V IF (0 LEX-V) ;

LIST VDER-ONE = Der/h Der/st Der/d Der/l ;

LIST VDER-TWO =  Der/alla Der/halla Der/ahtti ;

LIST VDER-LAST = Der/NomAct Der/NomAg  Der/goahti ;

#!! * __derHderAlla, derAlla, derH, derST__ chosses longest Der/tag
REMOVE:DerDer1 VDER-ONE IF (0 VDER-TWO) ;
REMOVE:DerDer2 VDER-ONE OR VDER-TWO IF (0 VDER-LAST) ;
REMOVE:derPassderNomAct (Der/PassL Der/NomAct) IF (0 Der/Pass) ;
REMOVE:derAlla Der/halla IF (0 Der/alla) ;
REMOVE:derPass Der/Pass + Der/d ;

#!! * __vuohtaNotAttr__ removes Attr for Der/vuohta
REMOVE:vuohtaNotAttr Attr IF (0 Der/vuohta) ;
	# when attribute form predicative form

#!! * __derPassActio__ removes Actio Nom/Gen/Acc for passive forms. I don't think they exist in Sg, we prefer the PrfPrc analysis.
REMOVE:derPassActio Der/Pass + Actio IF (0 PrfPrc) ;
	#$ Dalle galget miesit leat merkejuvvon eaiggáda merkii.

#!! !Particular verbs

LIST NOT-REAL-V-LEMMAS = "álbmotregistreret" ;

#!! * __notRealV__ removes verb readings from verbs like álbmotregistreret
REMOVE:notRealV REAL-V (0 NOT-REAL-V-LEMMAS) ;


#!! * __leapmaDimin__ removes it
REMOVE:leapmaDimin ("leapma" Der/Dimin) ;

#!! * __Der/PassS__ removes some Pass-readings in favour of V not Pass
REMOVE:Der/PassS ("guovlat" Der/PassS) OR ("diehtit" Der/PassS) OR (".*ut"r Der/PassS) OR (".*ut"r Der/PassS) OR (".*ut"r Der/PassS) OR (".*ut"r Der/PassS)(0 V - (Der/PassS)) ;

LIST NOT-PASS = ("oaktut" Der/PassS Sg3) ("guovlat" Der/PassS Sg3) ;

#!! * __notPass__ removes som Pass readings which are not likely at all
REMOVE:notPass NOT-PASS ;
	#$ Dat gal lea maid okta fáktor muhtin guvlui, muhto gávdnojit maid eará.

LIST LEX-PASS = "galbmot" "gávdnot" "gullot" "láhppot" "oidnot" "váldot" "čirrot" "sorrot" "čadnot" "doddjot" "gessot" "dovdot" "rahppot" "savvot" "čoggot" ;

#!! * __LEX-PASS__ removes passive forms of some lemmas in favour for the lexixalised one
REMOVE:LEX-PASS (Der/PassS) IF (0 LEX-PASS LINK NOT 0 ("geassit")) ; # geassit vs gessot


#!! * __Ger and GER-NOTV__ remove Ger-forms which are not likely at all
REMOVE:Ger Ger IF (0 ("geavat" N) OR ("vásedin") OR ("hálihit"));

LIST GER-NOTV = "barggadit" "bijadit" "čohkkedit" "dagadit" "deaivvadit" "dutkkadit" "geahčadit" "jugadit" "lihkkat" "logadit" "manadit" "njuovadit" "ozadit" "šlivggodit" ;
	# "bargat" Ger is more likely than "barggadit" Ger, and so on.

REMOVE:GER-NOTV GER-NOTV IF (0 Ger);

REMOVE (PrsPrc Qst) IF (0 VFIN + Qst) ;

REMOVE Ord IF (0 ABBR) ;

REMOVE (A Comp) IF (0 Inf OR Comp LINK 0 ("jođiheapme") OR ("eavttuheapme") OR ("vuojaheapme"))  ;
	# jođihit Lene: contraints are coming

SELECT ("beaivádit" PrfPrc) IF (1 Sem/Date) ;

SELECT ("geahččat" Imprt Sg2) IF (0 VGen LINK NOT -1 Neg) ;

REMOVE:eadjit (".*#eadjit"r) ;

	# Lexicalised adjectives
	# ----------------------

REMOVE:r549 VGen IF (0 A);
	## Na varas guolli dal lea buorre.

REMOVE:Der/A (Der/A*) IF (0 PrsPrc LINK 1 N);
REMOVE:r550 PrsPrc IF (0 A);
	## Áššis biddjui mearrideaddji deaddu dasa ahte...

REMOVE:r551anotv (V*) - Der/NomAct IF (0 A-NOT-V) ;
	#$ Dat lea seammá guhkki go govdat.

REMOVE:A A IF (0 ("suoma")) ;

REMOVE:Der/NomAg Der/NomAg IF (0 PrsPrc LINK 1 N OR Attr LINK NOT 0 Prop) ;

#!! !!Propernouns
#!! ! Removing proper nouns that are lookalikes

#!! * __SamediggiProp__ selects Prop after Ášši 01/12
SELECT:SamediggiProp Prop IF (-2 ("ášši"))(-1 Num);
	#$ Ášši 02/13 Sámedikki čoahkkinortnet.

LIST NOT-PROP = "Vuoksá" "Heasta" "Stállu" "Guksi" "Návet" "Náhkki" "Njunni" "Áigi" "Ohcan" "Ovdal" "Sieidi" "Profehta" "Muvra" "Gielda" ;

#!! * Rules for removing some Props which are identical to common nouns
REMOVE:Prop=noun Prop IF (0 NOT-PROP) ;
# Comment: Min Áigi is Prop but Áigi in the beginning of the sentence is not.

#!! *Removes __PropPl__, but problems with names as Davviriikkaid Ráđi, there we want Prop Pl
REMOVE:PropPl (Prop Pl) IF (0 COMMON-N LINK NOT 1 ("ráđđi"))(NEGATE 0 VGen);
  	#$ Sápmelaččaide lea hui mávssolaš ahte sin dárbbut váldojit vuhtii.
  	#% Davviriikkaid Ráđi - It should not hit this

#!! * __PropVfin__ selects propernouns which can be Vfin in the beginning of a sentence
SELECT:PropVfin_a Prop IF (0 VFIN - Qst LINK *-1 BOS BARRIER WORD)(1 VFIN + Sg3);
SELECT:PropVfin_b Prop IF (0 VFIN - Qst LINK *-1 BOS BARRIER WORD)(1 Pcle)(2 VFIN + Sg3);
SELECT:PropVfin_c Prop IF (0 VFIN - Qst LINK *-1 BOS BARRIER WORD)(1 CC OR COMMA)(2 Prop OR Pers);
SELECT:PropVfin_d Prop IF (0 VFIN - Qst LINK *-1 BOS BARRIER WORD)(1 CC OR COMMA)(2 (Pers Gen));
SELECT:PropVfin_e Prop IF (0 VFIN - Qst LINK *-1 BOS BARRIER WORD)(1 CC OR COMMA )(2 Indef)(3 N);
 	#$ Lásse dat lei sihtan mu váldit dan.
 	#$ Máhtte ii sáhttán boahtit ovdal.
 	#$ Máhtte ja Ánne eaba sáhttán boahtit ovdal.

LIST CONF-PROP = "Lea" "Lassi" "Dan" "Hu" "Goa" "Mo" "Moa" "Man" "Naba" ;

#!! * __confProp, Lea, Man, Hui, Mo, Prop__ removes Props which confuces the analyser,

SELECT:confProp CONF-PROP + Attr - ("Lea") IF (-1 BOS OR PUNCT OR (":"))(*1 Sem/Sur OR FIRSTNAME BARRIER REAL-WORD-NOT-ABBR OR COMMA) ;
	#$ Dan Robert Larsen
	#$ Lea O. Olsen

REMOVE:confProp CONF-PROP IF (-1 BOS OR PUNCT OR (":"))(0 Sem/Sur OR FIRSTNAME OR Attr)(NEGATE 1 SG3-V);
  	#$ Lassi dutkandoarjagat sáhttet leat mielde...
 	#$ Lea dušše okta Ipmil. Lea idja.

REMOVE:confProp Prop IF (0 CONF-PROP LINK 0 Adv OR Pron OR V) ;
  	#$ De son celkkii sutnje: "Mana basadit Siloaláddos."

#!!  * __Dert__ Rule for removing Der/t Prop when there are other analysis
REMOVE:Dert (Der/t) IF (0 Prop) ;


REMOVE (Der/t) IF (0 Prop) ;
REMOVE Prop IF (0 Pron) ;
REMOVE:PropUnni Prop IF (0 ("unni" Attr))(1 Der/Dimin OR ("mánáš"));
SELECT:PropNorga ("Norga") IF (0 ("norga")); # The form "norga" is not in the dictionary...

#SELECT:Gen NP-HEAD-GEN IF (0 ("sieidi") LINK 1 ("bálvaleapmi"));
  	## Sieiddi bálvalemiin sturron eallu gohčoduvvui bálvvoseallun, mii sáhtii jávkat, go oamasteaddji jámii dahje go sieidi ii bálvaluvvon.

LIST LOHKU = ("[0-9]*-#lohku"r) ;

SELECT:lohku LOHKU IF ;
	# "1960-#lohku", not loahku or logus

#!! !Some adjectives are never derived as Adv
REMOVE:NeverAdv (Der/at Adv) IF (0 ("nuorra") ) ;

#!! !Rules for Prop Attr, Sem/Sur and Plc

#!! * __PropAttrIfPropx__ removes Attr if no Prop on the right side
REMOVE:PropAttrIfPropx (Prop Attr) - Der/laš IF (NOT *1 Prop BARRIER REAL-WORD-NOT-ABBR OR COMMA)(NEGATE 1 CC LINK 1 (Prop Attr) LINK *1 Prop BARRIER REAL-WORD-NOT-ABBR OR COMMA);
	#$ Ášši 05/01 Ođđa áššiid dieđiheapmi.

#!! *Selects __Prop__ if capital letter inside clause
SELECT:PropInside Prop IF (NEGATE *-1 BOS OR (":") BARRIER WORD - ("ášši") - Num)(0 N LINK NOT 0 (Der/t) OR (Der/laš));
	#$
	#%


#!! *Substitute __PlcSur__ (Sem/Plc) (Sem/Sur)
SUBSTITUTE:PlcSur0 (Prop Sem/Plc) (Prop Sem/Sur) TARGET (Prop Sem/Plc Ill) IF (-1 FIRSTNAME + Attr)(*0 ("riŋget") OR ("čuojahit") BARRIER REAL-V OR S-BOUNDARY OR CC) ;
	#$ Sárá riŋgii Trond Trosterudii.
	#% Son riŋgii Kárášjohkii.

SUBSTITUTE:PlcSur1 (Prop Sem/Plc) (Prop Sem/Sur) TARGET (Prop Sem/Plc) IF (*-1 FIRSTNAME OR <vdic> BARRIER REAL-WORD-NOT-ABBR OR COMMA LINK NOT 0 Sem/Sur OR Sem/Plc)(NEGATE 0 (Prop Ill) OR (Prop Loc) LINK NOT *-1 FIRSTNAME BARRIER REAL-WORD-NOT-ABBR OR COMMA)(NEGATE 0 (Prop Ill) OR (Prop Loc) LINK *-1 FIRSTNAME LINK *-1 MOVEMENT-V BARRIER NOT-NPMODADV) ;
  	#$ Linda England.
  	#$ Linda B. England.
  	#% De vuolgá Biret Trosterudii.
  	#% 1980:s manai Anny Hámmárfestii vázzit buohccedivššárskuvlla.


SUBSTITUTE:PlcSur2 (Prop Sem/Plc) (Prop Sem/Sur) TARGET (Prop Sem/Plc Com)IF (NEGATE 0 SAPMI OR SUOPMA)   ;
  	#$ Mun bohten Trosterudain.

SUBSTITUTE:PlcSur3 (Prop Sem/Plc) (Prop Sem/Sur) TARGET (Prop Sem/Plc Loc) IF (*0 ("jearrat") BARRIER (Prop Loc) OR REAL-V OR S-BOUNDARY OR CC) ;
	#$ Mun jerren juoidá eksámena birra Trosterudas.
  	#$ Mun jerren juoidá eksámena birra Trosterudas Guovdageainnus.

SUBSTITUTE:PlcSur4 (Prop Sem/Plc) (Prop Sem/Sur) TARGET (Prop Sem/Plc Ill) IF (*0 VERBAL-ACTIVITY-V OR ACC-ILL-V BARRIER REAL-V OR S-BOUNDARY OR CC) ;
	#$ Atte reivve Trosterudii.

SUBSTITUTE:PlcSur5 (Prop Sem/Plc) (Prop Sem/Sur) TARGET (Prop Sem/Plc Gen) IF (1 ("lusa") OR ("luhtte") OR ("geahčai") OR ("geahčen"));
	#$ Biret vuolgá Trosterud lusa.
	#$ Biret lea Trosterud luhtte.
	#$ Biret vuolgá Trosterud geahčai.
	#$ Biret les Trosterud geahčen.

SELECT:AttrPropDerlaš (Prop Der/laš Attr) IF  (1 N) ;
	#$ oslolaš gánda šattai Eurohpameašttirin skohtergilvvus.


REMOVE:PropAttr (Prop Attr) IF (NEGATE *1 Prop OR Ord BARRIER NOT-INITIAL-CC);
	#$ Dat lea dehálaš, lohká Svein Ottar Helander.

# All these superseded by the remove rule above:
SELECT:PropSur (Prop Sem/Sur) IF (*-1 VFIN BARRIER S-BOUNDARY) (1 FIRSTNAME LINK NOT 0 Sem/Sur) ;
	#$ De vuolgá Biret Turi Lene Antonsen lusa.
	#$ De vuolgá viimmat Biret Turi Lene Antonsen lusa.
	#$ Dovdágo Biret Turi Lene Antonsen?


SELECT:PropAttr1 Attr IF (0 FIRSTNAME OR Sem/Sur OR INITIAL)(NEGATE 0 Loc)(*1C Prop BARRIER REAL-WORD-NOT-ABBR OR COMMA LINK 0 Sem/Sur OR FIRSTNAME); # LINK NEGATE 0 ADVLCASE LINK -1 Nom LINK NOT *-1 Nom BARRIER S-BOUNDARY); #(NEGATE 0 Acc LINK *1 Acc BARRIER NOT-NP LINK NOT 0 FIRSTNAME) ; Lene: kommenterte ut denne fordi den hindret regelen i å fungere på alle setninger, vi får se på hva slags constraint det bør være.
	#$ Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna.
	#$ Dat lea dehálaš, lohká Svein Ottar Helander.
	#$ Do boahtá Biret Turi Sara.
	#$ Áirras nr. 17 Randi S. Pedersen oaččui permišuvnna.
	#$ De vuolgá Biret Turi Lene Antonsen lusa.
	#$ Ánde Máhtte Somby Sandvik beasai guokte vahku áigi diehtit ahte son beassá vuolgit.

SELECT:PropAttr2a (Prop Attr) IF (-1 (V Sg3) LINK 2 Prop LINK 1 Prop LINK 1 CLB);
SELECT:PropAttr2b (Prop Attr) IF (-1 Prop LINK -1 (V Sg3) LINK 3 Prop - Ill - Loc LINK 1 CLB);
#SELECT:Xr1914 (Prop Attr) IF (-1 Prop LINK -1 (V Sg3) LINK 3 Prop LINK 1 CLB);


SELECT:PropAttrCoord (Prop Attr) IF (NEGATE -1 Prop)(1 CC LINK 1 (Prop Attr) LINK *1 (N Prop Sem/Sur));
	#$ Dá leaba Brita ja Ove Kåven.

REMOVE:PropAttr1 (Prop Attr) IF (NEGATE 1 FIRSTNAME OR (Prop Sem/Sur) OR INITIAL OR Ord);
	#$ De vuolgá Biret Merete Trosterudii. #This one gets Sem/Sur

SELECT:PropAttr2 Attr IF (0 FIRSTNAME OR INITIAL)(NEGATE 0 Loc)(1 INITIAL LINK *1C (Prop Sem/Sur) BARRIER REAL-WORD-NOT-ABBR)(NEGATE 0 Acc LINK *1 Acc BARRIER NOT-NP LINK NOT 0 FIRSTNAME);

#SELECT:Xr1920- Attr IF (0 (Prop Sem/Sur))(NEGATE 0 Loc)(1C (N Prop Sem/Sur))(NEGATE 0 Acc LINK *1 Acc BARRIER NOT-NP  LINK NOT 0 FIRSTNAME);
	#$ Áirras nr. 17 Randi S. Pedersen oaččui permišuvnna.
	#$ Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna. # Two rules in order to avoid Sem/Sur Attr to FIRSTNAME.

SELECT:PropGen_a Gen IF (0C Prop)(1 ORGANIZATION); #Lene: la inn C for å unngå at den treffer Lea osv.
SELECT:PropGen_b Gen IF (0C Prop)(1 PROFESSION OR SAPMI LINK 1 ORGANIZATION); #Lene: la inn C for å unngå at den treffer Lea osv.
	#$ Oslo universiteahta namas Oslo namma lea genitiivvas.
	#$ 1982:s gitta 1993 rádjái jođihii son Norgga Álbmotmusea sámi ossodaga Oslos.

#!! *Removes __PropAttr__ if no Prop on the right side
REMOVE:XXPropAttr (Prop Attr) - Der/laš IF (NEGATE *1 Prop BARRIER REAL-WORD-NOT-ABBR) ;


#!! !MISC

#!! * sutnje is not verb
REMOVE ("suotnjat" Ind) IF (0 Pers LINK -1 VFIN) ;

#!!  * __ABBR__ Removes ABBR in favour of Adv, Pcle or Pron, e.g. "dii" when there is no punctuation
REMOVE:ABBR ABBR IF (0 Adv OR Pcle OR Pron) ;

#!! * __ollit__ removes ollit when ollu - move this one?
REMOVE:ollit ("ollit" V) IF (0 OLLU) ;

#!! * __FocbaDu3__ removes Foc/ba when Du3 verbs like máhttiba and Adv like juoba and Prop like Jáhkoba (Acc)
REMOVE:Focba Foc/ba IF (0 Du3 OR ("juo") OR Prop) ;

#!! * __Focge__ removes Foc/ge when Adv like dieđusge
REMOVE:Focge Foc/ge IF (0 ("dieđusge")) ;


#!! !!!ONE-COHORT DISAMBIGUATION - CYCLE 0

#!! The idea behind "cycle 0" is to have safe rules without context first.
#!! These rules typically chose lexicalisations over derivations,
#!! Saami words instead of marginal names, etc.


#!! !!Lexicalised derivations

#!! * Removes __derV__  DER-V if lexicalised.
REMOVE:derV DER-V IF (0 LEX-V)(NEGATE 0 (eaddji));

#!! *Removes __derN__ if lexicalised.
REMOVE:derN DER-N IF (0 LEX-N LINK NOT 0 Ess OR ("dahkat"));

#!! *Removes __derNEss__ if lexicalised, and both nouns are essive.
REMOVE:derNEss DER-N + Ess IF (0 LEX-N + Ess) ; #(NEGATE 0 Actio); # Vi hadde denne constrainten tidligere, vi får se på om vi trenger den
	#$ Dat lea oahpaheaddjin doppe.

#!! *Removes __derA__ or PrsPrc or VGen if lexicalised. VGen is a chance.
REMOVE:derA DER-A OR PrsPrc OR VGen IF (0 LEX-A);
 	#$ Dábálaš mearrádusat.
 	#$ Áššis biddjui mearrideaddji deaddu dasa ahte...
 	#$ Na varas guolli dal lea buorre.

#!! *Removes __derAdv__  when Adv is lexicalised.
REMOVE:derAdv (A* Adv) IF (0 LEX-ADV);


#!! * Removes __derVhmm__ Does this function?
REMOVE:derVhmm DER-V IF (0 LEX-V) ;

#!! * __derHderAlla__ removes Der/h Der/alla if Der/halla.
REMOVE:derHderAlla (Der/h Der/alla) IF (0 Der/halla) ;
	#$ Mii bovdehalaimet heajaide.

#!! * __derAlla__ removes Der/halla if Der/alla.
REMOVE:derAlla Der/halla IF (0 Der/alla) ;
	#$ Mii bovdehalaimet heajaide.

#!! * Removes __derH__ if Der/goahti.
REMOVE:derH Der/h IF (0 Der/goahti) ;
	#$ Nillá-mága gasttihišgođii.

#!! * Removes __derST__ if Der/ahtti
REMOVE:derST Der/st IF (0 Der/ahtti) ;
	#$ Heasta vuoiŋŋastahttá juolggi go čuožžu golbma juolggi nalde.

#!! * __derVuohta__ removes A Attr Der/vuohta if A Der/vuohta.
REMOVE:derVuohta Attr IF (0 Der/vuohta) ;

REMOVE (Der/heapmi A Comp Attr) ;
REMOVE:ihttin ("ihtit") IF (0 ("ihttin")) ;
REMOVE:olgu ("olgu") IF (0 ("olgun")) ;

#!! !!Adjectives or nouns, not adverbs

#!! * __Aifeambbo__ selects A after eambbo
SELECT:Aifeambbo A IF (-1 ("eambbo") OR ("eanet"))(NEGATE 0 Pl);
	#$ Muhto eambbo eahpidahtti lea čielggadusa s. 17 cealkámuš.

SELECT:Aeambbo Attr IF (0 ("eanet") OR ("eambbo") LINK 1 N)(NEGATE -1 ("ahte"));
	#$ Diŋgojitgo eambbo biergu?

#SELECT:r1306 Adv IF (0 ("eambbo"));
	#$ Gittabivdosiin ii leat lohpi bivdit deanu eambbo go beallái.

#!! * __muhtunlagan__ removes lága Ess if Indef ja lágan A
SELECT:muhtunlagan A IF (-1 Indef)(0 ("lága" Ess));
 	#$ muhtun lágan

#!! !!Adverbs

#!! * IFF __buotAdv__ : buot Adv in front of Superl
IFF:buotAdv ("buot" Adv)  IF ((1 Superl) OR (-1 Acc LINK 2 Inf) OR (-1 TRANS-V LINK 2 ("dat" Acc) OR (Dem Acc)) OR (-1 COPULAS LINK 2 (N Ess)) OR (-1 REAL-V + IV LINK NOT 0 Pron LINK 2 COMMA OR EOS) OR (-1 (Nom Pl) LINK 2 VFIN + Pl3)) ;
 	#$ Buot buoremus lea ieš vuolgit.
 	#$ Son dáhtui buot dan ollašuvvat, vai eatnat olbmot bisošedje heakkas.
 	#$ Son lea njuoskan buot, mii lea fuones ášši.
 	#$ Skávžá lea buot soalsin ja njuoskkas.


SELECT:AdvLACCAT1 Adv IF (NEGATE *-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Pl) OR PPRON-DU-PL BARRIER Nom LINK 0 Nom)(NEGATE 0 (Comp Sg) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Sg) OR SG-PRON BARRIER Nom LINK 0 Nom)(0 LACCAT-ADV) ;
SELECT:AdvLACCAT2 Adv IF (0 LACCAT-ADV LINK NEGATE 0 Pl LINK *-1 COPULAS BARRIER NOT-A-ADV LINK *-1 NP-HEAD-NOM + Pl BARRIER NOT-ADV-PCLE)(1 A OR N LINK NOT 1 go) ;
	#$ Guovddáš lea fágalaččat iešbirgejeaddji ásahus.
	#$ Divššohas lea duođalaččat roasmmuhuvvan.

#!! !Lexicalised adverbs
#!! It is useful to select early the adverbial reading for potensial nouns or verbs.

#!! * __aibbasAdv__ áibbas dolin
SELECT:aibbasAdv ("áibbas" Adv)  IF (1 A OR Adv LINK *1 VFIN BARRIER S-BOUNDARY) ;
	#$ Áibbas dolin nissonat botne láiggi snálduin, dál sii botnet dorttiin.

#!! *__aloGen__ removes állu Gen,  álo  Adv vs. N Gen
REMOVE:aloGen ("állu" Gen) IF (NEGATE 1 Po) ;

#!! * __mannelTimeAdv__ golbma jagi maŋŋel
SELECT:mannelTimeAdv MANNEL + Adv IF (-1 VAHKKU-DUR) ;
	## Dat dáhpáhuvai golbma jagi maŋŋil.
	#$ Logi minuvtta maŋŋel nagodii Sørøy Glimt čiekči Stig Martinsen dulbet bohtosa ovttain moalain.
	#$ Go jagi maŋŋil álggahuvvui Kárášjoga gymnása, de fárrejin ruoktot ja vázzen guokte maŋimus jagi dáppe.

#!! * __AdvSTV__ váldit mielde, oahppat bajil. eará? STRICT-TRANS-V is too strong
SELECT:AdvSTV1 Adv IF (-1 Acc LINK *-1 ("váldit") OR ("oahppat") BARRIER SV-BOUNDARY OR Acc LINK NOT -1 Acc)(0 Po LINK NOT *1 Acc BARRIER NOT-A-ADV);
SELECT:AdvSTV2 Adv IF (-1 Acc LINK -1 (Pron Gen) LINK *-1 ("váldit") OR ("oahppat") BARRIER SV-BOUNDARY OR Acc LINK NOT -1 Acc)(0 Po LINK NOT *1 Acc BARRIER NOT-A-ADV);
	#$ Váldot beatnaga mielde!
	#$ Oahpa lávlaga bajil!

#!! * __cadaAdv__ if oažžut juoidá čađa
SELECT:cadaAdv ("čađa" Adv)  IF (*-1 ("oažžut") BARRIER REAL-V OR S-BOUNDARY)(NEGATE -1 ("geardi") OR TIME);
	#$ Mii oažžut daid áššiid čađa.


#!! * __gaskanAdvVGen__
SELECT:gaskanAdvVGen ("gaskan" Adv)  IF (1 VGen);
 	#$ Gaskan bora viehkalin olggos.

#!! * __gotAdv__
SELECT:gotAdv ("got" Adv)(-1 ("jur")) ; #eksempler på CS?

#!! * __ikteAdv__
SELECT:ikteAdv ("ikte" Adv) IF  (*0 VFIN BARRIER S-BOUNDARY) ;
	#$ Mu mielas lei heajut dálki ikte.

#!! * __miehtaV__
SELECT:miehtaV VFIN IF (NEGATE *-1 VFIN BARRIER S-BOUNDARY)(NEGATE -1 Gen LINK NOT 0 Nom)(0 ("miehtá"))(NEGATE 1 Gen LINK 0 TIME OR PLACE);
 	#$ Jus divššohas dasa miehtá, dahje dilálašvuohta lea nu,...
	## Jus divššohas dasa miehtá,...

#!! * __miehtaV__
SELECT:mannelAdv_a Adv IF (1 TIME-UNIT + Acc LINK 1 MANNEL);
SELECT:mannelAdv_b Adv IF (1 Num LINK 1 TIME-UNIT + Acc LINK 1 MANNEL);

#!! * __aigiAdv__  guokte vahku áigi
SELECT:aigiAdv Adv IF (NEGATE -2 (Num Gen))(-1 TIME-UNIT + Acc)(0 ("áigi"));
	#$ Ánde Máhtte Somby Sandvik beasai guokte vahku áigi diehtit ahte son beassá vuolgit.

#!! *  __viimmatAdv__
SELECT:viimmatAdv ("viimmat" Adv) IF  (NOT 1 Inf);


#!! * __dalleAdv__
SELECT:dalleAdv ("dalle" Adv);

#!! *  __dusseAdv__
SELECT:dusseAdv1 ("dušše" Adv) IF ((*-1 VFIN OR (Neg Foc/ge) BARRIER NOT-ADV) OR (*-1 go BARRIER REAL-V OR S-BOUNDARY LINK -1 VFIN LINK -1 BOC) OR (-1 NP-BOUNDARY OR S-BOUNDARY LINK 2 Num LINK *1 N BARRIER NOT-A-ADV LINK 1 VFIN) OR (*1 Num BARRIER NOT-ADV LINK *-2 VFIN BARRIER S-BOUNDARY) OR (1 N OR Pron LINK 1 Rel) OR (1 Nom - Pl - Pl3));
	#$ Dan mun muitalan dušše dutnje.
	#$ Áiggun geahččat dušše moadde filmma. # This function because geahččat is homonym to VFIN
	#$ Lea go dus dušše okta mánná?
	#$ Dušše ovtta lanjas leat jorba beavddit.
	#$ Buot álbmogiid hearvásvuođat ja riggodagat buktojuvvojit gávpogii , muhto ii mihkkege buhtismeahttumiid beasa sisa , ii ge oktage fastivuođa ja giellása bálvaleaddji , muhto dušše sii geaid namma lea čállojuvvon Lábbá eallima girjái.

SELECT:dusseAdv2 ("dušše" Adv) IF (1 (N Pl) LINK *1 VFIN + Pl3 BARRIER S-BOUNDARY);
	#$ Dušše inuihtat ja sápmelaččat sáhttet adnojuvvot Davviriikkaid eamiálbmogin.

LIST NEG-ADV-PRON = "gostege" "goassege" "mihkkege" "giige" (Adv Foc/ge) ;
SELECT:negadvpron NEG-ADV-PRON IF (-1 Neg - Sup) ;
	#$ eai gostege

#!! * __alggageAdv__
SELECT:alggageAdv ("álggage" Adv) IF  ((*-1 Nom BARRIER S-BOUNDARY) OR (*1 Nom BARRIER S-BOUNDARY));
	#$ In mun leat álggage heajut go dát.
	#$ Sis, geain eai lean álggage njuovvanbohccot, ...

#!! * __bearraiAdv__
SELECT:bearraiAdv ("bearrái" Adv) IF  (*-1 ("geahččat") BARRIER NOT-ADV) ;
	#$ geahččat bearrái

#!! * __buresAdv__
SELECT:buresAdv ("bures" Adv) IF  (*1 ("mannat") BARRIER REAL-V) ;
	#$ Bures dat manná.

#!! * __cadatAdv__
SELECT:cadatAdv ("čađat" Adv)  IF ((-1 VFIN - Neg - Pl3) OR (1 VFIN - Pl3)) ;
	#$ Lea čađat dárbu ođasmahttit jorgalusaid.
	#$ Son muitala ahte dii čađat jurddašehpet min iluin.

#!! * __cuozzutAdv__
SELECT:cuozzutAdv Adv IF (-1 ("bissut"))(0 Inf) ;

#!! * __dadjatAdv__
SELECT:dadjatAdv ("dadjat" Adv)  IF ((-1 COMMA) OR (1 ("juo")) OR (*-1 VFIN - AUX - INF-V BARRIER NOT-ADV-PCLE)) ;
	#$ Muhto joddobuođuiguin bivdojuvvo luossa miehtá deanu, dadjat njálmmi rájes gitta gierragii.
	#$ Háhkan lea šaddan dadjat juo cieggan riektedilli.
	#$ Dan ii máhte ipmirdit eambbo dat geat dainna sohtet measta dadjat beaivválaččat.

#!! * __dainnaAdv__
SELECT:dainnaAdv ("dainna" Adv)  IF (*1 ("ahte") BARRIER WORD);
	#$  ...muhto sáhttá vuordit dainna go ferte vuoruhit dikšuma. #Pers
	#$  Kvaliteahtta sihkkarasto dainna, ahte bargit dovdet kultuvrra.

#!! * __daninAdv__ selects danin Adv. It is a special rule, only negative restrictions.
SELECT:daninAdv ("danin" Adv) IF (NEGATE *1 (N Ess) BARRIER NOT-A-ADV)(NEGATE 0 Ess LINK NOT *1 Nom BARRIER NOT-ADV LINK *-1 Ess LINK *-1 COPULAS BARRIER NOT-ADV)(NEGATE 1 CLB)(NEGATE *-1 OPRED-V BARRIER NOT-ADV)(NEGATE *1 OPRED-V BARRIER NOT-ADV);
	#$ Danin badjánii jurdda lága ráhkadit.
	#$ Sierralágan diehtojuohkingeahččaleamit leage danin dehálaččat.
	#$ Návetruittu lávejit danin atnit. #Essive - hence the restrictions. Special rule, only negative restrictions.

#!! * Select Ess, and then kill?
#SELECT:r1286 Ess IF (*-1 COPULAS BARRIER NOT-ADV)(0 ("danin"))(1 CLB);
#	## ...jos fierpmit leat danin,...
#SELECT:r1287 Ess IF (0 ("danin"))(*1 OPRED-V BARRIER NOT-ADV-PCLE);
#SELECT:r1288 Ess IF (0 ("danin"))(*-1 OPRED-V BARRIER NOT-ADV-PCLE);
#	## Návetruittu lávejit danin atnit.

#!! * __dassaAdv__
SELECT:dassaAdv_a Adv IF (0 ("dassá") OR ("dassái"))(1 go LINK *1 VFIN BARRIER S-BOUNDARY);
SELECT:dassaAdv_b Adv IF (0 ("dassá") OR ("dassái"))(-1 VAHKKU-DUR);
	#$ Dassá go son jámii, lea guhkes áigi.
	#$ Vuosttažettiin adnui sámi kultuvra árvvoheabbon go dáža kultuvra 100 jagi dassá.

#!! * __dakkoAdv__
SELECT:dakkoAdv ("dákko" Adv)  IF (*0 VFIN BARRIER S-BOUNDARY) ;
	#$ ... oaiviliid ja ákkaid bokte mat dákko ovddiduvvojit,...
	#$ Hammerfeastta suohkanbáhppa Harboe lea dákko maid, nu go eará oktavuođain, buorre gáldu.

#!! * __duoAdv__
SELECT:duoAdv ("duo" Adv)  IF (NEGATE -1 Dem or (A Attr))(0 ("duo"));
	#$ Áddjá go duo lea boahtime?

#!! * __duodaidAdv__
SELECT:duodaidAdv1 ("duođaid" Adv) IF  ((-1 BOC LINK 2 COMMA) OR (-1 Nom LINK 2 VFIN));
	#$ Duođaid, mun cealkkán didjiide:...
SELECT:duodaidAdv2 ("duođaid" Adv)  IF (-1 COPULAS)(1 Ess);
	#$ Eatnandoallu šattai duođaid buressivdnádussan sámiide, nugo dat lei iežáge álbmogiidda.

#!! * __AdvNotNA__ Adverbs, not nouns or adjectives
LIST ADV-NOT-NOMEN = "bealleheggii" "beare" "čoahkkái" "čoahkis" "fárrui" "fárus" "gárrenoaivvis" "ihttážii" "goalmmádassii" "nuppádassii" "oidnosis" "oidnosii" "ođđasis" "olámuddui" "oppalohkái" "ovdagihtii" "ovttasráđiid" "ruossalassii" "ruovttoguvlui" "sullii" "válbmasii" ;

REMOVE:AdvNotNA N OR A IF (0 ADV-NOT-NOMEN) ;

#!! * __AComp__ remove A Comp when Adv
REMOVE:AComp (A Comp) IF (-1 DYNAMIC-V)(0 Adv LINK NOT 0 ("eanet") OR Indef OR ("buorre"))(NEGATE *1 go BARRIER SV-BOUNDARY);
	#$ Eadni ráhkadii johtilit ruovttusiiddu.
	#% Mun čállen romit reivve go don.

REMOVE:ACompAdv (A Comp) IF (-1 BOS OR CLB)(0 Adv)(1 EOS OR CLB);
REMOVE:ACompAdv (A Comp) IF (-1 V)(0 Adv)(1 (N Ess));

REMOVE:buoretAdv ("buoret" Adv)(0 (A Comp) LINK 1 N) ;

#!! * __birrasii__ removes birrasii N
REMOVE:birrasii N IF (0 ("birrasii") LINK 1 Num)(NOT -1 Gen)  ;

#!! * __doarvaiAdv__ removes birrasii N
SELECT:doarvaiAdv ("doarvái" Adv) IF  ((-1 COPULAS LINK 2 Nom OR CS) OR (-1 TRANS-V LINK 2 (N Acc)) OR (-1 (Ind Neg) LINK 2 Adv)) ;
	#$ Vaikko mánáin lei doarvái bargu skuvllas ja bihtáidbargamiin beaivvi mielde, de berre liikká dál maŋŋá fuomášuhttit ahte máŋggaid jagiid ii lean makkárge lágiduvvon asttuáigefálaldat mánáide.



#!! * __eanasAdv__
SELECT:eanasAdv Adv IF (*-1 NP-HEAD-NOM BARRIER S-BOUNDARY)(0 ("eanas") OR ("eanaš"))(NEGATE *1 N BARRIER NOT-A-ADV);
	#$ Muhto báinnan dat gal eanaš lea.
	#$ Áššemeannudeapmi lea eanaš dárogillii.
	#$ Nuppádassii leat čielga gáibádusat eaktodáhtolaš oskui, mii guoská giddodahkii, eanaš vuođđuduvvon registrerema oktavuođas.

#!! * __eambbogo__ selects Adv eambbo go
SELECT:eambbogo Adv IF (0 ("eambbo") OR ("eanet") LINK -1 REAL-V)(1 ("go")) ;


#!! * __eanetAdv__
SELECT:eanetAdv1 ("eanet" Adv) IF ((-1 ("ja") OR ("ahte") LINK -1 ("eanet")) OR (1 ("ja") OR ("ahte") LINK 1 ("eanet"))) ; #(*1 A OR REAL-V BARRIER S-BOUNDARY);
	#$ Dutkangaskkusteapmi ja jearru dutkanbohtosiidda šaddá eanet ja eanet guovddáš oassi dutkama árgabeaivvis.
SELECT:eanetAdv2_a Adv IF (0 ("uhcit") OR ("unnit") OR ("eanet")) (1 ("uhcit") OR ("unnit") OR ("eanet"));
SELECT:eanetAdv2_b Adv IF (0 ("uhcit") OR ("unnit") OR ("eanet")) (-1 ("uhcit") OR ("unnit") OR ("eanet"));
	#$ ...muhto dál lea eanet uhcit jávkan.
	#$ Go vierroopmodaga geavaha ja eaiggát unnit eanet čielgasit dasa leat miehtan,...
	#$ Go eaiggát eanet unnit čielgasit dasa lea miehtan.

SELECT:eanetAdv3 ("eanet" Adv) IF (1 A-CASE OR REAL-V - ("leat") LINK NEGATE *1 N BARRIER NOT-A-ADV) ;
	#$ Báhkkodeamis ferte leat sáhka geavaheamis mii lea oktoráđđejeaddji dahje ainjuo čielgasit eanemus čalbmáičuohcci.

REMOVE:eanetV V IF (0 ("eanet" Adv))((*0 COPULAS BARRIER S-BOUNDARY) OR (*1 N BARRIER NOT-A-ADV LINK *-1 ("eanet") LINK -1 VFIN)) ;
	#$ Dál go ruhtadilli lea buoret go ovdal, de leat gávttis eanet čiŋat.
	#$ Ovdal ledje eanet láttagat Olmmáivákkis, muhto boanddat leat goikadan ollu jekkiid ja gilván daidda rásiid.

SELECT:eanemusatAdv ("eanemusat" Adv) (1 Num) ;
	#$ Juohke mánná oaččui guokte ja eanemusat golbma mánu oahpahusa jahkái.

#!! * __AdvComp__
#SELECT:AdvComp Adv IF (0 Comp)(NEGATE 0 Pron)(NEGATE 1 Dem OR N OR A);
	#$ Dat lea eanet go 10.
	#$ Lea 43 olbmo unnit go diibmá dán muddui.
	# Ambiguous: 	## ...hálida universitehta almmuhit eanet dieđalaš artihkkaliid sámegillii. #- hence the condition (NEGATE 1 A).

#!! * __easkkaAdv__
SELECT:easkkaAdv1 EASKKA + Adv IF (0 EASKKA)(1 ("dalle"));

SELECT:easkkaAdv2 Adv IF (0 EASKKA)(*1 TIME BARRIER NOT-A-ADV);
	#$ ...mii easkka maŋimuš áiggiid lea jávkágoahtán.
	#$ Dat lei boahtán ruoŧagielain easkka jagi 1863.
	#$ Muhto easkka 1898:s dovdagohte Láhppiolbmot dan mii maŋŋelis bođii.
 # Ambiguous: 	## Dat čielgá dálá oasálaččaide easkka proseassas.

SELECT:easkkaAdv3 Adv IF (0 EASKKA)(1 Num OR MANNEL);
	#$ ...easkka maŋŋel 1529 ásaiduvve olbmot duođas báikkiide.

LIST CUT-V = "boatkanaddat" "botket" "boatkanit" "oažžut" "gáskkistit" "čuohpastit" "čuolastit" "dodjit" "doadjit" "čuohppat" "gáskit" "mannat" ;

#!! * __gaskatAdv__
SELECT:gaskatAdv ("gaskat" Adv)  IF (*-1 CUT-V BARRIER S-BOUNDARY) ;

#!! * __goassigeAdv__
SELECT:goassigeAdv ("goassige") OR ("goassege")  IF (*-1 Neg BARRIER VFIN) ;
	#$ Ii leat stáhtas várra goassige leamaš ruđalaš ávki dán ámmahis.

#!! * __gosaAdv__
SELECT:gosaAdv ("gosa" Adv) IF  (*1 VFIN BARRIER S-BOUNDARY);
	#$ Gosa don čihket dan?
	#$ Son oinnii báikkiid gosa juo ledje ásaiduvvan.

#!! * __gustoAdv__
REMOVE:gustoAdv ("gusto" Adv) IF (NEGATE -1 VFIN - Neg)(NEGATE 1 VFIN - ("duššat")) ;
	#$ Juos evttohusa meahcceeatnamiid nuppástuvvan geavaheami birra, mii juogo gusto dušše Kárášjoga, Guovdageainnu, Unjárgga, Porsáŋggu ja Deanu gildii dahje gusto dušše restii Finnmárkkus, dorjot golbma ja dušše golbma stivrralahtu, de sáhttet golbma stivrralahtu ovtta oli gáibidit ášši gieđahallat oktii vel.

#!! * __guhkasAdv__
SELECT:guhkasAdv Adv IF (*-1 REAL-V BARRIER NOT-ADV-N)(0 ("guhkká") LINK 0 Loc OR Gen)(NEGATE 1 COPULAS OR Acc)(NEGATE 1 (V Neg) LINK 1 COPULAS) ;
SELECT:guhkasAdv Adv IF (-2 REAL-V)(-1 (A Nom) OR (N Nom))(0 ("guhkká") LINK 0 Loc OR Gen) ;
SELECT:guhkasAdv Adv IF (-1 BOS OR CC OR CS)(0 ("guhkká") LINK 0 Loc OR Gen)(*1 REAL-V LINK -1 NP-HEAD-NOM) ;
	#$ Dilli joatkašuvvá dákkárin guhkás boahtteáigái.
	#$ Son bálkestii spáppa guhkás.
	#$ Guhkás son bálkestii spáppa.

#!! * __VifVFIN__ removes V
#REMOVE:VifVFIN V ((0 Adv LINK 1C VFIN OR PrfPrc) OR (0 Adv LINK -1C VFIN OR PrfPrc));
	#$ Don gal gusto beroštat čuovvut.

#!! * __harveAdv__
SELECT:harveAdv ("hárve" Adv)  IF (NEGATE *-1 PLANT OR ("bealdu") OR ("eana") OR ("gieddi") OR ("tráktor") OR ("traktor") BARRIER S-BOUNDARY)(0 Adv)(NEGATE *1 PLANT OR ("bealdu") OR ("eana") OR ("gieddi") OR ("tráktor") OR ("traktor") BARRIER S-BOUNDARY);
	#$ Boazoealáhusas ovddasteaddjit hárve vuosttaldit viessohuksema.

#!! * __juogoQst__
IFF:juogoQst ("jogo" Qst) IF  (NEGATE *1 ("dahje"));
	#$ Jogo gikse dat guolit?

#!! * __justeAdv__
SELECT:justeAdv ("juste" Adv) IF  ((-1 ("jur")) OR (1 PAST-MARKER OR PRESENT-MARKER OR MO));
	#$ jur juste
	#$ juste dalle


#	# láhkai
#	# - - - -
#SELECT:r1345 N IF (-1 Dem OR Attr OR (Interr Gen))(0 ("láhkai"));
#	## Seammá láhkai go váldojournála ja lassijournála geavaheami oktavuođas...
#	## Dán barggus lea erenoamáš deaŧalaš bures gulahallat Sámedikkiin guđe láhkai galgá návccaid atnit.

#!! * __loahpasAdv__
SELECT Adv IF (NEGATE -1 Gen)(0 ("loahpas"));

#!! * __liikkaAdv__
SELECT:liikkaAdv ("liikka" Adv)(0 N LINK -1 VFIN)(1 A OR Adv) ;
	#$ Dalle šattašii liikka buoret dálki go mannan sotnabeaivve.

LIST SLIP-V = "luoitit" "beassat" "gaikkihit" "luovvanit" "oažžut" "nuollat" ;

#!! * __luovosAdv__
SELECT ("luovos" Adv)  IF (*0 SLIP-V BARRIER S-BOUNDARY) ;


#REMOVE Interj ((-1 WORD - Interj) OR (1 WORD - Interj)) ;

#!! * __maninAdv__
SELECT:maninAdv ("manin" Adv) IF (NEGATE *-1 REAL-V BARRIER S-BOUNDARY)(NEGATE 0 Ess LINK *1 OPRED-V LINK -1 Acc) ;
	#$ Manin don leat heittohit nohkkan?
	#% Manin don geavahat dan?

#!! * __manneAdv__
SELECT:manneAdv1 ("manne" Adv)  IF (*-1 BOC OR PUNCT BARRIER NOT-ADV-PCLE)(*1 VFIN BARRIER S-BOUNDARY OR CC LINK NOT 0 N)(NEGATE 0 Qst)(NEGATE 1 Qst)(NEGATE -1 ("de"));
	#$ Ja manne moraštehpet biktasiid dihtii?
	#$ Naba dii, manne dii ieža rihkkubehtet Ipmila báhkkomiid?
	#$ Manne dis leat diekkár bahás jurdagat din váimmuin?
	#$ Manne rievddadit luonddudilálašvuođat?
	#% Sii manne isideaset lusa ja muitaledje buot. # BARRIER CC!

SELECT:manneAdv2 ("manne" Adv)  IF (*-1 VFIN BARRIER S-BOUNDARY OR CC OR CLB LINK NOT 0 Neg) ;
	#$ Jos oktage jearaš dudnos manne doai dahkabeahtti dan,...

#!! * __muhtuminAdv3__
SELECT:muhtuminAdv ("muhtumin" Adv) ;
	#$ Muhtumin lea deaŧalaš čállit journála dalán.

#!! * __njuolgaAdv__
SELECT:njuolgaAdv ("njuolga" Adv)  IF (*1 Ill BARRIER NOT-A-ADV) ;
	#$ Nuppi vuoru goalmmát minuvttas Anette nivkalii čiehkačievččastaga čábbát njuolga mollii.

#!! * __oddasitAdv__
SELECT:oddasitAdv1 ("ođđasit" Adv)  IF (-1 ("fas")) ;
	#$ Elia gal boahtá dahkat buot fas ođđasit.

SELECT:oddasitAdv2 ("ođđasit" Adv) IF (*-1 Nom BARRIER S-BOUNDARY)(*1 NOT-AUX-V BARRIER S-BOUNDARY);
	#$ Eat mii sáhte ođđasit ásahit vuođu.

#!! * __oktanAdv__
SELECT:oktanAdv Adv IF (0 ("ovttas") OR ("oktan"))(*1 Com BARRIER NP-BOUNDARY);
	## Divššohasas lea vuoigatvuohta iežas journála geahččat oktan mildosiiguin.

#!! * __ovttasAdv__
REMOVE:ovttasColl ("ovttas" Coll) ;
REMOVE:oktaLoc ("okta" Num Loc) - Attr IF (NEGATE 1 COPULAS + Pl3 OR COPULAS + Sg3) ;

SELECT:ovttasAdv1 ("ovttas" Adv) IF (-1 (Pl Nom))(1 PL3-V);
	#$ Ferte oaidnit ollislaš gova maid soahpamušat ovttas vuođđudit.

SELECT:ovttasAdv2 ("ovttas" Adv) IF (-1 N LINK *-1 NUM + Nom BARRIER NOT-A-ADV)(*1 PL3-V BARRIER NOT-ADV-PCLE);
	#$ Ferte oaidnit ollislaš gova maid dát guokte soahpamuša ovttas vuođđudit.

#!! * __oktiiV__ remove
REMOVE:oktiiV SG3-V IF  (*-1 VFIN BARRIER S-BOUNDARY)(-1 ("dušše"))(0 ("oktii"));
	#$ Vuoigatvuohta gusto dušše oktii.
	#$ Dušše oktii lean oaidnán čájáhusa mii čájehii álgoálbmoga rávdebarggu.
	#$ Ja dušše oktii lean oaidnán čájáhusa mii čájehii álgoálbmoga rávdebarggu.
	#$ Lulli-sámiguovllu biirret heajumusat go sis lea dušše oktii leamaš listu mas nisu lea njunnožis.

#!! * __oktiiAdv__ select
SELECT:oktiiAdv1 ("oktii" Adv) IF (-1 BOC)(1 go OR (N Pl));
	#$ Oktii go leimmet mannamin rohkadallanbáikái, bođii min ovddal muhtun šlávvanieida.
	#$ Oktii cuohpaid galgá biddjot luossa go sáltejuvvo.

SELECT:oktiiAdv2 ("oktii" Adv) IF (1 ("njunni" N Pl Gen) OR ("juolgi" N Pl Gen) OR ("sealgi" N Pl Gen) OR ("geahči" N Pl Gen));
	#$ Dat čohkkáiga oktii njuniid.

SELECT:oktiiAdv3 ("oktii" Adv) IF (*-1 OKTII-V OR COM-V) ;
	#$ Porsáŋggu loguin leat buot klássaid submi biddjon oktii.

SELECT:oktiiAdv4 ("oktii" Adv) IF (*1 TIME + Loc BARRIER N OR V OR Pron)(*1 Ill OR Loc BARRIER NOT-A-ADV);
	#$ Oktii stuorradiggeáigodagas ovddiduvvo dieđáhus daid doaibmabijuid birra mat galget sihkkarastit ja ovddidit sámi giela.

SELECT:oktiiAdv5 ("oktii" Adv) IF (-1 ("vel") OR REAL-V) ;
	#$ Lea go Sámedikki mielas Microsoft duođaid ánssášan vel oktii nuvttá máidnosa, mii ii leat duohta ge?

#!! * __ollasitAdv__ selects
SELECT:ollasitAdv1 ("ollásit" Adv) IF ((-1 REAL-V - INF-V) OR (*1 Loc OR Ill BARRIER NOT-A-ADV) OR (*1 NP-HEAD-GEN BARRIER NOT-A-ADV LINK 1 Po) ) ;
	#$ ...ahte historjjálaš ovdáneapmi čielggaduvvo ollásit.

#!! * __radjaiPo__ selects
SELECT:radjaiAdv Po IF (-1 Num)(0 ("rádjai"));
	#$ Njuolggadusat nággolága § 204:s § 209 rádjái ožžot seammá geavahusa.
# # This rule should be extended to all postpositions likely to occur after
# # number expressions. Todo: Make a set.

#!! * __seahkáPl__ selects Pl
SELECT:seahkáPl Pl (1 ("seahká"));
	#$ Ja soames báikkiin lea smávva sieđga, muhto jos eai leat goikesieđggat, de lea headju buollit, muhto go leat goikan sieđggat seahká, de lea buorre buollit.


#!! * __seammaAdv__ selects
SELECT:seammaAdv ("seammá" Adv) IF (1 OLLU);
	#$ Soabadanráđis galget leat golbma miellahtu ja seamma ollu várrelahtut.

#!! * __unnanAdv__ selects
SELECT:unnanAdv ("unnán" Adv) IF (1 VFIN OR PrfPrc LINK NEGATE 0 N);
	#$ Eanaš duopmárat unnán dovdet eamiálbmogiid historjjá.
	#$ Dat orru oppanassii unnán čilgejuvvon 1800-logu gaskamuttu.

#!! * __varraAdv__ selects
SELECT:varraAdv1 ("várra" Adv) IF (*0 (N Nom) BARRIER S-BOUNDARY OR CC);
	#$ Mearkkašahtti Ruonáeatnama ruovttustivrii dál várra lea váttisvuohta ruhtadit buot áššesurggiid hálddašeami.
SELECT:varraAdv2 ("várra" Adv) IF (-1 CLB)(NEGATE *1 SG3-V);
	#$ Sápmelaččat geat ledje birgegoahtán dušše boazodoaluin, várra ledje buot friddjamusat.

#!! * __vehaziidAdv__ selects
SELECT:vehaziidAdv ("vehážiid" Adv) IF ((-1 ("vehážiid")) OR (1 ("vehážiid"))) ;
	#$ ...mii easkka maŋimuš áiggiid lea vehážiid vehážiid jávkagoahtán.

#!! * __visotdAdv__ selects
SELECT:visotAdv ("visot" Adv) IF (1 Dem LINK *1 N BARRIER NOT-A) ;
	#$ Visot dat stuora mánát leat boahtán.

#SELECT:r1426 ("visot" A Attr) IF (0 ("visot"))(1 (A Attr));
#	## Jus ulbmilat politihkain galget šaddat duohtan fertejit visot náššuvnnalaš unnitálbmogiid dárbbut ja beroštumit váldojuvvot vuhtii juohke oasis riikkas ja buot servodaga dásiin.
#SELECT:r1427 ("visot" A Attr) IF (0 ("visot"))(*1 (N Pl Gen) BARRIER NOT-PRE-A-N);
#SELECT:r1428 ("visot" A Attr) IF (0 ("visot"))(*1 (N Pl) BARRIER NON-PRENP);
#SELECT:r1429 ("visot" A Attr) IF (0 ("visot") LINK 1 Refl LINK *1 (N Pl) BARRIER NON-PRENP);
#	## Visot mánát leat boahtán.
#	## Ii leat vel visot čohkkejuvvon.
#	## Visot dan maid NRK sádde digitála fierpmis sáhttá maid gullat neahttaradios.
#	# This one doesn´t work.
#

#!! * __vuhtiiAdv__
SELECT:vuhtiiAdv ("vuhtii" Adv) IF (-1 ("váldit")) ;
	#$ ...muhto ferte váldit vuhtii máná agi ja láttasvuođa.

#!! !!Pronouns

#!! * __recipr, reciprPl__ select Recipr

SELECT:recipr Recipr IF ((-1 Recipr) OR (1 Recipr));
SELECT:reciprPl Pl IF (-1 (Recipr Pl))(0 Recipr);
REMOVE:reciprPl Pl IF (-1 (Recipr Du))(0 Recipr);
 	#$ ...de dadje guhtet guoibmáseaset:...
 	#$ Gielddat berrejit ásahit fásta dulkaveaga, kánske ovttasbarggus nubbi nuppiin.

REMOVE:Killrecipr Recipr IF ;
 	#$ Jos dajan nubbái: Mana! de son manná.

#!! !!Variant lemmas
LIST LEMMA1 = "Annika" "álbmot" "bargi" "bealli" "beassážat" "boadnji" "boalvvir" ("boaris" A Comp) "buvssat" "čábmit" "čáppat" "čuohti" "dadjalit" "dahkamuš" "dálolaš"  ("dát" Pron Dem Pl Com) "dološ" "eanaláigo#boahtu" "Elle" "gámmir" (".*geaisár") (".*gursa"r) "Jovnna" "kaleandar" (".*keallir"r) "lávži"
"lihpar" ("[0-9]*-lohku"r) "Máret" ("mieđihit") (".*mirko"r) "moattis" "motocrossa" "moraš" (".*muslima"r) (".*Norga"r) "oapman" "oasálaš" (".*rádji"r) "rájes" ("rikkis" A Comp Pl Nom) "ruošša#čuhti" "ruvdnu" "sáhpán" "smávvadálolaš" "speajal" "suoivva" "visot" "viššal" "vuollái" ;
    # first dictionary form

LIST LEMMA2 = "Annik" "álbmut" "bargit" "bealle" ("beassi" Der/Dimin) "boadnjá" "boalvár" ("boaris" A* Comp Der/at Adv) "buksa" "čápmit" "čáppis" "čuođi" ("dadjat" V* TV Der/l) ("dahkat" V* TV* Der/muš) ("dáigut" V* IV* Der/NomAg N Sg Com) ("dállu" N* Der/laš) ("doallu" N* Der/Dimin) "eanaláigo#boađđu" "Elli"  "gámmár" (".*geaissir") (".*gurse"r) "Jovdna" "Jovna" "kaleanddar" (".*keallár"r) "lávžá"
"libar" ("[0-9]*-loahku"r) ("[0-9]*-logus"r) "Máre" "mieđidit" (".*mirku"r) (".*mirkko"r) "moattes" "motocross" "mora" (".*muslim"r) (".*Norge"r) "oama" ("oassi" N* Der/laš) (".*rádjá"r) "rájis" ("rikkis" A* Comp Der/at Adv) "ruošša#čuđđi" "ruvdno" "sábin" ("smávvadállu" N* Der/laš) "speadjal" "suoivvan" "viso"  "viššalaš" "vuolli" ;

#!! * Remove lemma2 if lemma 1
REMOVE:lemma2 LEMMA2 IF (0 LEMMA1);
	#% Leago Čáhppes Bearral oahpis dutnje?

REMOVE:derHeapmi (N* Der/heapmi) IF (0 Der/NomAct) ;

#!! !Nouns, not verbs
#    ----------------

#!! * __headisge__, ii heađisge.
SELECT:headisge ("heahti" Loc Foc/ge) IF (0 ("heahtit")) ;
	## ii heađisge

#!! * __loahppa__ after TIME Gen.
SELECT:loahppa ("loahppa") IF (0 REAL-V LINK -1 TIME + Gen) ;

REMOVE:r595 VAbess IF (0 ("orohat") OR ("vuojahat"));
	## Guovlu lea orohaga olggobealde.

REMOVE:fatnasit ("fatnasit" Sg2) IF (0 ("fanas"));

REMOVE:PrsPrc PrsPrc IF (0 LEX-N + Sg)(NEGATE -1 ("nu") LINK 2 go)(NEGATE *1 Acc BARRIER NOT-A-ADV)(NEGATE -1 PLACE + Loc OR Adv OR Po)(NEGATE -1 ("leat") LINK 2 N) ;
	#$ Dainna ii leat vissis mearri galljodagas.
	# Should not hit cealkkavástaga with PrsPrc + Obj.

REMOVE:NomcopNom V (0 (N Sg Nom) LINK NOT 0 VFIN LINK 1 COPULAS LINK 1 (Sg Nom)) ;
	#$ Ii ge ságastallan leat vejolaš jus eai leat journalisttat geat máhttet sámegiela bures.


#!! !Lexical selection - nouns

    # ambigous lemma
    # - - - - - - - -
    # ambigous with respect to case, in which case one should not simply remove lemma2 (such as AMB-LEMMA2 Nom could be AMB-LEMMA1 Gen/Acc)

LIST AMB-LEMMA1 =  (".*Afrihká"r) (".*Amerihká"r) (".*bearal"r) (".*bovra"r)
(".*fenomena"r) (".*gámmir"r)  (".*guolbba"r)
(".*kaleandar"r) "keaissir" "lassá" (".*lassá"r) (".*kapihtal"r)(".*meašttir"r)
(".*partihkal"r) "savu" "skážir" (".*skibir"r) (".*speajal"r) (".*spigir"r) "varis" (".*varis"r)  ;

LIST AMB-LEMMA2 =  (".*Afrihkká"r) (".*Amerihkká"r) (".*bearral"r)  (".*bovrra"r)
(".*fenomen"r) (".*gámmár"r)  (".*guolban"r)
(".*kaleanddar"r) (".*kapihtal"r) "keaisár" "lasá" (".*lasá"r) (".*meaštár"r)
(".*partihkkal"r) "savvon" (".*skihpár"r) "skáhčir" (".*spihkár"r) (".*speadjal"r) "varris" (".*varris"r) ;

REMOVE:amblemma AMB-LEMMA2 IF (0 AMB-LEMMA1 LINK NEGATE 0 (Sg Nom) LINK NOT 0 Px);

LIST LEMMA-ADJ1 = "asehaš" "čáppat" "eahpesihkar" "guorus" "njálggat" "oppalaš" "sihkar" ;

LIST LEMMA-ADJ2 = "aseheapme" "čáppis" "eahpesihkar" "guoros"  "njálggis" "oppal" "sihkar" ;

REMOVE:lemmaAdj LEMMA-ADJ2 + A IF (0 LEMMA-ADJ1);

	# nisson, skihpár, spihkár jnv.
	# - - - - - - - - - - - - - - -
SELECT:r622 (".*nisu"r) (0 (".*nisson"r) LINK NOT 0 (Sg Nom)) ;
SELECT:r623 (".*nisu"r) (0 (".*nisson"r) LINK 0 (Sg Nom) LINK 0 Px) ;
  	#$ Gielddat sáhttet eambbo ulbmillaččat stivret iežaset servodaga ealáhusovdáneami, nu ahte sin gielda šáddá buore gieldan maiddái nissoniidda orrut.


	# albmi vs. almmái
	# - - - - - - - - -
SELECT ("albmi")(0 Ill LINK *-1 ("čohkket") OR ("ihtit") OR ("bajás") OR ("máhccat") OR ("váldit") OR ("mannat") OR ("čállit" Der/PassL) BARRIER SV-BOUNDARY) ;
	## Muhto čohkkejehket alccceseattet dávviriid albmái.

SELECT ("albmi")(0 Ill LINK *1 ("rahpat") BARRIER SV-BOUNDARY) ;

    # áitit vs. náitit
    # - - - - - - - - -
SELECT:r626 ("goddin#áitit") IF (0 ("goddi#náitit"));


    # bassi vs bas'si
    # - - - - - - - - -
REMOVE:bassiEss NomAg IF (0 ("bassi" Ess) LINK 1 VFIN);
REMOVE:bassi1 N IF (0 (A Ess) LINK *0 OPRED-V);
	## Maŋŋá jápmimis de aliduvvui Gonagas Olav bassin.
	# Try to make a general rule.


SELECT:bassi2 (A Attr) IF (0 ("bassi") LINK 1 (N Ess) OR ("vuoigŋa")) ;
	## Suomagiella šattai bassi giellan.
	## Bassi Vuoiŋŋas dievva Jesus máhcai Jordanis


SELECT:bassi3 NomAg IF (0 ("bassi" Sg Nom) LINK *-1 (N Gen) BARRIER NOT-NPMODADV) ;
	## Gulan go Bienne Káre-Ánne, skuvlla bassi, boahtá.


SELECT:bassi4 NomAg IF (0 ("bassi" N Ess) LINK *-1 ("bargat" V TV)) ;
	## Mun lean bargan bassin.


    # biile vs. biila
    # - - - - - - - -
SELECT:r638 VEHICLE IF (0 (".*biile"r));


    # boahtu vs. boađđu vs. boađus
    # - - - - - - - - - - - - - - -

SELECT:r660 ("boađus") IF (-1 COPULAS)(0 ("boahtu"))(1 Num);
SELECT:r661 ("boađus") IF (0 ("boahtu"))(1 COPULAS)(2 Num);
	## Maŋŋil láivves vuosttaš vuoru lei boađus 0-0.

	# borri
	# - - - -
REMOVE NomAg (0 ("borri") LINK *-1 (Num Gen))	;


    # čiekči vs. čiekčá
    # - - - - - - - - -
SELECT:r665 ("čiekči") (0 ("čiekčá") LINK *0 ("spábba") BARRIER S-BOUNDARY);



	# čuođi vs čuohti
	# - - -
SELECT:r667 ("čuohti") IF (0 ("čuđđi"))(1 MEASURE) ;
  	## Sin jahkásaš johtolagat sáhtte leat čuđiidge kilomehteriid, ja olle dálá Suomabeale vuovdeguovlluid dálveorohagain Jiekŋameara rittu geasseorohagaide.


    # eamit vs. eapmi
    # - - - - - - - -
REMOVE:r675 ("eapmi" Pl) IF (*1 SG-V BARRIER VFIN OR S-BOUNDARY);

	# eana
	# - - -
LIST EANAN = "eana"  ;

REMOVE:r677 Sg IF (*-1 ("stáhta") OR ("priváhta") OR ("matrikuleret"))(0 EANAN + (Pl Loc));
	#$ Stáhta eatnamiin.
SELECT ("eana") (0 EANAN) ;


    # geassi vs. geasis vs. geassi
    # - - - - - - - - -
REMOVE NomAg (0 ("geassi") LINK -1 DURATION-A OR TIME-A) ;
REMOVE NomAg (0 ("geassi")) ; #  This is preliminary

	# gielda vs gieldda
	# - - - - - - - - -
SELECT:r681 GIELDA IF (NEGATE 0 GIELDDA);
  	## Sosiála ja dearvvašvuođadepartemeanta lea dorjon Spesialistadoavtterguovddáža ja fylkkagieldda psykiatriija dearvvašvuođafuolahusa Kárášjogas.

SELECT:r682 ("gielda") IF (-1 FYLKA + Gen);


# gilvu vs. gilvvu Adv
# - - - - - - - - - -
SELECT:r684 ("gilvu") (0 Adv LINK *0 ("vuoitit") BARRIER SV-BOUNDARY);


	# guovddáš
	# - - - - -
SELECT:r693 Attr IF (0 ("guovddáš"))(1 N LINK NOT 0 TIME OR PLACE);
	## Guovddáš doaibmabidju lea vuođđudit sámegiela álgooahpahusa.


    # gurra
    # - - -
SELECT:r697 ("bákte#gurra") IF (0 ("bákte#guorra"));
SELECT:r698 ("lássa#gurra") IF (0 ("lássa#guorra"));


    # juogus vs. jugus
    # - - - - - - - - -

LIST JUGUS-V = "juhkat" "návddašit" "šlaggat" "vuoššat" ;

SELECT:jugus1 ("jugus") (0 ("juogus" Acc) LINK *0 JUGUS-V BARRIER S-BOUNDARY) ;
	## Áhčis biográfiijas Vest geažida mo duon erenomáš juhkosa návddašeapmi skihpáriiguin lei šaddagoahtán dehálaš áigegollun maŋŋá go lei máhccan ruoktot.

SELECT:jugus2 ("jugus") (0 ("juogus") + $$CASE LINK *0 FOOD + $$CASE BARRIER S-BOUNDARY) ;
 	## Mun válddán juhkosa ja márffiid mielde.

SELECT:jugus3 ("juogus") IF (-1 NATION + Gen OR SUOPMA + Gen OR SAPMI + Gen)(0 ("jugus")) ;
# LINK NOT *0 ("juhkat"));
	## Maŋŋel bođii Suoma juhkosa evttohus.

    # juste
SELECT:juste1 Adv IF (-1 V) (0 ("juste"));
SELECT:juste2 Adv IF (0 ("juste"))(1 Nom);

	# lága vs láhka
	# - - - -

SELECT:r712 ("lága") IF (0 ("lága"))(-1 ("eará") OR ("iežá"))(*-1 BOS LINK *1C ("lága")) ;
  	## Lága gáibida menddo olu eará lágaid ektui.
  	## Sii bidjet menddo stuora gáibádusaid min lágii eará lágaid ektui.
SELECT:r713 LAGA IF (0 LAGA) ;

	# láhka
	# - - -
 # The point here is to disambiguate "láhka" from "lága".

SELECT:r714 LAHKA IF (0 LAHKA);
  	## Čujuhuvvo maiddái sierramotiivvaide dearvvašvuođalága 45. paragráfii čuolbmačilgehusaid birra jávohisvuođageatnegasvuođa ja geigenlobi ektui epikriissa oktavuođas (oasi dearvvašvuođabargiidlágas oktan mearkkašumiiguin).

SELECT:r716 ("láhka") IF (0 ("lága"))(*1 ("fápmu") BARRIER V - COPULAS LINK 1 ("boahtin"));

#SELECT:r717 ("láhka") IF (0 ("lága") LINK *1 LAHKA-N OR ("kriminaliseret") BARRIER V - COPULAS);
	## Norgga stáda lea iešguđet lágaid ja láhkaásahusaid bokte geatnegahtton.
	## Sosiálabálvalusaid lága § 4-3.

#SELECT:r718 ("láhka") IF (0 ("lága"))(*-1 LAHKA-N OR ("gč") OR ("kriminaliseret") BARRIER V - COPULAS) ;
	## Gč. lága suoidnemánu b. 1992 nr. 97.
  	## Fápmuiboahtin ja eará lágaid rievdadusat.
  	## Gonagas addá láhkaásahusa lága geavaheami birra.

#TEMPLATE lahkanoun = (*-1 LAHKA-N OR ("gč") OR ("kriminaliseret") BARRIER V - COPULAS) OR (*1 LAHKA-N OR ("kriminaliseret") BARRIER V - COPULAS) ;

#SELECT ("láhka") IF (T:lahkanoun)(0 ("lága")) ;

#SELECT ("láhka") IF ((*-1 LAHKA-N OR ("gč") OR ("kriminaliseret") BARRIER V - COPULAS) OR (*1 LAHKA-N OR ("kriminaliseret") BARRIER V - COPULAS)) ;


SELECT:r719 ("láhka") IF (0 ("lága"))(-1 ("rievdadit") OR ("jorgalit") LINK 0 Der/Pass);
SELECT:r720 ("láhka") IF (0 ("lága"))(*1 ("rievdadit") OR ("jorgalit") BARRIER NOT-AUX-V LINK 0 Der/Pass);
	## Rievdaduvvon láhka....
	## Ođđa lágat galget jorgaluvvot sámegillii.

SELECT:r721 ("láhka") IF (*-1 MANNU LINK 1 Ord LINK 1 ABBR LINK 1 Num)(0 ("lága"));
	## Dat lea dahkkon skábmamánu 28. b. 1898 lága mielde.

SELECT:r722 ("láhka" Loc) IF (0 ("lága"))(*-1 BOC LINK *1 LAHKA-V-PASS LINK 0 Der/Pass) ;
	## Dat lea mearriduvvon lágas 33.
  	## Ii adnon norgga lágas daid attáldagaid birra maid dáččat mákse iežaset gonagassii.

SELECT:r723 ("láhka") IF (0 ("lága" Loc))(*-1 ("reguleret") OR ("sihkkarastit") OR ("ovdanboahtit") OR Der/Pass + ("oaidnit") BARRIER REAL-V OR S-BOUNDARY) ;
	## Dan ferte reguleret lágain.

SELECT:r724 ("láhka") IF (0 ("lága"))(1 ("guoskat") OR ("gustot")) ;
SELECT:r725 ("láhka") IF (0 ("lága"))(*1 Rel BARRIER WORD LINK 1 ("guoskat") OR ("gustot")) ;
	## Lágat gusket moadde gielddaide Norrbottena leanas.
	## Vuođđun leat maiddái dálá lágat, mat gustojit mánáid- ja nuoraidsuorgái.

SELECT:r726 ("láhka") IF (*1 ("mearridit") OR ("čuoldit") BARRIER VFIN)(0 ("lága" Sg Gen))(*1 Po BARRIER V);
	## Dat lea mearriduvvon lága 3 mielde.
SELECT:r727 ("láhka") IF (*-1 Gen BARRIER NOT-A LINK 0 LAHKA-MOD OR NATION-NOT-NORGA)(0 ("lága"));
  	## Ráđđehus háliida lágidit dearvvašvuođa- ja sosiálabálvalusaid oktagaslaš dárbbuid mielde, earret eará pasientavuoigatvuođaid lágain ja dearvvašvuođabargiid lágain.

SELECT:r728 ("láhka") IF (-1 LAHKA-A)(0 ("lága"));

SELECT:r729 ("láhka") IF (0 ("lága"))(1 LAHKA-POST)(*-1 BOC LINK *1 LAHKA-N OR LAHKA-V-PASS) ;
SELECT:r730 ("láhka") IF (0 ("lága"))(1 LAHKA-POST)(*1 LAHKA-N OR LAHKA-V-PASS BARRIER S-BOUNDARY) ;
  # In addition to a LAHKA-POST, there is a noun or pass.verb which make it more likely to be "láhka".
  	## Sis lea leamaš geatnegasvuohta čállit journálaid maiddái ovddeš lágaid mielde.
  	## Lága mielde lei vejolaš oažžut albma eanangirjji mii dáhkidii  oamastanvuoigatvuođa gávpe- ja árbeoktavuođain ge.

SELECT:r731 ("láhka") IF (0 ("lága"))(*-1 BOS LINK *1 LAHKA-SUBJ-N LINK 0 NP-HEAD-NOM LINK *1 LAHKA-V) ;
  	## Guovddášeiseválddit árvaledje lága vai sidjiide livčče vejolaš váikkuhit Finnmárkku čearddalašvuođadiliide.

SELECT:r732 ("láhka") IF (0 ("lága"))(*-1 BOS LINK *1 LAHKA-V LINK *1 LAHKA-ASAHUS LINK 0 Ill OR Loc) ;
  	## Dáidá šaddat váttis ovddidit lága Stuoradiggái dál.

SELECT:r733 ("láhka") IF (0 ("lága"))(-1 ("čuovvut") OR ("čuovvulit")) ;
  # If it´s "lága", then one should expect a Gen., like "sin lága, gávpoga lága".
  	## Olbmot eai čuovo lága.

SELECT:r734 ("láhka") IF (0 ("lága" Gen))(*1 LAHKA-GEN-N BARRIER NOT-A) ;
  	## Dan oaidná lága ráhkkananbarggus.

SELECT:r735 ("láhka") IF (0 ("lága"))(1 LAHKA-POST)(2 ("lohpi") OR LAHKA-N) ;
SELECT:r736 ("láhka") IF (-1 ("lohpi") OR LAHKA-N)(0 ("lága"))(1 LAHKA-POST) ;
  	## Sis lei lága mielde lohpi vuosttaš vuovdimis prioriteret dážaid sámiid ja láddelaččaid ovddabeallái.
  	## Sis lei lohpi lága mielde.

SELECT:r737 ("láhka") IF (0 ("lága"))(1 ("olis") OR ("vuođul") OR ("ovdal")) ;
  	## Ii mihkkege hehtte nammadeames ovddasvástideaddji maiddái dakkár doaimmain mat eai adno
  	## dearvvašvuođaásahussan lága olis.

SELECT:r738 ("láhka") IF (0 ("lága") LINK *1 ("vuostá" Po) BARRIER NOT-N LINK 1 ("váldit") LINK 1 ("vuhtii"));
	## Mii geahččat bearrái ahte lágat vealaheami vuostá váldojuvvojit vuhtii.


SELECT:r739 ("láhka") IF (0 ("lága" Acc))(*-1 BOC LINK *1 LAHKA-V) ;
  	## Ja de bohciida jearaldahkan leat go sii geat dán lága čálle, bidjan sápmelaččaid sisriikalažžan.
  	## Stáhta galgá jorgalit lága sámegillii.
	# láhka in coordination

SELECT:r740 ("láhka") IF (0 ("lága"))(-1 ("eará") OR ("iežá"))(*-1 BOS LINK *1C LAHKA) ;
  	## Dan áiggi rájes go láhka fápmui biddjo, dahkkojit čuovvovaš njuolggadusat eará lágain.

SELECT:r741 ("láhka") IF (0 ("lága"))(*-1 LAHKA) ;
SELECT:r742 ("láhka") IF (0 ("lága"))(*1 LAHKA) ;
  	## Go teavsttas lea guhkes láhkanamahus, de sáhttá lága namahusa bidjat aisttonmearkkaid sisa.
  # This one might make trouble for "lága".

SELECT:r743 ("láhka") IF (0 ("lága"))(1 CC OR COMMA LINK 1 ("konvenšuvdna") OR ("láhkaásahit" Der/NomAct) OR ("láhkaovdabargu") OR ("politihkka") OR ("ekonomiija") OR ("riektegeavat") OR ("hálddašit" Der/NomAct));
SELECT:r744 ("láhka") IF (0 ("lága"))(-1 CC OR COMMA LINK -1 ("konvenšuvdna") OR ("láhkaásahit" Der/NomAct) OR ("láhkaovdabargu") OR ("politihkka") OR ("ekonomiija") OR ("riektegeavat") OR ("hálddašit" Der/NomAct));
	## Gihpa gieđahallá maid vuoigatvuođaid, mat vuođđuduvvet lágaide ja konvenšuvnnaide.
  	## Sámi perspektiiva lea mielde dehálaš dearvvašvuođa- ja sosiálpolitihkalaš guorahallamiin ja mearrádusdokumeanttain, nugo lágain, láhkaásahusain ja bagadusmateriálain.

SELECT:r745 ("láhka") IF (*-1 CC BARRIER NOT-NPMOD LINK -1C LAHKA OR TEXT OR ("stáhta"))(0 ("lága"));
SELECT:r746 ("láhka") IF (*1 CC LINK *1C LAHKA OR TEXT OR ("stáhta") BARRIER NOT-NPMOD)(0 ("lága"));
  	## Máŋggabealatvuohta ja ovttadássásašvuohta čuovvula maiddái ođđa pasientavuoigatvuođalágaid ja lága dearvvašvuođabargiid birra.


    # lávka/lávki vs. lávke
    # - - - - - - - - - - -
SELECT:r747 ("jiehtanas#lávki") IF (0 ("jiehtanas#lávke"));

  	# logi
  	# - - -
SELECT:r749 ("logi") IF (0 ("loahki" Com) LINK *1 (N Com) BARRIER NOT-A);
	## Almmiriika sáhttá veardiduvvot logiin nuorra nieiddain geat válde lámppáset ja vulge olggos irggi ovddal.


    # luohkká vs. luohká vs. luohkki
    # - - - - - - - - - - - - - - - -
LIST LUOHKKA = "gártnet#luohkká"  ;

SELECT:r757 LUOHKKA IF (0 LUOHKKA);

	# luopmu vs. lupmu (we don´t know the semantics of the latter one)
	# - - - - - - - - -
SELECT ("luopmu") (0 ("lupmu")) ;

  	# mális vs málli
  	# - - - - - - - -
REMOVE:r3586 ("málli") IF (*0 ("duoldat") BARRIER SV-BOUNDARY)(0 ("mális" Nom));
REMOVE:r3586 ("málli") IF (*-1 go BARRIER NOT-NPMOD LINK -1 ("duoldat"))(0 ("mális" Nom));

	## Duolddaža go mális vel?

    # mánni vs. mánná vs mánná G3
    # - - - - - - - -
REMOVE G3 (0 ("mánná") LINK NEGATE *-1 ("borrat") BARRIER NOT-ADV-PCLE LINK *1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE) ; # Vi må lage flere regler for når det kan være G3. i NT er det mánna

	# meassut
	# - - - - -
SELECT ("meassut") (0 ("meassu"));


	# meastagis
	# - - - - -
SELECT:r764 N IF (0 ("measta" Adv Foc/gis));


    # moalla vs. molla
    # - - - - - - - -
LIST MOALLA-V = "báhčit" "boahtit" "bidjat" "bidjalit" "coggalit" "čiekčat" "čievččastit" "deaddilit"  "dearpalit" "fieraldahttit" "galkat" "gárrut" "joavdat" "loktestit" "nahkehit" "nivkalit" "roaiskalit" "viehkat" ;
# when "moalla" Ill

SELECT ("moalla") IF (-1 ("rabas"));
SELECT:r766 ("moalla") IF (*-1 MOALLA-V BARRIER SV-BOUNDARY)(0 ("molla" Ill));
SELECT:r768 ("moalla") IF (*-1 ("bidjat") OR ("mannat") BARRIER SV-BOUNDARY)(0 ("molla" Ill));


    # ráhkis vs. ráhkká
    # - - - - - - - - - -
REMOVE ("ráhkká" Ill) (0 ("ráhkis") LINK 1 Po);
	## Nieida bođii ráhkkásis mielde.

    # reavri vs. revre
    # - - - - - - - - -
SELECT:r773 ("ruovde#revre") IF (0 ("ruovde#reavri"));

    # riggi vs. rikkis
    # - - - - - - - - -
SELECT:r774 ("riggi") IF (*0 ("dolla") OR ("lávvu") OR ("báhti") OR ("árran") OR ("heaŋgát") BARRIER S-BOUNDARY)(0 ("rikkis")) ;


    # ruoktu vs. rukto/ruktu
    # - - - - - - - - - - - -
#SELECT:r777 ("ruoktu") + Px ;
	## Ale bala váldimis Márjjá ruktosat áhkkán.

SELECT:r778 ("poasta#ruktu") IF (0 ("poasta#ruoktu"));
SELECT:r779 ("turnea#ruvtto") IF (0 ("turnea#ruktu"));

    # ruvke vs ruvki
    # - - - - - - - -
SELECT:r780 ("urána#ruvke") IF (0 ("urána#ruvki"));


	# Prop Plc Der/t
	# - - - - - -
REMOVE:r782 (Prop Der/t) IF (0 ("sápmi") OR ("ruošša") OR ("duiska"));

    # sáhka vs. sáhkat
    # - - - - - -- - -
#!! * __sahkaEss__ if Mii lea sáhkan.
SELECT:sahkaEss ("sáhka" Ess) IF (0 PrfPrc LINK -1 COPULAS LINK -1 ("mii" Nom)) ;

#!! * __sahkaPl__ after PLURALIZER in NP
SELECT:sahkaPl ("sáhka" Pl) IF (0 TV LINK *-1 PLURALIZER BARRIER NOT-NPMOD) ;


	# sissi vs siessá
	# - - - - - - - -
REMOVE:r783 ("siessi") + Px IF (0 ("siessá"));


	# soadji vs. sodji
	# - - - - - - - - -
SELECT:r785 ("soadji") IF (0 ("sodji"))(1 BIRD LINK 0 Gen);
	## Eará symbola lei Gáissáža soajit, ja manin dás geavahedje juste lotti sojiid?

    # soahti vs. sohti/sođđi
    # - - - - - - - - - - - -
SELECT:r787 ("Krim#soahti") IF (0 ("Krim#sohti"));

 	# suorgi vs. suorggis
	# - - - - - - - - - -
SELECT:r789 ("suorgi") IF (-1 ("priváhtta"))(0 ("suorggis"));
SELECT:r790 ("áittardan#suorgi") IF (0 ("áittardan#suorggis"));
  	## Mot lea sámi nissoniid posišuvdna obbalaččat bargoeallimis (vuođđoealáhusain, almmolaš- ja priváhta suorggis , smávva fitnodagain je.) ja erenoamážit go leat bargin sámi ásahusain ja organisašuvnnain ?
  # A coordination rule might work in this case too.


	# testameanta vs. testamentet V
	# - - - - - - - - - -
REMOVE:r792 V IF (-1 ("ođas"))(0 ("testameanta"));


	# viergi vs. virgi
	# - - - - - - - -
LIST VIRGI =  (".*virgi"r) ;

LIST VIRGI-V = "almmuhit" "árvalit" "ásahit" "bealljemerket" "dagahit" "deavdit" "geahpedit" "gilvalit" "gustot" "guoskat" "hálddašit" "heaittihit" "lasihit" "nannet" "ohcat" "ruhtadit" ;

LIST VIRGI-ADJ = "almmolaš" "bealle" "bissovaš" "bistevaš" "dieđalaš" "fásta" "girkolaš" "hálddahuslaš" "lassi" "mearrideaddji" "rabas" "sadjásaš" "2/3" "7,5" ;

# Verbs are in Der/NomAct:
LIST VIRGI-N = "ásaheapmi" "bálká" "deavdin" "doaibmagollu" "gollu" heaittiheapmi "lohku" "virgáibidjan" ;

LIST VIRGI-LOC-N = "almmuhusteaksta" "vuorrobargu" ;

SELECT:r796 VIRGI IF (0 VIRGI) ;

SELECT:r797 ("virgi") IF (*-1 ORGANIZATION OR INSTITUTION BARRIER NOT-A LINK 0 Gen)(0 ("viergi"));
	## Nuoraidskuvlla virggiin lea buorre bálká.
SELECT:r798 ("virgi") IF (0 ("viergi"))(1 ORGANIZATION OR INSTITUTION LINK 0 Loc);
	## Dat sáhtášedje maid viššaleappot čuovvulit báikegotti studeanttaid ja fállat daidda virggiid ruovttugielddas.

SELECT:r799 ("virgi") IF (-1 PROFESSION)(0 ("viergi"));
  	## Eai leat lahkage doarvái logopedat, geat dikšot geavaheaddjiid klinihkain, eaige sámiid ássanguovllus leat galle gieldda, mat leat ásahan logopeda virggiid.

SELECT:r800 ("virgi") IF (-1 VIRGI-ADJ)(0 ("viergi")) ;
	## ... almmolaš virggiid ..
SELECT:r801 ("virgi") IF (*-1 VIRGI-V BARRIER SV-BOUNDARY)(0 ("viergi" Acc)) ;
SELECT:r802 ("virgi") IF (*1 VIRGI-V BARRIER SV-BOUNDARY)(0 ("viergi" Acc)) ;
	## Olbmot eai oza diekkár virggiid.
	## Hálddahus berre nannet suorggi ovttain virggiin.

SELECT:r803 ("virgi") IF (0 ("viergi") LINK *-1 ("bidjat") OR ("oažžut") OR ("gáibidit") OR ("beassat") OR  ("háliidit") BARRIER SV-BOUNDARY LINK *-1 ("olmmoš") OR ("fágaolmmoš") OR ("kandidáhta") OR EDUCATION OR HUMAN BARRIER NOT-A-ADV) ;
SELECT:r804 ("virgi") IF (0 ("viergi") LINK *1 ("bidjat") OR ("oažžut") OR ("gáibidit") OR ("beassat") OR  ("háliidit") BARRIER SV-BOUNDARY LINK *1 ("olmmoš") OR ("fágaolmmoš") OR ("kandidáhta") OR EDUCATION OR HUMAN BARRIER NOT-A-ADV) ;
	## virggiide biddjojit olbmot
	## oažžut olbmuid virggiide

SELECT:r805 ("virgi") IF (*0 ("fállat") OR ("beassat"))(0 ("viergi" Ill)) ;

SELECT:r807 ("virgi") IF (1 ("guoskevaš") LINK 1 ("resursa"))(0 ("viergi" Ill)) ;
SELECT:r808 ("virgi") IF (*1 ("hálddahus") BARRIER NOT-NPMOD)(0 ("viergi" Ill)) ;
SELECT:r809 ("virgi") IF (0 ("viergi" Ill) LINK *-1 ("ohcat") BARRIER SV-BOUNDARY);

SELECT:r810 ("virgi") IF (0 ("viergi") LINK *-1 BOS LINK *1 ("virgáibidjat") OR ("váldo#bidjan") OR ("bálká")) ;

SELECT:r811 ("virgi") IF (0 ("viergi" Gen))(*1 VIRGI-N BARRIER NOT-A) ;
	## Mii váldit Finnmárkku olggobeale virggiid goluid, maiguin ásahit dakkár gelbbolašvuođa...

SELECT:r812 ("virgi") IF (*-1 ("luohpat") OR ("bissut") OR ("bidjat") BARRIER NOT-ADV)(0 ("viergi" Loc));
SELECT:r813 ("virgi") IF (0 ("viergi" Loc))(*1 ("luohpat") OR ("bissut") OR ("bidjat") BARRIER NOT-NPMOD);
   	## Virgeloguid lea suohkan dássážii geahpedan dainna lágiin go olbmot luhpet virggiin.
   	## Doavttir lea máŋgii biddjon eret virggiin miehtá Suoma.

SELECT:r814 ("virgi") IF (0 ("viergi" Loc))(1 COPULAS)(*2 HUMAN OR VIRGI-LOC-N BARRIER NP-HEAD-NOM LINK 0 Nom);
SELECT:r815 ("virgi") IF (*-1 HUMAN OR VIRGI-LOC-N BARRIER NOT-NPMOD OR REAL-V LINK 0 NP-HEAD-NOM)(0 ("viergi" Loc));
	## Virggiin leat nu ollu sámegielat doaktárat go vejolaš.

SELECT:r816 ("virgi") IF (*0 ("resursavátnivuohta") OR ("servodatsuorgi") BARRIER NOT-NPMOD)(0 ("viergi" Gen))(1 ("ektui"));

SELECT:r818 ("virgi") IF (0 ("viergi" Loc) LINK 1 COPULAS LINK 1 VIRGI);
	## Okta dain virggiin lea giellamielbargivirgi.

	# vuorru
	# - - - -
REMOVE G3 (0 ("vuorru") LINK -1 HAB-ACTOR + Gen)	;
	## Odne lea mu vuorru.
	## Vuorru lea leamaš jagi jogas.


    # vuovdi
    # - - - -

LIST FOREST-V = "boaldit" "hukset" "šaddadit" "njáskat" "njiedjat" "njeaidit" "čuollat" "čorget" "njiedjat" "ođasmuvvat" "ridnut" "čáhpodit" "ávkkástallat" "šuvvat" ;
 # "hukset" does possibly not belong to this set, same goes for "šaddadit". May need spesific rules.

LIST FOREST-ADJ = "galmmas" "suohkat" "viiddis" "stuoris" "smávis" "goikkis" "ruonas" ;

LIST FOREST-PREP = "guovdu" ;

LIST FOREST-WORD = "Amazonas" ;

SELECT:vuovdiHumanActivity NomAg (0 ("vuovdi" Sg Nom) LINK *0 HUMAN-ACTIVITY-V + Sg3 OR DADJAT + Sg3 OR VERBAL-ACTIVITY + Sg3 OR HUMAN-AGENT-V + Sg3 BARRIER SV-BOUNDARY OR COMMA) ;
	## Vuovdi bolddii buot báhpáriid ovdalgo politiijat bohte.
	## Vuovdi dajai munnje dan.

SELECT:vuovdiPlc Sem/Plc IF (*-1 MOVEMENT-V BARRIER NOT-ADV-PCLE)(0 ("vuovdi" Sg Ill)) ;
	## Luonddufága láide dien guvlui ahte galgá váldit vuođu báikkálaš luonddus, muhto buot ovdamearkkat váldet gis vuođu máttanorgga diliin, oahppit galget mannat vuovdái geahččat giđđaliđiid ja čoaggit hasselnihtiid ja ruvsumurjjiid.
	## Go Niillas olle vuovdái ja čuollá ovtta muora, de son oaidná ahte ii doppe nagot čuollat buot muoraid.

SELECT:vuovdiPlc ("vuovdi" Sem/Plc) IF (0* RIEKTEDILLI BARRIER S-BOUNDARY) ;

SELECT:vuovdiPlc2 Sem/Plc IF (*-1 FOREST-ADJ + Attr BARRIER NOT-NPMODADV)(0 ("vuovdi" Sg Nom) OR ("vuovdi" Sg Ill)) ;
	## Diekko galgá šaddat nu stuora ja suhkkes vuovdi ahte ii mihkkege beasa čađa.
	## Áhkku manai stuora, sevdnjes vuovdái.

SELECT:vuovdiCOP ("vuovdi" Nom NomAg) IF (*1 COPULAS BARRIER V OR S-BOUNDARY LINK *1 NUMAG-A + Nom BARRIER NOT-ADV-PCLE) ;
	## Vuovdi lea čeahppi.

SELECT:vuovdiPlcCOP ("vuovdi" Nom Sem/Plc) IF (*1 COPULAS BARRIER V OR S-BOUNDARY LINK *1 FOREST-ADJ + Nom OR FOREST-V + (TV Der/PassS PrfPrc) OR FOREST-V + (TV Der/PassL PrfPrc) OR FOREST-V + (IV PrfPrc) BARRIER NOT-ADV-PCLE) ;
	## Vuovdi lei goikkis dego geassit láviige.
	## Vuovdi lea njeidon.

REMOVE:vuovdi1 Actor IF (*-1 FOREST-V OR ("báhcit") BARRIER S-BOUNDARY)(0 ("vuovdi" Ill)) ;
	## Áhčči njiejai váris vuovdái.
	## Piera bázii vuovdái, muhto áhčči manai ruoktot.

REMOVE:vuovdi2 NomAg IF (*1 ("buollit") OR ("šuvvat") BARRIER S-BOUNDARY)(0 ("vuovdi" Nom));
	## Vuovdi bulii mannan vahkkus Ivgubađas.

REMOVE:vuovdi3 NomAg IF (*-1 FOREST-PREP BARRIER S-BOUNDARY)(0 ("vuovdi" Sg Nom)) ;
	## Dii galgabehtet čuohppat aivve guovddáš vuovddi das go guovdu lea buoremus vuovdi.

REMOVE:vuovdi4 NomAg IF (*1 FOREST-V BARRIER S-BOUNDARY)(0 ("vuovdi" Sg Nom)) ;
	## Vuovdi ii bálle ođasmuvvat go ealgavallji lea buot ođđa muoraid jávkadan.

REMOVE:vuovdiEss Sem/Plc IF (*-1 ("bargat") BARRIER S-BOUNDARY)(0 ("vuovdi" Ess)) ;
	## Mun barggan vuovdin.

#REMOVE:vuovdiNUMAG-A Sem/Plc IF (-1 NUMAG-A)(0 ("vuovdi"));
#REMOVE:vuovdiLOCATION NomAg IF (-1 ADJ-LOCATION)(0 ("vuovdi"));
	#I suspect these do not have any function at this stage. Concider removing them.


REMOVE:vađastit NomAg IF (*-1 ("vađđa"))(0 ("vuovdi" Nom)) ;
	##Vađas goasii vuovdi jávká.

REMOVE:vađastit2 (V "vađastit") IF (0 ("vađđa" Sem/Plc)) ;
	##Vađas goasii vuovdi jávká.
	#Not a good rule if the tag "Sem/Plc" is given an other name in the future.




#!! * __UsImprt__ removes Imprt Sg3 for all nouns in -us
LIST UN-N = (".*us"r N) ;
REMOVE:UsImprt Imprt IF (0 UN-N OR ("oažžut") LINK NOT 0 ("šaddat")) ;

#!! * __SUBImprt__ removes Imprt when it can be a part of an NP
REMOVE:SUBImprt Imprt IF (0 (Sg Nom) OR Attr) ((*1 (V Sg3) BARRIER S-BOUNDARY OR REAL-V) OR (*1 (Rel Nom) LINK *1 VFIN BARRIER S-BOUNDARY OR REAL-V LINK *1 COMMA LINK 1 (V Sg3))) ;
	#$ Bargu sámi fanasduddjonvieruid ektui lea dakkár bargu masa Sámediggi sáhtášii váikkuhit eanet dutkama bokte .

REMOVE:SUBImprt2 (TV Imprt) IF (0 Attr LINK NOT *1 Acc BARRIER S-BOUNDARY)(1 N)   ;
	#$ Njuolggadusat meahccegeavaheami rievdamis – rabas gulaskuddan.

REMOVE:r599 VFIN IF (-1 ("makkár"))(0 N);
REMOVE:r600 Inf IF (-1 ("makkár"))(0 N);
	## Makkár gáhkut dis leat?

#!! * __oahppit__, ii Imprt.
SELECT:oahppit ("oahppi" Pl Nom) (0 Imprt LINK 1 VFIN + Pl3) ;
	#$ Oahppit leat čeahpit.

#!! !mánnu vs mánus
REMOVE ("mánus") (0 ("mánnu" Loc) LINK 0 Gen) ;

#!! !Not noun


#!! !!Not adposition

#!! * The rules __Pooaivai, Pogiedas__ removes oaivái and gieđas as Po
REMOVE:Pooaivai Po IF (0 ("oaivái") LINK NOT -1 HUMAN) ;
	#$ Eksempel fra N/S som skal ha Po: Válden vealggi vielljan oaivái.
REMOVE:Pogiedas Po IF (0 ("gieđas") LINK 1 ("eret")) ;
	#$ Eksempel fra N/S som skal ha Po: Go bohccot leat olbmuid gieđas.

#!! !!Not Qst

#!! * __AdvQst__ removes dego/nugo Qst
REMOVE:AdvQst (Adv Qst) OR (Po Qst) OR (Pr Qst) IF (0 CS)(NEGATE 1 ("?"));
 	#$ Su ámadadju báittii dego beaivváš.
 	#$ Nugo dieđát, de lean dál.


#!! !!Interjektions

#!! * __Interjlemma__ voja voja nana nana select interj if repeated
LIST INTERJ-LEMMA = (".*"r Interj) ;

SELECT:Interjlemma_a $$INTERJ-LEMMA IF (1 $$INTERJ-LEMMA);
SELECT:Interjlemma_b $$INTERJ-LEMMA IF (-1 $$INTERJ-LEMMA) ;
	#$ Guohttehehket dál dearvan mu bohccuid, nana nana voja voja.


#!! !!Southern Locative vs. Essive

#!! * __SouthLoc__ removes Southern Locative vs. Essive
REMOVE:SouthLoc (Loc South) ;
#REMOVE:SouthLoc (N Sg Loc South) OR (A Sg Loc) IF (0 Ess)(NEGATE -1 CC LINK -1 Loc LINK NOT 0 Ess);
#REMOVE:SouthLoc (N Sg Loc South) OR (A Sg Loc) IF (0 Ess)(NEGATE 0 Actio LINK NOT 0 ESS-ADVL-N)(NEGATE -1 CC LINK -1 Loc LINK NOT 0 Ess);
 	#$ girkun # This is a rule that prevents southern dialectal forms to destroy the rest of the # parser. It should be commented out whenever analysing texts with Locative -n only. # All words with Cg will avoid this rule, since e.g. várin is southern Loc only.
	#$ Luossa-bivdin joddu-buođuin deanus.

#!! !!Px-rules for special nouns

#!! * __NnoPx__ Remove Px for special nouns
REMOVE:NnoPx Px IF (0 N-NOPX + Px OR Num + Px OR Der/NomAct + Px OR MANNU + Px OR WEATHER-SITUATION + Px);
	#$ Čoahkkin lea eahkedis.
	#$ Mun gal ceavzzán du goargŋumis.

#!! * __gaskaneaset__ selects Po for gaskaneaset
SELECT:gaskaneaset Po IF (0 ("gaskan") + Px)(NOT 1 VGen);
	#$ Sii árvališgohte gaskaneaset.

#!! !!Some verb rules

#!! * __vfingo__ selects VFIN in front of go Qst
SELECT:vfingo VFIN IF (-1 BOC)(NOT 0 OLLU OR Adv)(1 go + Qst LINK NOT 1 VFIN) ;
	#$ Dáhtut go min gaikut dan eret?
	#$ Lea go dus ruhta?

#!! * __buoritV__ removes buorit as V
REMOVE:buoritV V IF (0 ("buorre" Pl Nom) LINK 1 (N Pl)) ;
	#$ Das leat buorit lanjat.

#!! * Some brave rules for removing Imprt
REMOVE:TESTImprt Imprt - ConNeg - Neg IF (NEGATE -1 CC OR BOS OR COMMA OR (":") OR ("de") OR PUNCT OR ("""))(NEGATE 0 (Imprt Sg3) LINK -1 (Pron Sg3) OR (N Sg) OR ("nu"))(NEGATE 0 (Imprt Sg3) LINK 1 (Pron Sg3) OR (N Sg));
	#% Geas ležžet bealjit, son gullos!
	#% Nu báitos din čuovggas ge olbmuide.

SELECT:RelVFIN VFIN (0 Pl3 OR Du3 LINK -1 (Rel Pl Nom)) ;
	#$ Oahpaheaddjit geat leat leamaš dán suorggis leat sihke sámit ja dážat, muhto dat geat bohte olggobealde ledje ovdal bargan dearvvašvuođabálvalusas sámi guovlluin


#" syntax colouring.

REMOVE:ImprtCase Imprt IF (0 $$CASE LINK -1 COMMA OR CC LINK -1 $$CASE)(NOT 0 (Imprt Sg3))(NEGATE -3 BOS) ;

REMOVE:ImprtCoor Imprt IF (-1 CC LINK -1 VFIN-NOT-IMPRT) ;


#!! * __ImprtCopPrfPrc__ removes imperative readings in front of coopulas and PrfPrc
REMOVE:ImprtCopPrfPrc Imprt IF (-1 BOS)(*1 COPULAS BARRIER NOT-ADV LINK 1 PrfPrc) ;
	#$ Oahppit leat lohkan ollu girjjiid.

#!! * __FocV__ revmoves Foc when Actio, PrfPrc, VGen, e.g. čađahan, ovttasge
REMOVE:FocV Foc IF (0 Actio OR PrfPrc OR Pron LINK NOT 0 Pron + Foc);
	#$ Studeanttat geat leat čađahan váldofágaeksámena...
	#% Mo son Duommá lea dahkan? #Comment: Hits, but shouldn't.




#!! !Particular CS

#!! * __madeCS__ for mađe/mađi and dađe/dađi
IFF:madeCS MADE + CS IF  (*1 DADE) ;

#!! * __dadeCS__ for mađe/mađi and dađe/dađi
IFF:dadeCS DADE + CS IF (*-1 MADE) ;
	#$ Mađe mielde oaivi, dađe mielde ságat.
	#$ Mađe stuorit RAM-muitu du dihtoris lea, dađe eanet dihtorprográmmaid sáhtát geavahit oktanaga.
#!! !Verb or Noun?

SELECT:buresboahtinIll Ill IF (*-1 ("boahtin" Nom) BARRIER REAL-V OR S-BOUNDARY LINK -1 ("bures")) ;
	#$ Bures boahtin Čáhppes Bearralii.


SELECT:VFINSg3TEST VFIN-NOT-IMPRT + Sg3 IF (*-1 NP-HEAD-SG-NOM BARRIER NOT-NPMODADV LINK NOT 0 VFIN LINK NOT *-1 VFIN BARRIER S-BOUNDARY)(NEGATE *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 Pl3 LINK *-1 NP-HEAD-PL-NOM) ;
	#$ Fápmu čiehká iežas luottaid.
	#$ Go čálalaš kultuvra lea eahpedábálaš minoritehtaid gaskkas ja go oskkáldasvuohta ja heahpu lea leamaš dábálaš, de sáhttá dás oaidnit ráhkadusdovdomearkka, nammalassii ahte fápmu čiehká iežas luottaid.
	#% Bures boahtin Čáhppes Bearralii.
	#% Nuorat ođđe buvrriin geasset, muhtumin dálvet maid.


#!! * __Včiehká__ selects V instead of N when nomintive to the right and accusative to the left ''fápmu čiehká luottaid''
SELECT:Včiehká (V TV)  IF (*-1 (N Nom) BARRIER SV-BOUNDARY OR NP-BOUNDARY)(0 ("čiehká"))(*1 (N Acc) BARRIER NOT-NPMODADV) ;
	#$ Go čálalaš kultuvra lea eahpedábálaš minoritehtaid gaskkas ja go oskkáldasvuohta ja heahpu lea leamaš dábálaš, de sáhttá dás oaidnit ráhkadusdovdomearkka, nammalassii ahte fápmu čiehká iežas luottaid.


#!! !!Adpositions

#!! !Adpositions, not verbs

#!! * __bealisPo__ removes imperatives when Po lookalikes
REMOVE:bealisPo Imprt IF (-1 Gen)(0 Po);
	## Dan bealis.





#!! !!!Section 2: LOCAL DISAMBIGUATION - CYCLE 1
# ==================================
# ==================================

SECTION


#!! !!FAMILY pronouns

#!! !Pron Pers 1. p.

#!! * __moai__ This rule is not in use because of REMOVE:Prop
#SELECT:moai MOAI ( *1 DU1-V);
	#$ Moai čilgejetne dan ášši.

#!! * __miiPersLeft1, miiPersLeft2, _miiPersRight__ select mii Pers
SELECT:miiPersLeft1 Pers IF (0 MII-PERS) ((-1 go LINK -1 PL1-V LINK -1 BOC) OR (-1 (V Ind Prs Pl1 Qst)) OR (-1 (V Neg Pl1))) ;
	#$ Eat go mii leat profehtastallan du nammii?
	#$ Eat mii sáhte vuolgit.

#SELECT:miiPersLeft2 Pers (*-1 PL1-V BARRIER NOT-ADV OR ("seammá"))(0 MII-PERS)(NEGATE 0 Rel LINK *1 SG3-V BARRIER NOT-ADV);
SELECT:miiPersLeft2 Pers IF ((-1 PL1-V LINK -1 MO) OR (-1 ("go" Qst) LINK -1 PL1-V) OR (-1 PL1-V + TRANS-V LINK -1 Acc) OR (-1 PL1-V LINK -1 Acc LINK 3 PrfPrc + TRANS-V)) ;
	#$ Goas oinniimet mii du nelgon dahje goikan?
	#$ Dán leat mii bargan ovttas guoskevaš čearuid, boazoorohagaid, ja eiseválddiid áirasiiguin.

SELECT:miiPersRight Pers IF (0 MII-PERS)((*1C Rel BARRIER WORD) OR (*1 PL1-V BARRIER S-BOUNDARY OR VFIN LINK NOT 0 Adv) OR (1 CC LINK 1 (N Nom))) ;
	#$ .. de mii, guđet leat eatnagat, leat okta rumaš. #funker ikke pga 1C Rel
	#$ Dasgo mii oinniimet su násti badjáneamen.
	#$ Mii ja farisealaččat atnit borakeahttáivuođa.

#!! !Pron Pers 2. p.

#!! * __donPers__ selecst don as Pers instead of Dem
SELECT:donPers Pers IF (0 DON)((*-1 (V Sg2) BARRIER SV-BOUNDARY LINK NOT 0 Adv) OR (*1 SG2-V BARRIER PUNCT));
 	#$ Liikotgo don Abbai?
 	#$ Don it leat nama čállán.

SELECT:donPersEllipse Pers IF (0 DON)(**1 (A Sg Nom) OR (N Sg Nom) BARRIER NOT-A LINK 1 ("^excl"));
	#$ Son celkkii: Don ipmilmeahttun jalla!


#!! !Pron Pers 3. p.

#!! * __sonSG3V, sonRel, goson__ select son as Pers, Rel or Pcle
SELECT:sonSG3V (Pron Pers Sg3 Nom) IF (0 ("son"))(NEGATE -1 Qst OR go) ((*-1 SG3-V BARRIER SV-BOUNDARY) OR (*1 SG3-V BARRIER N)) ;
	#$ Ii son liikon dohko.
	#$ Son fertii vuolgit.

SELECT:sonRel Pers IF (0 ("son" Pron Pers Sg3 Nom))(1 (Pron Rel Sg));
	#$ Son gii addá veahki galgá mearridit.

SELECT:goson Pcle IF (NEGATE -2 CLB)(-1 Qst OR go)(0 ("son"))(1 Nom LINK NEGATE 0 Refl OR Inf OR RCmpnd);
	#$ Lea go son Máret boahtán?


#!! * __sutnje__ ( = forms of the verb "suotnjat")
SELECT:sutnje_a (Pron Ill) IF (0 ("son"))(1 (Pron Rel Sg));
SELECT:sutnje_b (Pron Ill) IF (0 ("son"))(*-1 (V Ind));
	#$ Dearvvašvuođabálvalus galgá addit sutnje gii ohcá ...
	#$ Son galgá addit sutnje dárbbašlaš dieđuid.

#!! * __danin__ (Pron Ess OR Adv)
SELECT:danin (Pron Ess)  IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 ("danin"))(1 EOS OR CLB);
	#$ ...jos oapmi fierpmit leat danin,...

#!! * __datPlIll__ selects dát Pron Dem Pl Ill
SELECT:datPlIll Ill IF (0 ("dáidda"))((-1 NP-HEAD-NOM OR ("lassi")) OR (*1 (N Pl Ill) BARRIER NOT-A-ADV));
	#$ ...ja dáidda gielaide sáhttá čállit journálaid.
	#$ Sus lea álgovuorus dušše guohtunvuoigatvuohta dáidda.
	#$ Lassin dáidda lea Sámi vuoigatvuođalávdegotti ovdaolmmoš...

#!! * __daiddaVerb__ removes dáidda N Sg Nom
REMOVE:daiddaVerb Nom IF (0 ("dáidda"))((*-1 TRANS-V OR ILL-V BARRIER SV-BOUNDARY) OR (*1 ("bidjat") OR ("lasihit") BARRIER NOT-AUX-V)) ;
	#$ ...lea čađahan iskamiid muhtun orohagain ja ávžžuhan boazologu dáidda.
	#$ ...mat gullet dáidda 1972-konvenšuvnna mielde.
	#$ Ferte mearridit makkár deattu dáidda galgá bidjat.


#!! * __dasaVGen, dasaLassin dasa,datSg3, datSg3PrfPrc__ ( = forms of the verb "dassat"):
REMOVE:dasaVGen VGen IF (0 Pron);
	#$ Na muhto don it leat dasa nama čállán!

#!! * __dasaILLV__ choses dasa to the left of verbs like duhtat, suhttat, luohttit
SELECT:dasaILLV (Pron Ill) (0 REAL-V) (1 ILL-V - TV)  ;
	#$ Muhto ii Emil dasa duđa.

SELECT:dasaLassin Ill IF (0 DAT-PRON)(1 ("lassi" Ess));
	#$ Dasa lassin buvttán dás lasseargumenttaid.

# selects if there is no Pl3-verb, and in front of Rel Sg, and if the next word is not N
SELECT:datSg3 Sg3 IF (NEGATE *0 PL-V BARRIER S-BOUNDARY)(0 ("dat" Sg3 Nom) LINK NOT 1C (Pron Pl))(NEGATE 0 Pcle LINK -1 (N Nom) OR (Pron Nom))(NEGATE *1 (N Nom) BARRIER VFIN OR S-BOUNDARY LINK NOT 0 VFIN)(*0 SG3-V BARRIER PL-V) ;
	#$ Dat movt sámi oahpahus lea doaibman...
	#$ Dat orru leamen buorre fanas.
	#$ Dat gal lei beare unnán.

# selects dat in front of leamaš
SELECT:datSg3PrfPrc Sg3 IF (NEGATE *-1 PL-V BARRIER S-BOUNDARY)(0 ("dat" Sg3 Nom))(1 ("leat" PrfPrc) LINK *1 Sg BARRIER NOT-A-ADV) ;
	#$ Dat leamaš hui stuorra noađđi midjiide.


# selects dat Sg in front of Rel Sg
SELECT:datSgRel (Pron Sg Nom) IF (*1 (Rel Sg) BARRIER WORD) ;
	#$ Dat mii sáhttá háhkkojuvvot, lea ovdalis juo namuhuvvon..

# selects dat Pl in front of Rel Pl
SELECT:datPlRel (Pron Pl Nom) IF (*1 ("mii" Rel Pl) BARRIER WORD) ;
	#$ Dat mat sáhttet háhkkojuvvot, leat ovdalis juo namuhuvvon..


#!! * __DemPlLoc__ selects Dem when Dem Pl Loc and agreement, perhaps no need for it here because we have agreement-rules later. Men viktig: her blir vi kvitt duo N.
SELECT:DemPlLoc Dem IF (*1 (Pl Loc) BARRIER NOT-A-ADV)(0 (Pl Loc));
  	#$ Fertet vázzilit vai it báze duoin earáin. # viktig: her blir vi kvitt "duo" N
  	#$ Miessegietti dállu lei okta dain dáluin Davvi-Romssas maid duiskkat eai boaldán. #denne er det ikke hast med
  	#$ Dain hirbmat váriin ja mehciin sin guovllus lei earenoamáš vallji sihke elliid ja eará dáfus. #denne disambigueres først seinere med agreement-regler pga av at her er "hirbma" N

#!! * __DemPlCom__ selects Dem when Dem Pl Com and agreement, perhaps no need for it here because we have agreement-rules later.
SELECT:DemPlCom Dem IF (*1 (Pl Com) BARRIER NOT-A-ADV)(0 (Pl Com));
	#$ Gal mun goasttan daiguin sabehiiguin.

#!! * __datPersCopulas__ select Pers in front of copula. I setninger som Riššat dat gal leat musge, jus eai leačča njuoskan. tolker jeg dat som Pcle. Derfor constraint hva som kommer etter.
SELECT:datPersCopulas Pers IF (-1 Nom)(NEGATE -1 Pers)(0 ("dat" Nom))(*1 REAL-COPULAS BARRIER NOT-ADV-PCLE LINK 1 CLB OR EOS);
	#$ Hei, soiddas dat lea, máistte don dan gáfe!
	#$ Gean mearkka bohccot dat ledje?

#!! * __datPcle1__ selects dat Pcle between N and finite, even if there is agreement between verb and dat .
SELECT:datPcle1 ("dat" Pcle)  IF (-1 (N Nom) OR (Pers Nom) OR (Pers Loc) LINK NOT 0 A OR Interr OR PrfPrc)((*1 VFIN BARRIER NOT-ADV-PCLE) OR (1 Adv LINK *1 VFIN BARRIER NOT-ADV-PCLE));
	#$ Máret dat boahtá.
	#$ Mun dat lean.
	#$ Nissonat dat eanaš duddjojit sámi biktasiid.
	#% Ođđa plána dat lea.

#!! * __datPcle2__ selects dat Pcle when there is no agreement between verb and dat .
SELECT:datPcle2 Pcle IF (NEGATE *-1 (V Sg3) OR (V Du3) OR (V Pl3) BARRIER SV-BOUNDARY)(NEGATE -1 BOC)(0 ("dat" Nom))(NOT *1 Rel BARRIER WORD)(NEGATE *1 N BARRIER NOT-NP)(NEGATE *1 (V Sg3) OR (V Du3) OR (V Pl3) BARRIER SV-BOUNDARY);
	#$ Na vuolggán dat gal mun ge.

#!! * __KilldatPcle__ removes the remaining dat Pcle
REMOVE:KilldatPcle ("dat" Pcle) ;


#!! * __PersAcc__ selects Pers Acc in accusativ infinitive clauses with object
SELECT:PersAcc (Pers Acc)  IF (-1 Acc LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-A-ADV)(0 Dem)(1 REAL-V LINK NOT 0 VFIN) ;
	#$ Son logai oappás dan bargan.


#!! * __datPers__ selects Pers. I made it stronger than it was. ref. r897 in sme-dis.rle
SELECT:datPers Pers IF (0 DAT-PRON)(NEGATE 0 ("dasgo") OR ("danin") OR ("dainna"))(NEGATE 1 A OR Num OR N OR Indef OR Attr)(NEGATE 1 PrfPrc LINK *1 N BARRIER NOT-A-ADV)(NEGATE 0 Nom LINK 1 ("gal") OR Pcle LINK 1 (N Nom))(NEGATE 1 NP-HEAD-GEN LINK 1 (Actio Nom) LINK 1 N)(NEGATE 1 ("seammás"))(NEGATE 0 Acc LINK *1 STRICT-TRANS-V BARRIER S-BOUNDARY LINK NOT *1 Acc BARRIER NOT-A-ADV);
	#$ Dá dat leat.
	#$ Moai soabaime dasa.
	#$ Aiddo sii movttidahttojuvvojit daid buot smiehttat sámegillii.

#!! * __datDemSg__ selects Dem from Pron Pers Sg3 Gen
SELECT:datDemSg (Dem Sg)  IF (NEGATE *-1 STRICT-TRANS-V LINK NOT *2C Acc BARRIER N)(0 (Pron Pers Sg3 Gen))(NEGATE 1 Po)(NEGATE *1 STRICT-TRANS-V BARRIER S-BOUNDARY OR Acc)(NEGATE *1 Pl OR Ess OR Com BARRIER N)(NEGATE -1 BOS LINK 2 Prop) ;
	#$
	#% Nubbi noađđeheargi goallostuvvo giehtahearggi lávžái ja goalmmát fas dan nuppi lávžái. #It shouldn´t take this one.
	#% Jearahallan ii goassege deaddiluvvon áviissas, daningo daláš doaimmaheaddji Bjarne Store Jakobsen biehttalii dan deaddileames.
    # It shouldn´t hit: Mun gohčodan dan eadnin. Sii čurvot dan dáhkiid alde. De dearvvahehket dan ráfiin. Son dasto attii dan eadnásis. Disse vil bli tatt hånd om i More pronouns

#!! * __datPersPl3__ selects dat Pl3 in front of V Pl3 and V Du3 and Rel Pl
SELECT:datPersPl3 DAT-PRON + (Pers Pl3) IF (*1 (V Pl3) OR (V Du3) OR (Rel Pl) BARRIER NOT-ADV-PCLE OR Neg)(NEGATE -1 Neg) ;
	#$ Dan guovtto dat leat.

SELECT:DemAccNum (Dem Sg Acc) (1 Num LINK 1 Sem/Time + Gen LINK NOT 1 N) ;
	#$ Mun ledjen man nu láhkai oktavuođas internáhttaohppiin dán 22 jagi maid ledjen skuvllas.

#!! !!An early rule for "eanaš"/"eanas"

#!! * __eanasPron__ selects Pron in front of Pron Loc
SELECT:eanasPron Pron IF (0 ("eanaš") OR ("eanas"))(1 (Dem Loc) OR (Pers Loc));
	#$ Eanaš dáin gálduin leat hui unnán dieđut.

#!! !!Px constraints
#!! First select Px, then remove all remaining Px

#!! * Nouns: __NomPxSg1__ (not Ess) as the only word in a sentence. Needs no disambiguation.
SELECT:PxSg1Nom N-PX + (N Nom PxSg1) IF (-1 BOS)(NEGATE 0 VFIN)(*1 EOS OR VFIN - Der/Pass BARRIER WORD);
	#$ Ustibažžan!
	#$ Vielljan sáhttá vuoitit gilvvu.

#!! * Nouns:  __AccPxSg1__ after a TV verb. Exception for Aux.
SELECT:PxSg1Acc (N Acc PxSg1)  IF ((-1 TRANS-V + Sg1 - OPRED-V LINK NOT 0 AUX) OR (-1 TRANS-V + PrfPrc - OPRED-V LINK -1 COPULAS + Sg1) OR (-1 TRANS-V + ConNeg - OPRED-V LINK -1 Neg + Sg1))(NEGATE *-1 VFIN - Sg1 BARRIER S-BOUNDARY OR CS)(NEGATE *1 Acc BARRIER REAL-V LINK NOT 1 Po)(NEGATE *1 ("ahte") BARRIER WORD);

#!! * Nouns:  __AccPxSg1__ after a TV Inf verb.
SELECT:PxSg1Acc (N Acc PxSg1)  IF (*-1 TRANS-V + Inf - OPRED-V BARRIER NOT-NPMOD LINK *-1 SG1-V BARRIER NOT-ADV-PCLE LINK NOT 0 ("leat"))(NEGATE 0 Dem) ;
SELECT:PxSg1Acc (N Acc PxSg1)  IF (*-1 TRANS-V + Inf - OPRED-V BARRIER SV-BOUNDARY OR CC OR CS LINK *-1 SG1-V BARRIER NOT-ADV-PCLE LINK NOT 0 ("leat"))(NEGATE 0 Dem) ;
	#$ Mun oidnen vielljan.
	#$ Mun lean oaidnán vielljan.
	#$ Mun in oaidnán vielljan.
	#$ Dál mun ferten gaikkodit visson. # visson is also PrfPrc
	#$ Mun attán heaggan sávzzaid ovddas.
	#$ Addet munnje eatnama hávdebáikin, vai mun beasan hávdádit jápmán áhkkán.
  	#% Mun válddán ovdamearkan Buolbmága. # It shouldn`t take this example.


#!! * __PxSg1LocAcc__ is Acc to the right.
SELECT:PxSg1LocAcc (Loc PxSg1) IF (-1 SG1-V + TRANS-V)(1 Acc);
	#$ Mun roggen ozastan njálgáid mánáide.

#!! * __PxSg1Acc__ is Acc to the right.
SELECT:PxSg1Acc (N Acc PxSg1) IF  (-1 TRANS-V - OPRED-V)((*-1 SG1-V BARRIER VFIN OR Acc OR S-BOUNDARY) OR (-2 COPULAS LINK -1 SG1-V))(NEGATE 0 PrfPrc);
	#$ Mun háliidan mánnán boahtit ruoktot!
	#$ Mun in oaidnán vielljan jahkái.
	#$ Mun in leat oaidnán vielljan jahkái.


#!! * coordination __PxSg1coord__
SELECT:PxSg1coord1 (PxSg1)  IF (*-1 SG1-V - ("leat"))(NEGATE 0 Dem)(1 CC OR COMMA LINK 1 PxSg1) ;

#!! * __PxSg1coordLast__ for the last word of a coordination
SELECT:PxSg1coordLast (PxSg1)  IF (*-3 SG1-V - ("leat"))(-1 CC LINK -1 PxSg1)(NEGATE 0 Dem);
	#$ Mun attán heaggan, áhkkán ja ruhtan sávzzaid ovddas.

#!! * __ReflPxSg1__ lean oahppan alddán
SELECT:ReflPxSg1 (Refl PxSg1)  IF (*0 Sg1 BARRIER VFIN)(0 Po); #Kanskje er denne for tidlig i fila pga manglende verbdis?
	#$ Dan lean oahppan alddán.


#!! * Nouns: __PxSg2__ if SG2-V. The rule needs no disambiguation. The DON-constraint because of homonymi with (N Pl)
SELECT:PxSg2 (PxSg2)  IF (-1 SG2-V - REAL-COPULAS LINK -1 DON)(NEGATE 0 Attr OR Adv LINK *1 N BARRIER NOT-PRE-A-N);
	#$ Don attát heakkat mu ovddas?

#!! * __PxSg2Acc__ if  TV to the right
SELECT:PxSg2Acc (Acc PxSg2) IF (0 (N Pl Nom))((*-1 TRANS-V + Sg2 BARRIER SV-BOUNDARY OR COMMA OR Acc OR ADVLCASE LINK NOT 0 A OR Imprt OR Inf OR Pl3 LINK NOT *-1 Sg1 OR Pl1) OR (*-1 TRANS-V + ConNeg BARRIER NOT-A-ADV OR COMMA LINK -1 (Neg Sg2)) OR (*-1 TRANS-V + PrfPrc BARRIER NOT-A-ADV LINK *-1 COPULAS + Sg2 BARRIER NOT-ADV))(NEGATE 0 ("lohkat") + TV);
	#$ Máhtát go namat čállit?
	#$ Leat go oaidnán oappát?
	#$ Ale cáme beatnagat, ii dat leat sivalaš.

#!! * __PxSg2AccImprt__ if  TV Imprt to the left
SELECT:PxSg2AccImprt (Acc PxSg2)  IF (*-1 (Imprt Sg2) - AUX BARRIER NOT-NPMODADV LINK *-1 BOC OR COMMA BARRIER WORD)(NEGATE 0 Adv)(NEGATE 0 (N Pl Nom) LINK *-1 CC OR CLB BARRIER NOT-NPMOD LINK -1 Nom)(NEGATE 0 (N Pl Nom) LINK -1 Attr LINK 0 Imprt)(NEGATE 0 Gen LINK 1 Po) ; #(NEGATE 0 (N Pl Nom) LINK *1 PL3-V BARRIER NOT-ADV-PCLE) ;
	#$ Váldde mánát sallasat!
	#$ Buvtte dasto oaffarskeaŋkkat!
	#$ Lihka bajás, váldde seaŋggat ja vázzil!
	#$ Viečča vieljat boradit!

#!! * __PxSg2AccPrfPrc__ after PrfPrc
SELECT:PxSg2AccPrfPrc (Acc PxSg2)  IF (-1 PrfPrc + TRANS-V - LOC-V LINK *-1 SG2-V BARRIER NOT-AUX-V)(0 N-PX - Nom);
	#$ Leat go oaidnán oappát?

#!! * __PxSg2GenPo__ if in front of Po, after til verb
SELECT:PxSg2GenPo (Gen PxSg2)  IF (*-1 Sg2 BARRIER S-BOUNDARY OR COMMA)(0 (N Pl Nom))(1 Po LINK NOT 0 N)(NEGATE *2 NP-HEAD-GEN BARRIER NOT-NPMOD);
	#$ Go don rohkadalat, de mana gámmárat sisa.

#!! * __PxSg3Loc__ after habitivconstruction
SELECT:PxSg3Loc (N Loc PxSg2) IF  (-1 COPULAS + VFIN LINK -1 HAB-ACTOR) ;
	#$ Mii dus lea ozastat?

#!! * Nouns: __PxSg3Acc__ if Sg3 or Sg to the left
SELECT:PxSg3Acc (Acc PxSg3)  IF ((*-1 TRANS-V + Sg3 BARRIER Nom OR S-BOUNDARY OR Acc OR COPULAS
LINK *-1 (Nom Sg) OR (Sg3 Nom) BARRIER NOT-ADV-PCLE LINK NOT *-1 Acc BARRIER S-BOUNDARY OR REAL-V OR CC) OR (*-1 TRANS-V + ConNeg BARRIER Nom OR S-BOUNDARY OR Acc OR COPULAS LINK NOT 0 LOC-V LINK *-1 (Neg Sg3) BARRIER NOT-ADV LINK NOT *-1 Acc BARRIER S-BOUNDARY OR REAL-V OR CC) OR (-1 Qst LINK -1 TRANS-V + Sg3) OR (*-1 TRANS-V + Sg3 BARRIER Nom OR S-BOUNDARY OR Acc OR COPULAS LINK -1 go OR CC))(0 Loc - PLACE)(NEGATE 0 ("árvu") LINK *-1 ("atnit") BARRIER NOT-ADV)
(NEGATE *-1 LOC-V BARRIER SV-BOUNDARY)
(NEGATE 0 Attr LINK *1 N - SAPMI BARRIER NOT-NPMOD)
(NEGATE 0 (N Gen) LINK 1 (N Acc) - Sem/Time - SAPMI LINK NOT 0 Po LINK NOT 1 Loc)
(NEGATE 1 Po LINK NEGATE 0 Pr LINK 1 Gen) ;
	#$ Son logai oappás dan bargan.
	#$ Son beastá álbmogis sin suttuin.
	#$ Son hárve gullá eatnigielas. # This one needs verbdisambiguation of hárve
	#$ Dat guhte ráhkista bártnis dahje nieiddas eanebut go mu, ii dohkke munnje.
	#$ Jesus leai sullii golmmalotjahkásaš go álggahii doaimmas.
	#$ Bargojoavku ovddidii árvalusas čakčamánu 8. b. 2003.
	#$ Vearrámus lea go nu váttis lea oažžut eret dan gova, lohká nisu, gii ii hálit namas aviisii.
	#$ Bárdni ozai beatnagis miehtá gili.
	#$ Juos mearriduvvo meahcceeatnamiid nuppástuvvan geavaheapmi golmma ja dušše golmma stivrralahtu doarjagiin ja olles uhcitlohku vuođđudallá oainnus sámi kultuvrra, boazodoalu, meahcásteami, ealáhusdoaimmaheami ja servodateallima destii.
	#$ Son vuolggahii muhtun bálvaleaddji lusa vai son oččošii oasis viidnegárddi šattuin.
	#% Mun muittán ahte mun máŋgii ijas fertejin lihkkat deavdit parafiinna petromávssaide ja pumpet daid.

#!! * Nouns: __PxSg3AccPrfPrc__ if PrfPrc and Sg3 to the left
SELECT:PxSg3AccPrfPrcInf (N Acc PxSg3)  IF ((-1 PrfPrc + TRANS-V - LOC-V LINK *-1 SG3-V BARRIER NOT-AUX-V OR Nom OR Acc LINK *-1 (Sg Nom)) OR (-1 Inf + TRANS-V - LOC-V LINK *-1 SG3-V BARRIER NOT-AUX-V OR Nom OR Acc LINK *-1 (Sg Nom)) OR (-1 Inf + TRANS-V - LOC-V LINK -1 (Sg Nom) LINK *-1 SG3-V BARRIER NOT-AUX-V OR Nom OR Acc)) ;
	#$ Nu geavai ovdal go Márjá lei riegádahttán bártnis.
	#$ Gozihanlávdegoddi galgá čilget gozihandoaimmas, árvvoštallat stivrra jahkemuitalusa ja dilálašvuođaid maid dat siskkilda, ja árvvoštallat mo dát láhka doaibmá ja leago sávahahtti dan nuppástuhttit.
	#$ Raporttas galgá gozihanlávdegoddi čilget gozihandoaimmas, árvvoštallat stivrra jahkemuitalusa ja dilálašvuođaid maid dat siskkilda, ja árvvoštallat mo dát láhka doaibmá ja leago sávahahtti dan nuppástuhttit.

SELECT:PxSg3AccPrfPrcInf (N Acc PxSg3)  IF (1 Inf + TRANS-V - LOC-V LINK NOT *1 Acc BARRIER REAL-V)(*-1 SG3-V + AUX - ("leat") BARRIER NOT-AUX-V OR Acc)(NEGATE -1 Num) ;
	#$ Dalle fertejit buotlágan ávnnaslaš eavttut maid dárbbaša vai duođalaččat galgá sáhttit kultuvrras dikšut.


#!! *  __PxSg3CC__ in coordination with the owner
SELECT:PxSg3CC (PxSg3)  IF (-1 CC LINK -1 N-PX + Sg);
	#$ Mun lean boahtán bidjat soahpameahttunvuođa bártni ja áhčis gaskii.

#!! *  __PxSg3GenPo1__ in front of Po, to the left of the owner
SELECT:PxSg3GenPo1 (Gen PxSg3)  IF (*-1 BOC OR COMMA BARRIER NOT-NPMOD)(1 Po)(*2 SG3-V BARRIER NOT-ADV-PCLE LINK NOT 0 A LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD);
	#$ Raporttas bokte dáhttu bargojoavku čalmmustahttit Sámedikki.

#!! *  __PxSg3GenPo2__ in front of Po, to the left of the owner
SELECT:PxSg3GenPo2 (Gen PxSg3) IF  (1 Po OR ("veara") LINK NOT 0 CS LINK NEGATE 0 Pr LINK 1 Gen)(0C N) ;
	#$ Mánná bođii eatnis mielde.
	#$ Ábo logai eatnis maŋis eahketrohkosas.
	#$ Terje hárve hupmá mediain, muhto son áinnas hupmá sápmelašvuođas birra.

#!! * __Genguossis__ is  selection Gen, not only with Px. The FAMILY-set would be better than Sem/Hum-tag, but there is often a propernoun connected to the noun. guossái and guossis should have Po analysis?
SELECT:Genguossis Gen IF (0 Sem/Hum LINK 1 ("guossi" Ill) OR ("guossi" Loc)) ;
	#$ Odne lea Joneš-bojá vuolgimin Biette-eagis guossái.

#!! * __GenNPFinal__ selects Gen as the modifier of a noun in the end of a sentence.
SELECT:GenNPFinal Gen + Px IF (-1 COPULAS)(0 (N Loc) LINK NOT 0 Attr)(1C N LINK 1 EOC) ;
	#$ Ábo lei eatnis čalbmebealle.

#!! * __PxSg3Nom__
SELECT:PxSg3Nom (Nom PxSg3) IF  (-1 go LINK -1 Comp LINK *-1 NP-HEAD-NOM BARRIER Nom OR S-BOUNDARY OR COMMA)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD);
	#$ Máhttájeaddji ii leat stuorit go oahpaheaddjis, ii ge bálvaleaddji stuorit go hearrás.
	#$ Mávssut leat adnon eambbo go bušeahtas. #Denne skal ikke ha Px

#!! * __PxGenPo__
SELECT:PxGenPo (Gen PxSg3) IF (0 N OR A) (*-1 (Sg Nom) OR (Sg3 Nom) BARRIER Pers OR S-BOUNDARY OR LOC-V OR Num OR Dem LINK NOT 0 Attr)(NEGATE *-1 (Imprt Sg2) BARRIER SV-BOUNDARY)(NEGATE -1 (N Nom) OR (N Acc))(0 N-PX)(NEGATE 0 Loc LINK 1 (N Ill) OR ("leat") + VFIN)(1 N OR Po) ; #(NEGATE *-1C (V TV Ind) BARRIER SV-BOUNDARY)
	#$ Nieida bođii ráhkkásis mielde.
	#$ Son ii váiban goassege guldaleames áhčis muitalusaid.
	#% Dat ferte addojuvvot dan dihte mánnái buoidi njammat, go ii eatnis leat mielki.

#!! * __PxGenPr__
SELECT:PxGenPr (N Gen PxSg3)  IF (-1 Pr LINK -1 BOC)((*1 VFIN + Sg3 OR (Pers Nom Sg3) OR (N Nom Sg) BARRIER NOT-ADV-PCLE) OR (1 Adv LINK 1 VFIN + Sg3 OR (Pers Nom Sg3) OR (N Nom Sg))) ;
	#$ Maŋŋá jápmimis de aliduvvui Gonagas Olav bassin. # "de" er en del av NOT-ADV-PCLE, derfor to regler

#!! * __PXGenoaivai__  for oaivái Po, there could be more Po for this rule?
SELECT:PXGenoaivai FAMILY + Gen IF (1 ("oaivái")) ;
	#$ Válden vealggi vielljan oaivái.

#!! * __PXAccCoor__
SELECT:PXAccCoor Px + Acc IF (-1 CC LINK -1C Acc) ;
	#$ Dat guhte ráhkista bártnis dahje nieiddas eanebut go mu, ii dohkke munnje.

#!! * __PXAccCoor__
REMOVE:gaskaEss ("gaska" Ess)(0 ("gaskan" Adv) + Px) ;

#!! We end section 2 by removing all remaining Px

#!! * __KillPx__ removes all remaining Px readings
REMOVE:KillPx Px ;


#!! !!Section 3: Certain verb readings
SECTION


#!! * __FinGoInf__ for  vai áigu go njulget.. Lene:  we don't need this
#REMOVE N OR PrsPrc IF (1 ("go" Qst))(2 Inf);
	## vai áigu go njulget boares boasttuvuođaid

#!! verb or adv
SELECT Adv IF (0 VFIN LINK 1 CC LINK 1C Adv) ;
	#$ Dás galget nuorat, čohkut dahje veallut duorggaid alde buolli árrandola ovddas vuostáiváldit oahpahusa sis.


#!! * __NotVGenIfDer__ removes VGen if 0 = Der/Pass or Der...(r947)
REMOVE:NotVGenIfDer VGen IF (0 Der/Pass OR Der/goahti OR Der/st OR Der/l);

#!! * __NotVGenIfDer__ removes VGen if 0 = Der/Pass or Der...(r947)
SELECT:ActioEss (Actio Ess) IF (0 (N Ess) LINK *-1 COPULAS OR ("fitnat") BARRIER REAL-V);


	#!! !All imperatives

#!! For imperative disambiguation we need the following:
#!! Pick imperative contexts, and thereafter remove imperative.
#!! Such contexts are: Imperative verb sentence-initially with exclamation mark

# Imperative examples:
	## ..., geahčá sámediggeválgalága láhkaásahusas.

# Non-imperative examples:
	## Rievdaduvvo leat ná:
	## Doaimma birra¶
	## Evttohus 1¶
	## Bargu sámi fanasduddjonvieruid ektui lea dakkár bargu masa Sámediggi sáhtášii váikkuhit eanet dutkama bokte .
	##

#!! * __NotEmbeddedImprt__ removed Imprt after CS
REMOVE:NotEmbeddedImprt Imprt IF (*-1 CS BARRIER ("de" Adv) OR (":") OR ("""));
	#$ Go ollet sáhpánvári ala, de muitte čuovvolit bálgá.
	#$

#" syntax couloring.

#!! * __NotImprtWhenInd__ removes Imprt if part of an Ind domain
REMOVE:NotImprtWhenInd Imprt IF (*-1C Ind OR Rel BARRIER SV-BOUNDARY)(NEGATE -1 COMMA);
	#$ Beana jávkkai guhká ovdalgo bođii njága suolggaid, vilppui munnje ja manai fas.
	#% Olles máilmmis ii lean dakkár gievrras boles, jurddaš.

#!! * __NotImprtWhenIndCoor__ removes Imprt when coordination of an Ind domain - a very special case
REMOVE:NotImprtWhenIndCoop Imprt IF (0 ConNeg LINK -1 ("ja" CC) OR ("dahje" CC) LINK **-1 ConNeg LINK -1 Neg);
	#$ Bearehan dal son ii geige vel gieđa ja váldde maiddái eallima muoras ja bora ja beasa nu eallit agálaččat.


#!! * __NotImprtIfAttrLeft__ removes Imprt after attribute
REMOVE:NotImprtIfAttrLeft Imprt IF (NOT -1 BOC OR COMMA)(NEGATE -1 CC LINK -1 Imprt LINK -1 BOC)(0 (N Nom) OR PrsPrc);
	#$ Buot oahppit leat mannan.
	#% Gárvodeahkku ja mannu geahččat dien nieidda, gii váccii stuora gápmagiiguin.

#!! * __NotImprtIfRel__ removes Imprt after Rel, unify this with other left context (r948)
REMOVE:NotImprtIfRel Imprt IF (*-1C Rel BARRIER S-BOUNDARY OR CC OR COMMA);

REMOVE:muhtoNotConNeg ConNeg IF (NEGATE *-1 Neg BARRIER ("muhto"));

	#!! !Sg1 - early cycle, safe rules

#!! * __VSg1IfLeftMun__ selects Sg1 when "mun" is to the left (r949)
SELECT:VSg1IfLeftMun (V Sg1) IF (*-1 MUN BARRIER Nom OR SG1-V OR S-BOUNDARY LINK NOT *-1 SG1-V BARRIER NOT-ADV-PCLE)(NEGATE 0 PrfPrc LINK *-1 REAL-COPULAS BARRIER S-BOUNDARY OR CLB)(NEGATE 0 PrfPrc LINK *-1 SG1-V BARRIER NOT-ADV-PCLE);
 	#$ Dego mun dás vuollelis mottiin ovdamearkkain čájehan,...

SELECT:VSg1NoMun (V Sg1) IF (-1 Po LINK -1 Gen LINK -1 BOS) ;
 	#$ Telefuvnna bokte ožžon Álttá Johtolatstašuvnna ságaide.

#!! * __VSG1IfRightMun__ selects Sg1 when "mun" is to the right (r950)
SELECT:r950 (V Sg1) IF (*1 MUN BARRIER NOT-ADV-PCLE);
 	#$ Man guhká ferten mun gierdat din?


	#!! !Sg2 - early cycle, safe rules


#!! * __VSG2IfLeftDon__ selects Sg2 when "don" is to the right (r951)
SELECT:VSG2IfLeftDon SG2-V IF ((*-1 DON BARRIER SG2-V OR ConNeg OR S-BOUNDARY OR VFIN LINK NOT *-1 SG2-V BARRIER S-BOUNDARY) OR (-1 (Rel Nom) LINK -1 DON));
 	#$ ...jos don fal luoitádat ja gudnejahtát mu.
 	#$ Rohkadallet dasto ná: Áhččámet, don guhte leat almmis.

#!! * __VSG2IfRightDon__ selects Sg2 when "don" is to the left (r952)
SELECT:VSG2IfRightDon SG2-V IF (*1 DON BARRIER S-BOUNDARY);
 	#$ Leat go don dat guhte galgá boahtit?

#!! * __VInfIfAhte__ removes Inf if there is no other VFIN between BOS and "ahte" (r953)
REMOVE:VInfIfAhte Inf IF (*-1 BOS BARRIER VFIN)(*1 ("ahte" CS) BARRIER NOT-ADV);
 	#$ Dainna lágiin fuobmát jođánit ahte sámi gielladilis lea árvu maid eará riikkain.


	#!! !Sg3 - early cycle, safe rules

#!! * __VSG3IfLeftSon__ selects Sg3 when "son" is to the left (r954)
SELECT:r954 (V Sg3) IF (*-1 SON BARRIER Nom OR SG3-V OR CS OR PUNCT LINK NOT *-1 SG3-V BARRIER NOT-ADV-PCLE)(NEGATE 0 Adv LINK *1 SG3-V);
 	#$ Geas ležžet bealjit, son gullos!

#!! * __VSG3IfRithgSon__ selects Sg3 when "son" is to the right (r954)
SELECT:r955 (V Sg3) IF (*1 SON BARRIER NOT-ADV-PCLE OR go)(NEGATE 0 CS)(NEGATE 0 Adv LINK *1 (V Sg3) BARRIER SV-BOUNDARY);
 	#$ ...oažžu son atnit dušše dan čázis mii sutnje lea mearriduvvon.

#!! * __VNotSg3When12Left__ removes Sg3 if 12 Pron immediate left (r955)
REMOVE:VNotSg3When12Left (V Sg3) IF (-1 (Pron Nom))(NEGATE -1 Sg OR Sg3);
 	#$ Sii manne vissui.

#!! * __VNotSg3IfCom__ removes Sg3 in X with Y is... (r957)
REMOVE:VNotSg3IfCom (V Sg3) IF (-1 (Sg Com))(-2 (N Nom))(0 Du3);
 	#$ Biret-guovttos Juffáin leaba čeahpit duddjot.


REMOVE:r958 (V Pl3) IF (-1 (Pron Rel Sg))(0 (V Sg3));
 	#$ Kapihttalis mas čilge orrunlanjadagaid latnjačovdosa nannejuvvo ahte lanjadat leat plánejuvvon orruide geat dárbbašit divššu guhkit.

#!! * __NegSg3BeforeFoc__ selects Neg before Foc/ge or ConNeg (r959)
SELECT:NegSg3BeforeFoc (V IV Neg Ind Sg3) IF (0 ("ii"))(*1 Foc/ge OR ConNeg BARRIER COMMA OR PUNCT);
 	#$ Ii oktage lea boahtán.

#!! * __vfin__ removes verb reading when the reading should be noun
REMOVE:vfin1 VFIN - Qst IF (0 (N Sg Nom) LINK *1 VFIN + Sg3 BARRIER NOT-ADV-PCLE OR Qst) ;
REMOVE:vfin2 VFIN IF (0 Acc LINK *-1 TRANS-V BARRIER NOT-ADV-PCLE)(*-1 VFIN BARRIER S-BOUNDARY LINK NOT 0 Po)(NEGATE 1 Acc OR go) ;
REMOVE:vfin3 VFIN IF (0 N LINK 1 ("vai" CC) LINK 1 N LINK NOT 0 VFIN) ;
	#$ Birasgáhttenlihkadus ja álgoálbmogat – ovttasbargit vai vuostálagaid?
REMOVE:vfin4 REAL-V IF (0 N LINK 1 ("dat" Pcle)) ;
	#$ Riššat dat gal leat musge, jus eai leačča njuoskan.
REMOVE:vfin5 VFIN IF (0 (N Pl Nom) LINK *1 VFIN + Pl3 BARRIER NOT-ADV-PCLE LINK NOT 0 Inf) ;

REMOVE:vfin4 VFIN - Qst IF (0 N LINK *-1 AUX BARRIER NOT-ADV-PCLE OR Qst)(*1 Inf BARRIER NOT-ADV-PCLE) ;
	#$ Áigugo fas skuvlii álgit?

SELECT:vfin5 VFIN + LOC-V IF (0 Ill LINK -1 CC)(1 Loc) ;
	#$ Tommi geahčai boksii dugo heajuid vihkume ja jearai sus.

REMOVE:vfinPrfPrc Der/Pass + Sg1 IF (0 PrfPrc LINK *1 N)(NEGATE *-1 (Pron Sg1)) ;


#!! Negative verb, not abbreviation or roman numeral Ii.

	#!! !Du1 - early cycle, safe rules

#!! These Du1, Du2 rules are (almost) not in use in our corpus, but we keep them for completeness.

#!! * __VDu1IfMoaiLeft__ selects Du1 when "moai" left (r960)
SELECT:VDu1IfMoaiLeft (V Du1) IF (*-1 MOAI BARRIER DU1-V OR S-BOUNDARY OR PUNCT LINK NOT *-1 DU1-V BARRIER NOT-ADV-PCLE);
 	#$ Moai dihte ahte dát lea munno bárdni.

#!! * __VDu1IfMoaiRight__ selects Du1 when "moai" right (r961)
SELECT:VDu1IfMoaiRight (V Du1) IF (NEGATE 0 Adv LINK *-1 BOS OR PUNCT BARRIER NOT-CC)(NEGATE 0 Adv LINK *-1 (V Du1))(*1 MOAI BARRIER S-BOUNDARY);



	#!! !Du2 - early cycle, safe rules

#!! The next two rules are not found in the corpus, but logically
#!! they belong, to cover the whole paradigm. There is no verb-internal
#!! homonymy here, but there is homonymy with e.g. Illative for certain verbs.

#!! * __VDu2IFDoaiLeft__ selects Du2 if "doai" to the left (r962)
SELECT:VDu2IFDoaiLeft (V Du2) IF (*-1 DOAI BARRIER DU2-V OR SV-BOUNDARY OR PUNCT LINK NOT *-1 DU2-V BARRIER NOT-ADV-PCLE);
 	#$ Manne doai ozaide mu?

#!! * __VDu2IFDoaiRight__ selects Du2 if "doai" to the right (r963)
SELECT:VDu2IFDoaiRight (V Du2) IF (*1 DOAI BARRIER NOT-ADV-PCLE);
 	#$

	#!! !Du3 - early cycle, safe rules

#!! The competitor to Du3 is -ba Foc.

#!! * __VDu3IfSoaiLeft__ selects Du3 when "soai" left (r964)
SELECT:VDu3IfSoaiLeft (V Du3) IF (*-1 SOAI BARRIER DU3-V OR SV-BOUNDARY OR PUNCT LINK NOT *-1 DU3-V BARRIER NOT-ADV-PCLE);
 	#$ Soai leaba dahkan čielga oavdudagu.
 	#$ Soai ferteba mannat.

#!! * __VDu3IFSoaiLeft__ selects Du2 if "doai" to the right (r965)
SELECT:VDu3IFSoaiRight (V Du3) IF (*1 SOAI BARRIER NOT-ADV-PCLE);
 	#$ Fertebago soai mannat?

#!! * __VDu3IfGuokteLeft__ selects Du3 if "guokte" left (r966) - 15
SELECT:r966 (V Du3) IF (*-1 ("guokte" Nom) BARRIER SV-BOUNDARY) ;
 	#$ Guokte nissona ferdneba milluin.

#!! * __VDu3IfGuokteRight__ removes Sg3 if "guokte" right and 0 Du3 (r967)
REMOVE:VDu3IfGuokteRight (V Sg3) IF (0 Du3)(*1 ("guokte" Nom) BARRIER NOT-NPMODADV);
 	#$ Mus leat dušše dát guokte oappá geaid don dás oainnát.

#!! * __VDu3IfNjaNLeft__ selects Du3 as verb with coordinated subject to the left (r968) - 43
SELECT:VDu3IfNjaN (V Du3) IF (*-1 (N Nom) BARRIER NOT-ADV-PCLE LINK *-1 CC BARRIER NOT-NPMOD LINK -1 (N Nom));
 	#$ Per Berg ja Rolf Olsen leaba doppe.
 	#$ Deanu Searat ja Sámi Siida leaba ovttas ordnen dáid gilvvohallamiid.
	#$ Stuorra nieida ja unna gánda ferteba leat doppe.
 # Note that 'Searat' is Pl, hence no number requirement in this rule.
  	#$ Mu vánhemat, sihke eadni ja áhčči, leaba jápmán.

#!! * __VDu3IfNjaNRight__ selects Du3 as verb with coordinated subject to the right (r969) - 12
SELECT:VDu3IfNjaNRight (V Du3) IF (*1 (Sg Nom) BARRIER NOT-NPMODADV LINK 1 CC LINK *1 (Sg Nom) BARRIER NOT-NPMOD);
 	#$ Dá leaba Brita ja Ole Kåven.

#!! * __VDu3IfCollLeft__ hmm, remove this?
REMOVE:r970 (V Sg3) IF (*-1 (Coll Nom) BARRIER SV-BOUNDARY )(0 (V Du3));
 	#$ Biera-guovttos Márehiin leaba daid mánáid váhnemat.

	#!! !Pl1 - early cycle, safe rules

#!! The competitor here is obviously Inf, but also Pl3 and Prt Sg2.

#!! * __VPl1IfMiiLeft__ selects Pl1 if "mii" Pron to the left (r971) - 3163
SELECT:VPl1IfMiiLeft (V Pl1) IF (*-1 MII-PERS BARRIER VFIN OR PUNCT LINK NOT *-1 PL1-V BARRIER NOT-ADV-PCLE)(NEGATE *-1 AUX + Pl1 BARRIER REAL-V OR COMMA);
 	#$ Mii sápmelaččat gal leat vásihan dán...

#!! * __VPl1IfMiiRight__ selects Pl1 if "mii" Pron to the right (r972) - 272
SELECT:VPl1IfMiiRight (V Pl1) IF (*1C MII-PERS BARRIER NOT-ADV-PCLE OR ("seammá") OR S-BOUNDARY LINK NOT *1 SG3-V BARRIER NOT-A-ADV OR CLB);
#SELECT:VPl1IfMiiRight (V Pl1) ((1 ("go" Qst) LINK 1 MII-PERS) OR (-1 MO OR ("danne") LINK 2 MII-PERS) OR (0 TRANS-V LINK -1 Acc LINK 2 MII-PERS)) ;
 	#$ Galgat go mii vuordit muhtun eará?

#!! * __VPl1NotImprIfMiiLeft__ removes Imprt if if "mii" Pron to the left and 0 = "mii" (r973) - 557
REMOVE:r973 Imprt IF (-1 MII-PERS OR (N Pl Nom))(0 (V Prs Pl1));
 	#$ Mii áigut maiddái leat aktiivvalaččat mielde juohke dásis.

	#!! !Pl2 - early cycle, safe rules

#!! These rules are not used when disambiguating the corpus
#!! * __VPl2IfDiiLeft__ selects Pl2 if "dii" Pron to the left (r974) - 0
SELECT:r974 (V Pl2) IF (*-1 DII BARRIER NOT-ADV-PCLE);

#!! * __VPl2IfDiiRight__ selects Pl2 if "dii" Pron to the right (r975) - 0
SELECT:r975 (V Pl2) IF (*1 DII BARRIER NOT-ADV-PCLE OR MO);
 	#$ Addet dii sidjiide borramuša!


#!! !Pl3 - early cycle, safe rules

#!! Select...

#!! * __r976__ SE V Pl1 if *-1 SII
SELECT:r976 (V Pl3) IF (*-1 SII BARRIER PL3-V OR S-BOUNDARY OR PUNCT LINK NOT *-1 PL3-V BARRIER NOT-ADV-PCLE)(NEGATE 0 Adv LINK *1 PL3-V BARRIER S-BOUNDARY);
 	#$ ...vai sii áiggi mielde oččoše doarvái gelbbolašvuođa.

#!! * __r977__ SE V Pl1 if *1 SII
SELECT:r977 (V Pl3) IF (*1 SII BARRIER NOT-ADV-PCLE)(NEGATE 0 Adv OR Po LINK *1 PL3-V BARRIER S-BOUNDARY);
 	#$ Máhttego sii čilget áššiset dárogillii?

#!! * __VPl3jaPl3__ selects Prt Pl3 in coordination (r978)
SELECT:VPl3jaPl3 (Prt Pl3) IF (*-2C (Prt Pl3) BARRIER VFIN)(-1 CC);
 	#$ Ja buot olbmot imaštalle sakka ja jerre.


#!! The following two may be joined:

#!! * __VPl3IfPronRelLeft1__ selects Pl3 if -1 Rel is linked to -2 Pl (r979) - 7801
SELECT:VPl3IfPronRelLeft1 (V Pl3) IF (-1 (Pron Rel Pl) OR MOVT)(-2 (N Pl));


#!! * __VPl3IfPronRelLeft2__ selects Pl3 if -1 Rel is linked via COMMA to -3 Pl (r980) - 853
SELECT:VPl3IfPronRelLeft2 (V Pl3) IF (-1 (Pron Rel Pl) OR MOVT)(-2 COMMA)(-3 (N Pl));
 	#$ Njuolggadusat movt meannudit oanehisáigge vealgegáibideami, beaivádan 25.04.00.
 	#$ Vai bálvalusas lea buohccedikšofágalaš dássi eaktuduvvo ahte leat fágabargit geat virgáibiddjojit.

#!! * __VPl3IfCSLinkPl3Left__ selects Pl3 if -1 Rel is linked via COMMA to -3 Pl (r979) - 341
SELECT:VPl3IfCSLinkPl3Left (V Pl3) IF (-1 CS)(*-2 (V Pl3) BARRIER VFIN LINK -1 (N Pl Nom) OR (Pron Pers Pl3 Nom))(NEGATE 0 Inf LINK 1 COMMA OR CC LINK 1 Inf);
 	#$ Sii ožžot smiehttanáiggi jos háliidit muitalit dan birra.
 	#% Diet vuoigatvuođat leat ráddjejuvvon guoskat dan vuoigatvuhtii ahte oamastit, atnit, ovddidit ja mearridit daid eatnamiid ja riggodagaid mat sis leat odne árbevirolaš čanastumi dihte.


#!! Remove...


#!! The following two may be joined:

#!! * __r982__ removes Prt Sg2 if Pl3 subject - 6002
REMOVE:r982 (V Prt Sg2) IF (*-1 NP-HEAD-PL-NOM BARRIER VFIN OR S-BOUNDARY)(0 (V Pl3));
 	#$ Daid gažaldagaide, mat dán čielggadeamis galget gieđahallot, ...

#!! * __r983__ removes Prt Sg2 if Pl3 subject via CS - 305
REMOVE:r983 (V Prt Sg2) IF (-1 CS LINK *-1 NP-HEAD-PL-NOM)(0 (V Pl3));
 	#$ Hálddašanásahusat maid várra fertejit dákkár deasttaid atnit rievttálaš árvvoštallamis go galget mearridit mii dan sisdoallu berrešii leat.

#!! * __VPl3Lookalikes__ removes "verbs" like "manne" and "dušše" (r984) - 274
REMOVE:VPl3Lookalikes (V Ind Pl3) IF (1C VFIN)(NEGATE 1 Pl3) ;
 	#$ Manne ii leat áviisa boahtán?
 	#$ Máhtte manai čuoigga skuvlii.
 	#$ Vihtta bivdovuogi mun gal dušše namahin.

#!! * __VSg3Lookalikes__ removes "verbs" like "skuvlii"
REMOVE:VSg3Lookalikes (V Ind Sg3) IF ((*-1C VFIN BARRIER S-BOUNDARY OR REAL-V OR CC OR CS OR COMMA LINK NOT -1 Rel) OR (0 (N Ill) LINK -1 Gen - TIME))(NEGATE 1 (Nom Sg3) OR (N Nom Sg)) (NEGATE -1 (Nom Sg3) OR (Nom Sg)) ;
	#$ Moai vázze ruovttus skuvlii.
	#% Vai bálvalusas lea buohccedikšofágalaš dássi eaktuduvvo ahte leat fágabargit geat virgáibiddjojit.


#!! * __VPl3NotSg2BefPassive__ removes Sg2 for Pl3 and Inf before passive (r985)
REMOVE:r985 (Prt Sg2) IF (NEGATE *-1 (Pron Pers Sg2))(0 (Prs Pl3))(*1 Der/Pass BARRIER NOT-ADV);
 	#$ Reivelonuhallan sámi institušuvnnaiguin galgá dahkkot sáme- ja dárogillii.
	# Probably not prodrop if passivized Sg2.

#!! * __EssNotV__ selects Ess instead of VFIN
SELECT:EssV Ess IF (0 ESS-ADVL-N)(*-1 VFIN BARRIER S-BOUNDARY) ;
	#$ Beaskkain bivvá buolašin.

REMOVE Du2 IF (0 ("goahti") LINK NOT *-1 (Nom Du2)) ;
	# gođiide

	#!! !PrsPrc
	# - - - -

#!! * __PrsPrc__ selects PrsPrc if coordinated with A - 10
SELECT:PrsPrcA PrsPrc IF ((-1 GRADE-ADV LINK 2 N) OR (1 CC OR COMMA LINK 1 A LINK 1 N));
 	 #$ Soahpamušas váilot njuolggadusat smávit vahágahtti ja geavatlaččat bággolonistemiide. #Dette eksemplet inneholder Adv, ikke A
 	#$ Mearridanváldi berre leat nu čadni go vejolaš.
 	#$ Mudden ferte dagahit janza ja hui vahágahtti meassamiid.
#!! Early rule since many PrsPrc readings are removed later.

SELECT:PrsPrcAdvl PrsPrc IF ((-1 PLACE + Loc) OR (-1 PLACE-PO LINK -1 Gen))(NEGATE 0 VFIN LINK *-1 (Nom Sg)) (1 N);
	#$ Finnmárkkus ássi olbmuid divat ii galgga leat alit go guovtti dan mađe go gielddas ássi olbmuid divat.
	#$ Uvnna duohken orru sáhpánat rikkerejit beaivvi.
	#$ Guhkkelis boaittobeale orru mánát bohte Gárdima ja Mollešjoga, Šuoššjávrri ja Iešjohleagi duoddarstobuin.
	#$ Ledje gal geahččalan, vaikko unnán lihkostuvve, ásahit smávva skuvlastobožiid, gos johtti oahpaheaddji ja skuvliiohcaleaddji nuorat sáhtte gávnnadit moadde vahkku ain háválassii.

SELECT:PrsPrcAdv2 PrsPrc IF (-1 COPULAS)((-2 PLACE + Loc OR Adv) OR (-2 Po LINK -1 Gen))(1 N);
	#$ Muđui ledje johtti oahpaheaddjit, muhto eai doarvái.

SELECT:PrsPrcImprt PrsPrc IF (0 (Imprt Du2) LINK 2 (Neg Pl3)) ;  #Bør utvides
	#$ Leahkki vuoigatvuođat eai lihkahallojuvvo.

#!! * Actio Gen
SELECT:GenVeara_a (Actio Gen) IF  (1 ("veara") OR ("dihte"));
SELECT:GenVeara_b (Actio Gen) IF  (1 CC LINK **1 Gen BARRIER NOT-NPMOD-ACC LINK 1 ("veara") OR ("dihte")) ;
	#$ Dasgo bargi lea bálkkás veara.
	#$ Guoimmuhan ja lassiruđa háhkama dihte lágiduvvojedje maiddái eará doalut, nugo iešguđet lágan smávit gilvvožat.

SELECT:ActioNom (TV Actio Nom) IF (0 N LINK NOT 0 Der/Pass LINK -1 HAB-ACTOR + Gen OR (Pers Gen))(*1 N BARRIER NOT-A LINK NOT 0 ("dihte")) ;

REMOVE:ActioGen (Actio Gen) IF (NOT 1 Po)(NEGATE 1 CC LINK 1 Gen LINK 1 Gen LINK 1 Po) ;
	#$ Sii lohket fatnasa gopmánan.
	#$ Sii lohket bártni lápmašuvvan.

SELECT:ENr2996b PL3-V IF (NOT *-1 POSITIVE-V BARRIER CS OR CC)((*1 (Pl Nom) BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf) OR (1 ("leat" PrfPrc) LINK *1 (Pl Nom) BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf) OR (1 ("leat" PrfPrc) LINK *1 NUM + Nom BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf) OR (*1 NUM + Nom BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf)) (NEGATE 0 ("leat" Imprt Pl2))(NEGATE 0 Inf LINK -1 MOVT)(NEGATE 0 Imprt LINK -1 CC OR BOS OR COMMA OR (":") OR ("de") OR PUNCT)(NEGATE 0 Po LINK -1 Gen);
	## Muhto dorskit gal leat varrasat.
	## Ja dis leat velá buot oaivvi vuoktačalmmit lohkkojuvvon.
	#$ Son lea čállán lohkameahttun čállosiid sámi historjjás ja sus leat leamaš ollu luohttámušdoaimmat.
	 #% Mii doarjut Sámelávdegotti evttohusa oainnu ahte sámegiela berre sáhttit válljet fágan dan sadjái go duiskkagiela gymnásaráhkkaneaddji linnjás nuoraidskuvlla ovccát jagis.

SELECT:ENleatPl3Rel ("leat" Pl3)  IF (*-1 PL3-V BARRIER VFIN OR S-BOUNDARY LINK -1 (Rel Nom Pl) LINK -1 NP-HEAD-PL-NOM) (*1 NP-HEAD-PL-NOM BARRIER NOT-NPMODADV) ;
	#$ Oahpaheaddjit geat leat leamaš dán suorggis leat sihke sámit ja dážat, muhto dat geat bohte olggobealde ledje ovdal bargan dearvvašvuođabálvalusas sámi guovlluin ja ledje integrerejuvvon sosiálalaččat ja dađistaga maiddái gielalaččat. # leat sihke

#!! * __ENInf1__
SELECT:ENInf1 Inf IF (*-1 INF-V OR MOD-ASP BARRIER S-BOUNDARY OR CS OR Neg OR COMMA LINK NOT 0 N - Der/NomAg OR PrfPrc)(NEGATE *1 Inf BARRIER SV-BOUNDARY OR Acc)(NEGATE 0 Adv);
  	#$ Bissuin galgá heahkkahuhttit, ii nie váigadit goddit; sáhttá leat váiga.
	#$ Dearvvašvuođabargit galget divššohasa agi, silolaš dilálašvuođa, láddama ja vásáhusduogáža ektui láhčit dilálašvuođaid buoremus lági mielde.
	#$ Vuoittut mat galge čohkket olbmuid básárii ja oažžut sin rahpat sin juovlaburssaid, ledje gal anolaš ja ođđaáigásaš diŋggat.

#!! * __ENInf2__ selects Inf (NOTE, this was further down in sme-dis)
SELECT:ENInf2 Inf IF (*-1 INF-V + V BARRIER SV-BOUNDARY OR Neg OR CS OR COMMA)(NEGATE -1 Attr)(NEGATE -1 Attr OR PrfPrc LINK *-1 CC BARRIER NOT-ADV) ;
	#$ Skuvla galggai oahpahit sámemánáide dárogiela.
	#$ Prošeavtta áigut čađahit.
	#% Mearrasápmelaččat ledje áŋgiris ja oahppan bivdit.


#!! !!!Section 4: CYCLE 1B: REMOVING THE READINGS THAT WERE LEFT FROM THE 1A RULES
# =====================================================================
SECTION

#!! We don't need more Px sections, it's done alrady

#!! !!Noun, adjectiv, PrsPrc or not?
SELECT:NCoor (N Nom) (0 VFIN LINK -1 ("ja") OR COMMA LINK -1 (N Sg Nom))((1 EOS) OR (1 ("ja") LINK 1 (N Sg Nom))) ;
	#$ Seamma gusto uhcit fuđđožiidda nu go neahti, čeavrris, mievri ja rieban.


SELECT:NounNotEará N IF (-1 (A Attr) OR (Indef Attr))(NEGATE -1 Adv OR Interr)(NEGATE 0 Inf OR VFIN OR Der/veara OR ("veara") OR ("leat" PrfPrc) OR Indef)(1 NP-BOUNDARY);
	#$ Dat lea čuovvovaš sajis.
	#$ Áhku vuostá lea buoremus vuostá maid mun dieđán.
	#$ Jáhkán livččii váddáseabbu leamaš jus ii livčče juogalágan bearašoktavuohta, lohká Budeaju nieida gii lea bajásšaddan gávpogis.
	# It should not take the latter one.


#!! * __NnotAcoord__ removes A instead of N (earlier: selects N instead of A), based on coordination with N, and a vfin-verb
REMOVE:NnotAcoord A IF (NEGATE 0 PrfPrc LINK *0 REAL-COPULAS + PL-V BARRIER SV-BOUNDARY)(0 N LINK NOT 0 A LINK 1 COMMA OR CC LINK *1 HEAD-N BARRIER NOT-NPMOD OR CC LINK 1 CC OR EOC OR VFIN);
#SELECT:NnotAcoord N (NEGATE 0 PrfPrc LINK *0 REAL-COPULAS + PL-V BARRIER SV-BOUNDARY)(0 A LINK 1 COMMA OR CC LINK *1 HEAD-N BARRIER NOT-NPMOD OR CC LINK 1 CC OR EOC OR VFIN);
	#$ Sápmelaččat ja dážat leat ovdal eallán ovttas.
	#$ ...ležžet sii dál sápmelaččat, dážat dahje kveanat.

#!! * __NPlbeforeRel, NSgbeforeRel__ select N in front of Rel and MO
SELECT:NPlbeforeRel N + Pl IF (NEGATE 0 Inf OR Adv)(*1 Rel + Pl BARRIER WORD LINK NOT 0 Sg) ;
SELECT:NSgbeforeRel N + Sg IF (NEGATE 0 Adv OR Po OR VFIN)(NEGATE -1 HEADOFPARTS OR (Dem Pl Loc))(*1 Rel + Sg BARRIER WORD LINK NOT 0 Pl) ;  # Lene: fjerna MO og la til not Sg/Pl pga av maid som kan være både Sg og Pl
SELECT:NbeforeRel N IF (NEGATE 0 Adv OR Po OR VFIN OR Inf)(NEGATE -1 HEADOFPARTS OR (Dem Pl Loc))(1 Rel LINK *1 VFIN) ;
	#$ Dat leat doaimmat, maiguin mun barggan.
 	#$ Gažaldat lea dušše, man ollu ... # NOT 0 Adv!
 	#$ Lea miellagiddevaš lohkat, maid ...# NOT 0 Inf!
	#% Nuba áddjá árvala ahte eiseválddit galggaše bálkáhit baicce vuorrolohkkiid, vai rehkenasto vuorruid mearri maiddái, iige dušše dat mearri guliin mii goddo deanus.

#!! !!Adjectives and adverbs

#!! !Adv or not?
#!! maid has many readings and as Rel it is a member of S-BOUNDARY. Therefore we need to disambiguate is early in this file. Most important is to select Adv. Because of that A ang N still can have Vfin readings, it is difficult to make very general rules.

#!! * __vaikkomii__
IFF:vaikkomiiIndef ("mii" Indef) OR ("gii" Indef) OR ("makkár" Indef) IF (-1 ("vaikko") OR ("vaikke"))(NEGATE 0 (Sg Nom) LINK *1 VFIN + Pl1 BARRIER SV-BOUNDARY)(NEGATE 0 ("man") LINK 1 A) ;
SELECT:vaikkomiiAdv_a ("vaikko" Adv)  IF (1 ("mii" Indef) OR ("gii" Indef) OR ("makkár" Indef) LINK NEGATE 0 (Sg Nom) LINK *1 VFIN + Pl1 BARRIER SV-BOUNDARY) ;
SELECT:vaikkomiiAdv_b ("vaikko" Adv)  IF (1 ("man") LINK NOT *1 VFIN) ;
SELECT:vaikkomiiIndefSg ("mii" Indef Acc Sg)  IF (-1 ("vaikko") OR ("vaikke")) ;

#!! * __Interj__ or not
IFF:Interj Interj  IF (*-1 BOS BARRIER WORD LINK *1 Interj LINK 1 COMMA OR EOS);
 	#$ Maid, iigo leat boahtán?

#!! * __maidAdv1__ selects maid Adv when there is no vfin to the right.
SELECT:maidAdv ("maid" Adv) IF ((NEGATE *1 VFIN BARRIER S-BOUNDARY OR CS) OR (*1 COMMA BARRIER VFIN LINK 1 CC) OR (1 Nom LINK -2 Loc LINK *-1 COPULAS BARRIER NOT-PCLE LINK NOT *-1 Nom))(NEGATE *1 NP-HEAD BARRIER NOT-NPMOD LINK 1 Rel LINK *1 VFIN LINK *1 VFIN)(NEGATE 1C CC)(NEGATE -1 BOS LINK *1 EOS BARRIER VFIN) ;
 	#$ Dán oktavuođas čujuhuvvo maid oanáduslistui ja gáldolistui.
 	#$ Sápmelaččat oasálaste maid dasa, muhto lassin ledje sis vel eará resursat.
 	#$ Dá lea reive maid.
 	#$ Dattetge bohciida maid sierra gažaldat, ...
 	#$ Boahtágo son maid?
 	#$ Riggodagaide gullet maid fuođđut ja lottit.
  	#% Maid mii dál vihtaniiguin?

#!! * __maidAdv2__ selects maid Adv copulas and PrfPrc or Actio Ess. We need this rule because of that there can be an Inf to the right which also has Vfin reading.
SELECT:maidAdv ("maid" Adv)  IF (*-1 COPULAS BARRIER NOT-ADV)(*1 PrfPrc OR (Actio Ess) BARRIER S-BOUNDARY OR ("leat"));
 	#$ Lea maid lassin gávnnahan metodalaččat váttisin mearridit...


#!! * __maidAdv3__ selects maid Adv even if there is a vfin to the right.
SELECT:maidAdv ("maid" Adv)  IF (*-1 CS - go OR BOC OR Rel BARRIER VFIN OR CLB)(NEGATE -1 go)(NEGATE 0 Rel LINK -1 Acc LINK -1 BOS)((*1 VFIN - Pot LINK NOT *1 VFIN BARRIER CC OR COMMA) OR (NEGATE **1 TRANS-V BARRIER CC OR COMMA OR REAL-V) OR (*1 COPULAS BARRIER REAL-V LINK 1 PrfPrc OR (Actio Ess) LINK NOT *1 VFIN)) (NOT *1 ("?"));
 	#$... nu ahte dat maid siskkilda ávnnaslaš kultureavttuid.
   	#$ Maid moarsi lea borran.
   	#$ ... mat maid leat politihkalaš bellodagat.
 	#$ Suomas maid lei dilli váttis.
 	#$ Sidjiide maid lei Ruija.
 	#$ Lea go stáhtas maid geatnegasvuohta doarjut dán kultuvrra.
 	#$ Leago stáhtas maid geatnegasvuohta doarjut dán kultuvrra.
	#$ Maid moarsi ja irgi leaba čiŋadan gávttiiguin.
	#% Na maid dal juo dagaža ja maid ii dagaš. #derfor VFIN - Pot
	#% Dan maid dalle dárbbaša fievrridit, ferte olmmoš noađuhit.

#!! * __maidAdv4__ selects maid Adv between two verbs or the verb after is IV
SELECT:maidAdv ("maid" Adv) IF (NEGATE -1 TIME OR MEASURE) (1 VFIN + IV LINK NEGATE 0 AUX LINK *1 Inf OR PrfPrc OR (Actio Ess) OR ConNeg BARRIER S-BOUNDARY OR COMMA);
SELECT:maidAdv ("maid" Adv)  IF (*-1 INF-V BARRIER REAL-V)(*1 Inf BARRIER REAL-V);
 	#$ Mun háliidan maid mannat.
 	#$ Sápmelaččat galge maid vearu máksit Danmárku-Norgii.

#!! * __maidAdv5__ selects maid Adv in front of Comp which at this stage can have vfin analysis.
SELECT:maidAdv ("maid" Adv)  IF (-1 COPULAS)(*1 (A Comp) BARRIER NOT-ADV);
 	#$ Geainnut ledje maid guhkit rabas.

#!! * __maidAdv6__ selects maid Adv between copulas Pl3 and N Pl.
SELECT:maidAdv ("maid" Adv) IF  (-1 COPULAS + Pl3)(*1 (N Pl Nom) BARRIER NOT-NPMODADV);
 	#$ Romssa universitehtas leat maid plánat álggahit fálaldagaid.

#!! * __maidAdv7__ in a special construction with geahččat
SELECT:maidAdv ("maid" Adv) IF (*1 ("geahččat") BARRIER SV-BOUNDARY LINK 0 VFIN OR ConNeg OR PrfPrc OR Inf LINK 1 Ill);
 	#$ Maid don geahčat munnje?

#!! * __maidAdv8__ selects maid Adv after a Pers
SELECT:maidAdv8 ("maid" Adv) IF  (-1 (Pron Pers Nom) - ("dat")) ;
	#$ Mun maid ferten málestit guktii vahkus.
	#$ Jus lean váiban ja don maid leat váiban, de manne nohkkat.

#!! * __maidAdv9__ selects maid Adv even
SELECT:maidAdv9 ("maid" Adv) IF  (-1 (N Sg) - Sem/Group LINK NOT *-1 REAL-V)(1 VFIN LINK NOT *1 VFIN + Sg3) ;
	#$ Lágamánneriekti maid dubmii dievddu máksit 130 000 kruvdnosaš buhtadusa gillájeaddjái.

#!! * __KillmaidAdv__ removed the remaining maid Adv
REMOVE:KillmaidAdv ("maid" Adv) ;

#!! __matPcle__
IFF:matPcle ("mat" Pcle) IF (-1 Imprt LINK *-1 BOS BARRIER WORD) ;

#!! The following two rules are omitted. They only inflect on the disambiguation of mat pcle, a wackernagel, which is done in the rule over here, I think.


# * __miiRel__ selects maid Rel when there is vfin to the right and comma
#SELECT:miiRel ("mii" Rel)(*1 VFIN BARRIER S-BOUNDARY LINK *1 COMMA BARRIER S-BOUNDARY LINK 1 VFIN)(NEGATE -1 (N Pl) LINK *2 PL3-V BARRIER NOT-ADV);
 	#$ Dan maid dalle dárbbaša fievrridit, ferte olmmoš noađuhit.
   	#$ Buot maid dii dahkabehtet, lea buorre.
 	#$ Don galggat doallat dan maid leat vurdnon Hearrái.

# * __miiRelcoord__ selects maid Rel in a coordination with Rel or MO
#SELECT:miiRel ("mii" Rel) (1 CC LINK 1 MO OR Rel);
 	#$ Lea registreren maid ja man ollu lea borran.


SELECT:manAdv ("man" Adv) IF (1 (A Nom) OR ("uhcán") OR ("unnán") OR OLLU LINK NOT 0 Der/jahkásaš OR Der/diibmosaš);
 	#$ Man stuoris mánná lea?
REMOVE:manAdv ("man" Adv) IF (1 N + ADVLCASE);

#!! * __olluNom__
SELECT:olluNom OLLU + Nom IF ((0 Qst) OR (1 go))(*1 COPULAS)(NOT -1 ("nu") OR ("na"));
	#$ Ollugo diibmu lea?
	#$ Ollu go diibmu lea?
	#$ Man ollu lea diibmu?
	#$ Bargiid guottut mearkkašit seamma ollu go fáktadieđut.


REMOVE:manV-N V OR N IF (-1 ("man" Adv) OR ("mihá"))(NEGATE 0 Po);
	#$ Man guhkki lea girkui?
	#$ In dieđe man buorit dat leat.
	#$ Muhtun riggodagaid geavahit mihá unnit dál.

REMOVE:seammaV V IF (-1 ("seammá"))(*1 go BARRIER NOT-N-A LINK NOT 1 VFIN);
SELECT:Vihkinassii REAL-V IF (-1 ("ihkinassii"));
 	#$ Mun áiggun čuovvut du ihkinassii manažat.

REMOVE:NgradeAdv N IF (-1 GRADE-ADV - Foc)(0 Adv OR A OR PrfPrc);
 	#$ Mun lean nu suhttan.
 	#$ Lea hui easkka čuožžilan gažaldat.

SELECT:CopAdvPrfPrc Adv IF (-1 COPULAS)(1 PrfPrc - ("leat"));
SELECT:ovttasAdv Adv IF (-1 REAL-V OR Com)(0 COM-ADV)(NEGATE 0 Foc/ge OR Num);
 	#$ Divššohas lea duođalaččat roasmmohuvvan.
 	#$ Bátnedoavttir galgá ovttas eará bargiiguin mearridit.

#!! * __olluAdv__
SELECT:olluAdv1 OLLU + Adv IF ((-1 AUX LINK 2 Inf OR PrfPrc) OR (1 Adv LINK NOT 0 N) OR (1 go LINK 1 (A Nom) OR VFIN) OR (-1 REAL-V LINK NEGATE 2 A OR N)) ;
	#$ Dolin olbmot lávejedje ollu dulkot nieguid.

SELECT:olluAdv2_a OLLU + Adv IF (NOT *-1 REAL-V BARRIER S-BOUNDARY)(1 Pron LINK *1 VFIN);
SELECT:olluAdv2_b OLLU + Adv IF (NOT *-1 REAL-V BARRIER S-BOUNDARY)(1 VFIN LINK NOT 0 N) ;
	#$ Nissonat duddjojedje dieđinge biktasiid, gávnniid ja biergasiid maid ieža atne, muhto ollu sii vuvde márkaniin, sihke Bossegohpis ja Ivgubađas.

SELECT:olluAdv3 OLLU + Adv IF (-1 GRADE-ADV LINK -1 REAL-V - AUX) (1 N + ADVLCASE OR Pron) ;
	#$ Ja go geahččá maŋás historjjás, de lea Bargiidbellodat bargan hui ollu sámepolitihkain, dadjá Ørnebakk.
	# Uløste:
	 #$ Man ollu sudnos lea alddiska ruhta?
	 #$ Girkonjárggas fuomášin man ollu negatiiva oainnut dan áigge ledje sámiid vuostá.
	 #$ Ollu eará maŋŋá dollojuvvon boazosápmelaččaid riikkačoahkkimiin leat skuvladilálašvuođat leamaš áššin.


IFF:ruovttusAdv ("ruoktu" Sg Loc)((*1 Ill BARRIER NOT-NPMOD) OR (-1 Gen)) ;
	## Adv

SELECT:vuoiA (A Nom) IF (-1 ("vuoi"));
 	#$ Vuoi surgat dan gándda!

SELECT:eanasAdv Adv IF (-1 COPULAS)(0 ("eanas") OR ("eanaš"))(1 N)(NEGATE 1 TIME)(*2 EOC BARRIER N);
 	#$ Dál lea eanaš ovttamielalašvuohta, ahte...
 	#$ Ovdal ledje eanas vuonaid bivdán, muhto go skoavdefatnasat bohte...
 	#$ Ealus leat eanaš álddut.


SELECT:eanasAttr (A Attr) IF  (-1 V OR Acc)(0 ("eanas" A) OR ("eanaš" A))(1 N LINK NOT 0 Ill);
	#$ Ná sáhttá geavvat eanas geasi.

SELECT:CompahteComp (A Comp) (-1 ("ahte") LINK -1 (A Comp)) ;
	#$ Dat šattai heajut ahte heajut.

REMOVE:A A IF (*-1 IV BARRIER SV-BOUNDARY OR CC LINK NOT 0 COPULAS OR ("áibbas") OR Indef)(0 Adv)(1 EOC);
REMOVE:Amannit A IF (*-1 IV BARRIER SV-BOUNDARY LINK NOT 0 COPULAS)(0 ("maŋŋit" Adv))(1 Ill OR DOHKO)(2 EOC);
	#$ Boađán maŋŋeleappos.
	#$ Eatnandoallu bođii maŋŋit Sápmái.

SELECT:gradeAdv GRADE-ADV IF (0 REAL-V LINK 1 A OR Adv) ;
SELECT:gradeAdvA GRADE-ADV + Adv IF (0 A OR Indef LINK 1 (A Nom)) ;
SELECT:gradAdvAdv MOD-ADV-ADV IF (1 Adv OR Indef LINK NOT 0 Po OR N) ;
	#$ Dii uhccán oskkolaččat.
	#$ Man uhccán osku dus lea!

SELECT:jogoAdv Adv (0 ("jogo") OR ("juogo"))(NEGATE *-1 VFIN BARRIER S-BOUNDARY) ;
	#$ Jogo gikse dat guolit?

#!! !!Disambiguating abbreviations

#!!	Abbreviations are also disambiguated via normal case disambiguation rules,
#!!	but some contexts are special to abbreviations, and therefore dealt with
#!!	separately here. Keep in mind: perhaps the placement of these rules is to early.

#!! * __AttrABBRNum__
SELECT:AttrABBRNum Attr IF (*-1 BOS LINK NOT *1 V)(0 (N ABBR))(1 Num);
	#$ Sd.prp.nr. 63:s.

SELECT:AttrNr Attr IF (0 NUMMAR LINK 1 Num);
	#$ Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna.


#!! !!Disambiguating particles

#!! * __sonPcle__ selects son Pcle, the remaining Pcle are removed
SELECT:sonPcle Pcle IF (0 ("son"))(1 NP-HEAD-NOM)((NOT -1 PUNCT LINK -1 BOS) OR (-1 N LINK *-1 BOS BARRIER NOT-NPMOD)) ;
	#$ Mo son Duommá lea dahkan?
	#$ Dan viesu son mun dovddan.

REMOVE:KillSonPcle Pcle IF (0 ("son"));
 	#$ Son mu nama ipmirdii boastut.


#!! !!Disambiguating clitics

REMOVE:Dego Qst IF (0 ("dego") OR ("dugo"));
	## Su ámadadju báittii dego beaivváš.

REMOVE:Nugo Qst IF (NEGATE *-1 REAL-V BARRIER WORD)(0 Adv)(NEGATE *1 ("?"));
	## Nugo dieđát, de lean dál.

REMOVE:leagis Foc/gis IF (0 ("leahki")) (1 EOC OR CCCOMMA);
	## Mánát bohte Kárášjoga leagis, Deanu leagis ja Anárjoga leagis.
# Philosophy: "Dat leagis duohta" (if possible?): -gis modifies something to the right.
# If correct, the rule might be generalised.

#!! !!Disambiguating numerals


#!! !!Disambiguating adpositions

#!! !čađa
#!! * __caddaN__ if čađa and movement-v
REMOVE:caddaN ("čađđa") IF (0 ("čađa") LINK 0* MOVEMENT-V BARRIER SV-BOUNDARY) ;
	#$ Gal sii besset čađa.


#!! Commented out som adp-rules we don't need anymore:

# čađa Po and Pr if Sem/Time Gen
#SELECT:cadaPoTime ("čađa" Po) (-1 TIME + Gen)(NOT 1 TIME + Gen);
	#$ Ii sáhte dieđusge dušše suohtastallat go rievtti mielde leat dát meassut áiggiid čađa leamaš girkostallanmeassut.
#SELECT:cadaPo1 Po (0 ("čađa") LINK -1 Gen)(1 MANNU + Gen LINK 1 Ord);
#SELECT:cadaPrTime ("čađa" Pr) ((1 ("gaska" Gen)) OR (1 Sem/Time + Gen LINK NOT -3 Sem/Time + Gen));
	#$ Nu dáhpáhuvai čađa gaskka.


#!! !geahčai
#!! * __geahcaiPP__ not geahččat V
REMOVE:geahcaiPP ("geahčai")  IF ((0 ("geahččat") LINK NOT -1 Gen) OR (0 ("geahččat") LINK *1 Ill BARRIER NOT-NPMOD));
 	#$ Áhkku geahčai iežas speadjalis.
 	#$ Tommi geahčai boksii.


#!! !guovddaš
#!! * __guovddasPP__ or not
REMOVE:guovddasPo ("guovddáš" Po) IF (NEGATE -1 GENERAL-PLACE OR VAHKKU-DUR) ;
REMOVE:guovddasPr ("guovddáš" Pr) IF (NEGATE 1 GENERAL-PLACE OR VAHKKU-DUR) ;
REMOVE:guovddasA ("guovddáš" A Sg) OR ("guovddáš" A Ess) IF (NEGATE -1 COPULAS) ; #Needs constraints
	#% Dat váccaša guovddáš márkana.

#!! !mađe
#!! * __madePo__ after Num Gen
SELECT:madePo MADE + Po IF (-1 (Num Gen)) ;

#!! * __NumMade__ Num before mađe
SELECT:NumMade Num IF (1 MADE);

#!! !miehta
#!! * "miehtá" is also VFIN, and miehtá needs special treatment
#!! ** __miehtaPo__ after place or time Gen
SELECT:miehtaPo ("miehtá" Po)  IF (-1 PLACE + Gen OR VAHKKU-DUR + Gen OR (Sem/Plc Gen)) ;
	#$ Áldu mii dálvvi miehtá njamaha miesi gohčaduvvo čearpmateadni.
	#$ Beaivvi miehtá galget earát lohkat galle geardde viehká mearriduvvon guhkkodaga, ja galgá sáhttit árvidit galle geardde háhppeha viehkat 12 diimmus.

#!! ** __miehtaPr__ before place or time Gen
SELECT:miehtaPr ("miehtá" Pr)  IF (1 PLACE + Gen OR VAHKKU-DUR + Gen OR (Sem/Plc Gen)) ;
 	#$ Dalle bisttii luossaguolli miehtá dálvvi.
 	#$ Sii dieđihedje dan miehtá guovllu, ja sii bukte su lusa buot buhcciid.
 	#$ Searvegottiin lei dál ráfi miehtá Judea ja Galilea ja Samaria.
 	#$ Áldu mii dálvvi miehtá njamaha miesi gohčoduvvo čearpmateadni.
 	#$ Dat sáhttet leat ovttasbargošiehtadusat oanahis áigái, dahje oktasašdoallu muhtin áiggiid jagis, dahje vaikko miehtá jagi.
 	#$ Beaivvi miehtá galget earát lohkat galle geardde viehká mearriduvvon guhkkodaga, ja galgá sáhttit árvidit galle geardde háhppeha viehkat 12 diimmus.
	## Olbmuin bisttii luossaguolli miehtá dálvvi.


#!! * "ovddas" has many readings and needs special treatment
#!! ** __ovddasPo__  - commented out because we don't need it
#SELECT:ovddasPo ("ovddas" Po) (-1 Gen LINK *-1 NP-HEAD-NOM BARRIER S-BOUNDARY)(1 V); #we don't need it anymore
  	#$ Láilá Susanne Vars lea ge dál Sámedikki ovddas bargamin njuolggadusaiguin Finnmárkoláhkii.

#!! * special rules for rastá because it often is Adv, and it can be an object connected to the PP
#!! ** __rastaAdv__  čuohppat/časkit/sahet rastá
SELECT:rastaAdv ("rastá" Adv) IF (*0 ("čuohppat") OR ("časkit") OR ("čuohpastit") OR ("sahet") BARRIER SV-BOUNDARY) ;

#!! ** __rastaPo, rastaPr__  fievrridit olbmo man nu rastá
REMOVE:rastaPo ("rastá" Po) IF (-1 HUMAN + Acc OR (Pers Acc) LINK *0 ("doalvut") OR ("fievrridit") OR ("guoddit") OR ("láidet") BARRIER SV-BOUNDARY) ;
REMOVE:rastaPr ("rastá" Pr) IF (1 HUMAN + Acc OR (Pers Acc) LINK *0 ("doalvut") OR ("fievrridit") OR ("guoddit") OR ("láidet") BARRIER SV-BOUNDARY) ;

#!! ** __rastaPr__  rastá ráji/rájá
SELECT:rastaPr Pr IF (0 ("rastá") OR ("badjel"))(1 ("rádji" Gen) OR ("rádjá" Gen)) ;

#!! * __birraPo, birraPr__ special rules for birra because it often is Adv, and it can be an object connected to the PP
SELECT:birraPo ("birra" Po) IF ((-1 ("máilbmi" Gen)) OR (-1 NP-HEAD-GEN LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-ADV-PCLE)) ;
	#$ Dattetge ii leat muitaluvvon geavahanriiddu birra dálolaččaid ja boazodoalu gaskkas.
	#$ Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu birra dálolaččaid ja boazodoalu gaskkas. #This doesn't function yet.
SELECT:birraPr ("birra" Pr) IF (1 ("máilbmi" Gen))  ;
	#$ E-boasta lea hui álkes ja jođánis vuohki sáddet reivviid birra máilmmi.

#!! * "vuostá" has many readings and needs special treatment
#!! ** __vuostaAdv__  váldit vuostá/vuostái
SELECT:vuostaAdv Adv IF (-1 ("váldit"))(0 ("vuostá") OR ("vuostái"));
 	#$ Sámi servodagas váilu árbevierru váldit vuostá almmolaš diehtojuohkima dakkár hámis.
#!! ** __vuostaPr__  váldit vuostá/vuostái
REMOVE:vuostaPr Pr IF (0 ("vuostá") OR ("vuostái"))(*-1 REAL-V BARRIER S-BOUNDARY LINK NOT 0 REAL-COPULAS) ;


#!! * vuollel ja badjel as Adv in front of Num
SELECT:vuollelbadjelAdv Adv IF (0 ("vuollel") OR ("badjel"))(1 Num LINK *1 Loc OR Gen BARRIER NOT-NPMOD-ACC) ;
	#$ Vuollel 12 jahkásaš mánáin ii leat iešheanalis geahččanvuoigatvuohta.
	#$ Vuosttasnaššuvnnaid čoagganeapmi ovddasta badjel 300 indiánaservodaga miehtá Kanada ja gáibida dál iešstivrejumi Davvi-Amerihká buot eamiálbmogiidda.

#!!	!Adpositions that take modifiers

SELECT:GenPrGen Pr IF (-1 Gen)(0 ("bealde") OR ("beale") OR ("beallái") OR ("geahčai") OR ("geahčen") OR ("geaže"))(1 Gen)(NEGATE 1 TIME);
 	#$ Daid sáhttá heaŋggastit dan heargái mii daid eará goahtemuoraid lea guoddimin, guokte bealji goappáge beallái hearggi.

SELECT:gaskalPo GASKAL + Po IF (-1 NP-HEAD-GEN LINK -1 CC LINK -1 NP-HEAD-GEN) ;
 	#$ Dat lei min ja din gaskkas.

SELECT:gaskalPrNum GASKAL + Pr IF (1 Num LINK 1 CC LINK 1 Num);
 	#$ Gaskal 12 ja 16 jahkásaš mánáin lea muhtun muddui iešheanalis.

SELECT:gaskalPoNum GASKAL + Po IF (-1 Num LINK -1 CC LINK -1 Num) ;
 	#$ Alimusriekti veadjá leat jagiid 1854 ja 1897 gaskka doallan siskkáldas čoahkkima.

SELECT:gaskalPr GASKAL + Pr IF (*1 NP-HEAD-GEN BARRIER SV-BOUNDARY LINK 1 CC LINK *1 NP-HEAD-GEN BARRIER SV-BOUNDARY);
 	#$ Ferte navdit ahte dáža duopmostuolut ja eará riektegeavaheaddjit dábálaččat dulkojit dáža siskkáldas rievtti nu ahte garvá vejolaš vuostálasvuođa gaskal dákkár álbmotriektenorpma ja siskkáldas norgga njuolggadusa.
 	#$ Lága mearrádusat galget váikkuhit luohttevašvuođa gaskal divššohasa ja dearvvašvuođabálvalusa.

SELECT:gaskalPr GASKAL + Pr IF (*1 (N Gen) BARRIER NOT-NPMOD LINK *1 CC BARRIER NOT-NP LINK *1 (N Gen) BARRIER NOT-NPMOD LINK 0 HEAD-N);
 	#$ Dán láhkai lea leamaš muhtun muddui lotnolas doahttaluvvon dássádatdilálašvuohta gaskal stáhta almenneteaiggádin ja geavahanvuoigaduvvon giliássiid.

SELECT:gaskalPrNum GASKAL + Pr IF (*1 NUM BARRIER NOT-NPMOD);
 	#$ Ferte oaidnit oktavuođa gaskal dan guovtti soahpamuša.

SELECT:gaskalPrA GASKAL + Pr IF (1 A LINK 1 CC LINK 1 A LINK *1 (N Gen) + HEAD-N);
 	#$ Galgá válljejuvvot gaskal vejolaš ja dohkálaš iskkadan- ja dikšunvugiid.

SELECT:gaskalPo GASKAL + Po IF (-1 (N Gen) LINK *-1 CC BARRIER NOT-NPMOD LINK -1 (N Gen)) ;
 	#$ Riiddut boazodolliid ja eará vuoigatvuođaoamasteddjiid gaskkas livčče lassánan. # Does not function because of barrier

#!! !LIST LG-MATERIAL = Inf Adv Nom ;
LIST LG-MATERIAL = Inf Adv Nom ; # Eará?

#!! * __gaskasPosticky, gaskasPrsticky__ selects Po after coordinating language materials
SELECT:gaskasPosticky GASKAL + Po IF (-1 $$LG-MATERIAL LINK -1 CC LINK -1 $$LG-MATERIAL) ;
SELECT:gaskasPrsticky GASKAL + Pr IF (1 $$LG-MATERIAL LINK 1 CC LINK 1 $$LG-MATERIAL) ;
 	#$ Sániid oamastit ja eaiggáduššat gaskkas geavahuvvo "ja".

#!! * __PoParantes__ selects Po after paranteces
SELECT:PoParantes Po IF (-1 (PUNCT RIGHT) LINK *-2 (PUNCT LEFT) LINK -1 Gen);
	## ...sáhttá dulkot telestudio dahje govvatelefovnna (ruoná nummira) bokte.

#!!	!Requiring Genitive

#!! * __PoNomCompl__ removes Po if no possible complement to the left
REMOVE:PoNoCompl Po IF (NEGATE *-1 NP-HEAD-GEN OR (A Gen) OR (Num Gen) OR (Actio Gen) BARRIER WORD)(NEGATE 0 ("gaskan"));
	#$ Jus divššohas lea badjel 16 jagi boaris,...
	#$ Dat lea dušše badjelaš állanbeali guhku.
	#$ Jesus manai fatnasii ja vulggii rastá jávrri iežas gávpogii.
	#$ ... muhto ovdal dan áiggi ledje "sápmelaččat boahtán dán guvlui".
	#$ Ovdal dan áiggi ledje sápmelaččat boahtán dán guvlui.
	#$ Vuollel 12 jahkásaš mánáin ii leat iešheanalis geahččanvuoigatvuohta.
	#$ Dát dáhpáhuvvá dávjjit go dat, maid ovdal namuhin.
	#$ Seamma go ovdalis namahuvvon riektehistorjjálaš ovdanbuktimis leat dán barggus maid muhtun buhtes historjjálaš dilit, ja almmuhuvvo danne joavkku čielggadeami mielddusin (2. mielddus).
	#% Dorkka geavahedje gávtti vuolde.
	#% Beaskka geavahedje gávtti badjel.


#!! * __PoMeasure__ removes Po when MEASURE to the left
REMOVE:PoMeasure Po IF (-1 MEASURE LINK -1 Num)(1 Gen);
	#$ Ádjá viessu lea guokte miilla bajábeal márkana. #Contraints?

#!! * __PrGen1__ selects Pr
SELECT:PrGen Pr - Foc IF (NEGATE -1 NP-HEAD-GEN OR (A Gen) OR (Num Gen))(*1 NP-HEAD-GEN OR (A Gen) BARRIER NOT-NPMOD);
	#$ Maŋŋegiđa ja árrageasi guohtu boazu lulábeali rámaid, čohkaid ja jekkiid.
	#$ Jus divššohas lea badjel 16 jagi boaris,...
	#$ Jesus manai fatnasii ja vulggii rastá jávrri iežas gávpogii.
	#$ Ovdal dan áiggi ledje sápmelaččat boahtán dán guvlui.
	#$ Dat váccaša guovddáš márkana.
	#$ Vuosttasnaššuvnnaid čoagganeapmi ovddasta badjel 300 indiánaservodagaid miehtá Kanada ja gáibida dál iešstivrejumi Davvi-Amerihká buot eamiálbmogiidda.
	#$ 1978 láhka balansere gaskal sámi vuoigatvuođaid boarásmuvvan oainnu,...
	#$ Maŋŋil láivves vuosttaš vuoru lei boađus 0-0.
	#% Son muitala dás iežas skuvllafearaniid birra Buolbmát skuvllas. #Comment: Hits, but shouldn't.

#!! * __PrGen2__ selects Pr
SELECT:PrGen Pr - Foc IF (NEGATE -1 NP-HEAD-GEN OR (A Gen) OR (Num Gen))(*1 Attr BARRIER NOT-NPMOD LINK *1 NP-HEAD-GEN OR (A Gen) BARRIER NOT-NPMOD);
	#$ Loahpageahčen ovddit jahkečuođi bohte ollu láddelaččat Sápmái, ja sis lei buorre máhttu eatnandoalu birra.
	#$ Maŋŋá jápmimis de aliduvvui Gonagas Olav bassin.

#!! * __PrNoCompl__ removes Pr if no complement to the right
REMOVE:PrNoCompl Pr IF (NEGATE *1 NP-HEAD-GEN OR (A Gen) OR (Num Gen) BARRIER NOT-NPMOD) ;
	#$ Na mun gal hupmen dan birra.
	#$ Dán vuostá sáhttá dadjat, ahte ...
	#$ Dát dáhpáhuvvá dávjjit go dat, maid ovdal namuhin.


#!! * __PoGen__ selects Po
SELECT:PoGen Po - Foc - N-PO IF (-1 NP-HEAD-GEN - Qst LINK NEGATE 0 Acc LINK *-1 STRICT-TRANS-V BARRIER NOT-ADV-PCLE)(NEGATE -1 VAHKKU-DUR + Acc LINK -1 REAL-V) (NEGATE *1 NP-HEAD-GEN - (Rel) OR (A Gen) OR (Num Gen) BARRIER NOT-NPMOD LINK NOT *1 Po BARRIER SV-BOUNDARY) (NEGATE 1 STRICT-TRANS-V LINK NOT *1 Acc BARRIER NOT-NPMODADV);
	#$ Vuoi dan olbmo gean bokte fillehusat bohtet.
	#$ Nieida bođii ráhkkásis mielde.
	#$ Tommi ja Annika dárkilit geahččaba Pippi oaidnin dihte mo diŋggaohccit barget.
	#$ Mun gal attán dutnje bealji vuollái.
	#$ Mii bidjat boaldinmuoraid bearpmehiid gaskii.
	#$ Váldde biergasiid iežat fárrui!
	#$ Su vuosttaš girji bođii jagi 1986, ja girji lea muitalus nuorra sámenieidda birra gii šaddá bajás internáhtas.
	#$ Lávvomuoraid nala bidjat álo loavdaga.
	#$ Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu birra dálolaččaid ja boazodoalu gaskkas.
	#% Ii galgga leat vejolaš addit nuppi áššebeallái viidát vuoigatvuođaid dahje geatnegasvuođaid go alddis leat.
	#% Áhku vuostá lea buoremus vuostá maid mun dieđán.

SELECT:PoGenLahka Po - Foc - N-PO IF (-1 Num LINK -1 KLASS LINK -1 TEXT + Gen OR LAHKA + Gen) ;
	#$ Dearvvašvuođaásahusas mearriduvvo divvun dahje sihkkun dearvvašvuođabargiidlága § 44 mielde.

SELECT:PoGenSTV Po - Foc - N-PO IF (-1 NP-HEAD-GEN - Qst LINK *-1 STRICT-TRANS-V BARRIER NOT-ADV-PCLE LINK -1 Acc LINK NOT 0 Nom LINK NOT *-1 TRANS-V BARRIER Acc)(NEGATE -1 VAHKKU-DUR + Acc LINK -1 REAL-V)(NEGATE *1 NP-HEAD-GEN OR (A Gen) OR (Num Gen) BARRIER NOT-NPMOD) (NEGATE 1 STRICT-TRANS-V LINK NOT *1 Acc BARRIER NOT-NPMODADV);
	#$ Beaskka geavahedje gávtti badjel.
	#$ Dorkka geavahedje gávtti vuolde.
	#% Sámediggi lea bivdán ráđđehusa váldit Sámedikki mielde árvoháhkanprográmmabargui.

#!! !Diambiguation  Noun vs. Po or Pr:

#!! * __vuollaiPo__ selects
SELECT:vuollaiPo Po IF (*-1 Gen BARRIER WORD)(1 CC)(NEGATE -1 Acc LINK *-1 CC BARRIER NOT-NP LINK -1 Acc LINK *-1 TRANS-V BARRIER NOT-NPMODADV)(NEGATE 0 ("mielde") OR ("fárrui"));
	#$ Jos sáddo deavdá buođu vuollái dahje jos sáddo hávddarda buođu vuolde...

#!! * __beallaiPo__ selects
SELECT:beallaiPo Po IF (-1 Gen LINK NEGATE 0 Loc LINK *2 NP-HEAD-GEN BARRIER NOT-NPMOD)(NEGATE -1 REAL-V OR Adv)(NEGATE 0 TIME-PP)(1 TIME);
	#$ Sámi guovlluin ožžo Sámeparlameantta Suoma beallái jagi 1976.

#!! * __ovdalPr__ selects
SELECT:ovdalPr Pr IF (0 TIME-PP)(1 TIME OR ("soahti") LINK NEGATE 1 Num)(NEGATE 2 Po)(NEGATE 0 ("čađa") LINK 1 MANNU LINK 1 Ord LINK -3 Gen);
	#$ Bođeža go Ánne ovdal gaskaija?

#!! * __gaskanPo__ selects
SELECT:gaskanPo ("gaskan" Adv) IF  (-1 ("guovttá")) ;
	## Mun áiggošin duinna guovttá gaskan hupmat.

#!! * __gaskkasPo__ selects
SELECT:gaskkasPo Po IF (-1 Gen)(0 ("gaskkas") OR ("vuostá"))(NEGATE 0 ("vuostá") LINK *-2 BOC BARRIER NOT-NPMOD);
	#$ Son lea buoremusaid gaskkas.

#!! * __ovddasPo1__ selects
SELECT:ovddasPo1 ("ovddas" Po)  IF (-1 Gen)(*1 ("máksit") OR ("máksu") BARRIER SV-BOUNDARY);
	#$ Spider-Man-gova ovddas máksojuvvo bures!
	#$ Daid ovddas gal in máksán maidege.

#!! * __ovddasPo2__ selects
SELECT:ovddasPo2 ("ovddas" Po) IF (-1 Gen LINK *-1 ("máksit") OR ("máksu") BARRIER NOT-NPMOD) ;
	#$ Logut čájehit makkár kilohatti gaskamearálaččat Finnmárkkus ferte máksit ealggabierggu ovddas.

#!! * __ovddasPo3__ 	selects
SELECT:ovddasPo3 ("ovddas" Po) IF  (*-2 Acc BARRIER NOT-NPMOD LINK *-1 ("máksit") OR ("máksu") BARRIER NOT-NPMOD)(-1 Gen) ;
	#$ ...maid ovddas sihtá máksit bálkámassima 30 beaivvi ovddas.

#!! * __ovddasPocoord__ selects
SELECT:ovddasPocoord ("ovddas" Po) IF  (-1 Gen LINK *-1 (V Neg) BARRIER V OR Pr OR Po LINK *-1 ("ovddas" Po) BARRIER NOT-NPMOD);
	#$ Sámediggi lea miehtan sutnje máksit 800 ruvnno juohke árgabeaivvi ovddas, muhto ii fal lávvordagaid, sotnabeivviid ja bassebeivviid ovddas.

#!! * __NwhenPo__ removes N if Po
REMOVE:NwhenPo N IF (-1 Gen LINK NOT 0 Num)(0 Po)(NEGATE 0 N-PO)(NEGATE 1C Po);
	#$ Ábo logai eatnis maŋis eahketrohkosa.

#!! * __VwhenPo__ removes V if Po
REMOVE:VwhenPo V IF (-1 Gen)(0 Po)(NEGATE 1C Po);
	#$ ... maid ektui sáhtii leat buori oskkus.
	#$ Gávpot mii lea vári alde, ii sáhte leat čihkosis.

SELECT:GengoGenPo Gen (0 Acc LINK 1 go LINK **1 Gen BARRIER NOT-NPMOD-ACC LINK 1 Po LINK 1 EOS) ;
	#$ Dieđálin eanet Oslo go iežan gili birra.

SECTION
# 9


#!! !!Some particular subjunctions and Neg Sup

#!! * __amasCS__ selects CS, not A or Neg Sup
SELECT:amasCS CS IF (0 ("amas"))(1 Neg) ;
	#$ Prinsihpa maid lean čuovvon lea juohkehačča ektui fuolahan, amas ii vuovdi vahágahtto.

#!! * __amasA__ selects A, not CS or Neg Sup
SELECT:amasA A IF (NEGATE -1 Neg LINK NOT *2 N BARRIER NOT-NPMOD)(0 ("amas") LINK NOT *1 Inf BARRIER S-BOUNDARY-NOT-CVP)(NOT 1 Neg);
	#$ Máŋgasat leat sakka fuolastuvvan amas ja dovddus vahágiin.
	#$ Ollu sápmelaččaide lea dál ain amas jurdda.
	#$ Eai dovdda amas olbmo jiena.
	#$ Amas olbmo jiena ii dovdda.
	#$ Sáhpánvuovddi bajil lea máŋga beaivvi girddašan amas, stuora loddi.

#!! * __amasNegSup__ selects Neg Sup, not CS or A
SELECT:amasNegSup (Neg Sup) IF (0 ("amas"))(*1 Inf BARRIER S-BOUNDARY-NOT-CVP);
	#$ Bávlos lei nappo mearidan gárvit Efesosa, amas ájihuvvot Asia eanangottis.
 	#$ ...amas lojes hearggit livvut.

#!! * __amatNegSup__ selects Neg Sup, not CS
SELECT:amatNegSup (Neg Sup) IF (0 ("amat") LINK *1 Inf BARRIER NOT-ADV);
	#$ Sii guddet du gieđaideaset alde, amat norddastit juolggát geađgái.

#!! * __dasgoCS__ selects CS, not Qst
SELECT:dasgoCS CS IF (0 ("dasgo"))((NEGATE *1 ("?")) OR (1 Interr)) ;
	#$ Dasgo Herodes lea ohcamin máná goddin dihtii dan.
	#$ Dasgo goabbá lea stuorit,...

#!! * Removes __vaikkoAdv__ , it is always CS because of abbr: "vaikko_mii" Pron Indef
SELECT:vaikkoAdv Adv IF (0 ("vaikko") OR ("vaikke")) ((0 Foc/ba) OR (-1 AUX + VFIN LINK *2 Inf BARRIER NOT-ADV-PCLE)) ;
REMOVE:vaikkoAdv Adv IF (0 ("vaikko") OR ("vaikke") LINK NOT 0 Foc/ba) ;
	#$ Ásahusa journálaovddasvástideaddji sáhttá liikká rievdadit journála vaikko gustojit vuolláičállojuvvon journálamerkesteamit.
	#% Ja moai boazodoallu áirasat áigguime vaikko guovttá joatkit barggu.

#!! !!go as CS and Qst Pcle
#!! First select all "go" Qst Pcle, then remove them so the rest will be "go" CS

#!! * __standQst__ selects Pcle in standard questions
SELECT:standQst Pcle IF (-1 VFIN LINK -1 BOS OR CLB OR PUNCT)(0 go LINK *1 ("?"));
	#$ Muhto boađát go?
	#$ Eai go báhkinat ge daga aiddo nu?
	#$ Judas, beahtát go Olbmobártni cummáin?
	#$ Addet go máŋga dearvvašvuođabargiidjoavkku veahki ja fállojuvvo go divššohasaide birrajándororrun?

#!! * __objQst__ selects Pcle in questions which function as object in the clause
SELECT:objQst Pcle IF (**-2 VFIN BARRIER S-BOUNDARY-NOT-CVP LINK NOT 0 Inf)(-1 VFIN LINK NOT 0 Inf OR Comp LINK NOT -1 (Ind Neg))(0 go)(NEGATE *1 VFIN BARRIER S-BOUNDARY-NOT-CVP LINK NOT 0 Inf)(NEGATE 1 (A Nom) LINK NOT -2 COPULAS);
	#$ Dalle ferten iskat lea go dus feber.
	#$ De oaidná olmmoš dárbbaša go oalgebáddi luoitiluvvot.
	#$ Dalle mun ferten iskat lea go dus feber, ja mihtidit dus varradeattu.
	#$ Farisealaččat atne Jesusa čalmmis oaidnit áigu go buoridit sábbáhin.
	#$ Politihkas lei 1866 rájes sáhka ahte galggai go stivrregoahtit gii oažžu eatnama láigohit stáhtas.
	#% Mearridanváldi berre leat nu stuoris go vejolaš
	#% Oidnen man váttis dilli lei mánáide geat eai máhttán go sámegiela.

#!! * __objQst2__ selects Pcle in standard questions where an object follows VFIN
SELECT:objQst2 Pcle (-1 VFIN LINK -1 BOS OR CLB OR PUNCT)(0 go LINK 1 Acc OR Gen) ;
	#$ Oinnežan go su ihttin, dan in dieđe.


#!! * __subQst__ selects Pcle in questions as subordinated clause
SELECT:subQst Pcle IF (*-2 VFIN-NOT-IMPRT LINK NEGATE 0 Inf LINK *-1 AUX BARRIER REAL-V)(-1 VFIN LINK NOT 0 Ill OR COPULAS LINK -1 COMMA OR CC)(0 go LINK NEGATE *1 VFIN) ;
	#$ Mun jurddašin iežainan, leažžá go duohta?
	#$ Eat dieđe, bođeža go Elle ihttin.
	#$ Addet go máŋga dearvvašvuođabargiidjoavkku veahki ja fállojuvvo go divššohasaide birrajándororrun.

#!! * __vaiQst__ selects Pcle in questions with vai
SELECT:vaiQst Pcle IF (-2 ("vai"))(0 go);
	#$ Boahtá go eadni, vai ii go boađe?
	#$ ...vai geahččaluvvo go gávdnat čovdosiid bissovaš rievttis,...
	#$ Dárbbahat go dálkasa vai hálidat go doaktáriin sárdnut?

#!! * __auxQst__ selects Pcle in questions as subordinated clause, starting with AUX
SELECT:auxQst Pcle IF (-1 (V Neg) OR MOD-ASP OR COPULAS)((*-2 Ger OR ConNeg OR VFIN BARRIER WORD LINK NOT *-1 Rel BARRIER COMMA) OR (*-2 (Refl Nom) LINK *-1 ConNeg OR VFIN BARRIER WORD))(0 go LINK *1 PrfPrc OR Inf OR ConNeg BARRIER AUX);
	#$ Árvvoštaladettiin galgá go addit lobi lea dattege gažaldat das,...
	#$ In mun gal dieđe sáhtežit go vuolgit dál.
	#$ Son jearrá it go boađe?
	#$ Sámedikki eará orgánat mearridit ieža galget go čoahkkimat leat rabas vai gitta.
	#$ Dan ferte oaidnit makkár diliin guovllut leat, leat go áitojuvvon dakkár stuorát sisabáhkkemiin.

#!! * __refQst__ selects Pcle in two main clauses, the first one a question which is referred to in the second.
SELECT:refQst Pcle IF (-1 VFIN LINK -1 BOS OR REAL-CLB OR PUNCT OR COMMA)(0 go)(*1 COMMA BARRIER VFIN LINK 1 ("dat" Pers));
	#$ Oinnežan go su ihttin, dan in dieđe.

#!! * __nounQst__ selects Pcle for go after NP
SELECT:nounQst Pcle IF (-1 N LINK *-1 BOS BARRIER NOT-NP)(NEGATE -1 ("hávvi"))(0 go)(*1 VFIN BARRIER S-BOUNDARY-NOT-CVP)(*1 ("?"));
	#$ Eatni goarrun gákti go dus lea die?

#!! * __poQst__ selects Pcle for go after Po
SELECT:poQst Pcle IF (-2 (N Gen) LINK *-1 BOS BARRIER NOT-NP)(-1 Po)(0 go)(*1 VFIN BARRIER S-BOUNDARY-NOT-CVP);
	#$ Girdi mielde go don manat Helssegii?

#!! * __AdvQst__ selects Pcle for go after WORD
SELECT:wordQst Pcle IF (-1 Adv OR A LINK *-1 BOS BARRIER NOT-PCLE)(0 go)(*1 VFIN BARRIER S-BOUNDARY-NOT-CVP)(*1 ("?"));
	#$ Na geasset go bođii diehtit ahte ii beasa skuvlii?

#!! * __killPcle__ removes all remaining Pcle for go
REMOVE:killPcle Pcle IF (0 go) ;

SELECT:ConNegIfNeg ConNeg IF (*-1 REAL-NEG BARRIER ConNeg OR PrfPrc OR SV-BOUNDARY OR CS)(NEGATE 0 N OR PrfPrc OR Pron LINK *1 ConNeg BARRIER NOT-ADV-PCLE)(NEGATE 0 Loc LINK -1 NEGFOC LINK -1 Loc)(NEGATE 0 Inf LINK *-1 REAL-NEG LINK -1 ConNeg LINK -1 REAL-NEG)(NEGATE 0 Comp LINK1 go) ; #(NEGATE *1 ("leat") + FMAINV );
	#% Máhttájeaddji ii leat stuorit go oahpaheaddjis, ii ge bálvaleaddji stuorit go hearrás.

#SELECT:ConNegIfNeg ConNeg IF (*-1 REAL-NEG BARRIER ConNeg) ; # OR PrfPrc OR SV-BOUNDARY)(NEGATE 0 N OR PrfPrc LINK *1 ConNeg BARRIER NOT-ADV-PCLE)(NEGATE 0 Loc LINK -1 NEGFOC LINK -1 Loc)(NEGATE 0 Inf LINK *-1 REAL-NEG LINK -1 ConNeg LINK -1 REAL-NEG) ;

 	#$ Iige njuovvan ja bierggu rádjan leat mihkkege dearvvašvuođaváttisvuođaid.Lea erenoamáš dehálaš movttiidahttit doaktáriid bargat gielddain.


#!! !!!Section 9 WORD-SPECIFIC RULES

	# ============== #
SECTION
	# ============== #


#!! !!Some particular subjunctions

#!! !!Adverb rules

#!! !!!MAPPING OF COMP-CS< , COMPLEMENTS OF PARTICLES IN COMPARISON
#!! First map all COMP-CS<, then remove the other readings


#!! * __compInf__ Inf go Inf
MAP:compInf (@COMP-CS<) TARGET Inf ((*-1 ("go" CS) BARRIER NOT-ADV LINK -1 Inf) OR (*-1 ("go" CS) BARRIER NOT-ADV LINK *-1 Comp LINK *-1 Inf))(NEGATE *1 VFIN BARRIER NOT-ADV-PCLE)(NEGATE 0 AUX + VFIN LINK *1 PrfPrc OR (Actio Ess)) ;
	#$ Anán buorebun odne vuolgit go ihttážii vuordit.
	#$ Čállit lea buoret go lohkat.

#!! * __ComptimeAdvl__ buoret go ovdal
MAP:ComptimeAdvl (@COMP-CS<) TARGET TIME-ADV (-1 ("go" CS) LINK -1 A OR ("seammá") OR ("eará"))(NOT *1 REAL-V BARRIER S-BOUNDARY) ;
	#$ Dál go ruhtadilli lea buoret go ovdal, de leat gávttis eanet čiŋat.

#!! * __ComptimeAdvl__ ii nu ollu go dál
MAP:ComptimeAdvl (@COMP-CS<) TARGET TIME-ADV (*-1 ("go" CS) BARRIER NOT-ADV LINK *-1 A OR ("seammá") OR ("eará") BARRIER REAL-V LINK *-1 Neg OR ConNeg BARRIER NOT-ADV-PCLE)(NOT *1 REAL-V) ;
	#$ Eai lean boraspiret, eai ealggat, eaige nu ollu bohccot dain duovdagiin go dál.

#!! * __Compadvlcase__ eará sivas go fuorrávuođas
MAP:Compadvlcase (@COMP-CS<) TARGET $$ADVLCASE (*-1 ("go" CS) BARRIER NOT-NPMOD LINK *-1 $$ADVLCASE BARRIER ADVLCASE)(1 COMMA OR EOC) ;
	#$ Juohkehaš guhte earrána áhkástis mange eará sivas go fuorrávuođas, dagaha su rihkkut náittosdili.

#!! * __CompNumP__ uhcit go njealji stivrralahtu doarjagiin
MAP:CompadvlcaseNum (@COMP-CS<) TARGET ADVLCASE + N (*-1 Num BARRIER NP-BOUNDARY LINK -1 ("go" CS) LINK -1 Comp) ;
	#$ Mearrádusaid gittaopmodaga luobaheamis, mat leat dahkkojuvvon uhcit go njealji stivrralahtu doarjagiin, ferte Sámediggi ja Finnmárkku fylkkadiggi dohkkehit.

MAP:Compadvl (@COMP-CS<) TARGET ADVLCASE + N (*-1 ("go" CS) BARRIER NOT-NPMOD LINK -1 Comp)(1 EOC) ;
	#$ Mávssut leat adnon eambbo go bušeahtas.
MAP:Compadvl (@COMP-CS<) TARGET ADVLCASE + N (*-1 ("go" CS) BARRIER NOT-NPMODADV LINK *-1 ("earalágan") OR ("seammalágan") OR ("seammá")) (1 EOC) ;

MAP:Comp>P (@COMP-CS<) TARGET Gen (*-1 ("go" CS) BARRIER NOT-NPMOD LINK -1 Gen LINK *-1 Comp BARRIER NOT-NPMOD)(1 Po LINK 1 EOS OR COMMA) ;
	#$ Dieđálin eanet Oslo go iežan gili birra.

MAP:Compadvl (@COMP-CS<) TARGET ADVLCASE + N (*-1 ("go" CS) BARRIER NOT-NPMODADV LINK *-1 ("earalágan") OR ("seammalágan") OR ("seammá") LINK -1 BOC)(1 NOT-NP) ;
	#$ Seamma go ovdalis namahuvvon riektehistorjjálaš ovdanbuktimis leat dán barggus maid muhtun buhtes historjjálaš dilit.

#!! * __CompEanet__ dohko eanet go
MAP:Compadvleanetgo (@COMP-CS<) TARGET $$ADVLCASE - Rel - Interr (*-1 ("go" CS) LINK -1 ("eanet") LINK *-1 COMMA LINK *-1 $$ADVLCASE) ;
	#$ Internáhta váhnenčoahkkimiidda bohte ollu váhnemat, ollu eanet go lágiduvvon biirečoahkkimiidda maidda bohte váhnemat geat orro skuvlla lahkosis.


#!! * __Compvejolas__ go vejolaš
MAP:Compvejolas (@COMP-CS<) TARGET ("vejolaš") - Attr (-1 ("go" CS) LINK -1 A OR Adv OR PrsPrc LINK -1 ("nu")) ;
	#$ Nu guhkás go vejolaš galgá dat čuovvoluvvot.
	#$ Sin mearridanorgánain berre mearridanváldi leat nu viiddis go vejolaš ja nu čadni go vejolaš.
	#$ Oahppit geahččaleigga sámástit nu ollu go vejolaš.
	#$ Dat galgá čuovvoluvvot nu guhkás go vejolaš.

#!! * __compNomHead__ NP-HEAD-NOM (ADVL) go NP-HEAD-NOM (ADVL). VFIN-NOT-IMPRT pga manglende disamgiguering
MAP:compNomHead (@COMP-CS<) TARGET NP-HEAD-NOM - Comp ((*-1 ("go" CS) BARRIER NOT-NP OR Nom OR REAL-V LINK NOT -1 VFIN-NOT-IMPRT OR CC OR BOC OR ("nu")) OR (-1 Attr LINK -1 ("go" CS) LINK NOT -1 VFIN-NOT-IMPRT OR CC OR BOC OR ("nu")))(*-1 ("go" CS) LINK *-1 Comp OR ("seammá") OR ("ovdal"))(0 Nom OR ADVLCASE LINK NEGATE 0 Gen LINK 1 N)(NEGATE 0 Attr OR PrfPrc OR TITLE LINK 1 N OR A)(NEGATE *1 VFIN BARRIER SV-BOUNDARY OR CC OR COMMA);
 	#$ Ovdabarggut állanit seamma beallái go "láhkateavstta lunddolaš lohkan".
 	#$ Ovdabarggut állanit seamma beallái go "láhkateavstta lohkan".
 	#$ Sápmelaččat guđet orrot Norgga bealde Deanu, geavahit seammá gávtti go sápmelaččat Suoma bealde Deanu.
	#$ Son lea viššaleabbo go mun.
	#$ Dalle dat viežžá čieža eará vuoiŋŋa vel, bahábuid go son ieš.
	#$ Máhttájeaddji ii leat stuorit go oahpaheaddjis.

#!! * __CompNomHead__ Comp NP-HEAD-NOM leat go NP-HEAD-NOM
MAP:CompNomHead (@COMP-CS<) TARGET NP-HEAD-NOM (*-1 ("go" CS) BARRIER NOT-NPMOD LINK *-1 NP-HEAD-NOM BARRIER S-BOUNDARY-NOT-CVP OR CS LINK -1 Comp)(NEGATE *-1 go BARRIER NOT-NPMOD LINK -1 VFIN LINK *-1 VFIN BARRIER S-BOUNDARY-NOT-CVP OR CS)(0 Nom OR ADVLCASE)(1 EOS OR CLB) (NEGATE 1 CLB LINK *1 VFIN BARRIER CC) (NEGATE 1 CLB LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD) (NEGATE 2 Interr LINK *1 VFIN LINK *1 VFIN BARRIER S-BOUNDARY-NOT-CVP OR CS);
	#$ ...muhto galjibut dat lea go guv-čalmmit.
	#$ Dán njuolggadusa deaŧalaš eaktu lea ahte ođđa siidaosiid ásaheapmi ii galgga dagahit ahte boazolohku šaddá alit go siidii mearriduvvon lohku, geahča dás maŋŋelis. ## Ii máhte vuos dán 'mearriduvvon' dihte


#!! * __compMisc__ go geassebuođut, go dán áigge
MAP:compMisc (@COMP-CS<) TARGET NP-HEAD-NOM - Attr - Comp OR TIME + Gen  (*-1 ("go") BARRIER NOT-NP OR Nom LINK NOT -1 VFIN OR BOS OR ("nu"))((1 S-BOUNDARY) OR (1 CLB OR CC LINK NOT *1 VFIN - Imprt BARRIER CC));
	#$ Muhto buođđogeažit ja giđđabuođut ráhkaduvvojit muđui seammá vugiid mielde go geassebuođut, ja danne heive buođđun birra obbalohkái juo muitaluvvot.
	#$ Šibihat ledje ovdal unnibut go dán áigge.
	#$ Viššalat ohppet eanet go láikkit.

MAP:compmade (@COMP-CS<) TARGET ("mađe") OR (A Nom) (*-1 ("go" CS) BARRIER NOT-NPMOD LINK -1 Comp OR ("seammá"))(1 go);
	#$ Finnmárkkus ássi olbmuid divat ii galgga leat alit go guovtti dan mađe go gielddas ássi olbmuid divat.


MAP:Compgo (@COMP-CS<) TARGET NP-HEAD-NOM - Px OR (Num Nom) ((*-1 ("go" CS) BARRIER NOT-NPMOD-ACC LINK NOT -1 ("nu")) OR (-1 Gen LINK -1 ("go" CS)) OR (-1 TITLE + Nom LINK -1 ("go" CS))) (*-1 ("go" CS) LINK *-1 Comp LINK *-1 VFIN - TRANS-V BARRIER S-BOUNDARY OR TRANS-V)(NEGATE *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 Attr OR Gen LINK 1 N)(NEGATE 0 Attr OR Gen LINK 1 Attr LINK 1 N)(NEGATE 0 TITLE LINK 1 (Prop Nom)) ;


#!! * __Compdego__ dego @COMP-CS<
MAP:Compdego (@COMP-CS<) TARGET NP-HEAD-NOM - Px OR (Num Nom) OR (ABBR Nom) ((*-1 XGO BARRIER NOT-NP OR CC LINK NOT *-1 (N Pl Nom) BARRIER WORD) OR (*-1 XGO BARRIER NOT-NP OR CC LINK -1 Inf LINK -1 COPULAS + VFIN))(NEGATE *1 VFIN-NOT-IMPRT OR AUX BARRIER S-BOUNDARY OR CC OR CS)(NEGATE 0 Attr OR (N Gen) LINK 1 N)(NEGATE 0 Attr OR Gen LINK 1 Attr LINK 1 N)(NEGATE 0 OKTA LINK 1 (N Nom))(NEGATE 0 (Refl Nom) LINK -1 NP-HEAD-NOM)(NEGATE *-1 XGO LINK -1 COMMA) ;
	#$ Sii leat dego eŋgelat almmis.
	#$ Son oahppaladdá min allagasas dego beaivváža badjáneapmi.
	#$ Dat lea dego olmmái gii manai olgoriikii.
	#$ Dego njamaheaddji eadni dikšumin mánáidis.
	#$ Dat guhte ii biso mu siste, bálkestuvvo olggos dego oaksi ja son goldná.
	#$ Ráhkkásiiddán, allet vajáldahte ahte Hearrái lea okta beaivi dego duhát jagi ja duhát jagi dego okta beaivi.

#MAP:CompdegoAdvl (@COMP-CS<) TARGET $$ADVLCASE - Px (*-1 XGO BARRIER NOT-NP LINK *-1 $$ADVLCASE BARRIER WORD)(NEGATE *1 VFIN-NOT-IMPRT OR AUX BARRIER S-BOUNDARY OR CC OR CS)(NEGATE 0 Attr OR (N Gen) LINK 1 N)(NEGATE 0 Attr OR Gen LINK 1 Attr LINK 1 N)(NEGATE 0 OKTA LINK 1 (N Nom));
	#$ Dolin sámit leat geavahan raddeleahpi eanaš guovlluin, nugo dán áigge julevsámi ja oarjesámi guovlluin.

#!! * __compAccdego__ Acc dego Acc
MAP:compAccdego (@COMP-CS<) TARGET NP-HEAD-ACC - Px (-1 XGO - ("nugo") LINK -1 NP-HEAD-ACC OR (Pron Acc))(NOT 1 Po)(NEGATE *1 VFIN-NOT-IMPRT OR AUX BARRIER S-BOUNDARY OR CS OR CC OR COMMA)(NEGATE 0 Attr OR Gen LINK 1 N LINK NOT 1 Po)(NEGATE 0 Attr OR Gen LINK 1 Attr LINK 1 N);
	#$ Mun vuolggahan din dego sávzzaid gumppiid gaskii.

#!! * __compAccgo__ Acc go Acc
MAP:compAccdego (@COMP-CS<) TARGET NP-HEAD-ACC (-1 go LINK -1 Comp LINK -1 NP-HEAD-ACC)(1 S-BOUNDARY OR COMMA);
	#$ Dat guhte ráhkista bártnis eanebut go mu, ii dohkke munnje.


#!! * __compNum__ TRANS-V eambbo go Num
MAP:compNum (@COMP-CS<) TARGET Num (-1 ("go" CS) LINK -1 Comp)(NEGATE 1 N + ADVLCASE)(NEGATE 1 Gen LINK 1 ("mađe")) (NEGATE *1 VFIN BARRIER S-BOUNDARY OR CS OR CC);
	#$ Ii heargi gal nagot guoddit eambbo go guokte diekkára.
	#$ Ii oktage sáhte leat lahttun eambbo go logi jagi oktilassii.

MAP:compEss (@COMP-CS<) TARGET (N Ess) (-1 ("go" CS) LINK -1 Nom LINK -1 Comp LINK *-1 (N Ess) BARRIER S-BOUNDARY) ;
	#$ Noađđeheargin dárbbašuvvojit sakka lojit hearggit go geresgeassin.

#!! * __compCoord__ coordination
MAP:compCoord (@COMP-CS<) TARGET NP-HEAD-NOM (*-1 CRD BARRIER NOT-NPMOD-ACC LINK -1 (@COMP-CS<))(NEGATE 0 Attr LINK 1 NP-HEAD-NOM)(NEGATE 1 VFIN) ; #(1 CRD) (*2 Nom BARRIER NOT-NPMOD);
	#$ Seamma gusto uhcit fuđđožiidda nu go neahti, čeavrris, mievri ja rieban.
	#$ Dat ledje beakkán ja dovddus olbmot nu go Diaz, Maria, Anne, Erik ja vel muhtin eará dovddus olbmot. # ii máhte maŋemuš oassi
 	#$ Dat ledje beakkán ja dovddus olbmot nu go Diaz, Maria Hætta, Anne Gaup, Erik ja vel muhtin eará dovddus olbmot. # ii vuos dán

#!! * __compCoordAttr__ coordination again, now with Attr. Speacial rule because of that Attr also has other readings.
MAP:compCoordAttr (@COMP-CS<) TARGET NP-HEAD-NOM (-1 Attr LINK -1 CRD LINK -1 (@COMP-CS<))(NEGATE 0 Attr LINK 1 NP-HEAD-NOM) ;
	#$ Jesus lea gievrrat go Sáhtán ja bahá vuoiŋŋat.

SECTION

#!! * __killAllnotComp__ Removes analysis which are not @COMP-CS<
SELECT:killAllnotComp  @COMP-CS< ;

#!! !! ** ''This was the kill all not Comp rule!!''

#!! * __goCSbeforeComp__ Selects CS analysis in front of @COMP-CS<
SELECT:goCSbeforeComp ("go" CS) IF (*1 @COMP-CS< BARRIER REAL-V OR CC) ;

#!! * __ACompgo__ Selects Comp analysis in front of go and @COMP-CS<
SELECT:ACompgo Comp IF (1 go LINK *1 @COMP-CS< BARRIER REAL-V OR CC)(NOT 0 ("dávji")) ;

SECTION

#!! !!!MAPPING OF CC AND CS

#!! Mostly we map both @CNP and @CVP, then we select @CNP, after that we remove them so @CVP remains

#!! * __cnpCompSC__ Map @CNP if @COMP-CS< or COMPAR ahte
MAP:cnpCompSC (@CNP) TARGET ("go" CS) (*1 @COMP-CS< BARRIER S-BOUNDARY OR CC OR CS);
	#$ Son lea buoret go mun.
	#$ Internáhta váhnenčoahkkimiidda bohte ollu váhnemat, ollu eanet go lágiduvvon biirečoahkkimiidda.

#!! * __cnpCompSpec__ special rule because of PrfPrc = VFIN
MAP:cnpCompSpec (@CNP) TARGET ("go" CS) (*1 PrfPrc BARRIER REAL-V LINK 1 NP-HEAD-NOM - Attr - Comp) ;
	#$ Dat ii galgga dagahit ahte boazolohku šaddá alit go siidii mearriduvvon lohku.

#!! * __CSasCNPCVP__ Map some CSs both @CNP @CVP
MAP:CSasCNPCVP (@CNP @CVP) TARGET CS (0 ("go") OR ("vai") OR XGO OR DADE);

#!! * __CSasCVP__ Map @CVP to CS
MAP:CSasCVP (@CVP) TARGET CS (NEGATE 0 Indef) ;
	#$ Leago guhkes áigi dassá go Máreha oidnet?

#!! * __CCasCNPCVP__ Map (@CNP @CVP) to CC
MAP:CCasCNPCVP (@CNP @CVP) TARGET CC ;

#!! * __ahteCNP__ ahte CC @CNP, remove the rest
IFF:ahteCNP ("ahte" CC @CNP) IF (-1 COMPAR OR Comp)(0 ("ahte"))(1 COMPAR OR Comp);
	#$ Dárogiel sánit bohtet eanet ahte eanet sámegiela sisa.
	#$ Dat šattai heajut ahte heajut.
	#$ Áiggi mielde eanet ahte eanet lohkagohtet sámegillii.

#!! * __killAllahtenotCS__ All other occurrences of "ahte" are CSs.
SELECT:killAllahtenotCS ("ahte" CS) ;

SELECT:degoCNP (@CNP) + XGO ((*1 @COMP-CS< BARRIER NOT-NPMOD) OR (NEGATE *1 VFIN BARRIER S-BOUNDARY) OR (-1 COPULAS LINK 2 Inf) OR (-1 COPULAS LINK 2 ADVLCASE OR Adv LINK 1 Inf))(NOT *-1 BOS BARRIER WORD) ;
	#$ Dat guhte ii biso mu siste, bálkestuvvo olggos dego oaksi ja son goldná.

#!! * __vaiCCCNP__ vai as CC or CS
SELECT:vaiCCCNP (CC @CVP) IF (0 ("vai")) (*-1 Inf BARRIER REAL-V OR CLB)(*1 Inf BARRIER REAL-V);
	#$ Buriid dahkat vai baháid dahkat.

#!! * __vaiCC__ remove vai as CC
REMOVE:vaiCC CC IF (0 ("vai"))(NEGATE *-1 Qst OR Interr OR ("go" Pcle))(NEGATE 1 Qst OR Neg)(NEGATE 2 ("go" Pcle))(*1 VFIN LINK NOT 0 N)(NEGATE *1 ("?"));
	#$ Árranis sii vušše liepmasa ealuide vai sestet goikerásiid.
	#$ Vai miehtan galgá leat lágalaš, ferte divššohas leat ožžon ...

#!! * __vaiCCNegQst1__ vai CC @CVP before Neg or question
SELECT:vaiCCNegQst1_a (CC @CVP) IF (0 ("vai")) (1 Neg LINK 1 Qst OR ("?"));
SELECT:vaiCCNegQst1_b (CC @CVP) IF (0 ("vai")) (1 VFIN + Qst);
SELECT:vaiCCNegQst1_c (CC @CVP) IF (0 ("vai")) (1 VFIN LINK 1 ("go" Pcle));
	#$ Leago sámeálbmogis vuoigatvuođat vai eai?
	#$ Dáhpáhuvvá go dat ođđa lágaid bokte, vai geahččaluvvo go gávdnat čovdosiid bissovaš rievttis?
	#$ Dáhpáhuvvá go dat ođđa lágaid bokte, vai geahččaluvvogo gávdnat čovdosiid bissovaš rievttis?

#!! * __vaiCCNegQst2__ vai CC @CNP in question about two alternatives
SELECT:vaiCCNegQst2 (CC @CNP) IF  (-1 N OR A OR Adv)(0 ("vai"))(*1 N OR A OR Adv BARRIER NOT-NPMOD LINK NOT 0 VFIN LINK NOT *2C VFIN BARRIER S-BOUNDARY OR CS) ; # (*1 ("?"));
	#$ Leago nieida vai bárdni?
	#$ Odne vai ihttin?
	#$ ...čállogo journála elektrovnnalaččat vai báhpárii?
	#$ Sámedikki eará orgánat mearridit ieža galget go čoahkkimat leat rabas vai gitta.

#!! * __killAllvainotCSCVP__ Select all vai CS @CVP
SELECT:killAllvainotCSCVP (CS @CVP) IF (0 ("vai"));

#!! * __dadeCNP__ removes dađe @CNP, so @CVP remains
REMOVE:dadeCNP (@CNP) IF (0 DADE) ;
	#$ Mađe mielde oaivi, dađe mielde ságat. # ii leat VFIN

#!! * __CVPNPron__ No finite verb or verbalactivity in front N/Pron @CNP N/Pron
REMOVE:CVPNPron (CC @CVP)  IF (
(-1 NP-HEAD-NOM OR (Pron Nom) LINK NOT *-1 REAL-V BARRIER S-BOUNDARY-NOT-CVP OR CS) OR
(-1 NP-HEAD-NOM LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-NPMOD))
(*1 NP-HEAD-NOM BARRIER NOT-NPMOD-ACC-ADV) (NEGATE -1 @COMP-CS<);
	#$ Maid moarsi ja irgi leaba čiŋadan gávttiiguin.
	#$ Mun ja mu ustibat manaimet kinoi geahččat buot ođđaseamos Harry Potter-filmma.
	#% Dat guhte ii biso mu siste, bálkestuvvo olggos dego oaksi ja son goldná.


#!! * __CVPnoVfin__ No potential finite verb following
REMOVE:CVPnoVfinAttr (@CVP) IF (0 CC LINK -1 N) (1 Attr LINK 1 N LINK NOT 1* VFIN BARRIER S-BOUNDARY OR CS OR PUNCT-RIGHT)(NEGATE *1 ("leat" PrfPrc) BARRIER S-BOUNDARY-NOT-CVP OR PUNCT-RIGHT OR CCCOMMA LINK NOT *-1 PrfPrc BARRIER REAL-V)(NEGATE 1 MO - ("nugo") OR ("de") OR Rel LINK *1 VFIN)(NEGATE -1 BOS)  ;


REMOVE:CVPnoVfin1 (@CVP) IF (NOT *1 VFIN BARRIER S-BOUNDARY OR CS OR PUNCT-RIGHT)
(NEGATE 0 ("muhto") LINK *1 Rel BARRIER REAL-V OR SV-BOUNDARY LINK *1 VFIN LINK *1 VFIN)
(NEGATE *1 ("leat" PrfPrc) BARRIER S-BOUNDARY-NOT-CVP OR PUNCT-RIGHT OR CCCOMMA LINK NOT *-1 PrfPrc BARRIER REAL-V)(NEGATE 1 MO - ("nugo") OR ("de") OR Rel LINK *1 VFIN)(NEGATE -1 BOS)(NEGATE 1 CC OR CS LINK *1 VFIN)(NEGATE *1 Inf BARRIER REAL-V OR S-BOUNDARY LINK *-2 Inf BARRIER REAL-V LINK *-1 MOD-ASP OR INF-V BARRIER REAL-V OR S-BOUNDARY)  ;
	#$ Mun máhtán láibut sihke šuhkoládagáhku ja lákcagáhku.
	#$ Mii bidjat dávviriid skáhpiide ja lođáide.
	#$ Mii vuolgit Londonii golmmain mánáin ja ovttain beatnagiin.
	#$ Dain hirbmat váriin ja mehciin sin guovllus lei earenoamáš vallji sihke elliid ja eará dáfus.
	#$ Bargiidbellodaga Per Nils Saari beasai suohkanstivrii ja válljii ovttasbargat olgešbellodagain, gurutbellodagain ja johttisámi listtuin.
	#$ Eai galgga sii geat gullet dakkáraš minoritehtaide ovttas eará láhttuiguin mat gullet sin jovkui, manahit vuoigatvuođaideaset dikšut iežaset kultuvra, "mihkkege" Pron Indef Sg Acc ja geavahit iežaset oskku, dahje geavahit iežaset giela. # This does not function yet because it needs verb disambiguation.
	#$ Muhto go Martha Hoffmann dáppe oastigođii ránuid maid son vuvddii Lulli-Norgii, sihke museaide ja olbmuide, de šattai fas gánnáhahtti gođđit ránuid.
	#$ Ja nugo olmmoš gohčodii guđege ealli, nu dat šattai navdojuvvot.

#!! * __CVPInfInf__ between to Inf
REMOVE:CVPInfInf (@CVP) IF  ((-1 Inf LINK NOT -1 N) OR (-1 Inf LINK -1 Acc LINK -1 REAL-V))(1 Inf);
	#$ Dii oahppabehtet lohkat ja čállit sámegillii.

#!! * __CVPInfInf__ between to Adv
REMOVE:CVPAdvAdv (@CVP) IF  (-1C Adv)(0 CC LINK 1C Adv);
	#$ čohkut dahje veallut


#!! * __CVPActioNom__
REMOVE:CVPActioNom (@CVP) IF  (-1 (N Nom) LINK -1 (Actio Nom) LINK -1 HAB-ACTOR + Gen)(*1 VFIN) ;
	#$ Stáhta nammadan lahttu ja sadjásašlahttu galgaba deavdit gáibádusaid.

REMOVE:CVPPrfPrc (@CVP)  IF (-1 PrfPrc)(0 CC)(1 PrfPrc LINK NEGATE 0 VFIN + AUX LINK *1 Inf BARRIER NOT-ADV-PCLE);
	#% Jus lean váiban dahje ferten lihkkat árrat, de manan nohkkat oalle árrat.

REMOVE:CVPCase (@CVP) IF  (-1 Com)(0 CC)(1 Com);
	#$ Son oažžu vuokkain ja luovosfirpmiin bivdit miehtá deanu.

REMOVE:CVPAPrcPrf (@CVP) IF  (-1 A)(0 CC)(1 PrfPrc LINK *1 N BARRIER NOT-NPMOD-ACC);
	#$ Sámeskuvla šattai oktasaš deaivvadanbáikin hárvelohkosaš ja hádjánan minoritehtii.


REMOVE:CVPNomNom (@CVP) IF (-1 (N Nom))(0 CC) (*1 (N Nom) BARRIER NOT-NPMODADV-INDEF LINK 1 EOS);
	#$ Pippi viesu bálddas lei nubbi gárdin ja nubbi viessu.
	#% Oabbá válddii biillas ja vuoddjái.

REMOVE:CVPnoVfin3 (@CVP) IF  (-1 N LINK NEGATE *-1 VFIN BARRIER BOS)(0 CC)(*1 N BARRIER NOT-A LINK *1 VFIN BARRIER S-BOUNDARY) ;
	#$ Grammatihkkavuđot giellateknologalaš reaiddut teakstaanalyseremii ja giellaoahpahus ja digitálalaš sátnegirjjit leat buot gielaide eaktun dasa ahte sáhttet ceavzit geavahangiellan ođđaáigásaš servodagas.

#!! * __CVPnoVfinAdvl__ No finite verb in front ADVLCASE @CNP ADVLCASE
REMOVE:CVPnoVfinAdvl (@CVP) IF  (-1C ADVLCASE OR Adv LINK NOT *-1 VFIN BARRIER S-BOUNDARY-NOT-CVP OR CS)(1 ADVLCASE OR Adv LINK 1 VFIN);
	#$	Universitehtain ja allaskuvllain leat ollu studeanttat.
    # Should not hit sentences like this:
 	#% Lávdegoddi lea dasa lassin guorahallan báikkálaš riekteáddejumiid Finnmárkkus ja vuordimis áigu dán ilbmadit iežas boahtte čielggadeami mielddusin.

	#$ Grammatihkkavuđot giellateknologalaš reaiddut teakstaanalyseremii ja giellaoahpahussii ja digitálalaš sátnegirjjit leat buot gielaide eaktun.	#% Dállu lea divrras midjiide danne go dat vuosiha govt mearrasámit elle ovdal ja makkár viesut sis ledje.


LIST MIELLA = "árbevierru" "bággu" "dárbu" "dilli" "ealjárvuohta" "geatnegasvuohta" "lohpi" "miella" "ovddasvástádus" "plána" "riekti" "váldi" "vejolašvuohta" "vuođđu" "vuohki" "vuoigatvuohta" "vuorru" ;
# These give Inf @N< instead of @ADVL (like more concrete nouns: ruhta, bileahta, dálki ...)


REMOVE:CVPMiella (@CVP)  IF (-1C ADVLCASE OR Adv LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(*1 ADVLCASE OR Adv BARRIER NOT-NPMOD-ACC-ADV) ;
REMOVE:CVPMiella (@CVP) IF  (-1C ADVLCASE OR Adv LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(*1 Attr BARRIER NOT-NPMOD-ACC-ADV LINK *1 ADVLCASE OR Adv BARRIER NOT-NPMOD-ACC-ADV) ;
	#$ De lea divššohasas ja su lagamus oapmahaččas vuoigatvuohta oažžut dieđuid.


#!! * __CVPAdvNom__ Nom @CNP Adv Nom
REMOVE:CVPAdvNom (@CVP)  IF (-1 NP-HEAD-NOM LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-NPMOD)(*1 NP-HEAD-NOM BARRIER NOT-NPMODADV);
	#$ Son lohká bargit ja maiddái fáŋggat fertejit oažžut sámegiel oahpahusa.

#!! * __CVPCopNomInf__ COPULAS Nom @CNP Nom Inf
REMOVE:CVPCopNomInf (@CVP) IF  (-1C (N Nom) LINK *-1 VFIN BARRIER NOT-NPMODADV)(1C (N Nom) LINK 1 Inf);
	#$ Jagi 2005 Sámediggi álggahii Divvuma, mii lei prošeakta ja bargojoavku ráhkadit sámegielaid sátnedivvunprográmmaid.


#!!  *__CVPoppramsing__ Lásse, Iŋgá ja mun
REMOVE:CVPoppramsing (@CVP) IF  (-1 Nom LINK *-1 COMMA BARRIER NOT-NPMOD LINK -1 Nom)(0 CC)(*1 Nom BARRIER NOT-NPMOD);
	#$ Lásse, Iŋgá ja mun leimmet Romssas.
	#  "Lásse" also have VFIN reading.


#!! *__CVPRCmpnd__ RCmpnd @CNP
REMOVE:CVPRCmpnd (@CVP) IF  (-1 RCmpnd);
	#$ Ulbmil seammádássásaš bohtosiid juksamis galgá gustot álo beroškeahttá makkár giella- dahje kulturduogáš dárbbašeaddjis lea.

#!! * __CVPwrongCmpnd__ wrongly formated compounds
REMOVE:CVPwrongCmpnd (@CVP) IF  (-1 ("\-") LINK -1 (\?) LINK -1 BOS)(0 CC LINK 1 N);
	#$ Eanandoallo- ja biebmoministtar Lars Sponheim lohká ahte goas boazodoalloláhka biddjo Stuoradikki ovdii mearrideapmái, lea dan duohken man muddui Oarje-Finnmárkkus lea dohkálaš boazolohku cuoŋománu 1. beaivvi.

#!! * __CVPAAttr__ A Attr @CNP A Attr
REMOVE:CVPAAttr (@CVP)  IF (-1 (A Attr) OR Gen LINK -1 CC OR CS)(0 CC)(1 (A Attr) OR Gen OR (@>N));
	#$ Olbmuin geain leat lihkadanváttut leat váttisvuođat go boares ruovdemáđiija vovnnaid oláheapmi ii leat nu buorre, ja ođđa ja stuorit stášuvnnain ges lea heajos čalmmálaš.

#!! * __CVPA__ A @CNP A
REMOVE:CVPA (@CVP)  IF (-1C A)(1C A LINK *1 N BARRIER NOT-NPMOD);
	#$ E-boasta lea hui álkes ja jođánis vuohki sáddet reivviid birra máilmmi.


#!! * __CVPAccAdv__ Acc @CNP Adv Acc
REMOVE:CVPAccAdv (CC @CVP) IF  (-1 Acc)(*1 Acc BARRIER NOT-NPMODADV LINK NOT 0 VFIN)(NEGATE 1 Num LINK 1 Gen LINK 1 VFIN);
#REMOVE:CVPAccAdv (CC @CVP) IF (-1 Gen)(*1 Gen BARRIER NOT-NPMODADV LINK 1 Po OR ADVLCASE);
	#$ Juos gorreválggain lea dárbbašlaš deavdit gáibádusa, man mielde lahtuid ja sadjásašlahtuid gaskkas galget leat sihke nissonat ja dievddut.
	#% Beassášmárkaniin fitne sullii 9000 guossi ja 3500 guossi ledje čakčamárkaniin.


SELECT:CVP (@CVP)  IF (*-1C VFIN BARRIER S-BOUNDARY)(*1C NP-HEAD-NOM BARRIER NOT-NPMOD LINK *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 go LINK *1 Inf BARRIER VFIN) ;
	#$ Áhčči vulggii, ovdalgo mun bohten.
	#$ Mun mannen maŋŋilgo mii leimmet čorgen. # fungerer ikke enda pga manglende verbdisambiguering

REMOVE:CS (@CVP)  IF (-1 CS)(1 CS) ;
	#$ Jos ja go suodjalus ii šat dárbbaš guovllu, dat galgá máhcahuvvot sámi geavahussii, Skum cealká.


#!! * __killAllCNP__ removes all remaining @CNP
REMOVE:killAllCNP @CNP IF (0 @CVP) ;

#!! * __XCC-CS__ removes CC and CS with no synttag
MAP:CC-CS (@X) TARGET CC-CS ;
REMOVE (@X) ;



#!! !!PRONOUNS

#!! !Plural?

#!! * __PlSg3V__ removes plural in front of Sg3 verb (and __SgPl3V__ does the opposite)
REMOVE:PlSg3V NP-HEAD-PL-NOM IF (0 (Inf) OR NP-HEAD-SG-NOM)(*1C SG3-V BARRIER NOT-ADV-PCLE);
	## Dát lei deháleamos ágga.

REMOVE:SgPl3V (Pron Sg Nom) IF  (*1C PL3-V BARRIER NOT-ADV-PCLE);
	## Dát lei deháleamos ágga.
	#$ Dathan han gal leat buorit guolit.


#!! !!Interrogative and relative pronouns


#!! * __Interr__ selects interrogative pronouns in questions

SELECT:Interr Interr IF ((-1 COMMA LINK *-1 CS LINK *-1 BOS BARRIER NOT-PCLE) OR (*-1 BOC BARRIER NOT-PCLE LINK NOT 0 ("maid") OR Interr))(0 Rel OR Indef LINK NOT 0 ("man" Adv) LINK *1 ("?"));
	#$ Juos Ipmil lea min beale, gii lea min vuostái?
	#$ Maid don doaivvut, mun gielistan?
	#$ Na maid don diŋgojit?
	#% Maid mii dál vihtaniiguin?

#!! * __InterrIfPot__ selects interrogative pronouns in potential sentences, and after that we remove the remaining Interr
SELECT:InterrIfPot Interr IF (*-1 BOC BARRIER NOT-PCLE)(0 Rel)(*1 TRANS-V + Pot BARRIER S-BOUNDARY OR VFIN-POS);
	#$ Na maid dal juo dagažat ja maid it dagaš.

REMOVE:KillInterr Interr IF ;

#!! * __munPl3__ removes Pron Pers Pl3 if there is no verb agreement
REMOVE:munPl3 Pers IF (NEGATE *-1 PL1-V BARRIER NOT-ADV-PCLE)(0 ("mii"))(*1 SG3-V BARRIER PL1-V OR SV-BOUNDARY LINK NOT 0 Pl1);
	#$ Dát ii guoskka dikšui mii boahtá psykiatriija vuollái.

#!! * __Rel__ selects Rel
SELECT:Rel Rel IF (*-1 NOT-AUX-V BARRIER S-BOUNDARY LINK *-1 NP-HEAD-NOM BARRIER S-BOUNDARY)(*1C NP-HEAD-NOM BARRIER S-BOUNDARY);
	#$ ...ahte olbmot besset oaidnit maid sii sáhttet vuordit.

#!! * __RelSg1, RelSg2__ select Rel
SELECT:RelSg1 (Rel Sg) IF  (-1C NP-HEAD-SG OR (Num Sg))(NEGATE *-2 NUM BARRIER NOT-NPMOD)(*1 VFIN)(*1 TRANS-V);
REMOVE:RelSg2 (Rel Sg)  IF ((*-1 (N Sg) BARRIER WORD LINK *-1 CC BARRIER NOT-NPMOD LINK -1 N) OR (-1 Interr));
	#$ Mun oasttán peanna ja bliántta, maid mun dárbbašan skuvllas.
	#$ Nubbi maid fuobmáimet lei ahte...

#!! * __RelPl__ selects Rel
SELECT:RelPl (Rel Pl) IF  ((-1 NP-HEAD-PL LINK NOT 0 NP-HEAD-SG) OR (-1 NP-HEAD-SG LINK -1 Num)) (*1 VFIN);
	#$ ...go lei sáimmaiguin bivdán jávrris maid stáhta lei láigohan.
	#$ Doaibma gullá doaimmaide maid ulbmil lea movttiidahttit sámi nuoraid...

#!! * __RelPl__ removes Rel
REMOVE:RelPl (Rel Pl) IF  (NEGATE *-1 (N Pl) OR NOT-SG-PRON BARRIER N OR Pron)(NEGATE *-1 N LINK *-1 Num OR CC BARRIER V)(0 (Rel Sg));
	#$ Lávvui gullá maid uvssot.

REMOVE:miiPl Pl IF (*-1 BOC-PUNCT OR (":" CLB) BARRIER NOT-ADV-PCLE)(0 ("mii" Pron Acc) LINK 0 Rel OR Interr);
	## Maid don doaivvut, mon gielistan?


#!! !!Emphatic ieš

#!! * __ies1Pl, ies2Pl__ select Pl for ieža

SELECT:ies1Pl Pl IF (*-1 PL-V OR (V Imprt Pl2) OR MII-PERS OR DII OR SII OR (Pron Rel Pl Nom) BARRIER SV-BOUNDARY OR PUNCT)(0 ("ieš" Pron Refl Pl Nom))(NEGATE *1 DU-V BARRIER NOT-ADV-PCLE);
	#$ Allet dubme, amadet ieža dubmejuvvot!
	#$ ...muhto leat maiddái muhtumat geat ieža leat dahkan iežaset
	#$ dohkketmeahttumin...

SELECT:ies2Pl Pl IF (0 ("ieš" Pron Refl Pl Nom))(*1 PL-V BARRIER SV-BOUNDARY);
	#$ ...ja didjiide mihtiduvvo seammá mihtuin mainna ieža ge mihtidehpet.

#!! * __iesDu__ select Pl for ieža
SELECT:iesDu Du IF (*0 DU-V OR MOAI OR DOAI OR SOAI BARRIER SV-BOUNDARY OR PUNCT)(0 ("ieš" Pron Refl Du Nom));
	#$ Doai galgabeahtti ieža mannat.
	#$ Ieža soai muitaleigga mii lei dáhpáhuvvan mátkkis.


#!! !!Numerals

#!! * __NifNum__
REMOVE:NifNum N IF (0 Num)(*1 (N Sg Gen) BARRIER NOT-A);
	#$ Mii finaimet doppe guđa geardde.

#!! * __AdvOvtta__
REMOVE:AdvOvtta Adv IF (0 OKTA + Acc)(1 (Sg Acc));
	#$ Soai vácciiga ovtta gaskka.

#!! * __AdvNumEss__
REMOVE:AdvNumEss Adv IF (0 OKTA + Ess)(1 Ess);
	#$ Mii barggaimet oktan joavkun.

SELECT:NumCurrency Num IF (1 CURRENCY OR MEASURE);
	#$ Dainna čuđiin ruvnnuin áiggun bierggu oastit.

SELECT:NumNom (Num Nom) IF (1 ("jahki" Pl Gen)) ;
	#$ 1970-1980 jagiid ásahedje maiddái sámi kulturguovddážiid dáid skuvllaid olis, ja maid stáhta ja fylkkagielda ruhtadedje.

SELECT:NumDassa (Num Nom) (1 (Sem/Time Sg Gen) LINK 1 ("dassá") OR ("dassái"))  ;

SELECT:NumAccCurrency (Num Acc) IF (NOT *-1 COPULAS BARRIER NOT-ADV - CLB)(NEGATE *-1 Nom BARRIER NOT-ADV LINK *-1 COPULAS BARRIER NOT-NPMOD)(NOT -1 Num)(1 CURRENCY + Acc OR MEASURE + Acc LINK NEGATE 0 Gen LINK 1 COPULAS OR Po);
	#$
	#% Ráhkkásiiddán, allet vajáldahte ahte Hearrái lea okta beaivi dego duhát jagi ja duhát jagi dego okta beaivi.


SELECT:NumNom (Num Nom) IF  (-1 ABBR OR ACR OR ("§") OR ("paragráfa") OR ("kapihtal") OR HEADOFPARTS OR JAHKI-NUM OR TEXT OR LAHKA LINK NOT 0 Ess)(NEGATE -1 ("b" ABBR))(NEGATE -1 ("nr"))(0 Num)(NEGATE 1 Ill - Attr OR Loc - Attr)(NEGATE -1 JAHKI-NUM OR TEXT OR LAHKA LINK 2 Po);
	#$ ... maid KITH gárvvistii ja almmuhii geasset 2001.
	#$ Son vuittii Davviriikkaid Ráđi girjjálašvuođabálkkašumi jagi 1991 dán girjji ovddas.
SELECT:NumNomCoord (Num Nom) IF  (-1 CC LINK -1 Num LINK -1 ABBR OR ACR OR ("§") OR ("paragráfa") OR ("kapihtal") OR HEADOFPARTS OR JAHKI-NUM OR TEXT OR LAHKA LINK NOT 0 Ess)(NEGATE -1 ("b" ABBR))(NEGATE -1 ("nr"))(0 Num)(NEGATE 1 Ill - Attr OR Loc - Attr);
	#$ Riekti oaivvilda dán vuoigatvuođadilálašvuođa dál sáhttit lea ain eanet cieggan almmá vuostálastima haga nu go leamaš vássán áiggis gitta das rájes go advokáhta Schiefloe almmuhii girjjiidis jagiin 1952 ja 1955.
 # These rules leave only the Nom Num reading,

SELECT:r1082 (Num Nom) IF (-1 COUNTABLE - Ess OR ("nr"))(0 ABBR OR ACR OR Num) ;
	#$ Dieđáhusa oasis I ovdanbuktojuvvo Bb:a sámepolitihkalaš vuođđolávdi.
	#$ Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna.

SELECT:year (Num Gen)  IF (-1 GEASSET) ;
	#$ ...maid KITH gárvvistii ja almmuhii geasset 2001.

SELECT:numunit (Num Gen) + NUMUNIT IF  (-1 (Num Acc)) ;
	## Dat máksá guokte duhát ruvnno.

SELECT:NumGenPo Gen IF (0 Num LINK **1 (N Gen) BARRIER NOT-NPMODADV - Acc - Indef LINK 1 Po) ;
	#$ Miehtá čájáhuslanja lea lebbejuvvon plastihkkagovččas ja gávcci máná njuni ovddas lea čáhcemála.

SELECT:jagiGen (Num Sg Nom) IF (-1 ("jahki" Gen)) ;
REMOVE:numGen (Num Loc Attr) IF (NOT *1 (N Loc) BARRIER NOT-NPMOD-ACC  - Indef) ;
REMOVE:numGen (Num Ill Attr) IF (NOT *1 (N Ill) BARRIER NOT-NPMOD-ACC) ;
REMOVE:numGen NUM + (Loc Attr) OR NUM + (Ill Attr) IF (1 (N Gen)) ;
SELECT:numPlAccn (Num Pl Acc)(1 MEASURE + Pl) ;

SELECT:WWNumOrdGen Gen IF (0 Num OR Ord LINK NOT 0 Acc)(1 (N Gen) LINK *1 (N Acc) OR (N Gen) OR (N Nom) BARRIER NOT-A)(NEGATE -1 TRANS-V LINK NOT *3 Acc)(NEGATE -1 TRANS-V LINK *3 Acc LINK 1 Po);
	## Dan eatnamis oažžu ovtta gusa fuođđara.


#!! * __WWNumOrdIllAttr__ selects Ill Attr and Loc Attr for numerals and ordinals
SELECT:WWNumOrdIllAttr (Ill Attr) IF (0 Num OR Ord) (*1 (N Ill) OR (Num Ill) BARRIER NOT-A);
	#$ Mun attán girjji ovtta oahppái.
	#$ Skuvlaáigi lei gáržžiduvvon golmma vahkkui.
	#$ Spábba guoskkai ovtta Snøgga čiekčái ja manai mollii.
	#Comment: We are lacking some rules for: 'Skuvlaáigi lei gáržžiduvvon 3 vahkkui.' and also: 'Skuvlaáigi lei gáržžiduvvon guovtti dahje golmma vahkkui, jus juo obanassiige čađahuvvui oahpahus.' the last one beeing a coordinated sentence.
SELECT:WWNumOrdIllAttr (Ill Attr) IF (0 NUM OR Ord) (1 (N Gen) LINK *1 (N Ill) BARRIER NOT-A)(NEGATE 0 (Num Nom) LINK 1 (N Sg Gen));
SELECT:WWNumOrdLocAttr (Loc Attr) IF (0 Num OR Ord)(*1 (N Sg Loc) BARRIER NOT-A - Indef OR Ord) ;
	## Ovtta ovddasteaddjis divššohasa ovddas lea vuoigatvuohta...
	## Na gal don dal oaččut dan beannot čuohtái, go juo válddežat.
REMOVE:WWNumOrdAttr (Num Attr) + ADVLCASE OR ("nubbi" Attr) + ADVLCASE  IF (NOT *1 ADVLCASE + N BARRIER NOT-NPMOD-ACC OR Ord) ;
	#$ Nuppi vuoru goalmmát minuvttas Anette nivkalii čiehkačievččastaga čábbát njuolga mollii.

SELECT:galleIndef ("galle" Indef)  IF (*-1 Neg BARRIER S-BOUNDARY) ;
	#$ Eaige sámiid ássanguovllus leat galle gieldda, mat...

REMOVE:galleIndef ("galle" Indef) ;

SELECT:galleNum ("galle" Num) IF (NEGATE -1 Neg)(*1 N BARRIER NOT-NPMOD);
	#$ ...ja govve galle joga leat Deanučázádaga njuoskkadagas.

SELECT:nubbiGen ("nubbi" Pron Indef Gen)  IF (1 ("dáfus" Po) OR ("beali" Po) OR ("hávvi") OR ("beallái" Po) OR ("bealli") OR ("sohkabealli") OR ("bealde") OR ("beales") OR ("láhkai") OR ("riika") OR ("máilbmi"));
	#$ Nuppe dáfus deattuha suohkan, ahte...

SELECT:nubbiIndef ("nubbi" Pron)  IF ((*-1 ("nubbi") OR ("guokte") BARRIER ("goalmmát") OR ("vuosttas") OR ("vuosttaš")) OR (*1 ("nubbi") BARRIER ("goalmmát"))) ;

SELECT:nuppiIndefCom ("nubbi" Pron Com) IF  (1 ("sátni" Com));

SELECT:IndefLoc (Indef Loc)  IF (-1 COMMA OR CC LINK -1 (N Nom) LINK *-1 COPULAS BARRIER REAL-V LINK -1 (N Pl Loc));
	#$ Muhtun siiddain leat moanat goađit, nuppiin unnánat, dan duohken leago rájá siskkobealde stuorra dahje unna eanaguvlloš.

#SELECT:nubbiIndefPl ("nubbi" Pl)(0 ("nubbi" Indef Pl) LINK NOT 1 PL-N); #not good

SELECT:nubbiOrd ("nubbi" Ord)  IF (*0 ("vuosttas") OR ("vuosttaš") OR ("goalmmát")) ;
	#$ Vuosttaš vuojahaga fávllimus čuolda lea goaratčuoldan ja nubbi čuolda fas gáddeoal-čuoldan.
	#$ Nubbi noađđeheargi goallostuvvo giehtahearggi lávžái ja goalmmát fas dan nuppi lávžái.
	#$ Sii leat Ohcejogas golggotmánu vuosttaš beaivvi, Gáregasnjárggas nuppi beaivvi ja Njuorggámis ges goalmmát beaivvi.

SELECT:nubbiOrdManu ("nubbi" Ord) IF  (-1 MANNU + Gen)(1 ("beaivi") OR ("b") OR ("vahkku"));
	#$ Son lei doppe Ohcejogas golggotmánu nuppi beaivvi.

SELECT:nubbiOrdSuperl ("nubbi" Ord) IF ((-1 ("juohke")) OR (1 A LINK 0 Superl OR Loc OR ("maŋit" Superl) OR ("maŋemuš")));
	#$ Listu lea gieldda nubbi stuorámus bellodat.
	#$ Nubbin vuolemusas lea Guovdageaidnu Finnmárkku listtus.

SELECT:nubbiIndefIll ("nubbi" Indef) IF  (-1 (N Sg Loc))(0 ("nubbi" Ill));
	#$ Son lea viehkan čoahkkimis nubbái maŋŋebárgga rájes.

SELECT:nubbiOrdLex ("nubbi" Ord) IF (1 ("geardi") OR ("buolva" Loc) OR ("giella") OR ("máilbmesoahti") OR ("lađas") OR ("čuokkis") OR ("vuorru"));
	#$ Henrik oaččui buot gussiid dearvan nuppi gearddi čađa, gos ii buollán.
	#$ Soai leigga fulkkežat nuppi buolvvas.

SELECT:nubbiIndefOkta ("nubbi" Indef)  IF (*-1 OKTA BARRIER S-BOUNDARY OR REAL-V);
	#$ Okta nubbi vuodjá tráktor máđiiráigge.
	 #$ Okta ášši lea mii lágas mearriduvvui, nubbi lea man kommišuvnnat čuvvo mearrádusaid.
	#$ Riektegeavadis sáhtášii danin geavahit guokte váldolinjjá, okta ovdal ja nubbi fas maŋŋá 1970.

SELECT:nubbiOrdEss ("nubbi" Ord Ess) IF  (*-1 MOVEMENT-V OR COPULAS BARRIER REAL-V) ;
	#$ Sii olihedje diibmá nu movt dán jagi nubbin.

SELECT:nubbiIndefAcc ("nubbi" Indef Acc)  IF (*-1 TRANS-V BARRIER SV-BOUNDARY)(1 Inf);
	#$ Ulbmil lea oažžut nuppi boagustit.

SELECT:nubbiIndefUnnimusat Indef IF (-1 ("unnimustá") OR ("unnimusat"))(0 ("nubbi"));
	#$ Mánáin lea ovttastallanvuoigatvuohta unnimustá nuppiin váhnemiin.

REMOVE:nubbiAttr Attr IF (0 ("nubbi"))(1 Dem OR Adv);
	#$ ... ja nubbi dán suorggi siskkabeale hálddahusortnegiid birra.
	#$ ...nubbi fas rávesolbmuid psykiatriijas.

SELECT:KillnubbiOrd Indef IF (0 ("nubbi" Ord));

#!! !!Indefinite pronouns
#!! The rules are not documented yet



REMOVE:r1567 (Indef Attr) IF (*-1 LEAT-FIN-NOT-IMPRT LINK NOT *1 PRC-NOT-LEAT BARRIER VFIN)(NEGATE *-1 COM-HEAD BARRIER NOT-NPMOD)(0 ("muhtun") OR ("soames") OR ("soamis"))(*1 Loc BARRIER NOT-NPMOD)(NEGATE *1 VFIN);
#REMOVE:r1568 (Pron @>N) IF (*-1 LEAT-FIN-NOT-IMP LINK NOT *1 PRC-NOT-LEAT BARRIER VFIN)(NEGATE *-1 COM-HEAD BARRIER NOT-NPMOD)(0 ("muhtun") OR ("soames") OR ("soamis"))(*1 Loc BARRIER NOT-NPMOD)(NEGATE *1 VFIN);
	## Son lea profehta, aiddo dego muhtun boares profehtain.
    # Both rules are necessary in exactly this order
#OBS

SELECT:r1569 (Indef Attr) IF (0C Pron - ("nubbi"))(NOT 0 Ill OR Loc)((*1 N - Prop BARRIER NOT-A-PCLE - Indef OR go) OR (1 (Actio Gen) LINK 1 ("veara") LINK 1 N - Prop))(NEGATE 0 ("eará") LINK -1 ("earret"));
	## Dan muitalii muhtin mátkkošteaddji gean namma lei Keilhau.
	## Njuolggadusain ledje eará ge mearrádusat.
	#$ Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu birra dálolaččaid ja boazodoalu gaskkas.

SELECT:r1570 (Indef Attr) IF (1 (A Attr))(*2 HEAD-N BARRIER NP-BOUNDARY);
	## ...ja eará almmolaš dahje priváhta doaibma.

#SELECT:r1571 (Indef Attr) IF (1 ("láhkai"));
	## Seammá láhkái go váldojournála ja lassijournála geavaheami oktavuođas...
REMOVE:r1572 Attr IF (0 Pron)(1 (Pron Rel));
	## Lea unnán maid sáhttit dahkat.
REMOVE:r1573 (Indef Attr) IF (1 (Pron Pers Loc));
  	## Ja muhtun sis viegai dakkaviđe su lusa.

SELECT:r1576 Indef IF (*-1 ("jus") OR ("jos") OR ("juos") OR ("joas") BARRIER S-BOUNDARY)(0 Interr);
	## Jus guhtege dadjá áhččásis dahje eadnásis:...
	## ...ahte doaimmat eai juogaduvvo menddo ollugiidda.

#SELECT:r1579 Indef IF (*-1 BOC BARRIER NOT-ADV)(*1 N BARRIER NOT-NPMOD);
	## Ollu amerihkálaččat doivot Oslo Ruoŧa oaivegávpogin.

REMOVE:r1580 Adv IF (*-1 REAL-COPULAS BARRIER S-BOUNDARY OR CC OR REAL-V)(0 Indef)(NEGATE 1 A)(NEGATE *1 REAL-V BARRIER S-BOUNDARY OR CC);
	## Doppe ledje ollu olbmot.
	## Diet gal lei beare unnán.
 # Indefinite pronouns, not adverbs, if there is no verb other than 'leat'.

REMOVE:r1581 Adv IF (*-1 CC OR CLB BARRIER NOT-ADV)(0 Indef)(NEGATE 0 Loc)(*1 N BARRIER NOT-NPMOD);
	## Dán áššis, nugo eanaš eará áššiin,...

REMOVE:r1583 Adv IF (1 Interr);
  	## Attášii sutnje buot maid dárbbašit.
  	## Danne lea váttis čohkket barggu buot surggiide oktanaga.
  # Possibly sentences such as:
  	## Son lea njuoskan buot, mii lea fuones ášši.
  # could be counterexamples, but until now a comma is supposed to be inbetween.


	# Particular indefinite pronouns
	# ------------------------------

	# This is an important section. It should first be expanded
	# and then probably generalised according to classes of
	# indefinite pronouns.

	# buot
	# - - -

IFF:r1586 BUOT + Attr  IF (*1 N OR (A Pl) BARRIER NOT-NPMOD);
	## Sii meannudit buot velggolaččaid máksingeahpádusohcamiid.
	## Mii addit bálvalusfálaldaga buot álbmogii.
	## De fertejit buot vihtta miellahtu oassálastit.
	## ahte buot náitalan olbmuin galggai leat guolástanvuoigatvuohta.


SELECT:r1590 (Pron Indef Nom) IF (0 BUOT)(1 PL3-V);
	## Vuollásaš ráđit buot heaittihuvvojedje oktasaš hálddahuslaš jođiheapmái.


SELECT:r1596 (Indef Nom) IF (NEGATE *-1 TRANS-V BARRIER REAL-V OR S-BOUNDARY)(0 BUOT)(NEGATE *1 TRANS-V BARRIER S-BOUNDARY LINK 0 MAIN-V OR Inf);
	## Buot lea lávkkas.

SELECT:r1597 Nom IF (-1 ("gullat" Pl3) LINK -1 Ill)(0 BUOT); # trenges ikke
	## Dat mearkkaša ahte dása gullet buot mat eai leat spiehkastuvvon.

#SELECT:r1598 Indef IF (*-1 TRANS-V BARRIER NOT-ADV-PCLE)(0 BUOT LINK 1 S-BOUNDARY OR REAL-CLB); # trenges ikke
	## Mun borren buot.

#SELECT:r1599 Indef IF (0 BUOT)(*1 TRANS-V BARRIER Acc OR S-BOUNDARY); # trenges ikke
	## Sii fertejit buot earret eatnamiid gohčodit danin.


# eará
# - - -

SELECT:r1601 (Indef Attr) IF (0 ("eará"))(1 PrfPrc)(2 N);
	## ...ja eará vuostáiváldojuvvon dokumeanttaid...

SELECT ("eará" Gen) IF (-1 ("earret")) ;

# earri
# - - -

REMOVE:r1602 ("earri" N Sg Acc PxSg2) IF (-2 (V Pl3))(-1 COMMA)(0 ("eará"))(1 (V Pl3));
	## Muhtimat čállet, earát ožžot jorgalan- ja bagadallanveahki dán bargui.

REMOVE:r1603 ("earri" N Sg Acc PxSg2) IF (-1 BUOT OR ("máŋga"));
	## Buot earát.

REMOVE:r1604 ("earri" N Sg Acc PxSg2) IF (NEGATE *-1 Sg2)(*1 (V Pl3));
	## ... go dat maid earát vásihit.

REMOVE:r1605 ("earri" N Sg Acc PxSg2) IF (1 (Pron Rel Pl));
	## ... ja earát geat háliidit ...

# eatnat
# - - -
SELECT:r1606 Indef IF (-1 ("giittos"))(0 ("eatnat"));

# juoga
# - - -
SELECT:r1607 (Indef Nom) IF (-1 CLB)(0 ("juoga"))(1 Rel);
	## ..., juoga mii boahtá ovdan Romssa universitehta plánain.

#!! * __miiIndef__ it vaikko mii or mii beare
IFF:miiIndef ("mii" Indef) IF ((-1 ("vaikko")) OR (1 ("beare")))(NOT *0 (V Pl1) BARRIER SV-BOUNDARY) ;

IFF:miiPl ("mii" Rel Pl) IF (*-1 (N Pl) BARRIER WORD) ;

# ollu
# - - -
REMOVE:killOLLUAdv OLLU + Adv ;
SELECT:r1608 Pron IF (-2 Pron)(-1 CC)(0 OLLU);
	## Váilos unnán dahje ollu.

SELECT:r1609 Pron IF (-1 ("seammá"))(0 OLLU)(*1 N BARRIER NOT-NPMOD);
	## Jus eanebut leat ožžon seamma ollu jienaid, de mearriduvvo boađus vuorbádemiin.

# seamma/seammá
# - - - - - - -

SELECT:r1610 Attr IF (0 ("seammá"))(*1 N BARRIER NOT-NUM);
	## Dasto smiehttatge fas seamma sullasaš jearaldagaid.
	## Sáhttá leat ulbmillaš, ahte seamma olmmoš áimmahuššá máŋga dain doaimmain.

SELECT:r1611 Attr IF (0 ("seammá"))(1 Adv OR OLLU + Pron)(NEGATE 1 REAL-V OR ("gusto")) ;
	## Mu oainnu mielde sáhttá seammá bures oaidnit dan láhkai, ahte...
  # The "gusto" here should be removed by a set of sentence adeverbials

	# Then we kill the rest of the attributes...
	# But did we catch enough attributes?

	#REMOVE Attr IF (0 ("seammá")) ;
	## Seammá gusto poliklinihkalaš divššus.
  	## Seamma gusto poliklinihkalaš divššus.

# unnán
# - - -
SELECT:r1612 Indef IF (*-1 COPULAS BARRIER NOT-AUX-V)(0 ("unnán"))(NEGATE 1 PrfPrc LINK *1 NP-HEAD-ACC BARRIER NOT-NPMOD);
	## Dat gal lei beare unnán.
	## Sii leat hui unnán dahkan guorahallat "sámi vieruid ja riekteáddejumiid".
	## Eanetlohku lea menddo unnán bidjan Finnmárkku mihtilmas diliid vuođđun.

 # In the last example "unnán" is an Adv. Hence the last condition.
 # Alternative: (NEGATE 1 PrfPrc LINK 1 Inf)?


#!! !!Demonstrative pronouns - should have a look at these



#!! * __DemPlIll__ removes Dem Ill and Dem Loc in front of Acc
#REMOVE:DemPlIll Dem (0 (Pers Ill) OR (Pers Loc))(1 (N Acc));
	#$ Muhto boanddat leat goikadan ollu jekkiid ja gilván daidda rásiid.


#!! * __DemSgNom__ selects Dem Nom Sg if VFIN Sg3
SELECT:DemSgNom (Dem Sg)  IF (*-1 VFIN + Sg3 BARRIER NOT-ADV-PCLE)(NOT 0 Pers LINK NOT *1 N BARRIER NOT-A-ADV OR PL-V);


#SELECT:DemPl (Dem Pl Acc) IF (0 Pers)(*1 PrfPrc BARRIER NOT-A-ADV LINK 1 (N Pl Acc) - TIME);
	#$ Guovtti dehálaš ja guovddaš rapporttain oaččuimet daid maŋimus divoduvvon veršuvnnaid 12.03.2010, dan beaivvi go mis lei dat maŋimus ovttasbargočoahkkin Finnmárkku fylkkain gos gárviimet min oktasaš evttohusa.

#!! * __DemIndefAttr__ selects Dem in front of Indef Attr, no verb to the left
#SELECT:DemIndefAttr Dem (NEGATE -1 REAL-V)(1 (Indef Attr));
	#$ Nuppi geahči dan seammá lávžžis fas biddjojuvvo bađa birra.

#!! * __DemGenSeammas__ selects dat Dem Gen in front seammás
#SELECT:DemGenSeammas (Dem Gen) IF (0 ("dat" Sg) LINK 1 ("seammás"));

#!! * __DemSg__ removes Dem Sg when there is no Sg N to the right
#REMOVE:DemSg (Dem Sg) IF (0 ("dat" Sg))(NEGATE *1 (N Sg) BARRIER NOT-NP LINK 0 HEAD-N)(NEGATE 1 PrfPrc LINK *1 (N Sg) BARRIER NP-BOUNDARY)(NEGATE 1 (Indef Sg) OR ("seammás"));
	#$ .. ja dan golut leat govttolaččat veahki ektui.

#!! * __datPersSg3__ selects dat Pers Sg3 when there is no N to the right
#SELECT:datPersSg3 ("dat" Pers Sg3)(NEGATE *1 (N Pl) OR (Indef Pl) OR NUM BARRIER NOT-NP)(NEGATE 1 NP-HEAD-GEN LINK 1 (Actio Nom) LINK 1 (N Pl))(NEGATE 0 Dem LINK *1 N BARRIER NOT-NPMOD-ACC) ;
	#$ ...ja ahte dat dáhttu čielgasit berre doahttaluvvot.
	#$ Harry viežžá báđi návsttis ja dainna vuoššat guliid dolas.
	#$ Dat, ahte Nielsenii eai lean čájehan makkárge hávdesajiid, sáhttá čuovvut máŋgga áššis.
	#$ In dieđe, vaikko vel sáhkohattažan dainna.


#!! * __PersNRel__ selects Pers Sg3 when there is a N and a Rel to the right
#REMOVE:PersNRel Pers (0 (Dem Gen))(1 N LINK *1 Rel BARRIER WORD);

#!! * __DemMeasure__ removes Dem in front of a Num and MEASURE or NUMUNIT in Ill
#REMOVE:DemMeasure Dem (0 Pers)(1 (Num Gen))(2 MEASURE + Ill OR NUMUNIT + Ill) ;
	#$ Na gal don dal oaččut dan beannot čuohtái, go juo válddežat.

SELECT:Attr Attr IF (*-1 Neg OR Qst OR ("go" Pcle))(0 (Pron Attr Foc/ge))(*1 N - Prop BARRIER NOT-NPMOD); # In use?

LIST MAKKAR = "makkár" "dakkár" "dákkár" "diekkár" ;

SELECT:DemAttr Attr IF (0 MAKKAR LINK *1 N BARRIER NOT-A) ;
	#$ Jáhkát go dakkár balddonasmuitalusaide?

#!! !!Disambiguating adjectives

#!! !Rules related to specific adjectives

#!! * jagáš
SELECT:DerDiminA ("jagáš" A Gen) IF  (-1 Dem)(0 Der/Dimin LINK 1 N) ;
	#$ Dán jagáš mánát gal leat buohkat fitnan Sámis ovdal.

#!! * boaris A or N
SELECT:boarisA A IF (0 ("boaris") LINK -1 Sem/Time) ;
	#$ Son lea vihtta sáhpánjagi boaris.

#!! * dáláš
REMOVE Imprt IF (0 ("dáláš"))(1 N + Nom);
	## Dáláš ortnet ahte dieđut juogaduvvojit soaittáhagas ii leat dohkálaš.

#!! * dološ
SELECT:r1684 (A Attr) IF (0 ("dološ"))(*1 N BARRIER NOT-NPMOD);
	#$ ...go lea gažaldat háhkamis dološ áiggi rájes geavaheami...

#!! * garra N vs. garas A
SELECT:garasAttr1 Attr IF (0 ("garas"))(1 (N Nom));
SELECT:garasAttr2 Attr IF (NEGATE *-1 SG3-V)(0 ("garas" A Attr))(1 A OR N)(NEGATE *1 SG3-V);
	#$ Norggas lea leamaš gal garra digaštallan sámi oahppoplána hárrái.
	#$ Beatnagat eai biva viššat garra buollašiid.

#!! * nanus
SELECT:nanusA A IF (-1 GRADE-ADV)(0 ("nanus"));
	#$ Dat lea hui nanus.

#!! * nuorra (vs. nuorrat V)
REMOVE:nuorraV V IF (0 ("nuorra") LINK *-1 NOT-COP-V BARRIER SV-BOUNDARY);
	#$ Son bođii dohko nuorran.
REMOVE:nuorraV V IF (0 ("nuorra") LINK *1 V BARRIER SV-BOUNDARY);
	#$ Min nuorat leat mielde nu olu doaimmain, sii leat oidnosis sápmelažžan.


#!! * sierra
SELECT:sierraAAttr (A Attr) IF (0 ("sierra"))(*1 N BARRIER NOT-NPMOD);
	#$ Sámelága vuođul lea ásahuvvon sierra ovddasteaddji orgána.

#!! * surgat
SELECT:surgatA (A Sg Nom) IF (0 ("surgat"))(-1 Interj);
	#$ Hei surgat, lea divrras!

#!! * veara
SELECT:vearaANom (A Nom)  IF (-1 Gen OR ("eanet"))(0 ("veara"));
	#$ De bođii geafes leaska ge gii bijai guokte smávvaruđaža, moatte šlántte veara.
	#$ ...buot dan mii leat ráhkistan ja gudnejahttin veara.

REMOVE:vearaN N IF (0 Der/veara OR ("veara"));
	#$ Buhttenrievttálaččat ii nuppástuvvon mearkkašanveara...

#!! * __vulitAttr__
SELECT:vulitAttr Attr IF (0 ("vuolit"))(1 ("gráda") OR ("dássi"));
	#$ Skuvla dušše addá oahpu vuolit dásis.

#!! * __Comp__ rules select Comp A
SELECT:Comp Comp IF (NEGATE -1 ("nu") OR ("seammá") OR CC)(NEGATE 0 (Pl Nom) LINK -1 Comp OR COMPAR LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(NEGATE 0 VFIN)(1 N LINK *1 go BARRIER NOT-N);
 	#$ Dážat navde Finnmárkku buoret guovlun go iežaset ruovttubáiki.

SELECT:Comp Comp IF (-1 ("mihá"));
SELECT:Comp Comp IF (-1 ("olu"))(NEGATE 0 (Pl Nom) LINK *1 PL3-V BARRIER NOT-ADV-PCLE);
	#$ Nuppe dáfus ledje mihá eanebut geat bivde lagabus gátti.
	#$ ...ja danin lea buot nu olu álkit inuihtaide.

#!! !Attribute disambiguation

SELECT:AttrTitle REAL-TITLE + Attr IF (0 N)(*1 FIRSTNAME OR Sem/Sur BARRIER REAL-WORD-NOT-ABBR);
	#$ Gilette, Mr. Sparrowis lea diibmu harcamuoras ja lea vahát jus boahtá maŋŋit.

#!! * __AttrVFIN__ removes Attr in front of VFIN
REMOVE:AttrVFINx Attr - DAT-PRON IF (1 VFIN LINK NOT 0 N OR A OR PrfPrc);
	#$ Lea dehálaš deattuhit, ahte IKT ii doaimma.
 	#$ Seamma guoská geassemánu.
	#$ Dan gal juohkehaš fuobmá dakkaviđe.

#!! * __AttrnotNA__ removes Attr when no N or A to the right
REMOVE:AttrnotNAx Attr - DAT-PRON IF (NOT 0 Indef OR Num OR ABBR)(NEGATE *1 N OR A OR (\?) OR  PrfPrc BARRIER NOT-NPMODCC)(NEGATE 1 CC LINK *1 (A Attr) OR PrfPrc BARRIER NOT-ADV-PCLE LINK *1 N BARRIER NOT-NPMODCC)(NEGATE 1 PUNCT-LEFT LINK *1 PUNCT-RIGHT LINK *1 N BARRIER NOT-NPMODCC);
   	#$ Dalle sáhttá beaktilit háhkat guovtte ja maiddái golmmagielat (maiddái suomagielat) gelbbolašvuođa.
	#$ Ollu go dus lea ruhta?
	#$ Maŋimus maid Will Turneris oinniimet lei go son vuojui meara bodnái.
	#$ Jos viessu lea dohkálaš, de ráfi orru dan badjel.

#!! * __ANomILLA__ selects Nom when ILL-A
SELECT:ANomILLA ILL-A + Nom IF (1 Ill);
	#$ Ja danin son lei šaddan nu bahá tálaide ja bustávaide.

#!! !Rules for Attr between Dem and N

#!! * __AAttrDemSg1, AAttrDemPl1__
SELECT:AAttrDemSg1 (A Attr)  IF (*-1 (Dem Sg Nom) BARRIER NOT-NPMOD)(*1 (N Sg Nom) BARRIER NOT-NPMOD);
SELECT:AAttrDemPl1 (A Attr) IF  (*-1 (Dem Pl Nom) BARRIER NOT-NPMOD)(*1 (N Pl Nom) BARRIER NOT-NPMOD);
 	#$ Dalle dat geahppaset gurppas daddjojuvvo veajan.
 	#$ ...jus dát máinnašuvvon árbevirolaš dilálašvuođat eai geahččaluvvo čuvgejuvvot.

#!! * __AAttrDemSg2, AAttrDemPl2__
SELECT:AAttrDemSg2 (A Attr)  IF (*-1 (Dem Sg Acc) BARRIER NOT-NPMOD)(*1 (N Sg Acc) BARRIER NOT-NPMOD);
SELECT:AAttrDemPl2 (A Attr) IF  (*-1 (Dem Pl Acc) BARRIER NOT-NPMOD)(*1 (N Pl Acc) BARRIER NOT-NPMOD);
 	#$ ...ja son doaimmaha dán dábálaš dahje eanaš geavaheami.
 	#$ Nuppe beales geahččalit dahkat dáid siskkáldas áddejumiid duođaštanfáddán.

#!! * __AAttrDemSg3, AAttrDemPl3__
SELECT:AAttrDemSg3 (A Attr) IF  (*-1 (Dem Sg Gen) BARRIER NOT-NPMOD)(*1 (N Sg Gen) BARRIER NOT-NPMOD);
SELECT:AAttrDemPl3 (A Attr)  IF (*-1 (Dem Pl Gen) BARRIER NOT-NPMOD)(*1 (N Pl Gen) BARRIER NOT-NPMOD);
 	#$ Dán konkrehta háve gustojedje gáržžideamit nappo earáide go boazosápmelaččaide.
 	#$ No cases left after the Dem Pl Acc-Attr-N Pl Acc rule?

#!! * __AAttrDemSgIll, AAttrDemPlIll__
SELECT:AAttrDemSgIll (A Attr) IF  (*-1 (Dem Sg Ill Attr) BARRIER NOT-NPMOD)(*1 (N Sg Ill) BARRIER NOT-NPMOD);
SELECT:AAttrDemPlIll (A Attr)  IF (*-1 (Dem Pl Ill) BARRIER NOT-NPMOD)(*1 (N Pl Ill) BARRIER NOT-NPMOD);
 	#$ Dás lei maiddái gažaldat geavahanvuoigatvuođas vuovdái ja dan vejolaš viidodahkii.
 	#$ ...mat gullet dáidda ja eará lunddolaš sullasaš doaimmaide.

#!! * __AAttrDemSgLoc, AAttrDemPlLoc__
SELECT:AAttrDemSgLoc (A Attr)  IF (*-1 (Dem Sg Loc Attr) BARRIER NOT-NPMOD)(*1 (N Sg Loc) BARRIER NOT-NPMOD);
SELECT:AAttrDemPlLoc (A Attr)  IF (*-1 (Dem Pl Loc) BARRIER NOT-NPMOD)(*1 (N Pl Loc) BARRIER NOT-NPMOD);
 	#$ Sápmelaččat ledje vuosttasin dahje okta dain vuosttaš álbmot-joavkkuin,...
 	#$ ...dainna váilevaš vejolašvuođain...

#!! * __AAttrDemComPl__
SELECT:AAttrDemComPl (A Attr) IF  (*-1 (Dem Pl Com) BARRIER NOT-NPMOD)(*1 (N Pl Com) BARRIER NOT-NPMOD);
 	#$ Geainnuin livččii eambbo turistajohtolat suohkana čoahkkeássanguovddážiid čađa daiguin ekonomalaš oalgeváikkuhusaiguin.

#!! * __AAttrDemdakkar__
SELECT:AAttrDemdakkar (A Attr)  IF (*-1C Dem BARRIER NOT-NPMOD)(*1 N BARRIER NOT-A-CC)(NEGATE 0 N LINK *1 VFIN BARRIER NOT-ADV)(NEGATE 0 Gen LINK -1 (Dem Gen)) ;
 	#$ Mo Alimusriekti meannudii vaikkoba dakkár vuolit dási olbmuid vihtančilgehusaiguin?
	#$ Muhto dakkár radikála rievdadus eahpitkeahttá buktá garra reakšuvnnaid.
	#% Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.

#!! !Other attribute rules

#!! * Not attribute in front of Ess: dovddus sánálaš nissonin
SELECT:dovddusEss (A Nom) IF (0 ("dovddus") OR ("beakkan") OR ("deaŧalaš"))(1 Attr OR Gen LINK *1 (N Ess) BARRIER S-BOUNDARY OR REAL-V) ;
	#$ Son lea dovddus sánálaš ja girjjálaš nissonin.

#!! * __AAttrN__ no copulas close to the left
SELECT:AAttrN (A Attr) IF (NEGATE *-1 COPULAS BARRIER NP-HEAD-NOM OR NOT-AUX-V)(NEGATE 0 Num OR Inf OR Comp OR ("nubbi") OR V) (NEGATE 0 Adv LINK 1 Gen LINK 1 N)(NEGATE 0 Adv LINK 1C (N Gen))(1 N OR A LINK NOT 0 TIME OR PLACE)(NEGATE 1 VFIN OR Inf OR PREGEN OR Prop)(NEGATE 0 (N Sg Nom) LINK *-1 SG3-V BARRIER NOT-NPMODADV LINK NOT *-1 NP-HEAD-NOM BARRIER S-BOUNDARY)(NEGATE *-1 ("go") BARRIER NOT-NP LINK NOT -1 VFIN LINK NOT 1 EOS)(NEGATE 0 Gen LINK -1 (Dem Gen));
	#$ Oainnán ollu olbmuid.
	#$ Gonagas sáhttá mearridit dábálaš njuolggadusaid báikkálaš dilálašvuođaid ektui.
	#$ Sis-Finnmárkkus lea vuolit gaskamearálaš bruttoboahtu go fylkkas...  # gaskamearálaš
	#$ ...go galgá válljejuvvot gaskal vejolaš ja dohkálaš iskkadan- ja dikšunvugiid. # dohkálaš
	#$ Olbmot geat ellet náitoslágan dahje guimmešvuođalágan gaskavuođain. # guimmešvuođalágan
	#$ Máttimus sápmelaččat eahpidedje dáža dutkiid eanet go davit sápmelaččat.
	#$ Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.
	#$ Dan dihte ohcat guokte lassi stipendiáhttavirggi sámi/eamiálbmotguoskevaš dutkamiidda.
	#$ Olles doaibmaviessu lea sullii 2300 njealjehasmehtera stuoru.
	#$ Lea erenoamáš dehálaš movttiidahttit sámegielat doaktáriid bargat gielddain.
	#$ Olggosaddin sáhttá liikká dáhpáhuvvat jos gávdnojit dasa beaktilis sivat.
	#$ Sámedikki vuosttamuš dábálaš dievasčoahkkima maŋŋá...
	#$ Oktasaš vuoigatvuođat ledje šaddan dološ áiggi rájes...
	#$ ...galget divššohasa lagamus oapmahaččat oažžut dieđu...
	#% Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.

#!! * __AAttrCop__  copulas close to the left
SELECT:AAttrCop (A Attr) IF (*-1 COPULAS BARRIER NOT-ADV)(NEGATE 0 Adv LINK 1 A)
(NEGATE 0 (A Pl) LINK *-1 COPULAS LINK -1 Pl OR Pl1 OR Pl2 OR Pl3)
(1 A OR N LINK NOT 0 Superl OR Ord)(NEGATE 1 Gen LINK *1 Po OR Loc OR Inf BARRIER NP-BOUNDARY OR Indef)(NEGATE 1 Adv OR REAL-V OR ADVLCASE)(*2 go OR EOC OR Neg BARRIER NOT-NP);
	#$ Sis-Finnmárkkus lea vuolit gaskamearálaš bruttoboahtu go fylkkas...  # vuolit
	#$ Interneahtta lea máilmmiviidosaš oktavuohta dihtoriid gaskkas.
	#$ Dieđuid diehtogieđahallama ovddasvástideaddjin leat guovlluguovdasaš dearvvašvuođadoaimmat.
	#$ Dán áiggi sii johtet biillaiguin, jos headjavissui lea guhkit mátki.
	#$ Sámegiella ja dárogiella leat ovttaárvosaš gielat.

SELECT:ANomCop (A Nom) IF (*-1 COPULAS BARRIER NOT-ADV LINK *-1 BOS BARRIER Nom)(0 DEHALAS + Attr LINK 1 N - Nom - Gen LINK NOT *1 REAL-V BARRIER S-BOUNDARY)
	#$ Lea suohtas kurssas.


#!! * __AttrPlacelaš__ This rule selects Sem/Plc Der/laš A Attr in front of Prop or N
SELECT:XAttrPlacelašr1718 (Sem/Plc Der/laš A Attr) IF (1 FIRSTNAME OR Sem/Sur OR N);
	#$ Oslolaš Jostein Johnsen šattai Eurohpameašttirin skohtergilvvus.
	#$ Oslolaš gánda šattai Eurohpameašttirin skohtergilvvus.

REMOVE:Xr1719 VFIN IF (NEGATE *-2 Rel OR CS BARRIER S-BOUNDARY)(-1C VFIN)(NEGATE 0 ConNeg);
	#$ Mun áiggun oastit biilla.
	#$ Gos dáppe lea máddi?
	#$ Ánne-Sofe buvttii varas biergguid.
	#$ Máhtte manai čuoigga skuvlii.
	#$ Dan dihte go lea čađat dárbu ođasmahttit jorgalusaid.
	#$ Tommi geahčai skuvlii.
 	#$ Bargu lei árvvoštallat dan.
 # As we see, this rule can lead to a verbal, a nominal, an adjectival or an
 # adverbial reading. It comes here because this is where it is first needed.

#!! * __AdvManimus__
REMOVE:AdvManimus Adv IF (NEGATE *-1 REAL-V BARRIER S-BOUNDARY)(0 A)(NEGATE 1C A)(NEGATE *1 REAL-V BARRIER S-BOUNDARY); # This rule makes sense to this sentence.
	#$ Maŋimus maid Will Turneris oinniimet lei go son vuojui meara bodnái.

#!! * __AttrIllCop__
SELECT:AttrIllCop Attr IF (-1 (N Nom) LINK *-1 COPULAS LINK -1 HUMAN + Ill OR (Pers Ill))(0 A)(1 (Nom)) ;
	#$ Sidjiide lei maid liegga Ruija geasuheaddji guovlu.

#!! * __AttrAdv__
SELECT:AttrAdv (A Attr) IF (-1 Adv LINK NOT 0 GRADE-ADV)(NEGATE 0 Inf OR VFIN OR RCmpnd)(*1C N BARRIER NOT-NPMOD)(NEGATE 1 CLB LINK 1 N)(NEGATE 1 Prop)(NEGATE 0 (A Pl) LINK *-1 PL-V OR DU-V BARRIER NOT-ADV-PCLE);
	#$ Mánná viehkalii olggos álás julggiid.
	#$ Universitehtas lea maiddái čielga nášunála ovddasvástádus...
	#$ Dalle sáhttá beaktilit háhkat guovtte ja maiddái golmmagielat (maiddái suomagielat) gelbbolašvuođa.Gonagas sáhttá mearridit dábálaš njuolggadusaid báikkálaš dilálašvuođaid ektui.

REMOVE:AttrVejolaš ("vejolaš" Attr)  IF (*-1 COPULAS)(1 REAL-V) ;
REMOVE:RCmpndA  (RCmpnd A) IF (0 (RCmpnd N)) ;

#!! * __ANom__  removes A Nom
REMOVE:ANom (A Nom) IF (NEGATE -1 COPULAS LINK 1 DEHALAS LINK *1 ADVLCASE BARRIER NOT-NPMOD)(0 (A Attr))(NEGATE 0 (Pl Nom) LINK *-1 PL-V BARRIER NOT-ADV-PCLE OR CS LINK NOT *-1 (Pl Nom) BARRIER S-BOUNDARY)(NEGATE 0 (Pl Nom) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3)(NEGATE *1 NP-HEAD-NOM OR Ess BARRIER NOT-A-ADV)(*1C N OR RCmpnd BARRIER NOT-NPMOD OR (PUNCT RIGHT) OR CC LINK NOT 1 Po OR PREGEN)(NEGATE 1 Prop OR Ill)(NEGATE 1 COMMA LINK 1 N)(NEGATE 1 Gen LINK *1 Po OR PREGEN BARRIER NP-BOUNDARY)(NEGATE 0 Pl LINK *-1 Pl3 BARRIER VFIN) ;
	#$ Dat lea čuovvovaš sajis.
	#$ Ánne-Sofe buvttii varas biergguid.
	#$ Mun čállen romit reivve go don.
	#$ Vuoigatvuohta leat Sámedikkiid jienastusloguin lea lassin dábálaš jienastanvuoigatvuođa eavttuide.
	#% Dálut eai lean nu ráhpadat ođđasishuksenáigge.

#!! * __AAttr__  selects A Attr
SELECT:AAttr (A Attr) IF  (NOT 0 (V* A) OR Adv OR Inf)(NEGATE 0 PrfPrc LINK *-1 REAL-COPULAS BARRIER REAL-V)(NEGATE 1 PREGEN)(NEGATE *-1 COPULAS BARRIER NOT-A-ADV-PCLE) (*1 N - Ess BARRIER NOT-NPMOD OR Dem LINK NOT 1 Po)(NEGATE 0 Gen LINK -1 (Dem Gen))(NEGATE 1 CC LINK 1 Num OR N) ;
	#$ Máttimus sápmelaččat eahpidedje dáža dutkiid eanet go davit sápmelaččat. # davit
	#$ ...main leat mielde dušše priváhta oasálaččat, ii leat...
	#$ Sámedikki ásaheapmi lea eanet ideologalaš go čielga mearrádus. # čielga
	#$ Lávži goallostuvvo dan ovddit noađđehearggi čeabehii.
	#$ Dábálaččat earuha gaskal dábálaš (oppalaš) geavahanvuoigatvuođaid ja sierranas (partiála) geavahanvuoigatvuođaid.
	#$ Váillui guhkit áiggi heivvolaš oaivil.
	#% lea dehálaš vuovdingálvun
	#% Dán girjji vuođul ja ságastallamiiguin lullisámiin lea munnje báhcán millii ahte internáhttadoaibma ii lean gal alla dásis, muhto internáhttaskuvlii dattetge bohte viššalit lullisámi bearrašat.

SELECT:OrdAttr (Ord Attr) IF (1 N) ;
	#$ Son lea viđát luohkás.


#!! * __AdvN__ removes Adv
REMOVE:AdvN Adv IF (NEGATE -2 NOT-AUX-V)(-1 Gen OR Dem OR Indef LINK NEGATE -1 PrfPrc)(0 Attr LINK 1 N);
	#$ Muhto dán geasi gal áiggun bargat vai dinen veháš ruđaid.

#!! * __AAttrPunct__
SELECT:AAttrPunct (A Attr)  IF (1 PUNCT LINK *1 Attr BARRIER NOT-ADV LINK *1 N BARRIER WORD) ;
	#$ Dalle sáhttá beaktilit háhkat guovtte ja maiddái golmmagielat (maiddái suomagielat) gelbbolašvuođa.Gonagas sáhttá mearridit dábálaš njuolggadusaid báikkálaš dilálašvuođaid ektui.

#!! * __AAttrgoAAttr__
SELECT:AAttrgoAAttr (A Attr) IF (-1 COMPAR)(1 go)(2 (A Attr) LINK NOT 0 Nom)(*3 N BARRIER NOT-A);
	#$ Sámedikki ásaheapmi lea eanet ideologalaš go čielga mearrádus. # ideologalaš

#!! * __AttrTIME__
SELECT:AttrTIME (A Attr) IF (NEGATE 0 Num OR PrfPrc)(NEGATE 0 Adv LINK *-1 REAL-V BARRIER NOT-ADV-PCLE)(NEGATE 0 Pl LINK *-1 Pl3 BARRIER VFIN)(1 TIME);
	#$ Guovdageaidnu čájehii fámuid vuosttaš minuvtta rájes.
	#$ Ollu jagiid mielde lei dáhpin šaddan ahte stuorra gánddat bohte min geahčái lávvardaga ja sotnabeaivve go radios ledje sáddagat skeittástan- ja čuoigangilvvuin.
	#% Dálut eai lean nu ráhpadat ođđasishuksenáigge

#!! * __AAttrCoord1__ coordination, first part
SELECT:AAttrCoord1 (A Attr) IF (1 CC OR COMMA LINK *1 (A Attr) OR ("nubbi") OR PrfPrc BARRIER NOT-ADV-PCLE LINK 1 N)(NEGATE -1 ("leat") LINK *3 Attr OR PrfPrc LINK NOT 1 Nom) ;
	#$ Duodji lei deaŧalaš sivva manne olbmot Gáivuonas birgejedje nu bures váttes jagiid vuosttaš ja nuppi máilbmesoađi gaskkas.
	#$ ...go galgá válljejuvvot gaskal vejolaš ja dohkálaš iskkadan- ja dikšunvugiid. # vejolaš
	#$ Olbmot geat ellet náitoslágan dahje guimmešvuođalágan gaskavuođain. # náitoslágan
	#$ ...go viidáset gaskkusta oppalaččat álbmoga historjjálaš, kultuvrralaš ja politihkalaš dieđuid. # kultuvrralaš
 	#$ Ráđđehus hálida erenoamažit buktit ovdan ILO-konvenšuvnna nr. 169 eamiálbmogiid ja čearddaid birra iehčanas stáhtain, jagi 1966 ON-konvenšuvnna siviila ja politihkalaš vuoigatvuođaid birra artihkal 27,...

#!! * __AAttrCoord2__ coordination, first part
SELECT:AAttrCoord2 (A Attr) IF (NEGATE 0 Prop)(1 COMMA LINK 1 Attr LINK 1 CC LINK *1 HEAD-N BARRIER NOT-NPMODADV);
	#$ ...go viidáset gaskkusta oppalaččat álbmoga historjjálaš, kultuvrralaš ja politihkalaš dieđuid. # historjjálaš

#!! * __PrfPrcCoordA__ selects PrfPrc in coordination with an A
SELECT:PrfPrcCoordA PrfPrc IF (*-1 CC OR COMMA BARRIER NOT-ADV LINK -1 A);
	#$ Dilálašvuođaid álgu sáhttá leat dovdameahttun dahje jávkan historjjá seavdnjatvuhtii.
	#$ Johtalus rabas dahje galbmon mearas lea muddejuvvon. # galbmon

#!! * __ACoordPrfPrc__ selects A itn coordination with PrfPrc
SELECT:ACoordPrfPrc (A Attr) IF (NEGATE -1 ("leat"))(1 CC OR COMMA)(*2 PrfPrc BARRIER NOT-ADV)(*3 N BARRIER NOT-NPMOD);
	#$ Johtalus rabas dahje galbmon mearas lea muddejuvvon. # rabas

#!! * __AAttrContra__ selects A itn coordination with PrfPrc
SELECT:AAttrContra (A Attr) IF (*-1 Neg BARRIER S-BOUNDARY OR VFIN)(*1 CONTRA BARRIER WORD LINK 1C Attr);
	#$ ILO-konvenšuvdna nr 169 ii vuođđuduva formála, muhto funktionála oamastanvuoigatvuođa doahpagii.

IFF:sapmelas ("sápmelaš" A) IF (0 Attr LINK *1 N) ;
	#$ Dušše inuihtat ja sápmelaččat sáhttet adnojuvvot Davviriikkaid eamiálbmogin.

#!! !Special rules for 'buorre' (the only adjective showing case agreement)

SELECT:SeCaseAgrBuorre $$CASE-AGREEMENT + A IF (0 ("buorre") OR OKTA OR Dem LINK NOT 0 Superl OR Comp) (*1 $$CASE-AGREEMENT BARRIER NOT-NPMOD LINK 0 N)(NEGATE -1 Nom LINK NOT 0 Gen) ;
SELECT:SeComAgrBuorre Com IF (0 ("buorre") OR OKTA OR Dem OR ("nubbi")) (*1 Com BARRIER NOT-NPMOD LINK 0 N) ;
SELECT:SeLocAgrBuorre (Pl Loc) IF (0 ("buorre") OR OKTA OR Dem) (*1 (Pl Loc) BARRIER NOT-NPMOD LINK 0 N) ;
	## Son dadjá dan nuppiin sániin.

SELECT:SeCaseHalfagrBuorre (A Gen) IF (0 ("buorre") OR OKTA OR Dem) (*1 CASE-HALFAGREEMENT + N BARRIER NOT-NPMOD);


#!!	!Comparatives V Pl3
#!! This block of rules is there to ensure case agreement for comparatives.

#!! * Select Pl Nom if V Pl3
SELECT:PlNomifVPl3 (N Pl Nom) IF (NEGATE -1 DADE OR MADE)(0 Comp LINK *1 (V Pl3) BARRIER VFIN OR S-BOUNDARY);
	#$ Lea buoret borramuš go dat maid nuorat dán áigge njillet.

#!! * Remove Nom, Acc and Gen if Comp
REMOVE:GenifComp Gen IF (0 Comp)(*1 HEAD-N BARRIER NOT-NPMOD LINK 0 NOT-GEN);
	#$ Sámedikki doarjjastivra lea doallan čoahkkima Ákšovuonas árat dán mánus. # árat
REMOVE:AccifComp Acc IF (0 Comp)(*1 HEAD-N BARRIER NOT-NPMOD LINK 0 NOT-ACC);
	#$ Ja ožžot eanet buhtaduvvot jávkan elliid ektui go boazodoallu vaikko eai leat luođus guhton eambbo snoagga ¼ oasi jagis, goit dáppe davvin. # eambbo

REMOVE:NomifComp Nom IF (0 Comp)(*1 HEAD-N BARRIER NOT-NPMOD OR PUNCT LINK 0 NOT-NOM LINK NOT 0 Inf)(NEGATE 1 COMMA OR CC)(NEGATE 0 (Pl Nom) LINK *-1 PL-V OR DU-V BARRIER NOT-NPMODADVIIP)(NEGATE 0 (Pl Nom) LINK *-1 ConNeg OR PrfPrc OR (Actio Ess) BARRIER NOT-NPMODADVIIP LINK -1 PL-V OR DU-V)(NEGATE 0 (Pl Nom) LINK *1 (V Pl3) BARRIER NOT-NPMODADV)(NEGATE 0 (Sg Nom) LINK *-1 SG3-V BARRIER NOT-NPMODADV)(NEGATE 1 Gen LINK *1 Po OR PREGEN BARRIER NP-BOUNDARY)(NEGATE 1 Dem);
	#$ Finnmárkkuopmodat sáhttá dárkileappot albmaduvvon guovlluide mearridit gáržžádusaid beassat ávkkástallat ođasmuvvi valljodagain nugo namahuvvo vuosttas lađđasis, juos deasta valljodagas gáibida dan. # dárkileappot
	#$ Finnmárkkus ledje ovdal sihke doaktárat ja buohccedivššárat dábálaččat Lulli-Norggas eret, ja máŋggas ledje dušše leamaš moadde jagi dahje vel oanibut davvin. # oanibut
	## Álggahit eará doaibmabijuid gáibida baicca lassi árjja...  #  álggahit
	#% Go dii lehpet šiegat ealibiin.
	#% Soai vácciiga seamma luohká go Liná, ja leigga čeahpit eanas fágain.

#!! !alit vs. allat Comp Attr

LIST ALLAT = "boazolohku" "dáiddaoahpahus" "dássi" "divišuvdna" "dutkan" "eallindássi" "gealdda" "gráda" "iešmássa" "lohku" "oahppa" "oahppolágádus" "oahppu" "oahpahus" "oahppat" "riekti" "sápmi" "lávdaoahppofálaldat" "virgi" ;

#!! * __allat__ in front of ALLAT OR MONEY OR EDUCATION OR go
SELECT:allat ("allat") IF (0 ("alit"))(1 ALLAT OR MONEY OR EDUCATION OR go);
	#$ Finnmárkkus ássi olbmuid divat ii galgga leat alit go guovtti dan mađe go gielddas ássi oblmuid divat. #PrsPrc needs a mapping rule
	#$ Dán njuolggadusa deaŧalaš eaktu lea ahte ođđa siidaosiid ásaheapmi ii galgga dagahit ahte boazolohku šaddá alit go siidii mearriduvvon lohku, geahča dás maŋŋelis.
SELECT:allatMeasure ("allat") IF (-3 ("badjel") OR ("vuollil"))(-2 Num)(-1 MEASURE)(0 ("alit"));

#!! * __alitColour__ in coordination with COLOUR
SELECT:alitColour ("alit") IF (0 ("allat"))(*0 COLOUR BARRIER NOT-CRD);
	#$ Fiskes, rukses, alit, ruoná ha muđuid buotlágaš ivnnit.

#!! * __alitN__ in front of VEHICLE, CLOTHES, BEDCLOTHES, BUILDING and more
SELECT:alitN ("alit") IF (0 ("allat"))(1 ("Volvo") OR VEHICLE OR ("čalbmi") OR ("dielku") OR ("russa") OR ("čuovga") OR ("albmi") OR ("ivdni") OR ("fearga") OR CLOTHES OR BEDCLOTHES OR BUILDING);
	#$ Min bussás leat alit čalmmit.

#!! * __alitEOS__ in the end of a sentence
SELECT:alitEOS ("alit") IF (-1 go OR Qst)(0 ("allat" Nom))(1 EOS);

#!!	!Comparative or A Pl

#!! * __APlNomafterCop__ selects A Pl Nom after copulas and Pl Nom OR Pl Pron
SELECT:WAPlNomafterCop (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Pl Nom) OR (Pron Pl Nom) OR (Pl1 Nom) OR (Pl2 Nom) OR (Pl3 Nom) BARRIER S-BOUNDARY OR (Sg Nom))(NEGATE 0 N)(NEGATE 1 A)(NEGATE 1 Adv LINK 1 A);
	#$ ...ja dan golut leat govttolaččat veahki ektui.
	#$ ...jus dat leat čuožžovaččat ja dárbbašlaččat.
	#$ Riekteháhkama eavttut geavahusa njuolggadusaid mielde eai oro absoluhtat.

#!! * __APlNomafterDu__ selects A Pl Nom after copulas and Du
SELECT:WAPlNomafterDu (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Du1 Nom) OR (Du2 Nom) OR (Du3 Nom) BARRIER S-BOUNDARY)(NEGATE 0 N)(NEGATE 1 A)(NEGATE 1 Adv LINK 1 A);
	#$ Vuoi, doai leahppi nu čeahpit!

#!! * __ASgNomNoSubj__ selects A Sg Nom after copulas Sg3 or Neg Sg3
SELECT:WASgNomNoSubj (A Sg Nom) IF ((-1 COMMA LINK NOT *2 VFIN) OR (*-1 ("leat") + Sg3 BARRIER NOT-ADV-PCLE) OR (*-1 ("leat") BARRIER NOT-ADV-PCLE LINK -1 (Neg Sg3))) (NEGATE 0 N OR Inf)(NEGATE 1 Adv LINK 1 A)(1 NP-BOUNDARY OR PrfPrc)(NEGATE 0 Attr LINK 1 N)(NEGATE 0 GRADE-ADV LINK 1 A);
	#$ Lea ártet go it gallet fulkkiid.
	#$ Ii leat vuogas geavahit hálduiváldima dálá fásta opmodagaide.
	#$ Gávpoga geaidnu lei ráhkaduvvon gollis, buhtis dego čađačuovgi láse.

#!! * __ASgNomafterCop__ selects A Sg Nom after copulas and Sg Nom, not so strong constraint for the target
SELECT:WASgNomafterCop (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Sg Nom) BARRIER NOT-ADV-PCLE LINK *-1 BOC OR CP BARRIER NOT-ADV-PCLE)(1 NP-BOUNDARY OR PrfPrc LINK NOT 0 N);
	#$ Vuoigŋa gal lea geargat, muhto oažži lea headju. # geargat, headju
	#$ ...dan dihte go geassegollebivdu lei áibbas heittot leamaš,... # heittot
	#$ Joddu suhppejuvvo go doaris lea gárvvis.
	#$ Buođđu lea dál gárvvis.
	#% Dat lea guhkimus fanas.


#!! * __ASgNomEssCopNeg__ selects A Sg Nom after copulas Sg3 or Neg Sg3s,
SELECT:WASgNomEssCopNeg (A Sg Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY LINK *-1 (Neg Sg1) OR (Neg Sg2) OR (Neg Sg3) BARRIER NOT-ADV-PCLE)(NEGATE 0 N)(NEGATE 1 N OR A)(NEGATE 1 GRADE-ADV LINK 1 A);
	#$ Boazosápmelaččain ii leat bassin vejolaš fievrridit gálvvu gerresiiguin.
	#$ Jos dat ii leat vejolaš, de mii fertet mannat.

#SELECT:WASgNomifCopEOC (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Sg Nom) BARRIER NOT-ADV-PCLE LINK *-1 BOC OR CP BARRIER NOT-ADV-PCLE)(*1 COPULAS BARRIER NOT-ADV-PCLE LINK *1 CLB OR EOS BARRIER NOT-ADV-PCLE);
#SELECT:Wr1769 (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 Nom BARRIER S-BOUNDARY LINK *-1 CC BARRIER NOT-NPMOD LINK -1 Nom)(NEGATE 0 N)(NEGATE 1 REAL-V OR A);

#!! * dsfa
SELECT:Wr1772xc (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Sg Nom) OR (Pron Sg Nom) OR (Sg1 Nom) OR (Sg2 Nom) OR (Sg3 Nom) BARRIER S-BOUNDARY LINK NOT 0 Ess)(NEGATE 0 N OR Inf OR Adv)(NEGATE 1 REAL-V)(NEGATE 1 CC LINK 1 Adv)(NEGATE *1 N BARRIER NOT-NPMOD);
	#$ ... berre mearridanváldi leat nu viiddis go vejolaš ja nu ...
	#$ Dilálašvuođaid álgu sáhttá leat dovdameahttun dahje jávkan historjjá seavdnjatvuhtii.

#!! * __AcompGo__ selects A Comp even if there is no verb (ellipse)
SELECT:AcompGo (A Comp Nom) IF (*-1 (N Sg Nom) OR (Pron Sg Nom) OR (Sg1 Nom) OR (Sg2 Nom) OR (Sg3 Nom) BARRIER NOT-ADV-PCLE LINK NOT 0 Ess)(NEGATE 0 N OR Inf OR Adv)(1 go);
	#$ Máhttájeaddji ii leat stuorit go oahpaheaddjis, ii ge bálvaleaddji stuorit go hearrás.


SELECT:Wr1775xc (A Sg Nom) IF (*-1 COPULAS + Sg3 BARRIER NOT-A-ADV)(NEGATE 0 Inf)(0 (N Sg Loc) OR Der/NomAg OR (N* A))(*1 EOS OR CLB BARRIER NOT-ADV-PCLE);
	#$ Dudno mielas lea suohtas.

#!! * __Wr1776xc__ selects (A Sg Nom)
SELECT:Wr1776xc (A Sg Nom) IF (-1 NP-HEAD-SG-NOM LINK NOT 0 (A Attr) OR Adv)(*-2 COPULAS BARRIER NOT-ADV-PCLE LINK NOT *-1 Nom BARRIER S-BOUNDARY)(NEGATE 0 Adv LINK 1 Adv)(NEGATE 0 Attr LINK *1 (N Sg Nom) BARRIER CLB OR NOT-NPMOD);
	#$ Dábálaččat lea čuollu guhkit, golbma, njeallje fierbmebaji guhku. # guhkit

SELECT:Wr1777c (Sg Nom) IF (*-1 Ill BARRIER NOT-ADV-PCLE LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(*-1 SG3-V BARRIER NOT-AUX-V)(NEGATE 0 PrfPrc OR ILL-A)(NEGATE 0 NUM LINK *1 ALU OR ("velggolaš") BARRIER NP-BOUNDARY);
	#$ ... muhtun gii lei sutnje velggolaš logi duhát taleantta.
	#$ Mus lea dutnje juoga dadjamuš.

SELECT:Wr1778xc (A Pl Nom) IF (*-1 (N Pl Nom) BARRIER NOT-ADV-PCLE LINK *-1 COPULAS BARRIER SV-BOUNDARY);
	#$ Mánnáguoddi-spagáin leat spahká-oaivvit sakka guhkibut. # guhkibut
	#$ Dán oktavuođas leat dološ geavahusa njuolggadusat miellagiddevaččat. # miellagiddevaččat

SELECT:Wr1779xc (A Pl Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY LINK *-1 (V Neg) BARRIER NOT-ADV-PCLE LINK 0 DU-PL-V)(NEGATE 0 N)(NEGATE 1 REAL-V OR A);

SELECT:Wr1780c (A Sg Nom) IF (*-1 BOS OR REAL-CLB BARRIER NOT-CC)(1 (Dem Sg Nom) OR MUN OR DON OR SON);
	#$ Ja ávdugas son guhte oskkui.

SELECT:Wr1781c (A Pl Nom) IF (*-1 BOS OR REAL-CLB BARRIER NOT-CC)(*1 NP-HEAD-PL-NOM BARRIER NOT-NPMOD);
	#$ Ávdugasat sii geat moraštit.

SELECT:W (A Nom)  IF (-1 go LINK -1 (A Nom) LINK *-1 COPULAS BARRIER S-BOUNDARY)(0 REAL-V LINK NEGATE 1 N) ;

REMOVE:Wr1782 REAL-V IF (-1 COPULAS)(0 Nom)(NEGATE 0 PrfPrc)(1 (Pron Rel));
	#$ Dat geaidnu lea govdat mii doalvu gáđohussii.
SELECT:Wr1783 (A Nom) IF (-1 COPULAS)(0 Nom)(1 (Pron Rel));
	#$ Dát dáidá leahkit duohta maid boazodoalli lohká.
	#$ Dat geaidnu lea govdat mii doalvu gáđohussii.

REMOVE:Wr1784 VFIN IF (*-1 COPULAS BARRIER NOT-ADV-INDEF)(0 Attr)(1 N OR EOS);

SELECT:Wr1785 A IF (*-1 COPULAS BARRIER NOT-ADV-INDEF)(NEGATE 0 REAL-V OR Ord OR N)(1 N OR EOS);
	#$ ...de dilli orru leamen earalágan.

SELECT:Wr1786xc A IF (-2 (N Sg Nom))(-1 ("leat" Sg3))(NEGATE 0 N OR PrfPrc OR Inf)(NEGATE 0 Ill LINK 1 PrfPrc)(NEGATE 0 Adv LINK 1 A);
	#$ Sin váilevaš oahppu ja giellamáhttu lea guhkit áiggi leamaš heađušteaddjin. 	 # guhkit

REMOVE:Wr1787c (A Attr) IF (0 A)(1 COMMA)(NEGATE 2 A OR PrsPrc)(NEGATE 2 CC LINK 1 Attr);

REMOVE:Wr1788xc (A Attr) IF (0 A)(NEGATE 1 A OR N OR Num OR Actio OR PrfPrc OR PrsPrc OR CC OR COMMA OR (\?));

REMOVE:Wr1789xc (A Attr) IF (0 A-CASE OR Adv)(1 CC LINK *1 A-CASE BARRIER NOT-ADV-PCLE);
	#$ Dan dihte leage riekta ja dehálaš go skuvla čielga vugiin dovddasta dat lea maiddái sámi ohppiid skuvla. # riekta

SELECT:Wr1791xxc (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV LINK 0 SG3-V LINK NOT *-1 Loc BARRIER N)(NEGATE -1 Gen)(NOT 0 WEATHER-SITUATION OR Inf OR Attr)(NEGATE 0 Adv LINK *1 REAL-V BARRIER NOT-ADV)(NEGATE 1 PrfPrc)(NEGATE 1 CC LINK 1 Adv) (NEGATE 0 Adv LINK *1 (A Sg Nom) BARRIER NOT-ADV-PCLE) ;
	#$ Lea veahá eahpidahtti, ahte eanaš oassi ođđa govvadáidagis,...  # eahpedahtti

SELECT:APlNom (A Pl Nom) IF ((*-1 COPULAS + DU-PL-V BARRIER NOT-ADV-PCLE LINK -1 NP-HEAD-PL LINK 0 Nom) OR (*-1 COPULAS - SG-V BARRIER NOT-ADV-PCLE LINK -1 DU-PL-V LINK 0 Neg OR COPULAS))(0 Sg);
	#$ Biktasat leat nannosat.
	#$ Bártnit ledje čeavlát.
	#$ Sii eai lean duhtavaččat priváhta olbmuid luhtte orrumiin ja háliidedje skuvlainternáhta nugo lei leamaš Guovdageainnus ja Buolbmágis.

SELECT:Wr1793c (A Pl Nom) IF (1 (Pron Pl2))(*2 COPULAS BARRIER REAL-V)(*2 PL2-V BARRIER NOT-ADV-PCLE);
	#$ Man čeahpit dii lehpet hilgut Ipmila báhkkomiid!

SELECT:Wr1795 (A Pl Nom) IF (*-1 ("orrut") BARRIER NOT-ADV-PCLE)(*-1 PL3-V BARRIER SV-BOUNDARY);

SELECT:Wr1796xc (A Pl Nom) IF (-2 (A Pl Nom) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(-1 CC);
	#$ ...jus dat leat čuožžovaččat ja dárbbašlaččat. # dárbbašlaččat

REMOVE:Wr1797 (A Pl Nom) IF (*-1 COPULAS BARRIER REAL-V)(*-1 SG3-V BARRIER NOT-AUX-V)(*1 (A Sg NOM) BARRIER SV-BOUNDARY OR Nom);
	## No hits?

	# Coordination
	# ------------
SELECT:Wr1798c (A Sg Nom) IF (-2 (A Sg Nom))(-1 CC OR COMMA)(1 CC OR COMMA)(2 (A Sg Nom));
	#$ e. e. man bissovaš, dovddus ja bindil dat lea. # dovddus
SELECT:Wr1799 (A Sg Nom) IF (-2 (A Sg Nom))(-1 CC)(1 CS OR EOS)(NEGATE 0 Inf LINK -2 Inf);
	## Vare livččet jogo galmmas dahje liekkas! # liekkas

REMOVE:Wr1800c (A Attr) IF (1 COMMA OR CC)(2 A)(NEGATE *1 N BARRIER NP-BOUNDARY);
	#$ ...man bissovaš, dovddus ja bindil dat lea. # bissovaš
	#$ Lehket álo ustitlaččat, vuollegaččat ja guhkesmielalaččat. # ustitlaččat

SELECT:Wr1801c (A Pl Nom) IF (-2 (A Pl Nom))(-1 CC OR COMMA)(1 CC OR COMMA)(2 (A Pl Nom));
	#$ Lehket álo ustitlaččat, vuollegaččat ja guhkesmielalaččat. # vuollegaččat

SELECT:Wr1802c (A Pl Nom) IF (-2 (Pl Nom))(NEGATE -2 Adv)(-1 CC)(1C NP-BOUNDARY);
	#$ Lehket álo ustitlaččat, vuollegaččat ja guhkesmielalaččat. # guhkesmielalaččat
	#$ De bohte ollu tuollárat ja suttolaččat ja čohkkájedje beavddis.  # suttolaččat

SELECT:Wr1803 (Pl Nom) IF (-2 (Pl Nom))(NEGATE -2 Adv OR Inf)(-1 CC)(1 CC OR CLB)(2 NP-BOUNDARY);

SELECT:PlNomCCPlNom (Pl Nom) IF (1 CC LINK *1C (Pl Nom) BARRIER NOT-NPMOD-ACC)(NEGATE 2 Interr);
	#$ Dása gullet Sámi sierrabibliotehka, oahpahushálddašeami doaimmat ja sierra sámi mánáidgárdedoarjagat.


#!! !!And now some rules for adverbs that modify adjectives

SELECT:AdvModA2 Adv IF (0 A LINK NEGATE 0 Inf)(1C A LINK NEGATE *1 N BARRIER NOT-NPMOD);
	## ...jus ii leat čielgasit dárbbašmeahttun dahje geavatlaččat veadjemeahttun.

SELECT:AdvModA3 Adv IF (0 A LINK -1 CC LINK -1C Adv); # not in use?

SELECT:AdvModA4 Adv IF (0 A LINK 1 CC LINK 1 Adv);
	## ...dan muddui go dat lea geavatlaččat ja ruđalaččat vejolaš. # Funker ikke med 1C Ad
	## Maŋŋá soađi lea sámi álbmot rahčan ollu vai dat adnojuvvošii seammá dássásažžan, sihke duođalaččat ja rievttálaččat.

#This rule comes ones more
SELECT:SECr2996b PL3-V IF (NOT *-1 POSITIVE-V BARRIER CS OR CC)((*1 (Pl Nom) BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf) OR (1 ("leat" PrfPrc) LINK *1 (Pl Nom) BARRIER NOT-NPMODADV LINK NOT 0 Adv OR Inf)) (NEGATE 0 ("leat" Imprt Pl2))(NEGATE 0 Inf LINK -1 MOVT)(NEGATE 0 Imprt LINK -1 CC OR BOS OR COMMA OR (":") OR ("de") OR PUNCT);
	## Ja dis leat velá buot oaivvi vuoktačalmmit lohkkojuvvon.
	#$ Son lea čállán lohkameahttun čállosiid sámi historjjás ja sus leat leamaš ollu luohttámušdoaimmat.
	#% Mii doarjut Sámelávdegotti evttohusa oainnu ahte sámegiela berre sáhttit válljet fágan dan sadjái go duiskkagiela gymnásaráhkkaneaddji linnjás nuoraidskuvlla ovccát jagis.

REMOVE:InfCS Inf IF (0 VFIN LINK NOT 1 REAL-V)((-1 CS) OR (-1 BOS OR Rel) OR (-1 COMMA LINK *-1 Rel BARRIER S-BOUNDARY LINK NEGATE *-1 AUX BARRIER REAL-V) OR (*-1 Acc BARRIER NOT-ADV-PCLE LINK *-1 (TV Pl3) BARRIER NOT-NPMOD LINK *-1 Rel BARRIER NOT-ADV-PCLE LINK -1 (N Nom Pl)))(NEGATE -1 go OR ("ahte") LINK NOT -1 BOC OR COMMA) ;
	#$ Go bassat lihtiid dahje málestit siiddas, de sáhttit seammás guldalit radio.
	#$ Organisašuvnna bláđđi man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.
	#$ Áirasat geat vuostálastet eavttuhusa jienastit nu ahte deaddilit boalu "vuostá" ja čuožžilit.


SELECT:InfCSPrt Prt IF (0 Prs LINK NOT 1 REAL-V)(-1 CS LINK -1 Prt) ;
	#$ Buorre lei go bohtet.

SELECT:vfin VFIN IF (NOT *-1 VFIN)(NOT *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 Attr LINK 1 N)(NEGATE -1 Nom LINK *-1 ("leat" PrfPrc))(NEGATE -1 MIELLA + Nom)(NEGATE 0 TRANS-V LINK -1 Acc LINK -1 MIELLA + Nom)(NEGATE -1 Acc LINK -1 BOS LINK 3 EOS OR ("vai"))(NEGATE 0 Inf - ("leat") LINK 1 EOS - ("?")) ;
	#$ Dál lean cealkán dan dudnuide.
	#$ Oahpa goarrut gávtti ja gođđit ránu.
	#% Njuolggadusat meahccegeavaheami rievdamis – rabas gulaskuddan.
	#% Leamaš váttis ipmirdit dan.

SELECT:HeadlinerInf Inf (NOT *-1 VFIN)(NOT 0 ("leat"))(NOT *-1 (Nom Pl1))(1 EOS - ("?")) ;
	#$ Áigemearri váidalusa buktit.
	#$ Vuoigatvuohta journála geahččat.

SELECT:Pl1coord (V Pl1) IF (0 Pl1 OR Inf LINK -1 CC OR ("de") LINK *-1C (V Pl1) BARRIER VFIN OR Inf) ;
	#$ Go bassat lihtiid dahje málestit siiddas, de sáhttit seammás guldalit radio.

REMOVE:ImprtQst Imprt IF (*1 Qst BARRIER REAL-V OR COMMA OR (":") OR (""")) ;
	#$ Girdi mielde go don manat Helssegii?

#" syntax formatting

#!! !!Proper nouns

#!! !!!VERBS

#!! !!Disambiguating verbs - part 1

#!! First ConNeg forms, they are dependent upon Neg verbs. Then Imperative
#!! (with their special syntax), infinitive, and other infinite forms.
#!! Person comes later (in part 2)




#!! !!ConNeg forms

#!! Number following the rule headers below refer to numbers of hit in a 13 053 859 word corpus.

#!! * __ConNegImp__ selects ConNeg Imprt if Neg Imprt to the left. - 4265
SELECT:ConNegImp (Imprt ConNeg) IF (*-1 (Neg Imprt) BARRIER ConNeg OR SV-BOUNDARY);
 	#$ Ale ge jorggit selggiid sutnje gii bivdá dus loanas.

#!! * __PrfPrcConNeg__  to ConNeg Aux after PrfPrc
SELECT:PrfPrcConNeg ConNeg + AUX IF (*-1 PrfPrc BARRIER REAL-V OR SV-BOUNDARY LINK *-1 REAL-NEG BARRIER NOT-ADV-PCLE)(NOT *1 REAL-V BARRIER NOT-ADV-PCLE);
	#$ Ii dal láhppán gal leat.

# Problem 1
#!! * __ConNegIfNeg__ selects Ind ConNeg if Neg Ind to the left. This is the main (and common) ConNeg rule. - 660327
SELECT:ConNegIfNeg ConNeg IF (*-1 REAL-NEG BARRIER ConNeg OR PrfPrc OR SV-BOUNDARY)(NEGATE 0 N OR PrfPrc LINK *1 ConNeg BARRIER NOT-ADV-PCLE)(NEGATE 0 Loc LINK -1 NEGFOC LINK -1 Loc)(NEGATE 0 Inf LINK *-1 REAL-NEG LINK -1 ConNeg LINK -1 REAL-NEG) ;
 	#$ Eai dat dohkke šat maninge.


#!! * __ConNegPrt__ selects Prt if Prt to the left
SELECT:ConNegPrt (ConNeg Prt) IF (0 (ConNeg Prs) LINK -1 REAL-NEG LINK *-1 S-BOUNDARY LINK *-1C Prt) ;
	#$ Marit lei áidna sis gii ii hállan sámegiela.

#!! * __ConNegCondIfNeg__ selects Cond ConNeg if Neg Cond to the left. Less used, obviously. - 0 - homonymi?
SELECT:ConNegCondIfNeg (Cond ConNeg) IF (*-1 REAL-NEG BARRIER ConNeg OR SV-BOUNDARY);
 	#$ Ii go son ollu eanet gárvvohivčče din?

#!! * __ConNegPrfPrc__ selects ConNeg for leat when topicalised PrfPrc between Neg and leat - 713
SELECT:ConNegPrfPrc ConNeg IF (*-1 PrfPrc BARRIER NOT-ADV-PCLE LINK *-1 Neg BARRIER SV-BOUNDARY)(0 ("leat"))(NEGATE *1 PrfPrc BARRIER NOT-ADV-PCLE);
 	#$ Ii dál láhppán gal leat.
	# Need more advanced rule to select ConNeg when there is more
	# material between Neg and ConNeg.

#!! * __ConNegImpCC__ catches the second ConNeg in cases like don't smile or laugh - 0
SELECT:ConNegImpCC (Imprt ConNeg) IF (*-1 ("dahje") + CC BARRIER NOT-ADV LINK *-1C (Imprt ConNeg) BARRIER REAL-V);

#!! * __ConNegIndCC__ catches the second ConNeg in cases like doesn't smile or laugh - 369
SELECT:ConNegIndCC (Ind ConNeg) IF (*-1 ("dahje") + CC BARRIER NOT-ADV LINK *-1C (Ind ConNeg) BARRIER NOT-ADV);
 	#$ Allet nappo moraš ja cealkke...

#!! * __NotConNegII__ removes ConNegII if no Neg Imprt around. This is important, as the homonym forms are common. - 30850
REMOVE:NotConNegII (ConNegII) IF (NEGATE *-1 (Neg Imprt) BARRIER SV-BOUNDARY);
 	#$ Dasgo mii oinniimet su nástti badjáneamen ja leat boahtán...
 	#$ Stáhta galgá fuolahit ahte gávdno divššohasbearráigeahčči juohke fylkkas.


#!! * __NotConNegIfNotNeg__ removes ConNeg if no Neg to the left. Consider unifying with NotConNegNotNeg. - 1094269
REMOVE:NotConNegIfNotNeg ConNeg IF (NEGATE *-1 Neg BARRIER CLB OR Ind)(NEGATE *-1 Nom LINK -1 Neg)(NEGATE *-1 (@CVP) BARRIER NOT-A-ADV LINK *-1 ConNeg BARRIER VFIN)(NEGATE -1 ("ja" CC) OR ("dahje" CC) LINK **-1 ConNeg LINK -1 Neg);
 	#$ Juohke muorra mii ii šaddat buriid šattuid, čullojuvvo ja bálkestuvvo dollii.
 	#$ Gávpot mii lea vári alde, ii sáhte leat čihkosis.
 	#% Bearehan dal son ii geige vel gieđa ja váldde maiddái eallima muoras ja bora ja beasa nu eallit agálaččat.
 	#% Ii dal láhppán gal leat.
	## Jesus celkkii: "Oanehis áiggi lean velá din luhtte."



#!! * __NotConNegNotNeg__ removes remaining ConNegs whenever no Neg to the left. - 5862
REMOVE:NotConNegNotNeg ConNeg IF (NEGATE *-1 Neg BARRIER SV-BOUNDARY OR go)(NEGATE -1 Rel LINK -1 Neg)(NEGATE -2 Rel LINK -1 Neg)(NEGATE *-1 (@CVP) BARRIER NOT-A-ADV LINK *-1 ConNeg BARRIER VFIN)(NEGATE -1 ("ja" CC) OR ("dahje" CC) LINK **-1 ConNeg LINK -1 Neg);
# Look at this and the previous one: they overlap appr 95%, but both catch some cases the other rule does not.






#!! !!Imperative

#!! See also ''Imprt or Ind'' some sections down.

#!! * __PassLNotImprt__ removes Imprt when passive (sentence-initial, hence important)
REMOVE:PassLNotImprt Imprt IF (0 Der/PassL)(*1 Rel or CS);
	#$ Ásahuvvo bargojoavku mas leat sámi mediaid ovddasteaddjit.
	# Perhaps without the Rel/CS condition as well.

#!! * __ImprtLeat__ says BOS Leat A is Imprt - 575
SELECT:ImprtLeat Imprt IF (-1 BOS OR PUNCT OR REAL-CLB)(0 ("leat"))(1 A);
 	#$ Leage buorre!

#!! * __ImprtDál__
SELECT:ImprtDál Imprt IF (-1 BOS OR PUNCT OR REAL-CLB)(1 ("dál"));
 	#$ Gullet dál, maid veardádus gilvi birra mearkkaša...

#!! * __SelImprtExcl__ selects initial Imprt when excl mark
SELECT:SelImprtExcl Imprt IF (-1 BOS OR PUNCT OR REAL-CLB)(*1 ("^excl"));
SELECT:SelImprtExclPl (Imprt Pl2) IF (-2 Nom + Pl)(-1 COMMA)(*1 ("^excl"));
	#$ Mánát, váldet alcceseattet láibevajahasaid!

SELECT:SelImprtExclSg (Imprt Sg2) IF (-2 Nom + Sg)(-1 COMMA)(*1 ("^excl"));

#!! * __ImprtComma__
SELECT:ImprtComma Imprt IF (-1 COMMA LINK -1 Imprt LINK *-1 BOS BARRIER NOT-ADV);

#!! * __ImprtNotVGen__
REMOVE:ImprtNotVGen VGen IF (-1 BOS OR PUNCT OR REAL-CLB)(0 Imprt)(NOT *1C VFIN BARRIER NOT-ADV-PCLE);

#!! * __NotImprtInd__
REMOVE:NotImprtInd Imprt IF (*-1 REAL-NEG BARRIER CLB OR Ind);

#!! * __NotImprtConNeg__
REMOVE:NotImprtConNeg Imprt IF (*-1 ConNeg BARRIER COMMA OR SV-BOUNDARY);
 	#$ Go bohciida soahpameahttunvuohta das ahte galget go journála...
 	#$ Mánát eai gillen dálu čorget.
 	#$ Na muhto don it leat dasa nama čállán!

#!! * __NotImprtA__
REMOVE:NotImprtA Imprt IF (-1 A LINK NOT 0 Ess);

#!! * __NotImprtN__
REMOVE:NotImprtN Imprt IF (-1 N OR Po);
 	#$ Čuorvvas gullo Ramas.
 	#$ Boarrásis leai dat vel eanaš ealáhussan ge.
 # This will work fine when headings no longer interfere.


#!! * __NotImprtVFIN__
REMOVE:NotImprtVFIN Imprt IF (*1C VFIN BARRIER NOT-ADV-PCLE);
 	#$ Lágas ii duššaduvvo oktage bustávaš dahje čuoggáš, ovdal go...


#!! * __NotImprtSlash__
REMOVE:NotImprtSlash Imprt IF (*-1 ("\(") BARRIER ("\)"))(0 N);
 	#$ Láigolihtus eai čuovo makkárge sierravuoigatvuođat gáddái (buo. riehkit, stárat).


# Moving candidates down:
#!! * __NotImprtGo__
REMOVE:NotImprtGo Imprt IF (1 ("go" Pcle));


#!! * bearrat TV or berret IV - berret is aux
SELECT:berret ("berret") (0 ("bearrat") LINK *1 Inf BARRIER REAL-V OR S-BOUNDARY) ;
	#$ Válljedettiin journálaovddasvástideaddji berre árvvoštallat su fágagealbbu.


#!! !!Infinitive


#!! * __r2974__ was moved up to select PL3-V after N Pl, might be relaxed to REMOVE Inf
SELECT:r2974 PL3-V IF (*-1C SUBJECTHEAD + Pl + Nom BARRIER Pl3 OR Du3 LINK *-1 BOC BARRIER Pl3 or Du3) ;
	## Divššohasaid birra geat leat uminddegin dahkkon.
	## Eatnagat bohtet nuortan ja oarjin.
	## Geafit gal leat álo din luhtte.

#!! * __r2976__ was moved up to select PL3-V after N Pl, might be relaxed to REMOVE Inf
SELECT:r2976 PL3-V IF (*1C NP-HEAD-PL-NOM BARRIER PPRON-NOT-SII OR S-BOUNDARY)(NOT *-1 POSITIVE-V BARRIER CS)(NEGATE 0 Inf LINK 1 COMMA LINK 1 Inf) ;
	#$ Ohppiidráđis lea stuora fápmu skuvllas dan dihte go leat nu máŋggas geat hupmet ovttas áššiid birra.
	#$ Dása gullet Sámi sierrabibliotehka, oahpahushálddašeami doaimmat ja sierra sámi mánáidgárdedoarjagat.
	#% Diet vuoigatvuođat leat ráddjejuvvon guoskat dan vuoigatvuhtii ahte oamastit, atnit, ovddidit ja mearridit daid eatnamiid ja riggodagaid mat sis leat odne árbevirolaš čanastumi dihte.

#!! * __r1809__ Not Pl1 (but Inf) if VFIN to the left, This is the basic Inf rule.
REMOVE:r1809 Pl1 IF (*-1 VFIN BARRIER NOT-ADV-PCLE-ILL-LOC-COM OR S-BOUNDARY LINK NOT 0 N)(0 Inf);
#REMOVE:r1809 Pl1 IF (-1 VFIN LINK NOT 0 N)(0 Inf); # old, careful version
 	#$ Bargu lei kánske árvvoštallat dan.

#!! * __r1812__
SELECT:r1812 Inf IF (*-1 NP-HEAD-SG-NOM BARRIER NOT-ADV-PCLE LINK *-1 CC BARRIER NOT-NPMODADV LINK *-1 Inf BARRIER NOT-ADV);
 	#$ Dearvvašvuođaveahkkedárbu galgá árvvoštallojuvvot ja diehtu addojuvvot.
 # This rule has to come before the ones below.


#!! * __InfCompCs__
SELECT:InfCompCs (Inf @COMP-CS<) IF  (NOT 1 Inf);
 	#$ Anán buorebun odne vuolgit go ihttážii vuordit. # For tøff regel
 	#$ Anán buorebun ahte mii vuolgit go ihttážii fertet vuordit. # For tøff regel
# The above rule leans on the following one:
# MAP (@COMP-CS<) TARGET Inf IF (*-1 ("go" CS) BARRIER NOT-ADV LINK -1 Inf)(NEGATE *1 VFIN BARRIER NOT-ADV-PCLE);

#!! * __r1811__
SELECT:r1811 Inf IF (*-1 ("ii" Neg Sup) BARRIER REAL-V);
 	#$ ...amaset sin čalmmit oaidnit,...

# ! * __r1814__
#SELECT:r1814 Inf IF (-1 BOS)(*1 ("ii" V IV Neg Ind Sg3) BARRIER V);
 	#$ Viežžat viervván rivgo čiektageregin ii leat ásti, iige sáváháhttin.

#!! * __EssInf__
SELECT:EssInf Inf IF (*-1 LEAT-FIN-NOT-IMPRT BARRIER NOT-ADV LINK -1 Ess);
	#$ Sin bargun lei váldit fáŋgan Gonagasa.

#!! Rules that prevent later selection of Inf for a finite verb in the frame
SELECT:InfCoord Inf IF (0 VFIN LINK 1 COMMA OR CC LINK 1 Inf LINK 1 COMMA OR CC LINK 1 Inf) ;
	#$ Diet vuoigatvuođat leat ráddjejuvvon guoskat dan vuoigatvuhtii ahte oamastit, atnit, ovddidit ja mearridit daid eatnamiid ja riggodagaid mat sis leat odne árbevirolaš čanastumi dihte.

#!! ''INF-V...CC...''

#!! * __r1816__
REMOVE:r1816 Inf IF (*-1 NP-HEAD-PL-NOM BARRIER NOT-NPMODADV LINK -1 CC)(NEGATE *-1 A BARRIER NOT-ADV-PCLE)(NEGATE *-1 INF-V OR Inf BARRIER REAL-V);

#! * __r1817__
#REMOVE:r1817 Inf IF (*-1 (Num Nom) BARRIER NOT-N-A LINK -1 CC LINK NOT *-1 Inf OR INF-V BARRIER VFIN);
 	#$ ...ja ollugat biehttalit iežaset sámevuođa.
 	#$ ...ja máŋga Davviálbmogiid fakultehta studeantta leat oasi oahpus gazzan Romssa universitehtas.

#!! * __r1818__
REMOVE:r1818 Inf IF (-1 CC LINK NOT *-1 Inf BARRIER S-BOUNDARY);
	#$ Interneahtta leat mediat mat leat guovddážis nuoraidkultuvrras , ja leat danin stuorra hástalussan barggus sihkkarastit.

#!! * __r1819__
SELECT:r1819 N IF (-3 (A Attr))(-2 CC)(-1 PrfPrc)(0 N);
 	#$ Áirasat geat vuostálastet eavttuhusa jienastit nu ahte deaddilit boalu "vuostá" ja čuožžilit.
 	#$ Mearrasápmelaččat ledje áŋgiris ja oahppan bivdit.


	# Going for Pl1 instead of Inf
	# - - - - - - - - - - - - - -

#!! * __r1820__
REMOVE:r1820 Inf IF (-1C Adv LINK *-1 BOS BARRIER NOT-NPMOD);
 	#$ Vulobealde guorahallat fas mo sáhtášii deattuhit dákkár jávohisvuođa.

#!! * __r1821__
REMOVE:r1821 Pl1 IF (-1 BOS)(0 Inf)(NEGATE 0 Qst OR Imprt);


	# Now we select Inf
	# - - - - - - - - -

 # The first rule seems much better without the NOT 0 Adv.

SELECT:XYr1822 Inf IF (*-1 INF-V + V BARRIER SV-BOUNDARY OR Neg)(NOT 0 Adv);
	## Skuvla galggai oahpahit sámemánáide dárogiela.
	## Prošeavtta áigut čađahit.
	#% Dás galget nuorat, čohkut dahje veallut duorggaid alde buolli árrandola ovddas vuostáiváldit oahpahusa sis.



#!! * __r1823__
SELECT:r1823 Inf IF (*-1 INF-V + V BARRIER NP-HEAD-PL-NOM OR SV-BOUNDARY LINK 0 VFIN OR PrfPrc OR Inf LINK NOT 0 N)(NOT 0 ("leat"));
 	#$ Son oažžu vuokkain ja luovosfirpmiin, namalassii golgadagain, bivdit miehtá deanu.

#!! * __r1824__
SELECT:r1824 Inf IF (-2 Inf)(-1 COMMA)(1 COMMA OR CC);
 	#$ Luondduresursaealáhusat sáhttet álggahuvvot, bisuhuvvot ja ovddiduvvot.

	# This rule looks backwards across a relative clause:
#!! * __r1825__
SELECT:r1825 Inf IF (*-1 VFIN BARRIER S-BOUNDARY LINK *-1 (Pron Rel) BARRIER S-BOUNDARY LINK *-1 INF-V BARRIER Inf OR SV-BOUNDARY);
 	#$ ...galgá sihke divššohas ja váhnemat dahje earát geain lea váhnenovddasvástádus oažžut dieđu.

#!! * __r1827__
SELECT:r1827 Inf IF (*-1 (A Nom) BARRIER NOT-ADV OR COMMA LINK *-1 COPULAS BARRIER NOT-ADV-PCLE);

#!! * __r1828__
SELECT:r1828 Inf IF (-1 ADVLCASE LINK *-1 (A Nom) BARRIER NOT-NPMOD LINK *-1 COPULAS BARRIER NOT-ADV-PCLE);
 	#$ Dál lea váttis dadjat juste beaivvi goas váttisvuođat čoavdašuvvet.
 	#$ Danne go mii orrut tunealla lahka, de lea álki isidii vuodjit tunealla go manná girdišilljui.




#!! !!Verbgenitive
#     ------------

# Background:
# VGen is a bad lookalike for various finite forms. It is rare, and occurs in
# special contexts.
# Philosophy:
# 1. We make rules selecting VGen where we feel safe to say we have found it.
# 2. We remove all remaining VGen readings.

# 1. Looking for VGen.

#!! * __VGen__ selects VGen
SELECT:VGen1 VGen IF (*-1 VGEN-V BARRIER Neg OR SV-BOUNDARY LINK 0 MAIN-V)(0 VGEN-V LINK NOT 0 Der/Pass OR Loc OR Inf OR ("mannat"));

#!! * __Gen2__
SELECT:VGen2 VGen IF (-1 ("gaskan"))(NEGATE 0 Der/Pass OR Loc OR Inf);
	## Gaskan bora viehkalin olggos.
#!! * __VGen3__
SELECT:VGen3 VGen IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 VGEN-V LINK NOT 0 Der/Pass OR Loc OR Inf);
	## Muhtumat ledje vácci, ja muhtumat ledje čuoigga.

#!! * __VGen4__
SELECT:VGen4 VGen IF (-2 COPULAS)(-1 Indef OR Adv)(0 VGEN-V LINK NOT 0 Der/Pass OR Loc OR Inf);
	## Dát ađa lea uhccán vuošša.


# 2. Done that, now we remove whatever VGen was left.

#!! * __KillAllVGen__ removes all VGen (r1842)
REMOVE:KillAllVGen VGen IF (0 (VGen));


#!! !!Supinum vs. potential -- no example found in large corpus

#!! !!Perfect Participle

#!! * __r1844__ removes PrfPrc if 0 is the second N in an N and ... N construction
REMOVE:Yr1844 PrfPrc IF (*-1 CC BARRIER NOT-NPMOD LINK -1 N LINK NOT *-1 REAL-COPULAS BARRIER NOT-NPMODADV)(0 N)(NEGATE 0 ("leapma" Der/Dimin))(NEGATE -1 CC LINK *-1 PrfPrc BARRIER REAL-V);
 	## Journála sirdin ja luoikan.
	## ...de lassánii maid sisafárren ja koloniseren dáidda guovlluide.

#!! * __r1844__ removes PrfPrc if 0 is the second N in an N and Gen ... N construction (this is marginal)
REMOVE:Yr1845 PrfPrc IF (*-1 Gen BARRIER NOT-NPMOD LINK *-1 CC BARRIER NOT-NPMOD LINK -1 N LINK NOT *-1 REAL-COPULAS BARRIER NOT-NPMODADV)(0 N)(NEGATE 0 ("leapma" Der/Dimin));
	## Iige njuovvan ja bierggu rádjan leat mihkkege dearvvašvuođaváttisvuođaid.
 # The reason for the specific mention of Gen in this rule is that at this stage,
 # the nominals in question are both Acc and Gen. Since Acc is NOT-NPMOD, it acts as a
 # barrier for the previous rule.

#!! * __PrfPrc_Ess__ removes N Ess if 0 PrfPrc
REMOVE:YPrfPrc_Ess (N Ess) IF (*-1 COPULAS + ConNeg BARRIER NOT-ADV-PCLE LINK *-1 Neg BARRIER NOT-ADV-PCLE)(0 PrfPrc)(NEGATE *1 ConNeg OR PrfPrc BARRIER S-BOUNDARY);
	## Manne son ii leat ohcan dan ođđa virggi?

#!! * __r1852__ selects PrfPrc if copula to the left
SELECT:Yr1852 PrfPrc IF (*-1 REAL-COPULAS BARRIER NOT-ADV-PCLE LINK 0 VFIN LINK NOT 0 PrfPrc)(NEGATE 0 Ess LINK 1 (A Nom) OR PrfPrc)(NEGATE 0 ESS-ADVL-N)(NEGATE 0 Actio LINK 1 PrfPrc LINK 0 MAIN-V LINK NOT 0 VFIN)(NEGATE 1 ("veara"))(NEGATE 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)(NEGATE 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS);
	## Norgga láhkaaddit leat áigá jo suokkardan vuđolaččat kvenaid eamiálbmotstáhtusa gažaldaga.

#!! * __r1853__ selects PrfPrc if Rel to the left which again is linked to copula
SELECT:Yr1853 PrfPrc IF (*-1C Rel LINK -1 Pron OR N LINK *-1 REAL-COPULAS BARRIER NOT-ADV-PCLE)(NEGATE 0 Ess LINK 1 (A Nom) OR PrfPrc)(NEGATE 0 Actio LINK 1 PrfPrc LINK 0 MAIN-V LINK NOT 0 VFIN)(NEGATE 1 ("veara"))(NEGATE 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)(NEGATE 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS);
	## Muhtin biirres leat gávcci 12 áirasis leamaš nissonolbmot, ja eará biirres leat buohkat geat leat válljejuvvon leamaš dievddut . # The relative sentence version.


#!! !Topicalized version
#   - - - - - - - - - -

#!! the following chapter should be possible to unify.

#!! * __r1855__ selects PrfPrc if Nom to the left linked to copula
SELECT:Yr1855 PrfPrc IF (*-1 Nom BARRIER SV-BOUNDARY LINK *-1 REAL-COPULAS BARRIER NOT-ADV-PCLE LINK NOT *-1 Nom BARRIER S-BOUNDARY)(NEGATE 0 Ess LINK 1 (A Nom) OR PrfPrc)(NEGATE 0 Actio LINK 1 PrfPrc LINK 0 MAIN-V LINK NOT 0 VFIN)(NEGATE 1 ("veara"))(NEGATE 1 CC LINK 1 Actio LINK NOT 0 PrfPrc)(NEGATE 0 Actio OR N LINK *1 ("leat" PrfPrc) BARRIER COPULAS)(NEGATE 0 AUX);
	## Muhtin biirres leat gávcci áirasa leamaš nissonolbmot, ja eará biirres leat buohkat geat leat válljejuvvon leamaš dievddut.

#!! * __r1857__ selects PrfPrc if Acc to the left linked to copula
SELECT:Yr1857 PrfPrc IF (*-1 Acc BARRIER NOT-ADV LINK *-1 REAL-COPULAS BARRIER NOT-NPMODADV)(NEGATE 0 Actio LINK -1 PrsPrc)(NEGATE 1 COMMA LINK *1 Actio BARRIER NOT-NPMOD)(NEGATE 0 Actio LINK *1 PrfPrc BARRIER NOT-ADV-PCLE);
	## Leago barggu ohcan?

#!! * __r1858__ selects PrfPrc if NP head to the left linked to copula
SELECT:Yr1858 PrfPrc IF (-1 NP-HEAD-NOM LINK *-1 REAL-COPULAS BARRIER NOT-NP)(NEGATE 0 Actio LINK -1 PrsPrc)(NEGATE 0 Actio LINK 1 CC LINK 1 Actio LINK *1 N BARRIER NOT-NP)(NEGATE 1 ("veara"));
	## Leatgo sii njuovadan juo?

#!! * __r1857__ selects PrfPrc if copula to the left
SELECT:Yr1859 PrfPrc IF (*-1 REAL-COPULAS BARRIER SV-BOUNDARY OR CC LINK NOT 0 PrfPrc LINK 0 VFIN)(NEGATE 0 Actio LINK *1 PrfPrc BARRIER NOT-ADV-PCLE LINK NOT 0 PrfPrc)(NEGATE 0 Ess LINK 1 (A Nom) OR PrfPrc)(NEGATE 0 ESS-ADVL-N)(NEGATE 1 ("dihte") OR ("dihtii"))(NEGATE 1 COMMA OR CC LINK *1 Actio BARRIER NOT-NPMOD)(NEGATE 1 ("veara"));
	## Biret-Elle lea easka skuvllas geargan.

#!! * __r1861__ selects PrfPrc if VFIN to the left
SELECT:Yr1861 PrfPrc IF (*-1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 N OR Actio OR Ess)(NEGATE *-1 CC BARRIER VFIN);
	## Sámediggi sáhttá mearridit meannudit ášši giddejuvvon uvssaid duohken.

 # PrfPrc if there is not a Sg1 to the left, but if there is a CC to
 # the right of a possible Sg1, then we forget about the Sg1.

#!! * __r3576__ selects PrfPrc if Acc to the left linked to activity verb
SELECT:Yr3576 PrfPrc IF (*-1 Acc BARRIER NOT-ADV LINK -1 Acc LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-NPMOD) ;
	## Son logai oappás dan bargan.

#!! * __r1863__ is the mannan vahkku rule
SELECT:Yr1863 PrfPrc IF (0 ("mannat"))(1 TIME);
	## Ii leat boahtán mannan vahkus.
	## Mannan čuohtejagi gaskkamuttos lei stuorra gilvu.


#!! !!Actio
#!! !!Present participle
#!! !!Rules for "orrut" (there are som more rules at #!! *orrut vs. orrot)
#!! !!Rules for "addit" (which is an adjective, but more often a verb)
#!! !!Actio Loc = N Loc

LIST JAHKKIT = "jáhkkit" "sávvat" "vuordit" "beassat" ;

#!! * __ActioLoc__ is an IFF rule, we also need rule for 'leat', like in lea go bieru oastimis
IFF:ActioLoc (Actio Loc) - JAHKKIT IF  (0 (N Loc) LINK *-1 ("ceavzit") OR ("heaitit") OR ("geargat") OR ("váibat") OR ("dolkat") OR ("ádjánit") OR ("beassat") OR ("vajálduhttit") OR ("hehttet") OR ("biehttalit") BARRIER REAL-V OR S-BOUNDARY) ;



#!! !!Actio Gen = Actio Ess
#!! !!Imprt or Ind



#!! * __removeAllImp__
#REMOVE:removeAllImp Imprt ;

#!! !!Nouns or verbs

#!! The rules are no documented yet

#!! * __VFINAttr__
REMOVE:VFINAttr VFIN IF (-1C Attr LINK NOT 0 ("eanet"));
  	#$ Eambbo doaimmat ruovttuin váikkuhit maiddái veahkkebálvalusossodaga.
  	#$ Máhtte-vilbealle duddjui ođđa fatnasa.

#!! * __NPlbuorit__
SELECT:NPlbuorit (N Pl) IF (-1 ("buorre" Pl Nom))(0 (V Ind));
	#$ Das leat buorit lanjat čáppa guovllus.

#!! * __ActioEssNum__
REMOVE:ActioEssNum (Actio Ess) IF  (-1 (Num Gen) OR (Num Nom))(0 (N Gen));
	#$ Odne leat golbma vihaheame.

#!! * __ActEssIfSensationv__
SELECT:ActEssIfSensationv (Actio Ess)  IF (-1 Acc LINK *-1 PERCEPTION-V OR DADJAT BARRIER SV-BOUNDARY);
	#$ Go gulan olbmuid sámásteame, de lea dego doložii máhccat, go mánnán gullen áhči, eagi ja Ristiinna-imi humadeame.

#!! * __NoActorIfSg3__
REMOVE:NoActorIfSg3 Actor IF (0 VFIN + Sg3) ;
	#$ Heasta čuožžu golmma juolggi nalde.
	#$ Dat orru buorre fanas.

#!! !!Demonstrative pronouns, agreement in DP - should it be moved to after verbmappings?
#!! The rules are no documented yet

#!! * __DemAttr__
SELECT:DemAttr $$CASE + (Dem Sg) IF (0 DAT-PRON)(*1 $$CASE + (N Sg) BARRIER NOT-NPMOD OR Sem/Time OR Num LINK NOT 0 VFIN OR ("leat") OR (Actio Ess) OR Po)(NEGATE 0 Acc LINK *-1 STRICT-TRANS-V BARRIER Acc LINK *1 Dem LINK NOT *1 Acc BARRIER NOT-NPMOD)  ;
	#$
	#% Mun muitalin dan vielljasan. # Should not hit this one.

#!! * __IndefAgree__ guhtege goappašat iešguhtege guhte

SELECT:IndefAgree $$CASE IF (0 ("guhtege") OR ("goappašat") OR ("iešguhtege") OR ("guhte"))(*1 $$CASE + N BARRIER NOT-NPMOD OR Sem/Time OR Num LINK NOT 0 VFIN OR ("leat") OR (Actio Ess) OR Po) ;

#!! * __DemCASEPl__
SELECT:DemCASEPl $$CASE + (Dem Pl) IF (0 DAT-PRON)(*1 $$CASE + (N Pl) BARRIER NOT-NPMOD) ;

#!! * __DemCASESg__
SELECT:DemCASESg $$CASE + (Dem Sg) IF (0 DAT-PRON)(1 Gen - Num LINK 1 $$CASE + (N Sg)) ;

#!! * __DemAttrNum__
SELECT:DemAttrNum $$CASE + (Dem Sg) IF (0 DAT-PRON)(1 $$CASE + Num LINK *1 (N Sg) BARRIER NOT-NPMOD OR Sem/Time OR Num LINK NOT 0 VFIN OR ("leat") OR (Actio Ess))(NEGATE 0 Acc LINK *-1 STRICT-TRANS-V BARRIER Acc LINK *1 Dem LINK NOT *1 Acc BARRIER NOT-NPMOD)  ;
	#$ Dovddat go dan vihtta bártni geat leat doppe.

#!! * __DemAcc__
REMOVE:DemAcc (Dem Acc) IF (0 DAT-PRON)(1C ADVLCASE)(NEGATE *-1 TRANS-V BARRIER REAL-V OR COMMA);
#SELECT:DemAcc (Dem Acc) IF (0 DAT-PRON)(1C ADVLCASE - Ill);
	#$ De našunálastáhtat válde daid háldoseaset.
	#Comment: Surely the original REMOVE is a mistake? or concider moving to after verbmappings!

#!! * __DemAttr__
REMOVE:DemAttr (Dem Attr) IF (0 DAT-PRON)(NOT *1 Loc OR Ill OR Com BARRIER NOT-NPMOD);

REMOVE:TESTAttr Attr IF (NEGATE 1 A OR N OR Num OR Pr OR CC OR COMMA OR PrfPrc OR Attr)(NEGATE 1 Actio LINK 1 ("veara") OR Po) ;
	#% Dattetge ii leat muitaluvvon makkárge namahan veara geavahanriiddu birra dálolaččaid ja boazodoalu gaskkas.
	#% Dat ledje muhtin eará dovddus olbmot.


#!! !!!VERB MAPPINGS
LIST TEXT-@>N = ("mearridit") ;


#!! !!Verbs as predicatives (@SPRED>) and (@<OPRED)

#!! !The tags (@SPRED>) and (@<OPRED) target  PrfPrc
#!! The rules are no documented yet

#!! * __spredPrfPrc__ Buressivdniduvvon lehkos (topicalised PrfPrc) -- was r494
MAP:spredPrfPrc (@SPRED>) TARGET PrfPrc (-1 BOC)(0 Der/Pass)(1 ("leat") LINK NOT 1 V)(*2 NP-HEAD-NOM BARRIER NOT-NP);
	#$ Buressivdniduvvon lehkos son guhte boahtá Hearránammii.

#!! * __opredPrfPrc__
MAP:opredPrfPrc (@<OPRED) TARGET PrfPrc (-1 (Refl Acc) LINK *-1 STRICT-TRANS-V BARRIER NOT-NPMOD)(0 Der/Pass LINK NOT 1 (Pl Loc))(NEGATE 0 TEXT-@>N  LINK 1 TEXT OR ABSTR-TEXT) ;
	#$ Seammás orru Alimusriekti Skjerstad-áššis dovdan iežas čadnojuvvon dupmui.

#!! * __opredPrfPrc__
MAP:opredPrfPrc (@<OPRED) TARGET PrfPrc (*-1 COMMA BARRIER NP OR S-BOUNDARY LINK -1 Ess)(1 EOC OR CC) ;
	#$ Dat boahtá dohko ja gávdná dan guorusin, suohpalastojuvvon ja čiŋahuvvon.


#!! !!Passive verbs often have

LIST PASS = Der/PassS Der/PassL "botnjasit" ;

MAP:-FMAINVPrfPrc (@-FMAINV) TARGET PrfPrc + PASS (*-1 REAL-COPULAS + VFIN + Sg3 OR REAL-COPULAS + ConNeg BARRIER NOT-ADV-PCLE LINK NEGATE -1 Nom)(NEGATE *-1C PrfPrc BARRIER REAL-V OR N) (*1 (N Sg Nom) BARRIER NOT-A) ;


#!! !!Verbs as prenominal participles (@>N):

#!! * Some verbs will not be @>N
LIST NOT-@>N = "boahtit" "buollát" "fitnat" "lebbet" "náđđut" "ihtit" "unnut" ;

#!! * __NPrfPrc1__ with 1C N Nom
MAP:>NPrfPrc1 (@>N) TARGET PrfPrc - NOT-@>N (NEGATE *-1 Neg BARRIER REAL-V)(NEGATE -1 COPULAS LINK *-1 Ill BARRIER NOT-A)(NEGATE 0 AUX)(NEGATE 0 TRANS-V LINK *1 Acc BARRIER NOT-NPMOD)(NEGATE 0 MOVEMENT-V LINK *1 Ill OR DOHKO OR ("lusa") BARRIER NOT-NPMOD)(NEGATE 0 PrfPrc LINK NOT 0 Der/Pass LINK 1 Attr)(*1 (N Nom) BARRIER NOT-A LINK NEGATE 0 TITLE LINK 1 FIRSTNAME OR Sem/Sur);
	#$ Son lea oahppan olmmoš.
	#$ .. maid almmolaš dohkkehuvvon dearvvašvuođabargit addet, ..
	#$ Mearrasápmelaččat ledje áŋgiris ja oahppan bivdit.
	#$ Sus leat hervejuvvon gálssohat main heaŋgájit čáppa diehpit.
  	#$ Sieiddi bálvalemiin sturron eallu gohčoduvvui bálvvoseallun, mii sáhtii jávkat, go oamasteaddji jámii dahje go sieidi ii bálvaluvvon.
	#$ Ruonáeana lea čielgasit ráddjejuvvon geográfalaš guovlu.


#!! * __NPrfPrc2__ with -1C Dem or Num or Attr or Indef
MAP:>NPrfPrc2 (@>N) TARGET PrfPrc - NOT-@>N (*-1C Dem OR Num OR Attr OR Indef BARRIER NOT-A)(NEGATE 0 AUX)(*1 N BARRIER NOT-A);
	#$ Galgá leat okta čohkkejuvvon divššohasjournála.

#!! * __NPrfPrc3__ with PrfPrc or ConNeg to the left, the N can be different cases
MAP:>NPrfPrc3 (@>N) TARGET PrfPrc - NOT-@>N (-1 PrfPrc LINK *-1 ("leat") OR Neg BARRIER REAL-V OR S-BOUNDARY)(NEGATE 0 AUX)(1 N - Ess);
	#$ ...dan mii lei dáhpáhuvvan bosttahuvvon olbmuide.
	#$ Ránnjá Ole Vuolab imašta manne eai leat čorgen eaige čázi ge gidden buollán viesus.
	#$ Ii lean boahtán mannan vahkus.

#!! * __NPrfPrc4__ mannat in front of TIME
MAP:>NPrfPrc4 (@>N) TARGET PrfPrc (0 ("mannat") OR ("vássit"))(1 TIME);
	#$ Dalle šattašii liikka buoret dálki go mannan sotnabeaivve.
	#$ Mu oambealle lei dáppe mannan čavčča maid.

#!! * __NPrfPrcPr__ after Pr
MAP:>NPrfPrcPr (@>N) TARGET PrfPrc - NOT-@>N (*-1 Pr BARRIER NOT-NP LINK -1 NP-BOUNDARY LINK NOT 0 ("leat"))(NEGATE 0 AUX)(1 (N Gen));
	#$ 1978 láhka balansere gaskal sámi vuoigatvuođaid boarásmuvvan oainnu,...

#!! * __NPrfPrcPo__ before Po
MAP:>NPrfPrcPo (@>N) TARGET PrfPrc - NOT-@>N (1 (N Gen) LINK 1 Po)(NEGATE 0 AUX)(NEGATE 0 MOVEMENT-V LINK *1 Ill OR DOHKO OR ("lusa") BARRIER REAL-V) ;
	#$ Áššiin meahcceeatnamiid nuppástuvvan geavaheami birra galget stáhtalaš, fylkkagielddalaš ja gielddalaš eiseválddit árvvoštallat, mo nuppástupmi váikkuha sámi kultuvrii, boazodollui, meahcásteapmái, ealáhusdoaimmaheapmái ja servodateallimii.

#!! * __NPrfPrcGen__ after Gen
MAP:>NPrfPrcGen (@>N) TARGET PrfPrc - NOT-@>N (-1 Gen LINK *-1 REAL-V - COPULAS BARRIER S-BOUNDARY)(NEGATE 0 AUX)(1 N - ADVLCASE)(NEGATE 0 TRANS-V LINK 1 Acc) ;
	#$ Juos ášši gusto restii Finnmárkku meahcceeatnamiid nuppástuvvan geavaheamis, de ii searvit Sámedikki válljen maŋimuš stivrralahttu gii ii ovddas boazodoalu.


#!! * __NPrfPrc6__ the verb can be to the right
MAP:>NPrfPrc6 (@>N) TARGET PrfPrc - NOT-@>N (*0 VFIN BARRIER S-BOUNDARY)(NEGATE 0 AUX)(*1 N - TIME - Prop BARRIER NOT-A LINK NEGATE 1 TIME - Sem/Date)(NEGATE *-1 COPULAS OR Neg BARRIER Nom OR S-BOUNDARY OR REAL-V)(NEGATE 0 TRANS-V LINK *1 Acc BARRIER NOT-NPMODADV LINK *-1 PrfPrc LINK *-1 COPULAS OR Neg BARRIER S-BOUNDARY OR REAL-V)(NEGATE 0 TRANS-V LINK 1 Ess LINK *1 Acc BARRIER NOT-NPMODADV LINK *-1 PrfPrc LINK *-1 COPULAS OR Neg BARRIER S-BOUNDARY OR REAL-V)(NOT 1 PUNCT)(NEGATE 0 TRANS-V LINK *1 Acc BARRIER NOT-A)(NEGATE 0 PrfPrc LINK NOT 0 Der/Pass LINK 1 Attr)(NEGATE -1 CC LINK *-1 PrfPrc BARRIER SV-BOUNDARY LINK -1 COPULAS) ;
	#$ Ovdamearkan oaivvilduvvon fálaldahkii lea sámi kulturdieđu lohkanbaji.
	#$ Sus lea ovddasvástádus gárrenávnnasfuolaheami spesialiserejuvvon osiin.
	#$ Son deaivvai náitalan nissona.
	#$ Seammago ovdalis namahuvvon riektehistorjjálaš ovdanbuktimis leat dán barggus maid muhtun buhtes historjjálaš dilit, ja almmuhuvvo danne joavkku čielggadeami mielddusin (2. mielddus).
	#$ Divššohasas, gii dárbbaša guhkit áiggi ja oktiiheivehuvvon dearvvašvuođabálvalusaid, ..
	#% Vuosttas čuokkis ii gusto daid opmodagaid luobaheapmái mat leat čuldojuvvon almmolaš juohkogávppi bokte ja mat leat biddjojuvvon huksenguovlun plánain plána- ja huksenlága mielde, dahje čuldojuvvon duktasajiide maidda lea huksejuvvon.

#!! * __NPrfPrc7__ Der/Pass, no TIME to the right
#MAP:>NPrfPrc7 (@>N) TARGET PrfPrc - NOT-@>N IF (*-1 S-BOUNDARY BARRIER NOT-NPMODADV)(0 Der/Pass)(*1 N - TIME BARRIER NOT-A);

#!! * __NPrfPrcCoor__ coordination
MAP:>NPrfPrcCoor (@>N) TARGET PrfPrc - NOT-@>N (1 CC LINK 1 PrfPrc - NOT-@>N LINK *1 N BARRIER NOT-NPMOD) ;
	#$ Ja de lei álgit fas álggus, bráhkáin ja eará gaskaboddosaš lanjain dahje báhcán ja mieskan ja bombejuvvon skuvlavisttiin.

#!! !(@+FAUXV) and (@+FMAINV) target Neg, orrut
#!! * __+FAUXVNeg__
MAP:+FAUXVNeg (@+FAUXV) TARGET Neg ;
	#$ In.

#!! * __+FMAINVorrut__ finite orrut
MAP:+FMAINVorrut (@+FMAINV) TARGET ("orrut") + VFIN (NEGATE *1 PrfPrc OR (Actio Ess) BARRIER SV-BOUNDARY)(NEGATE 0 PrfPrc LINK *-1 COPULAS BARRIER SV-BOUNDARY) ;
	#$ Sápmelaččat guđet orrot Norgga bealde ...
	#$ Dat orru buorre fanas.
	#$ Mun orun stuorra vilges viesus.
	#$ Oro jaska!

#!! * __FAUXVorrut__ finite orrut
MAP:+FAUXVorrut (@+FAUXV) TARGET ("orrut") + VFIN-NOT-IMPRT (*1 PrfPrc OR (Actio Ess) BARRIER REAL-V OR S-BOUNDARY) ;
	#$ Nieiddat orro váibagoahtán.
	#$ Mánás orru leamen borranmiella.

#!! * __FAUXVorrut__ infinite orrut
MAP:-FAUXVorrut (@-FAUXV) TARGET ("orrut") - VFIN (*1 PrfPrc OR (Actio Ess) BARRIER NOT-ADV-PCLE) ;
	#$ Mánás ii oro leamen borranmiella.

#!! !(@<SUBJ)  target Inf

#!!	* __SUBJInf__ infinitive
#	'lea suohtas oahppat'
MAP:<SUBJInf1 (@<SUBJ) TARGET Inf (NEGATE 0 AUX LINK 1 Inf)(*-1 A BARRIER WORD - Ill - Adv LINK *-1 COPULAS BARRIER NOT-ADV LINK NOT *-1 Nom BARRIER S-BOUNDARY);
	#$ Mu mielas lea suohtas oahppat sámegiela.
	#$ Ja ii leatge buorre sutnje orrut suhkkes vuvddiid siste, gos lea liegga ilbmi.
	#$ Danne go mii orrut tunealla lahka, de lea álki isidii vuodjit tunealla go manná girdišilljui.
MAP:<SUBJInf1b (@<SUBJ) TARGET Inf (NEGATE 0 AUX LINK 1 Inf)(*-1 A BARRIER WORD - Ill - Adv LINK -1 Adv LINK -1 COPULAS LINK NOT *-1 Nom BARRIER S-BOUNDARY);
	#$ Dárbbašlaš lea dikšut jávrriid ovdal go lea ekonomalaččat veara bivdit.

MAP:<SUBJInf1 (@<SUBJ) TARGET Inf (NEGATE 0 AUX LINK 1 Inf)(-1 Acc - MIELLA LINK *-1 (A Nom) BARRIER WORD - Ill - Adv LINK *-1 COPULAS BARRIER NOT-ADV LINK NOT *-1 Nom BARRIER S-BOUNDARY);
	#$ Raporttas galgá gozihanlávdegoddi čilget gozihandoaimmas, árvvoštallat stivrra jahkemuitalusa ja dilálašvuođaid maid dat siskkilda, ja árvvoštallat mo dát láhka doaibmá ja leago sávahahtti dan nuppástuhttit.


#	álki lea vánddardit
#!! * __<SUBJInf2__
MAP:<SUBJInf2 (@<SUBJ) TARGET Inf (NEGATE 0 AUX LINK 1 Inf)((*-1 ("leat") BARRIER NOT-ADV-PCLE LINK -1 A LINK *-1 BOC BARRIER NOT-ADV) OR (-1 Adv LINK -1 ("leat") LINK -1 A LINK *-1 BOC BARRIER NOT-ADV));
	#$ Vearrámus lea go nu váttis lea oažžut eret dan gova, lohká nisu, gii ii hálit namas aviisii.
	#$ Man álki livččii de vánddardit. # her  'de' brukt som Pcle

#!! * __r354__
MAP:r354 (@<SUBJ) TARGET Inf (NEGATE 0 AUX LINK 1 Inf)(-1 ADVLCASE LINK *-1 A BARRIER NOT-ADV LINK *-1 COPULAS BARRIER NOT-ADV LINK NOT *-1 Nom BARRIER S-BOUNDARY);

#	govt lei vuodjit
#!! * __<SUBJInf3__
MAP:<SUBJInf3 (@<SUBJ) TARGET Inf (-1 COPULAS LINK -1 Adv LINK -1 BOS)(NEGATE *1 REAL-V) ;
	#$ Govt lei vuodjit Olmmáivákkis deike?

#	lea suohtas beassat
#!! * __<SUBJInf4__
MAP:<SUBJInf4 (@<SUBJ) TARGET Inf (NEGATE -1 AUX)(*-1 A BARRIER NOT-ADV LINK *-1 COPULAS BARRIER NOT-ADV LINK NOT *-1 Nom BARRIER S-BOUNDARY);
	#$ Mu mielas lea suohtas beassat johtit riikkas riikii ja báikkis báikái.

#	mii ávkkiid lea min doppe johtit
#!! * __<SUBJInf5__
MAP:<SUBJInf5 (@<SUBJ) TARGET Inf (*-1 ("leat") BARRIER AUX LINK -1 (N Pl Acc) LINK -1 ("mii") OR ("gii") LINK 0 (Interr Sg Nom) LINK *-1 BOC BARRIER WORD);
	#$ Mii ávkkiid lea min doppe johtit?

#	bargu lei váldit
#!! * __<SUBJInf6__
MAP:<SUBJInf6 (@<SUBJ) TARGET Inf ((*-1 COPULAS BARRIER NOT-ADV-PCLE LINK -1 Ess LINK *-1 BOS BARRIER V) OR (-1 Ess - Actio LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 BOC OR COMMA BARRIER NOT-ADV)) ; # (NEGATE *1 REAL-V) ;
	#$ Sin bargun lei váldit fáŋgan Gonagasa.

#	vuolgit lea suohtas
#!! * __SUBJ>Inf__
MAP:SUBJ>Inf (@SUBJ>) TARGET Inf (1 COPULAS + VFIN)(*2 A BARRIER NOT-ADV);
	#$ Vuolgit lea suohtas.

#!! !(@<SPRED)  target Inf

#!!	* __SPREDInf__ infinitive, oaidnit dego
MAP:<SPREDoaidnit (@<SPRED) Inf (0 ("oaidnit") LINK *-1 ("leat") BARRIER NOT-ADV-PCLE LINK *-1 (N Nom) BARRIER WORD - ADVLCASE - Adv)(*1 ("dego") OR go BARRIER SV-BOUNDARY)  ;
	#$ Njunni lei oaidnit dego unna buđehaš.

#!!	* __SUBJInf__ infinitive
#	ulbmil lea oahppat
MAP:<SUBJInf (@<SUBJ) TARGET Inf (*-1 ("leat") BARRIER NOT-ADV-PCLE LINK *-1 (N Nom) BARRIER WORD - ADVLCASE - Adv)  ;
	#$ Gurssa ulbmil lea oahppat goarrut Ivgu gávtti.
	#$ Dan ulbmil das lea nannet oktavuođa ja mearridit vejolaš ovttasbargosurggiid mat leat ealáhusa oasálaččaide ávkin.

MAP:<SUBJInfcoor1 (@<SUBJ) TARGET Inf (0 ("oažžut") LINK -1 CC OR COMMA LINK *-1 Inf LINK *-1 ("oažžut" @<SUBJ))(*1 Inf)  ;
	#$ Dávjá váhnemat eai dušše gallestallan mánáid, muhto oahpaheaddji, ee. lei earánin oažžut su dulkot eiseválddiid breavaid ja oažžut veahki vástidit dáid.
MAP:<SUBJInfcoor2 (@<SUBJ) TARGET Inf (-1 CC OR COMMA LINK *-1 (Inf @<SUBJ) BARRIER SV-BOUNDARY)  ;
	#$ Ulbmil čoahkkimiiguin lea oažžut buoret oktavuođa EU:i, ovddidit sámepolitihka luondduvalljodatgeavaheami, davviguovlluid ovdánahttinplánaid, Davviriikkalaš sámekonvenšuvnna ja ruoššabeale sámiid dilálašvuođa ja riektadili ektui.



#!! !(@<ADVL)  target Inf, Actio Ess

#!!	* __ADVLActioEss__ Actio Ess
#	fitnat doppe oahppamen
MAP:<ADVLActioEss (@<ADVL) TARGET (Actio Ess)(*-1 ("fitnat") OR ("mannat") BARRIER S-BOUNDARY OR MAIN-V OR Nom);
	#$ Skuvlavázzit leat fitnan Sihčajávrris oahppamen mot sáhttá birget luonddus.

#!!	* __ADVLInf__ Inf
#	vulggii baikit
MAP:<ADVLInf (@<ADVL) TARGET Inf - ("leat") (*-1 MOVEMENT-V - AUX OR ("atnit") OR ("geavahit") OR ("bidjat") OR ("movttiidahttit") OR ("guođđit") BARRIER S-BOUNDARY OR COMMA OR REAL-V OR Ess LINK NOT 0 OLLU OR AUX OR SUBJ OR OBJ)(NEGATE -1 CC)(NEGATE 0 AUX OR PERCEPTION-V OR ("diehtit") LINK *-1 ("boahtit") BARRIER NOT-ADV-PCLE)(NEGATE -1 ("boahtit"));
	#$ Stálus bođii baikahoahppu, ja son guđii seahka ja vulggii baikit.
	#$ Boađe mu lusa geahččat ođđa govaid!
	#$ Boađe sisa gáfe juhkat.
	#$ Viečča vieljat boradit!

#!!	* __ADVLInf__ Inf
#	vulggii baikit
MAP:-f<ADVLInf (@-F<ADVL) TARGET Inf - ("leat") (*-1 MOVEMENT-V - AUX OR ("atnit") OR ("geavahit") OR ("bidjat") OR ("movttiidahttit") OR ("guođđit") BARRIER S-BOUNDARY OR COMMA OR REAL-V OR Ess LINK 0 SUBJ OR OBJ)(NEGATE -1 CC)(NEGATE 0 AUX OR PERCEPTION-V OR ("diehtit") LINK *-1 ("boahtit") BARRIER NOT-ADV-PCLE)(NEGATE -1 ("boahtit"));
	#$ Lea erenoamáš dehálaš movttiidahttit sámegielat doaktáriid bargat gielddain.


#!!	* __ADVLInf__ Inf in questions
MAP:<ADVLInf (@<ADVL) TARGET Inf (-1 Nom LINK *-1 MOVEMENT-V BARRIER NOT-PCLE LINK -1 BOC)(NEGATE 0 ("leat")) ;
	#$ Manaigo Biret-Elle viežžat boastta?

#!!	* __r360x__
MAP:r360x (@<ADVL) TARGET Inf IF (-1 ADVLCASE LINK NOT 0 NP-HEAD-NOM LINK *-1 NOT-AUX-V BARRIER NOT-NPMOD LINK NOT 0 TRANS-V)(NEGATE 0 ("leat"))(*-2 VFIN BARRIER S-BOUNDARY);

MAP:veahkkiF<ADVL (@-F<ADVL) TARGET Inf (-1 ("veahkki" Acc) LINK -1 (TV Inf @<SUBJ)) ;


MAP:veahkki<ADVL (@<ADVL) TARGET Inf IF (-1 ("veahkki" Acc) LINK *-1 NOT-AUX-V + TRANS-V BARRIER NOT-NPMOD)(NEGATE 0 ("leat")) ;
	#$ Dat lei earánin oažžut dulkot eiseválddiid breavaid ja oažžut veahki vástidit dáid.

#	lea dálki vuolgit
#!!	* __<ADVLInf__
MAP:<ADVLInf (@<ADVL) TARGET Inf (-1 (N Nom) LINK NOT 0 GEN-CONCEPT OR MIELLA OR (A Nom) OR Inf OR (Der/NomAct) OR REAL-V OR Px + Nom LINK *-1 ("leat") BARRIER NOT-NPMOD)(NEGATE 0 ("leat")) ;
	#$ Odne lea goalki, nu ahte lea buorre dálki vuolgit fávlái.
	#$ Dán golbmasis ii leat ruhta oastit bileahta.
	#$ Mus lea biergu vuoššat.
	#$ Mus livčče guolit viežžat.  # Nickel s. 471

#!!	* __InfdAdvl__
MAP:InfdAdvl (@<ADVL) TARGET Inf (-1 (N Nom) LINK NOT 0 GEN-CONCEPT OR MIELLA OR (A Nom) OR Inf OR (Der/NomAct) OR REAL-V OR Px + Nom LINK *-1 CC BARRIER NOT-NPMODADV LINK -1 (N Nom) LINK *-1 COPULAS BARRIER NOT-NPMOD)(NEGATE 0 ("leat")) ;

#!!	* __InfADVL__
MAP:InfADVL (@<ADVL) TARGET Inf (-1 (N Nom) LINK NOT 0 GEN-CONCEPT OR MIELLA OR (A Nom) OR Inf OR (Der/NomAct) OR REAL-V OR Px + Nom LINK *-1 ("leat") BARRIER NOT-NPMOD)(0 TRANS-V LINK NOT *1 Acc BARRIER NOT-NPMOD)(NEGATE 0 ("leat")) ;

#!!	* __ADVLInf__ Inf
MAP:F<ADVLInfReksj1 (@-F<ADVL) TARGET Inf ((*-1 REKS-V BARRIER NOT-ADV LINK NOT 0 PrfPrc LINK -1 COPULAS) OR  (0 TRANS-V LINK -1 Acc LINK *-1 REKS-V BARRIER NOT-ADV LINK NOT 0 PrfPrc LINK -1 COPULAS)) ;


#!! !@-F<OBJ  target Inf

#!!	* __FOBJInf__ Inf
MAP:-F<OBJInf (@-F<OBJ) TARGET Inf ((-1 TRANS-V + Inf LINK NOT 0 AUX LINK -1 ("leat")) OR (-1 TRANS-V + Inf LINK NOT 0 AUX LINK -1 MIELLA LINK *-1 ("leat") BARRIER NOT-ADV-PCLE) OR (-1 Acc LINK -1 TRANS-V + Inf LINK NOT 0 AUX LINK -1 ("leat") LINK -1 MIELLA OR GEN-CONCEPT)) ;
	#$ Gurssa ulbmil lea oahppat goarrut Ivgu gávtti.
	#$ Ulbmil lea oažžut nuppi boagustit.

MAP:-F<OBJInf (@-F<OBJ) TARGET Inf ((-1 TRANS-V + Inf LINK 0 SUBJ OR SPRED) OR (-1 Acc LINK -1 TRANS-V + Inf LINK 0 SUBJ OR SPRED)) ;

	#$ Giellatekno bargá dahkat dákkár prográmmaid olámuddui sámegielaide, ja maiddái dahkat álkibun eará vehádatgielaide ráhkadit dákkár prográmmaid.


#!! !(@A<)  target Inf
#!! * __AInf__ Inf
MAP:A<Inf (@A<) TARGET Inf ((*-1 (A Nom) BARRIER NOT-ADV LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 NP-HEAD-NOM BARRIER S-BOUNDARY) OR (*-1 ("vejolaš") OR ("lossat") OR ("álki") OR ("buorre") BARRIER NOT-ADV LINK *-1 COPULAS BARRIER VFIN OR FMAINV LINK *-1 HAB-ACTOR BARRIER SV-BOUNDARY OR N)) ;
	#$ Dološ olbmot ledje hui čeahpit dulkot nieguid.

#!! * __r368__
MAP:r368 (@A<) TARGET Inf (-1 NP-HEAD-ACC LINK -1 (A Nom) LINK *-1 ("leat") BARRIER NOT-ADV-PCLE LINK *-1 NP-HEAD-NOM BARRIER VFIN-NOT-AUX OR S-BOUNDARY);

#!! !(@N<)  target Inf, Actio Ess

#!!	* __NInf__ Inf
MAP:N<Inf1 (@N<) TARGET Inf (-1 MIELLA + Nom LINK NOT 0 PrfPrc LINK *-1 ("leat") BARRIER VFIN OR FMAINV LINK *-1 NP-HEAD-NOM OR PRESENT-MARKER OR PAST-MARKER OR Loc OR S-BOUNDARY OR BOS BARRIER VFIN-NOT-AUX OR S-BOUNDARY)(NEGATE 0 TRANS-V LINK -1 NP-HEAD-NOM LINK NOT 0 GEN-CONCEPT LINK *-1 COPULAS BARRIER NOT-NPMOD LINK -1 Loc LINK 0 HUMAN OR Pers LINK *-1 BOS BARRIER NOT-NPMOD);
	#$ Dál lea Bireha vuorru geahččat sávzzaid.
	#$ Sis lea nana árbevierru veahkehit ieš iežas persovnnalaš fierpmádagaid vehkiin.
	#$ Lea geatnegasvuohta ovddidit buot mánáid beroštumiid servodagas.

#!!	* __N<Inf2__
MAP:N<Inf2 (@N<) TARGET Inf (-1 NP-HEAD-ACC LINK -1 MIELLA + Nom LINK NOT 0 REAL-V LINK *-1 ("leat") BARRIER REAL-V LINK *-1 NP-HEAD-NOM OR PRESENT-MARKER OR PAST-MARKER OR Loc BARRIER VFIN-NOT-AUX OR S-BOUNDARY);
	#$ Dus ii leat lohpi dan dahkat!

#!!	* __N<Inf2Gen__
MAP:N<Inf2Gen (@N<) TARGET Inf (-1 NP-HEAD-ACC LINK -1 Gen LINK -1 MIELLA + Nom LINK NOT 0 REAL-V LINK *-1 ("leat") BARRIER REAL-V LINK *-1 NP-HEAD-NOM OR PRESENT-MARKER OR PAST-MARKER OR Loc BARRIER VFIN-NOT-AUX OR S-BOUNDARY);
	#$ Divššohasas lea vuoigatvuohta iežas journála geahččat oktan mildosiiguin.

#!!	* __N<Inf3__
MAP:N<Inf3 (@N<) TARGET Inf (-1 MIELLA + Acc LINK *-1 TRANS-V BARRIER Acc OR REAL-V OR SV-BOUNDARY LINK NOT 0 AUX) ;
	#$ Suohkanat addet váhnemiidda vejolašvuođa bidjat mánáideaset dakkár skuvllaide.
	#$ Máŋgga sámi guovllus eiseválddit eai váldán ovddasvástádusa lágidit bálvalusaid.

#!!	* __N<Inf4__
MAP:N<Inf4 (@N<) TARGET Inf (-1 MIELLA + Nom LINK -1 BOS) ;
	#$ Riekti oassálastit luondduriggodagaid hálddašeamis sáhttá čađahuvvot dakko bokte ahte Sámediggi nammada áirasiid oktasaš stivrenorgánaide, nu movt Finnmárkoopmodahkii ja guovllu boraspirelávdegottiide.

#!! * __N<Infcoor__
MAP:N<Infcoor (@N<) TARGET Inf (-1 ("ja") OR ("dahje") LINK *-1 (@N<) BARRIER REAL-V) ;
	#$ Dus lea vuoigatvuohta oažžut oahpahusa mearriduvvon oahppoplána mielde, muhto dus lea seammás geatnegasvuohta oassálastit aktiivvalaččat ulbmiliid juksamii ja oassálastit iežat oahppanbarggu plánemii ja árvvoštallamii.


#!! !(@<ADVL)  target Inf, Actio Ess

#!!	* __ADVLInf__ Inf
MAP:<ADVLInfReksj1 (@<ADVL) TARGET Inf ((*-1 REKS-V - ("jáhkkit") BARRIER NOT-ADV) OR (0 TRANS-V LINK -1 Acc LINK *-1 REKS-V - ("jáhkkit") BARRIER NOT-ADV)) ;
	#$ Go beassážiid leat čáppa dálkkit ja beaivvádat, de olbmot liikojit čuoigat meahcis dahje vuodjit mohtorgielkkáin.

#!!	* __<ADVLInfReksj2__
MAP:<ADVLInfReksj2 (@<ADVL) TARGET Inf (-1 CC LINK *-1 Inf BARRIER REAL-V LINK *-1 REKS-V BARRIER NOT-ADV LINK *-1 Nom BARRIER S-BOUNDARY);
	#$ Go beassážiid leat čáppa dálkkit ja beaivvádat, de olbmot liikojit čuoigat meahcis dahje vuodjit mohtorgielkkáin.

#!!	* __<ADVLInfReksj3__
MAP:<ADVLInfReksj3 (@<ADVL) TARGET Inf (-1 Acc LINK *-1 REKS-V BARRIER NOT-NPMOD) ;
	#$ Son lea dakkár mii ii beroš lága čuovvut!

#!! * __ADVLActioEss__ Inf
MAP:<ADVLActioEss (@<ADVL) TARGET (Actio Ess)(*-1 ("oidnot") OR ("gullot") BARRIER NOT-ADV-PCLE) ;
	#$ Olbmáguovttos jurddašišgođiiga: - Movt dat galgá leat go ii oidno goassege máksimen ja vuovdi cealká ahte lea jo máksojuvvon.

#MAP:TEST (@<OBJ) TARGET Inf IF (*-1 VFIN LINK 0 FMAINV) ;

#!! !(@<OBJ)  target Inf, Actio Ess, PrfPrc

#!!	* __OBJInf__ Inf
MAP:<OBJInf1 (@<OBJ) TARGET Inf (*-1 TRANS-V BARRIER SV-BOUNDARY OR AUX OR Acc OR CC OR COMMA LINK NOT 0 AUX OR Adv OR Acc LINK NOT 0C Ger)(NEGATE 0 Pl3 LINK *-1 TRANS-V LINK 0 (Pl Nom))(NEGATE -1 CC LINK -1 Inf LINK -1 AUX)(NEGATE 0 Inf LINK *-1 AUX BARRIER REAL-V) ;
	#$ Mun hálidan oahppat goarrut gávtti.
	#$ Oahpa goarrut gávtti.
	#$ Go ollet Sáhpánvári ala, de muitte čuovvolit bálgá mii manná olgeš guvlui.
	#$ Anán buorebun odne vuolgit go ihttážii vuordit.
	#$ Duosttan go dahkat dan?
	#$ Olbmot dattetge dinejedje veaháš go vuvde dujiid, eaige dárbbašan oastit ávdnasiid duddjomii.
	#$ Oahpa goarrut gávtti ja gođđit ránu.
	#% Áigugo fas skuvlii álgit?


#!!	* __<OBJInf2__
MAP:<OBJInf2 (@<OBJ) TARGET Inf + IV (*-1 HAB-ACTOR + Acc BARRIER REAL-V OR S-BOUNDARY LINK *-1 TRANS-V BARRIER NOT-NPMODADV LINK NOT 0 (A Attr) OR MOD-ASP OR Neg OR COPULAS LINK *-1 Nom)(NEGATE *-1 CC BARRIER VFIN LINK *-1 Inf BARRIER VFIN LINK *-1 AUX BARRIER NOT-ADV-PCLE LINK 0 VFIN)(NEGATE 0 Inf LINK *-1 MOD-ASP OR Neg OR COPULAS BARRIER REAL-V);
	#$ Mánnávuođas son vásihii dulvademiid ja eará luonddubillistemiid mat leat ožžon su čállit divttaid.
	#$ Mun háliidan mánnán boahtit ruoktot!

	# The last constraint is for elliptic phrases.

#!!	* __<OBJInf3__
MAP:<OBJInf3 (@<OBJ) TARGET Inf (*-1 HAB-ACTOR + Acc BARRIER NOT-A-ADV LINK *-1 TRANS-V BARRIER NOT-NPMOD LINK NOT 0 AUX OR (A Attr) LINK 0 VFIN OR Inf)(NEGATE *-1 CC BARRIER VFIN LINK *-1 Inf BARRIER VFIN LINK *-1 AUX BARRIER NOT-ADV-PCLE LINK 0 VFIN);
	#$ Dáhto mánát boahtit ruoktot!

#!!	* __1__ maps <OBJ to Inf if -1 TV etc.
MAP:1 (@<OBJ) TARGET Inf (*-1 TRANS-V BARRIER SV-BOUNDARY OR AUX OR Acc OR COMMA LINK NOT 0 AUX LINK 0 PrfPrc OR ConNeg LINK -1 Neg OR COPULAS) ;

#!!	* __<OBJInf4__
MAP:<OBJInf4 (@<OBJ) TARGET Inf (-1 Acc LINK NOT 0 TIME OR Rel LINK *-1 TRANS-V BARRIER SV-BOUNDARY OR AUX LINK NOT 0 AUX OR Adv OR N LINK NOT 0C Ger OR Imprt)(NEGATE 0 Pl3 LINK *-1 TRANS-V LINK 0 (Pl Nom)) ;
	#$ Eadni oahpaha nieidasis beaskka goarrut.

MAP:<OBJInfAux (@<OBJ) TARGET Inf (-1 Acc LINK NOT 0 TIME OR Rel LINK *-1 TRANS-V BARRIER SV-BOUNDARY OR AUX LINK NOT 0 AUX - ("sihtat") OR Adv OR N LINK NOT 0C Ger OR Imprt)(NEGATE 0 Pl3 LINK *-1 TRANS-V LINK 0 (Pl Nom)) ;
	#$ Soai siđaiga dávjá Liná lohkat jitnosit go sis lei lohkanhárjehallan.

# Coordination
#!!	* __<OBJInfCoord1__
MAP:<OBJInfCoord1 (@<OBJ) TARGET Inf (*-1 CC BARRIER NOT-ADV LINK *-1 Inf BARRIER REAL-V LINK *-1 NOT-AUX-V BARRIER NOT-ADV LINK NOT 0 Imprt OR AUX) ;
# LINK *-1 Nom BARRIER S-BOUNDARY);
	#$ Son boahtá stálljii ja veahkeha čorget ja biebmat heasttaid.
	#$ Mun hálidan oahppat goarrut gávtti ja gođđit ránu.

#MAP:<OBJInfCoord3 (@<OBJ) TARGET Inf (*-1 CC BARRIER NOT-ADV LINK *-1 (Inf @<OBJ) BARRIER REAL-V)(NEGATE 0 VFIN + AUX LINK *1 Inf BARRIER SV-BOUNDARY)  ;
MAP:<OBJInfCoord3 (@<OBJ) TARGET Inf (*-1 CC OR COMMA BARRIER SV-BOUNDARY OR REAL-V LINK *-1 (Inf @<OBJ) BARRIER SV-BOUNDARY OR REAL-V)(NEGATE 0 VFIN + AUX LINK *1 Inf BARRIER SV-BOUNDARY)  ;
	#$ Giellatekno bargá dahkat dákkár prográmmaid olámuddui sámegielaide, ja maiddái dahkat álkibun eará vehádatgielaide ráhkadit dákkár prográmmaid.

	#$ Go skuvla nogai sullii geassemánu 1. beaivve, de barge internáhttaoahpaheaddjit ijat beaivái láhkket ohppiid bevddiid, oljet láhttiid ja rádjat buot skuvlastohpogálvvuid ja dávviriid keallirlatnjii.


#!!	* __<OBJInfCoord2__
MAP:<OBJInfCoord2 (@<OBJ) TARGET Inf (-1 CC LINK *-1 Inf BARRIER REAL-V LINK *-1 NOT-AUX-V BARRIER NOT-ADV LINK 0 Imprt OR AUX LINK *-1 BOC BARRIER WORD);
	#$ Oahpa goarrut gávtti ja gođđit ránu.

#!! * __OBJActioEss__ Inf
MAP:<OBJActioEss1 (@<OBJ) TARGET (Actio Ess) (-1 Acc LINK NOT 0 TIME LINK *-1 TRANS-V + PERCEPTION-V OR DADJAT BARRIER SV-BOUNDARY OR AUX LINK NOT 0 AUX OR Adv OR N LINK NOT 0C Ger OR Imprt);
	#$ De oinnii olbmá čohkkámin tuolloviesus.

#!!	* __<OBJActioEss2__
MAP:<OBJActioEss2 (@<OBJ) TARGET (Actio Ess)(*-1 HAB-ACTOR + Acc OR (Sem/Hum Acc) BARRIER NOT-ADV-PCLE LINK *-1 TRANS-V + PERCEPTION-V OR DADJAT BARRIER SV-BOUNDARY)(NEGATE 0 AUX LINK 1 Inf);
	#$ Oađidettiin gullen olbmo boahtimin.

#!! * __OBJPrfPrc__ PrfPrc
#MAP:<OBJPrfPrc1 (@<OBJ) TARGET PrfPrc IF ((*-1 Acc BARRIER NOT-ADV LINK *-1 VERBAL-ACTIVITY-V OR PERCEPTION-V BARRIER NOT-NPMODADV) OR (*-1 Acc BARRIER NOT-ADV LINK -1 Nom LINK *-1 VERBAL-ACTIVITY-V OR PERCEPTION-V BARRIER NOT-NPMODADV));
MAP:<OBJPrfPrc1 (@<OBJ) TARGET PrfPrc IF ((*-1 Acc BARRIER NOT-ADV LINK *-1 VERBAL-ACTIVITY-V OR PERCEPTION-V BARRIER SV-BOUNDARY) OR (*-1 Acc BARRIER NOT-ADV LINK -1 Nom LINK *-1 VERBAL-ACTIVITY-V OR PERCEPTION-V BARRIER NOT-NPMODADV));
	#$ Sii lohket fatnasa gopmánan.
	#$ Olbmot muitaledje juo du vuolgán.

MAP:<OBJPrfPrcCoor (@<OBJ) TARGET PrfPrc IF (-1 CC LINK -1 (PrfPrc @<OBJ) );


#!!	* __<OBJPrfPrc2__
MAP:<OBJPrfPrc2 (@<OBJ) TARGET PrfPrc IF (*-1 Acc BARRIER NOT-ADV LINK -1 Acc LINK *-1 VERBAL-ACTIVITY-V BARRIER NOT-NPMODADV)(0 TRANS-V);
	#$ Son logai oappás dan bargan.

MAP:<OBJ (@<OBJ) TARGET Inf IF (*-1 CC BARRIER NOT-ADV LINK *-1 (V @<OBJ) BARRIER REAL-V OR SV-BOUNDARY) ;
	#$ Tretnes mielas lea áibbas endorii go Bargiidbellodaga parlamenttalaš jođiheaddji ja nubbinjođiheaddji Helga Pedersen čuoččuhii NRK Sámi Radios Bargiidbellodaga bargan eanemusat sámeáššiiguin ja nu mahkáš leat njunnošis sámeáššiin.



#!! !(@+FMAINV) and (@+FAUXV) and (@-FAUXV)

#!! * __+FMAINVaux__ AUX-OR-MAIN verbs
MAP:+FMAINVaux (@+FMAINV) TARGET VFIN IF (0 AUX-OR-MAIN + TV LINK NOT 0 ("áigut") LINK *1 HAB-ACTOR + Acc BARRIER NP-BOUNDARY LINK 1 Inf + IV) ;
	#$ Mun háliidan mánnán boahtit ruoktot!
	#$ Ruoŧa hállehus háliida min veahkehit. (ii galgga deaivat dan)


#!! * __+FMAINVcop__ COPULAS verbs
MAP:+FMAINVcop (@+FMAINV) TARGET VFIN + COPULAS (*1 Nom OR Ess BARRIER NOT-ADV-PCLE LINK NOT 0 (Actio Ess) LINK 1 Inf);
MAP:+FMAINVcop (@-FMAINV) TARGET PrfPrc + COPULAS (*1 Nom OR Ess BARRIER NOT-ADV-PCLE LINK NOT 0 (Actio Ess) LINK 1 Inf);
	#$ Lei váttis vuodjit odne.
	#$ Leamaš váttis ipmirdit dan.

#!! * __+FAUXVaux__ AUX  verbs
MAP:+FAUXVaux (@+FAUXV) TARGET AUX + VFIN-NOT-IMPRT (NEGATE 0 ("leat"))(*1 Inf BARRIER SV-BOUNDARY OR AUX LINK NOT 0 ADVL OR OBJ)(NEGATE 0 ("šaddat") LINK *1 Nom OR Ess BARRIER NOT-ADV-PCLE) ;
	#$ Máhtte áigu heaitit vuola juhkamis.
	#$ Dalle galget miesit leat merkejuvvon eaiggáda merkii.
	#$ Don šattat vel joksat Sirpmás gohcciid.
	#$ Mu mielas lea suohtas beassat johtit riikkas riikii ja báikkis báikái.

#!! * __+FAUXVboahtit__ boahtit as AUX
MAP:+FAUXVboahtit (@+FAUXV) TARGET ("boahtit") + VFIN-NOT-IMPRT (1 Inf)(NEGATE 0 ("boahtit") LINK *1 Nom OR Ess BARRIER NOT-ADV-PCLE) ;
	#$ Na geasset go bođii diehtit ahte ii beasa skuvlii?

#!! * __-FAUXVaux__ AUX verbs
MAP:-FAUXVaux (@-FAUXV) TARGET AUX + Inf (NOT -1 HAB-ACTOR + Loc)(*-1 AUX BARRIER REAL-V OR S-BOUNDARY)(*1 Inf BARRIER REAL-V OR S-BOUNDARY OR (A Nom) OR MIELLA) ;
MAP:-FAUXVaux (@-FAUXV) TARGET REAL-COPULAS + Inf (NOT -1 HAB-ACTOR + Loc)(*-1 AUX BARRIER REAL-V OR S-BOUNDARY)(*1 PrfPrc - @>N BARRIER REAL-V OR S-BOUNDARY OR (A Nom) OR MIELLA) ;
	#$ Don fertet máhttit čilget niegu veardádusaid, ovdalgo sáhtát dan dieđu ipmirdit.
	#$ Dalle galget miesit leat merkejuvvon eaiggáda merkii.
	#$ Čuollun sáhttá leat dušše okta fierbmi gurgaluvvon.
	#% Min ulbmil lea ahte galgá leat vejolaš eallit mielkebuvttadandietnasiin fylkkas, deattasta Tretnes.

#!! * __+FMAINVcopInfconstr__ leat before Inf
MAP:+FMAINVcopInfconstr (@+FMAINV) TARGET ("leat") + VFIN (NEGATE *-1 PrfPrc OR (Actio Ess) BARRIER SV-BOUNDARY OR COMMA)(*1 Inf BARRIER REAL-V) ;
	#$ Mis lea cahkkehit dola.
	#$ Govt lei vuodjit...
	#$ Gean vuorru lea dál bálkestit birccu?


#  __+FMAINVcopInfconstr2__  leat before Inf
#MAP:+FMAINVcopInfconstr (@+FMAINV) TARGET ("leat") + VFIN (-1 HAB-ACTOR + Loc)(1 Inf LINK *1 Acc BARRIER NOT-NPMOD); # perhaps no need for this?

#!! * __+FMAINVcopMannan__  leat before mannan TIME
MAP:+FMAINVcopMannan (@+FMAINV) TARGET ("leat") + VFIN (*1 ("mannat" PrfPrc) BARRIER SV-BOUNDARY OR REAL-V LINK 1 TIME);
	#$ Mu oambealli lei dáppe mannan čavčča maid.

#!! * __+FMAINVHabconstr__  in habitive constructions
MAP:+FMAINVHabconstr (@+FMAINV) TARGET ("leat" Pl3) OR ("leat" Sg3) (-1 HAB-ACTOR + Loc);
MAP:+FMAINVHabconstr (@-FMAINV) TARGET ("leat" PrfPrc) (-1 HAB-ACTOR + Loc);
	#$ Áhkus leat sávzzat.
	#$ Áhkus leamaš sávzzat.
	#$ Sus leat hervejuvvon gálssohat main heaŋgájit diehpit.
	#$ Mearrasámiinge ledje ovdal moadde dábmojuvvon bohcco maid guođohedje, muhto dat jávke loahpas siseatnama sámiid bohccuid sisa.
	#$ Eŋgelis lei gollemihttosoabbi gávpoga ja dan poarttaid ja muvrraid mihtideami várás .

#!! * __+FMAINVCoopCoord__ coordination
MAP:+FMAINVCoopCoord (@+FMAINV) TARGET ("leat" Du3) OR ("leat" Pl3) (*1 (N Nom) OR (A Nom) OR (Num Nom) BARRIER NOT-NPMODADV LINK *-1 VFIN LINK -1 NP-HEAD-NOM OR HAB-ACTOR + Loc OR TIME OR TIME-MARKER OR DOPPE)(NEGATE *1 N OR A LINK *1 (Actio Ess) OR PrfPrc BARRIER VFIN OR SV-BOUNDARY);
	#$ Nemi ja Pondus leaba sárgojuvvon hámit.

#!! * __+FAUXVleat__
MAP:+FAUXVleat (@+FAUXV) TARGET ("leat") + VFIN-NOT-IMPRT ((*1 PrfPrc - @>N OR (Actio Ess) BARRIER SV-BOUNDARY OR REAL-V LINK NOT -1 Num LINK NEGATE 2 (N Nom) LINK *-1 VFIN LINK -1 Nom OR HAB-ACTOR + Loc) OR (1 ("nu") LINK 1 MOVT))(NEGATE *1 ("mannat" PrfPrc) BARRIER SV-BOUNDARY OR REAL-V LINK 1 TIME);
	#$ Sápmelaččat ledje orron dáin guovlluin 1600-logu birrasiin.
	#$ Mun ledjen borramin.
	#% Ja soames báikkiin lea smávva sieđga, muhto jos eai leat goikesieđggat, de lea headju buollit, muhto go leat goikan sieđggat seahká, de lea buorre buollit.

#!! * __+FMAINVAux1__
MAP:+FMAINVAux1 (@+FMAINV) TARGET VFIN + AUX-OR-MAIN (NEGATE *1 Inf BARRIER S-BOUNDARY);
	#$ Maid Máret áiggui goarrumis?

#!! * __-FMAINVAux2__
MAP:-FMAINVAux2 (@-FMAINV) TARGET ConNeg + ("leat") (*1 (N Nom) BARRIER NOT-NPMOD LINK NOT 0 PrfPrc LINK *-1 REAL-NEG LINK -1 Nom OR HAB-ACTOR + Loc);
	#$ Muhto dus ii leat riekti dearpat su beare dan dihte.

#!! * __+FAUXVCop__  copulas coming after the mainverb
MAP:+FAUXVCop (@+FAUXV) TARGET VFIN-NOT-IMPRT + COPULAS (*-1 PrfPrc OR (Actio Ess) BARRIER NOT-ADV-PCLE LINK NOT 0 SYN-V OR (@>N)) ;
	#$ Ja boahtán ledje su gudnejahttit.

#!! * __+FAUXVCop__  copulas coming before the mainverb
MAP:+FAUXVCop (@+FAUXV) TARGET VFIN-NOT-IMPRT + COPULAS (*1 PrfPrc OR (Actio Ess) BARRIER VFIN OR S-BOUNDARY LINK NOT 0 (@>N) LINK NOT -1 (Num Nom));
	#$ Son lea leamaš oahpaheaddji maŋimuš 15-20 jagi, sihke mánáidskuvllas ja joatkkaskuvllas.

#!! * __+FAUXVboahtit__  boahtit coming before the mainverb
MAP:+FAUXVboahtit (@+FAUXV) TARGET VFIN-NOT-IMPRT + ("boahtit") (NOT 0 Imprt LINK *1 AUX + Inf OR PERCEPTION-V + Inf OR MOVEMENT-V + Inf BARRIER NOT-ADV) ;
	#$ Nuppi bealis lea vejolaš bidjat ráji oarjelii dainna lágiin ahte caggá Sirgása bohccuid beassamis Gjerdalenii, mii boahtá šaddat Hápmirii mávssolaš guohtunguovlun boahtteáiggis.
	#$ Jus dulvvi niegadat, de boađát gullat unohis šlárvaságaid.

#!! * __+FMAINVCop__  copulas
MAP:+FMAINVCop (@+FMAINV) TARGET VFIN + COPULAS (NEGATE *-1 Inf OR PrfPrc OR (Actio Ess) BARRIER VFIN OR S-BOUNDARY)(NEGATE *1 Inf OR PrfPrc - @>N OR (Actio Ess) BARRIER VFIN OR S-BOUNDARY);
	#$ Mun lean Romssas.

#!! * __+FMAINV__  to the remaining finite verbs which are not AUX
MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<)) (NEGATE *0 VFIN-NOT-AUX OR @+FMAINV BARRIER S-BOUNDARY OR COMMA)(NOT *-1 (V @>N) BARRIER NOT-A) (NOT *-1 REAL-V - @>N BARRIER S-BOUNDARY OR COMMA)(NEGATE *1 VFIN OR PrfPrc - @>N BARRIER S-BOUNDARY OR COMMA)(NEGATE 0 Inf LINK -1 CC LINK *-1 Inf BARRIER REAL-V LINK -1 AUX) ;
	#$ Boađe boahtte vahku.
	#$ Son lea oappáin náitalan.
	#$ Mánná čohkána láhttái.
	#$ Son gii oažžu lobi, galgá vuostáiváldit goartta dahje sullasačča, mii čájeha ahte sus lea lohpi.
	#% Mun lean ráhkkásisttán ožžon cummá.
	#$ Ovdal go čielggadan dan lagabui,...

MAP:+FMAINVC (@+FMAINV) TARGET VFIN-NOT-AUX IF (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<)) (0C VFIN) ;

MAP:+FMAINVObjV_a (@+FMAINV) TARGET VFIN-NOT-AUX + TRANS-V (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<) OR Ill)(1 V + OBJ);
MAP:+FMAINVObjV_b (@+FMAINV) TARGET VFIN-NOT-AUX + TRANS-V (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<) OR Ill)(1 Acc LINK 1 TRANS-V + OBJ) ;
	#$ Dii oahppabehtet lohkat ja čállit sámegillii.


#Lene holder på med reglene under her - disse er foreløpige

# Mapping both verbs in a rel bisetning even if no comma
MAP:+FMAINVRel1 (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<) OR Adv) (*-1 Rel BARRIER SV-BOUNDARY LINK NOT *-1 VFIN BARRIER S-BOUNDARY) ;
MAP:+FMAINVRel2 (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 V + OBJ OR V + SUBJ OR V + ADVL OR (V @>N) OR (V @N<) OR Adv OR N) (*-1 FMAINV BARRIER REAL-V LINK *-1 Rel BARRIER SV-BOUNDARY LINK NOT *-1 VFIN) ;
	#$ Áldu mii dálvvi miehtá njamaha miesi gohčoduvvo čearpmateadni.

MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 SYN-V) (*-1 (N Nom) OR (Pron Nom) BARRIER NOT-ADV-PCLE LINK NOT *-1 REAL-V - @>N BARRIER S-BOUNDARY OR COMMA)(NEGATE *1 VFIN OR PrfPrc - @>N BARRIER S-BOUNDARY OR COMMA) ;

MAP:-FMAINV (@-FMAINV) TARGET PrfPrc (NOT 0 SYN-V) (*-1 FAUXV BARRIER REAL-V OR S-BOUNDARY)(NEGATE 0 AUX LINK *1 Inf BARRIER REAL-V OR S-BOUNDARY) ;
	#$ Čuollun sáhttá leat dušše okta fierbmi gurgaluvvon.

MAP:-FMAINV (@-FMAINV) TARGET PrfPrc (NOT 0 SYN-V) (*-1 MOVT BARRIER REAL-V OR S-BOUNDARY LINK -1 ("nu") LINK -1 FAUXV)(NEGATE 0 AUX LINK *1 Inf BARRIER REAL-V OR S-BOUNDARY) ;


MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX + Imprt (NOT 0 SYN-V) (*-1 BOC BARRIER NOT-ADV)(NEGATE 1 Inf LINK NOT -1 INF-V) ;

MAP:+FMAINV (@+FMAINV) TARGET AUX + VFIN (NOT *1 Inf OR PrfPrc - @>N OR (Actio Ess)) ;
	#$ Mearrasápmelaččat ledje áŋgiris ja oahppan bivdit.

MAP:+FMAINV (@+FMAINV) TARGET (V Pl) (NOT *1 Inf OR PrfPrc) ;


MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX + Pl3 (NOT 0 SYN-V)(*1 NP-HEAD-PL-NOM BARRIER NOT-NPMODADV) ;

MAP:+FMAINVQst (@+FMAINV) TARGET VFIN-NOT-AUX (NOT 0 SYN-V)(*-1 BOS BARRIER WORD)(1 go + Qst) ;


MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX + Sg3 (NOT 0 SYN-V)(NOT *-1 REAL-V - @>N BARRIER S-BOUNDARY OR COMMA)(*1 NP-HEAD-SG-NOM BARRIER NOT-NPMOD-ACC-ADV LINK NOT 1 VFIN + Sg3) ;
	#$ Loahpas jearai Johttisámelisttu Anders Somby jr. sudnos njuolga háliida go čearru gulahallat, juo dahje ii.
	#% Ádjá viessu lea guokte miilla bajábeal márkana.


MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-AUX + 1-2-V OR VFIN-NOT-AUX + Sg3 (NOT 0 SYN-V)(*-1 BOC BARRIER REAL-V)(NEGATE 0 N LINK -1 Dem)(NEGATE 0 N LINK *-1 COMMA OR @CNP BARRIER NOT-A-ADV LINK -1 N)(NEGATE 0 Pl LINK *1 VFIN + Pl3 BARRIER REAL-V OR S-BOUNDARY OR COMMA)(NEGATE *1 VFIN OR PrfPrc - @>N BARRIER S-BOUNDARY OR COMMA)(NEGATE 0 Inf LINK -1 CC LINK *-1 Inf BARRIER REAL-V LINK -1 AUX)  ;


#!! !(@-FMAINV) and (@-FAUXV)

#!! * __-FAUXVConNegCop__  to ConNeg COPULAS
MAP:-FAUXVConNegCop (@-FAUXV) TARGET ConNeg + COPULAS (*-1 REAL-NEG BARRIER REAL-V)(*1 PrfPrc OR (Actio Ess) BARRIER REAL-V OR S-BOUNDARY);
	#$ Muhto juos Kristus ii leat bajásčuožžilan, ..

#!! * __-FAUXVConNegAux__  to ConNeg AUX-OR-MAIN
MAP:-FAUXVConNegAux (@-FAUXV) TARGET ConNeg + AUX-OR-MAIN (*-1 REAL-NEG BARRIER REAL-V)(*1 Inf OR PrfPrc BARRIER REAL-V OR S-BOUNDARY);
	#$ Eai guovttis ge nagot loktet dan geađggi.
	#$ In sáhte olles gáhku borrat.
	#$ Daid ii galgga suovvat bargat joavdelasaid.
	#$ Ii daid galgga joavdelasaid bargat suovvat.

#!! * __-FAUXVConNegAux__  to ConNeg AUX
MAP:-FAUXVConNegAux (@-FAUXV) TARGET ConNeg + AUX - ("leat") (*-1 REAL-NEG BARRIER S-BOUNDARY OR ConNeg)((*1 Inf BARRIER REAL-V OR S-BOUNDARY OR Attr) OR (0 TRANS-V LINK *-1 Acc BARRIER NOT-NPMOD LINK *1 Inf BARRIER REAL-V OR S-BOUNDARY OR Attr));
	#$ Ii sáhte suohtastallat.
	#$ Ii sáhte dieđusge dušše suohtastallat.
	#$ Eat mii sáhte vuolgit.
	#$ In sáhte gáhku borrat.
	#% It don leat mus goahtemuorran orron.



MAP:-FAUXVConNegAuxNeg (@-FAUXV) TARGET ConNeg + AUX - ("leat") (*-1 REAL-NEG BARRIER S-BOUNDARY OR ConNeg)(1 (Ind Neg) LINK *1 Inf BARRIER REAL-V OR S-BOUNDARY);
	#$ Lean diŋgaohcci ja in háliit in minuhta ge duššái golahit.

#!! * __-FMAINVConNeg__  to ConNeg
MAP:-FMAINVConNeg (@-FMAINV) TARGET ConNeg (*-1 REAL-NEG BARRIER REAL-V)(NEGATE *1 Inf OR PrfPrc OR (Actio Ess) BARRIER REAL-V OR S-BOUNDARY)(NEGATE **1 Adv BARRIER REAL-V OR S-BOUNDARY LINK 1 Inf)(NEGATE 0 Inf LINK -1 REAL-NEG LINK -1 ConNeg LINK -1 REAL-NEG) ;
	#$ Ii son liikon dohko.
	#$ Sieiddi bálvalemiin sturron eallu gohčoduvvui bálvvoseallun, mii sáhtii jávkat, go oamasteaddji jámii dahje go sieidi ii bálvaluvvon.
	#$ Iigo dutnje šat boađe áviisa?

MAP:-FMAINVConNegCoor (@-FAUXV) TARGET (Prs ConNeg) + AUX (-1 ("ja" CC) OR ("dahje" CC) LINK **-1 ConNeg LINK -1 Neg)(*1 Inf BARRIER REAL-V OR S-BOUNDARY)(NEGATE 0 VFIN);
	#$ Bearehan dal son ii geige vel gieđa ja váldde maiddái eallima muoras ja bora ja beasa nu eallit agálaččat.
	#% Olmmoš gean lusa stállu lea vuolgán, ii oaččo šat ráfi ja ferte vuolgit olggos fággádallat suinna.


MAP:-FMAINVConNegCoor (@-FMAINV) TARGET (Prs ConNeg) (-1 ("ja" CC) OR ("dahje" CC) LINK **-1 ConNeg LINK -1 Neg)(NEGATE 0 VFIN);
	#$ Bearehan dal son ii geige vel gieđa ja váldde maiddái eallima muoras ja bora ja beasa nu eallit agálaččat.
	#% Olmmoš gean lusa stállu lea vuolgán, ii oaččo šat ráfi ja ferte vuolgit olggos fággádallat suinna.


#!! * __-FMAINVConNeg__  to ConNeg
#MAP:-FMAINVConNeg (@-FMAINV) TARGET ConNeg (*-1 REAL-COPULAS + VFIN OR ("orrut") + VFIN OR Neg BARRIER SV-BOUNDARY)(NEGATE *-1C PrfPrc BARRIER REAL-V OR N);
MAP:-FMAINVConNeg (@-FMAINV) TARGET ConNeg (*-1 REAL-NEG BARRIER SV-BOUNDARY OR ConNeg)(NEGATE 0 AUX LINK *-1 PrfPrc BARRIER REAL-V)(NEGATE 0 AUX LINK 1 Inf)(NEGATE 0 Inf LINK -1 REAL-NEG LINK -1 ConNeg LINK -1 REAL-NEG) ;
	#$ Ii okta ge geahččalan eará sivaid ohcat.
	#% It don leat mus goahtemuorran orron.
	#% De ledje baicca eatnašat dan dilis ahte eai máhttán stávetge.

#!! * __-FMAINVConNeg__  to ConNeg Aux after PrfPrc
MAP:-FAUXVConNeg (@-FAUXV) TARGET ConNeg + AUX (*-1 PrfPrc BARRIER REAL-V OR SV-BOUNDARY LINK *-1 REAL-NEG BARRIER NOT-ADV-PCLE)(NOT *1 REAL-V BARRIER NOT-ADV-PCLE)(NEGATE 1 Nom);
	#$ Ii dal láhppán gal leat.
	#% Ii ge ságastallan leat vejolaš jus eai leat journalisttat geat máhttet sámegiela bures.

#!! * __-FMAINVConNegCop__  to ConNeg COPULAS
MAP:-FMAINVConNegCop (@-FMAINV) TARGET ConNeg + COPULAS (*-1 REAL-NEG BARRIER REAL-V)(NEGATE *1 PrfPrc OR Inf OR (Actio Ess) BARRIER REAL-V OR S-BOUNDARY OR Nom);
	#$ Ii lean šat nu álki vuovdit.


#MAP:ConNegCop (@-FMAINV) TARGET ConNeg (*-1 REAL-NEG BARRIER S-BOUNDARY OR ConNeg)(0 ("leat"))(1 (A Nom));
	#$ Ii ge almmolaš sámi ságastallan leat vejolaš jus eai leat journalisttat geat máhttet sámegiela bures.


#!! * __-FAUXVPrfPrcAux__  to PrfPrc AUX  before Inf or Actio Ess
MAP:-FAUXVPrfPrcAux (@-FAUXV) TARGET PrfPrc + AUX (*1 Inf OR (Actio Ess) BARRIER SV-BOUNDARY)(*-1 ("leat") BARRIER NOT-ADV-PCLE) (NEGATE 0 AUX-OR-MAIN LINK *1 Acc BARRIER NOT-ADV-PCLE LINK 1 Inf + OBJ);
	#$ In leat sáhttán gáhku borrat dan rájes.
	#% Jus lean váiban dahje ferten lihkkat árrat, de manan nohkkat oalle árrat.

#!! * __-FMAINVPrfPrc__  to PrfPrc
MAP:-FMAINVPrfPrc (@-FMAINV) TARGET PrfPrc (*-1 REAL-COPULAS + VFIN OR ("orrut") + VFIN OR Neg BARRIER S-BOUNDARY)(NEGATE *-1C PrfPrc BARRIER REAL-V OR N);
	#$ Muhto boanddat leat goikadan ollu jekkiid ja gilván daidda rásiid.
	#$ Mu mielas lea suohtas oahppat sámegiela.
	#$ Ja Biret-Elle lea easka skuvllas geargan.
	#$ Máŋgasat leat maid vásihan, ahte sii leat bártidan.
	#$ In leat leamaš mielde gáhku láibume ovdal.
	#$ Moanaid guovlluin gos lavdnji lea leamaš adnon boaldámuššan lea buot lavdnji dál čuhppon.
	#$ Oahppit leat lohkan ollu girjjiid.
	#$ Lean mun gal oaidnán čakčaseavdnjadiin dola rievdamiin čázi alde Deanus.

#!! * __-FMAINVPrfPrcEss__  to PrfPrc before Ess
MAP:-FMAINVPrfPrcEss (@-FMAINV) TARGET PrfPrc IF (*-1 REAL-COPULAS OR ("orrut") BARRIER PrfPrc OR (Actio Ess))(NEGATE 0 AUX LINK *1 Inf BARRIER S-BOUNDARY)(1 Ess);
	#$ Moanaid guovlluin gos lavdnji lea leamaš adnon boaldámuššan lea buot lavdnji dál čuhppon.

#!! * __-FMAINVPrfPrcleat__  to PrfPrc leat
MAP:-FMAINVPrfPrcleat (@-FMAINV) TARGET ("leat" PrfPrc) ;
	#$ Ránus lea álo leamaš buorre haddi.

#!! * __-FMAINVPrfPrcafterAuxAux__  to PrfPrc after two Auxs
MAP:-FMAINVPrfPrcafterAuxAux (@-FMAINV) TARGET PrfPrc (*-1 REAL-COPULAS OR ("orrut") BARRIER SV-BOUNDARY LINK *-1 Neg BARRIER NOT-ADV);
	#$ Olbmot eai orron čadnon dušše iežaset mearragáddái bivdit.

#!! * __-FMAINVPrfPrccoord__  to PrfPrc coordination
MAP:-FMAINVPrfPrccoord (@-FMAINV) TARGET PrfPrc (*-1 CC BARRIER REAL-V OR S-BOUNDARY LINK *-1 PrfPrc - @>N BARRIER REAL-V OR S-BOUNDARY LINK *-1 REAL-COPULAS OR ("orrut") BARRIER NOT-ADV-PCLE)(NEGATE 0 VFIN + AUX LINK *1 Inf BARRIER NOT-ADV-PCLE);
	#$ ... dákkár orgánii leat boahtán dahje ovddiduvvon.
	#$ Sápmelaččat ledje dohkkehuvvon Norgga álgoálbmogin ovdalgo konvenšuvnna ledje šiehtadan gárvásii ja dasa vuolláičállán.
	#%  Jus lean váiban dahje ferten lihkkat árrat, de manan nohkkat oalle árrat.

#!! * __-FMAINVPrfbeforeAux__  to PrfPrc before the Aux
MAP:-FMAINVPrfbeforeAux (@-FMAINV) TARGET PrfPrc (*1 REAL-COPULAS + VFIN BARRIER NOT-ADV-PCLE LINK NOT *1 PrfPrc OR (Actio Ess) BARRIER NOT-ADV-PCLE)(NEGATE -1 BOC LINK 1 Der/Pass LINK 1 ("leat") LINK *2 NP-HEAD-NOM BARRIER NOT-NP);
	#$ Ja boahtán ledje su gudnejahttit.

#MAP:r412 (@-FMAINV) TARGET PrfPrc IF (-1 VERBAL-ACTIVITY-V)(0 ("leat"));
	#$ Dat cuigo lean nu ropmi. hmmm...
MAP:-FMAINV (@-FMAINV) TARGET ("beaivádit" PrfPrc)(1 Sem/Date) ;


#!! * __-FMAINVInf__  to Inf
MAP:-FMAINVInf (@-FMAINV) TARGET Inf (NOT 0 FAUXV)(NEGATE 0 A LINK *-1 COPULAS BARRIER REAL-V OR (N Nom) OR ADVLCASE OR MIELLA OR (A Nom));
#(NEGATE 0 A LINK *-1 COPULAS + Sg3 BARRIER REAL-V OR (N Nom) OR ADVLCASE OR MIELLA OR (A Nom) LINK *-1 MIELLA OR GEN-CONCEPT BARRIER REAL-V) ;

MAP:-FMAINVInf (@-FMAINV) TARGET Inf (NOT 0 FAUXV)(*-1 COPULAS + Sg3 BARRIER REAL-V OR (N Nom) OR ADVLCASE OR MIELLA OR (A Nom) LINK *-1 MIELLA OR GEN-CONCEPT BARRIER REAL-V) ;
	#$ Eará guovddáš ulbmil prošeavttain lea fuomášahttit dan máhttodárbbu maid riikkaidgaskasaš ja sisriikkalaš birasulbmilat bohciidahttet.


#!! * __-FMAUXVActioEss__  to Actio Ess
MAP:-FMAUXVActioEss (@-FAUXV) TARGET (V Actio Ess) + AUX (*1 Inf BARRIER REAL-V);
	#$ Mun ledjen áigume čállit reivve.

#!! * __-FMAINVActioEss__  to Actio Ess
MAP:-FMAINVActioEss (@-FMAINV) TARGET (V Actio Ess)(NEGATE 0 AUX LINK 1 Inf)(NEGATE -1 OPRED-V);
	#$ Biret lea čállime.

#!! * __-FMAINVSup__  to Sup
MAP:-FMAINVSup (@-FMAINV) TARGET Sup ;
	#$ Áddjá bázii mállása vuoššažit.

#!! * __+FAUXV__ to  Aux
MAP:+FAUXV (@+FAUXV) TARGET VFIN-NOT-IMPRT + AUX (NEGATE 0 AUX-OR-MAIN LINK *1 Acc BARRIER NOT-ADV-PCLE LINK 1 Inf + OBJ) ;

MAP:+FMAINV (@+FMAINV) TARGET VFIN-NOT-IMPRT + AUX-OR-MAIN ;
	#$ Soai siđaiga dávjá Liná lohkat jitnosit go sis lei lohkanhárjehallan.
	#$ Lásse dat lei sihtan mu váldit dan.

#!! * __NPrsPrc1__ with 1C N Nom
MAP:NPrsPrc1 (@>N) TARGET PrsPrc
#((-1 (N Loc) OR BUOT OR Attr OR (N Gen)) OR (-1 CC LINK -1 Attr))
((*-1 VFIN BARRIER COMMA OR S-BOUNDARY) OR (1 N LINK *1 VFIN BARRIER S-BOUNDARY OR COMMA))
(NEGATE 0 N - Der/NomAg)
(1 N)(NEGATE 0 VFIN LINK *1 Inf BARRIER REAL-V) ;

MAP:NPrsPrc2 (@>N) TARGET PrsPrc ((0C PrsPrc) OR (0 ("johtit") LINK 1 N));
#MAP:NPrsPrc2 (@>N) TARGET PrsPrc (NOT 0 NomAg OR VFIN LINK 1C N) ;


#!! * __ActioNom__ with 1C N Nom
MAP:ActioNom (@>N) TARGET (Actio Nom)(-1 HAB-ACTOR + Gen)(1C (N Nom)) ;
	#$ Stáhta nammadan lahttu ja sadjásašlahttu galgaba deavdit gáibádusaid.

#!! * __<ADVLVAbess__ VAbess ADVL
MAP:<ADVLVAbess (@<ADVL) TARGET VAbess (*-1 VFIN BARRIER S-BOUNDARY)(NOT 0 SYN-V) ;
	#$ Mus báhce moadde girjji logakeahttá.
MAP:ADVL>VAbess (@ADVL>) TARGET VAbess (*1 VFIN BARRIER S-BOUNDARY) ;

#!! * __<ADVLVGen__ VGen ADVL
MAP:<ADVLVGen (@<ADVL) TARGET VGen (*-1 VFIN BARRIER S-BOUNDARY) ;
	#$ Máhtte manai čuoigga skuvlii.
MAP:ADVL>VGen (@ADVL>) TARGET VGen (*1 VFIN BARRIER S-BOUNDARY) ;

#!! * __<ADVLGer__ Gerundium ADVL
MAP:<ADVLGer (@<ADVL) TARGET Ger (NOT *1 VFIN BARRIER S-BOUNDARY OR COMMA) ;
	#$ Biigá lei bassaladdame báhpa boađedettiin.

#!! * __ADVLGer>__
MAP:ADVLGer> (@ADVL>) TARGET Ger (*1 VFIN BARRIER S-BOUNDARY);
	#$ Konvenšuvdnaevttohusa ráhkadettiin delegašuvnnat leat váldán vuhtii Ruoŧa ođđa boazodoallolága.
	#$ Báhpa boradettiin biigá lei bassaladdame.

#!! * __-FMAINVLoc__ Actio Loc
MAP:-FMAINVLoc (@<ADVL) TARGET (Actio Loc) ;

#!! * __>AActioGen__ Actio Gen
MAP:>AActioGen_a (@>A) TARGET (Actio Gen) (1 ("veara"));
MAP:>AActioGen_b (@>A) TARGET (Actio Gen) (1 CC LINK 1 Gen LINK 1 ("veara")) ;
	#$ Dasgo bargi lea bálkkás veara.



#!! !And then we remove the verbs which didn't get any syntactic tag, in favour of verbs with syntactic tags.

#!! * __realverbX__
MAP:realverbX (@X) TARGET REAL-V  ;
	## Oađidettiin gullen olbmo boahtimin.


#!! * __NomActLocX__
MAP:NomActLocX (@X) TARGET (Der/NomAct Loc)(0 @<ADVL)  ;

#!! * __NomActX__ removes other readings when PrfPrc Or Actio Ess
MAP:NomActX (@X) TARGET (Der/NomAct) OR Actio (0 (V @<OBJ) OR FMAINV OR (V @>N))  ;

#!! * __IfonlyVerb__ selects the FMAINV reading in the cohort
SELECT:IfonlyVerb FMAINV IF (NEGATE *0 FMAINV BARRIER S-BOUNDARY)(NOT 0 FAUXV)(NOT *-1 ("go") LINK *-1 VFIN) ;
	#$ Juohke loddi gielainis lávlu.
	#$ Das maŋŋá dajan juoidá álbmotrievtti mearkkašumi birra, ja geavahan das veahkkin ođđaseamos riektegáldu.
	#% Lea 43 olbmo unnit go diibmá dán muddui.
# The last NOT clause keeps the correct Ill in the 43 sentence.
# The intuition is that the frame "Lea ... go ... (lei)" is an ellipsis frame.

# Problem 2
#!! * __IfonlyConNeg__ ConNeg if it is @-FMAINV or @-FAUXV
SELECT:IfonlyConNeg ConNeg IF (0 (ConNeg @-FMAINV) OR (ConNeg @-FAUXV))(NEGATE 1 ("leat") + REAL-V) ;
	#% Iige almmolaš sámi ságastallan leat vejolaš.
	#% De ledje baicca eatnašat dan dilis ahte eai máhttán eai stávetge.


#!! !__killifVinCohort__ This rule removes all other readings, if there is a mapped V reading in the same cohort. Every case which this goes wrong, should be fixed in mapping rules or previous disrules.
SELECT:killifVinCohort SYN-V IF  ;

#!! * __TEST__ selects some infinte verb readings in the cohort
SELECT:TEST OBJ + V OR SUBJ + V OR V + @>N ;
 	#$ Mii ávkkiid lea min doppe johtit?

#!! * __X__
REMOVE:X (@X) ;

#!! !Substitute-rule __TV-IV__ for verbs which are both IV AUX and TV main verb: lávet and sáhttit
#!! * __TV-IV__
SUBSTITUTE:TV-IV (V TV) (V IV) FAUXV (0 ("lávet") OR ("áigut"));

#SELECT:Sg2 (V Prt Sg2) IF (0 (Prs Pl3) LINK *-1 BOS BARRIER WORD)(NEGATE 1 go LINK 1 HAB-ACTOR + Loc) ;



#!! !!!NOUNS
#!! !!Disambiguating nouns
#!! !!Nouns vs. adjectives
#!! !!Disambiguating proper nouns

#!! !!!CASE DISAMBIGUATION

#!! !!Num as subject, tricky cases - the rule should be here because of the verbdisambiguation
SELECT:NumNomSubj (Num Sg Nom)(0 NUM LINK -1 (V Pl3) LINK NOT 0 Imprt LINK NOT *-1 (N Pl Nom) OR (Pers Pl3) OR (Coll Nom) OR (Sem/Group Nom) OR NUM + Nom BARRIER SV-BOUNDARY) ;
SELECT:NumNomCoor (Num Sg Nom)(0 Num LINK *-1 CC BARRIER NOT-ADV LINK -1C (Num Nom)) ;
	#$ Juos evttohusa meahcceeatnamiid nuppástuvvan geavaheami birra, mii juogo gusto dušše Kárášjoga, Guovdageainnu, Unjárgga, Porsáŋggu ja Deanu gildii dahje gusto dušše restii Finnmárkkus, dorjot golbma ja dušše golbma stivrralahtu, de sáhttet golbma stivrralahtu ovtta oli gáibidit ášši gieđahallat oktii vel.

SELECT:NumNomCop (Num Nom) IF ((*-1 COPULAS + FMAINV BARRIER NOT-ADV-PCLE LINK *-1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE) OR (*-1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE LINK *-1 COPULAS + FMAINV BARRIER NOT-A LINK -1 (CC @CVP) OR ("de") OR BOS))(1 (Sg Gen))(NEGATE 2 Po OR (A Nom) OR Pr) ;
	#$ Ja go geassit eret dábálaš goluid, de lea buhtes sisaboahtu sullii 100 000 ruvnnu, muitala Eriksen.


#!! !!ACCUSATIVE-GENITIVE DISAMBIGUATION




#!! !!Secure rules for choosing Acc

# pron (select/remove Gen)


#!! * __PGenN__ selects Gen when (Pron Pers) to the left and N to the right ''mu sámevuođa iđuid''
SELECT:PGenN Gen IF (-1C (Pron Pers) + Gen LINK NOT -1 CC)(1 (N Acc) OR (N Com) OR (N Ill) OR (N Nom))(NEGATE 0 Num OR (Pron Rel))(NEGATE 1 V);
	#$ Dárogiella ja norgalaš cukcasat eai livčče sáhttán seammalágan fátmmasteaddji vugiin guoddit mu sámevuođa iđuid.
	#$ Hans A. Oppstad lei sátnejođiheaddji mu Skuvvanári álgojagiid.
	#$ Vearrámusat leat min iežamet olbmot.
	#% Soai lohkaba su girjji eaŋgalasgillii.

#!! * __CoGen1__ (quite strict) selects the first of coordinated genitives ''riikkaid, čearuid ja boazoorohagaid ovttasbarggu''
SELECT:CoGen1 Gen IF (0 Gen LINK 1 COMMA LINK 1C Gen LINK 1 CC LINK 1 Gen LINK 1 (N Acc));#(NEGATE -1 Gen)(NEGATE -1 ("sihke"))(NEGATE -1 CC LINK -1 Nom)(NEGATE 0 Num);
	#$ Konvenšuvdna galgá ovddidit ja ovdánahttit riikkaid, čearuid ja boazoorohagaid ovttasbarggu, nu maiddái ovttaskas boazodolliid ovttasbarggu.
	#$ Moai bijaime girjái elliid, guliid ja jávrriid dovdomearkkaid jna.
	#% De ledjen mun jo muhtun áigge muitalan gaskadási ohppiide sihke máidnasiid, cukcasiid ja gobmemuitalusaid sámi sisdoaluin.
	#% Mii juohkit guovllu viđa guovllulaš oassái: Romsa ja davágeaži Norrbotten, Davit Nordlánda ja gaska ja lulimus Norrbotten, Helgelánda ja Västerbotten, Davvi-Trøndelága ja davágeaži Jämtlánda. # Negate is for Västerbotten.
	#% Maŋŋil váccii joatkkaskuvlla Álttás ja logai psykologiija, davviriikalašfága ja pedagogihka váldofága Romssa Universitehtas.

SELECT:CoGen2 Gen IF (-1 COMMA LINK -1C Gen)(0 Gen)(1 CC LINK 1 Gen LINK 1C Acc);

#Comment: Make a rule that selects Gen for sentences like: Goalmmát oasis lea bargosajiid ja bargoeallima eavttuid birra, njealját oasis lea fidnooahpuid, giehtaduoji ja boaittobeali báikkiid ealáhusaid birra. => boaittobeali should not be Acc

#!! ! Semantihkka: Choosing accusative or genitive semantically

	# By frequence :
	# Xx Best rules!
	# X Good rules
	# x Found, but not necassarily good


#!! * vuoi selects accusative
SELECT:vuoiAcc Acc IF ((*-1 ("vuoi") BARRIER NOT-NPMODADV) OR (*-1 ("surgat") BARRIER NOT-NPMODADV LINK -1 ("vuoi"))) ;
	#$ Vuoi mu, maid mun dál galggan?
	#$ Vuoi dan olbmo gean bokte fillehusat bohtet.
	#$ Vuoi surgat dan gándda!


LIST SAPMI-N-HEAD =  "álbmot" "álbmotbeaivi" "álbmotdálkkodeapmi" "álbmotjoavku" "árbevierru" "ássanguovlu" "ássi" "áviisa" "beales" "beroštupmi" "boaris" ("boazodoallu" Nom) ("boazodoallu" Loc) "byråkratgiella" "diehtobáŋku" ("dearvvašvuohta" RCmpnd) "dievdu" "doaibma" "eamiálbmotdutkan" ("dutkan" Sg) "ealáhus" "fágabiras" "fierpmádat" "giella" "gilli" "gili#ássi" ("guovlu" Pl) "kulturovdáneapmi" "kulturviessu" "plána" "doaibmaplána" "geavaheaddji" "gelbbolašvuođa#doaibma" "giellaláhka" "girjjálašvuohta" "guolásteapmi" "guovddášguovlu" "hálddašanguovlu" "iešdovdu" ("nuoraidsearvi" Pl) "oahppoplána" "oktasaš" "perspektiiva" "mánná" "nisu" "álbmot" "bargi" "geavaheaddji" "jienastuslohku" "kulturmuitu" "kulturmuitohálddašeapmi" "kulturmuitosuodjalus" "kultuvra" "náhkkegálvu" "nuorra" "oktavuohta" "resursageavaheapmi" "riggodat" "riikkajoavku" "searvevuohta" "searvi" "servodat" ("siida" Pl) "siseana" "siseatnansiida" "sisafárren" "sosiáladutkan" "strategiijaplána" "valáštallan" "vierru" "vuohki" "vuoigatvuohta" "vuoigatvuođabargu" (PUNCT LEFT) ;

LIST SAPMI-PROP-HEAD = "allaskuvla" "bálvalusguovddáš" "cup" "dihtorlávdegoddi" "dearvvašvuođadutkanguovddáš" "doavttersearvi" "ealáhusfoanda" "eanahálddahus" "giellaláhka" "giellalávdegoddi" "giellaráđđi" "guovddáš" ("joatkka" SgNomCmp) "kulturmuitoráđđi" "l" "L-97" "leatna" "meašttirgilvu" "ossodat" "ovdanahttinfoanda" "parlamentáralaš" ("riddu" SgNomCmp) "sátnevuorká" "sierrabiblioteahka" ("siida" Sg)"teáhter" "valáštallanlihttu" "válgalávdegoddi" "válgalihttu" "vuoigatvuođalávdegoddi" "vuoigatvuođalávdegoddi" ;


# Gen MODIFIERS; most are kalled SEM+

# HUMAN

#!! * __SEMnotPossessor__ Removes Gen if you are not a possible possessor (a human) # HAB-ACTOR
REMOVE:SEMnotPossessor Gen IF (0 (N Acc) or (Rel Gen) LINK NOT 0 HUMAN-INDIVIDUAL OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR HAB-ACTOR OR ABSTR-TEXT OR ANIMAL OR TIME OR TITLE OR NATION OR ("dállu") OR ("báiki") OR ORGANIZATION OR Prop)(NEGATE -1 Num OR Pr)(*1 FAMILY BARRIER NOT-NPMOD)(NEGATE 0 ANIMAL LINK *1 FAMILY-ALSO-ABSTRACT BARRIER NOT-NPMOD)(NEGATE 0 ("viidni") OR ("čáhci") OR ("illu") LINK -1 ("eallin")) ;
	#$ Biret addá skeaŋkka Lemehii.
	#$ Moai oste bileahta Harry Potter-filbmii.
	#% Abrama áhká namma lei Sarai, ja Nahora eamit lei Milka, Hanana nieida.
	#% Loahpas jearai listtu Anders Somby jr. sudnos njuolga háliida go čearru gulahallat, juo dahje ii.
	#% Hearrá guhte lea profehtaid vuoiŋŋa Ipmil, lea vuolggahan eŋgelis čájehit bálvaleddjiidasas dan mii fargga dáhpáhuvvá.


#!! * __SEMnotHUM__ removes Gen. This is when an NP is thouht to be the OBJ, because it's not in the human sets and to the right is NON-FAMILY ''njálgáid mánáide''
REMOVE:SEMnotHUM Gen IF (0 (N Acc) LINK NOT 0 (Prop Sem/Plc) OR HUMAN-INDIVIDUAL OR HUMAN-GROUP OR PROFESSION OR OFFICE OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR ABSTR-TEXT OR PLACE OR NATION OR EDUCATION OR ORGANIZATION OR INSTITUTION OR INDUSTRY OR TIME OR Loc)(NEGATE 0 (NomAg Gen))(NEGATE -1 Num OR Ord OR Pr)(NEGATE -1 @CNP LINK -1 Gen)(*1 NON-FAMILY BARRIER NOT-A)(NEGATE 1 COMMA)(NEGATE *1 TRANS-V + FMAINV BARRIER S-BOUNDARY);
	#$ Mun roggen ozastan njálgáid mánáide.
	#$ Mun lean boahtán bidjat soahpameahttunvuođa bártni ja áhčis gaskii.
	#$ Lei hui vuogas boahtit Kárášjohkii nuppiid badjemánáid lusa, dáppe in dárbbašan doarrut ja cielauhuvvot ja doaruhuvvot go ledjen badjeolbmuid mánná.
	#% Čáhppesmurjjiid olbmot gazze milkkiin ovdal.
	#% Mo almitdási riekti meannudii vaikkoba dakkár vuolit dási olbmuid vihtančilgehusaiguin?

#!! * __SEMXr2066__ Removes Gen if there is a human or org to the right, exeption for ''čállingiela áhčči'' and so on
REMOVE:SEMXr2066 Gen IF (0 Der/NomAct)(*1 HUMAN-INDIVIDUAL OR PROFESSION OR OFFICE OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR ("bearaš") OR NATION BARRIER NOT-A LINK NOT 0 ("eadni") OR ("áhčči"));
	#$ Vuosttaš sámi oahppan dearvvašvuođabargit bohte Kárášjohkii 1970-logu álggus.
	#$ Dearvvašvuođabargiidlága § 10, mii gieđahallá dieđuid addima pasienttaide, ja pasientavuoigatvuođalága § 1-3, mii gieđahallá vuoigatvuođa informerejuvvon miehtamii, leat ovdamearkkat dakkár vuoigatvuođain.

#SELECT:SEMr2067 Gen IF (0 ("áhčči") OR ("ipmil"))(1 ("Vuoigŋa") OR ("vuoigŋa"));
	#$ Ehpet dii leat ieža sárdnumin, muhto Áhčádet Vuoigŋa sárdnu din bokte.

#REMOVE:SEMr2068 Gen IF (0 ("vuoigŋa"))(1 ("ipmil"));
	#$ Muhto jos mun áján olggos bahá vuoiŋŋaid Ipmila Vuoiŋŋa fámuin.

#!! * __SEMgenEss__ Removes Acc if there is Gen + Ess, like ''dálu eamidin''
REMOVE:SEMgenEss Acc IF (0 PLACE LINK 0 Gen)(1 HUMAN LINK 0 Ess);
	#$ Vánit eahpiduvvo ahte stáhta geatnegasvuođat sámiid ektui 27. artihkkala mielde dagahivččii stáhta dihto guovlluid eaiggádin geatnegassan doaimmahit eaiggáduššanvuoigatvuođa nu ahte deavddášii 27. artihkkala geatnegasvuođa.
	#$ Máret beasai dálu eamidin.


REMOVE:SEMTr Gen IF (0 (N Acc) - Sem/Time - Sem/Route LINK *1 (Sem/Time Gen) BARRIER NOT-NPMOD OR CC)(*-1 STRICT-TV-V BARRIER SV-BOUNDARY OR Num) ;
	#$ Vuojadansearvi váldá din fitnodagain oktavuođa vahkku 20:s, gullat mo dii sáhttibehtet veahkehit.

#REMOVE:SEMr2070 Gen IF (0 SINGLE-SOUND)(1 ROUTE);
	#$ Ii son bealkke ii ge čuorvvo, ii ge oktage gula su jiena gáhtaid alde.

#!! * __SEMXxr2071__ Revmoves genetiv: Nobody can possess a Proper name? Except from pers.pron. and FIRSTNAME
REMOVE:SEMXxr2071 Gen IF (NEGATE -1 Num OR Pr)(0 (N Acc) LINK NOT 0 NATION OR TIME OR TITLE OR FIRSTNAME OR ORGANIZATION OR ACR)(1 Prop LINK NOT 0 NATION OR ("Vuoigŋa"))(NEGATE 1 (Prop Sem/Org))(NEGATE 1 (Prop Attr) LINK 1 (Prop Sem/Org))(NEGATE 0 N-PX LINK 1 FIRSTNAME OR Sem/Sur LINK *1 Po BARRIER NOT-NPMOD)(NEGATE -1 CC LINK -1 (Prop Sem/Plc) LINK 3 Gen LINK 1C Nom);
	#$ Moai boraime njálgga gáhkuid Hánssa ođđa láibbohagas.#Comment: except it doesn't hit this at all..
	#$ ...go čielggadii oamastanvuoigatvuođa Finnmárkku eatnamiid hárrái.
	#% Dalle ledje njeallje bistevaš skuvlla Finnmárkkus. #dárbbaša constraint numerálagihpu birra
	#% Romssa ja davágeaži Norrbottena boazoorohagat ja čearut riikaráji guoras ja konvenšuvdnaguovlluin. #Comment: Needs a rule for the modifiers of Nom..
	#% Romsa ja davágeaži Norrbotten, Davit Nordlánda ja gaska ja lulimus Norrbotten, Helgelánda ja Västerbotten, Davvi-Trøndelága ja davágeaži Jämtlánda. #Comment: davágeaži can "possess" (or mofify if you will) Jämtlánda.
	#% Loahpas jearai listtu Anders Somby jr. sudnos njuolga háliida go čearru gulahallat, juo dahje ii.

#!! * __SEMXxPropOrg__ Removes genetiv: Who can possess Prop Sem/Org?
REMOVE:SEMXxrPropOrg Gen IF (NEGATE -1 Num)(0 (N Acc) LINK NOT 0 NATION OR TIME)(1 (Prop Sem/Org) LINK NOT 1 (Prop Sem/Org));
	#$ Álggos coggalii Anti vuosttaš spáppa Sørøy Glimt mollii 22 minuvtta maŋŋel go čiekčamat ledje álgán Niitoguolban stadionas.

#!! * __SEMdep__ select gen if main-organization in front of department
SELECT:SEMdep Gen IF (0 MAIN-ORGANIZATION OR EDUCATION)(1 DEPARTMENT) ;
#!! * __SEMorghum__ select gen if organization or education in front of human
SELECT:SEMorghum Gen IF (0 ORGANIZATION OR EDUCATION)(1 HUMAN OR TEXT) ;
	#$ gaskasási ohppiid


#!! * __SEMXr2073__ Remove genetive: Accusative in front of a human group, ''loktema sámiid buorrin''
REMOVE:SEMXr2073 Gen IF (0 (N Acc) LINK NOT 0 GENERAL-PLACE OR GEOGRAPHICAL-PLACE OR GEN-POLITICAL-PLACE OR NATION OR ORGANIZATION OR EDUCATION OR TIME OR Prop)(NEGATE -1 Num OR Ord)(1 HUMAN-GROUP);
	#$ Ráđđehus deattuha sámi kulturáddejumi gelbbolašvuođa loktema sámiid buorrin.

#!! * __SEMr2074__ Selects genetive in front of HUMAN-GROUP (or "joavku"?):
SELECT:SEMr2074 Gen IF (0 Sem/Plc OR Sem/Org OR ACR)(1 HUMAN-GROUP) ;	# or (".*joavku"r) ?
	#$

#!!  * __SEMactor__ Select genitive in front of ABSTRACT and RIEKTEDILLI: ''unnitlogu oaidninčiegas''
SELECT:SEMactor Gen IF (0 HAB-ACTOR + Acc OR (Pron Acc) LINK NOT 0 Loc)(*1 ABSTRACT OR RIEKTEDILLI BARRIER NOT-NPMOD)(NEGATE *-1 TRANS-V + FMAINV OR Inf BARRIER SV-BOUNDARY OR Acc)(NEGATE 1 ("ja"))(NEGATE 0 (Pron Dem) LINK 1 Acc);
	#$ Romeriid vealahanáššis lea dárbu geahččat sihke ovttaskas olbmo, struktuvrralaš ja unnitlogu oaidninčiegas.

#!! * __SEMXr2076__ Selects genitive if you are HUMAN or Pron with an ABSTRACT to your right, ''iežaset vuoigatvuođa''
SELECT:SEMXr2076 Gen IF (*-1 TRANS-V BARRIER SV-BOUNDARY OR Acc LINK 0 FMAINV OR Inf )(0 Acc + HUMAN OR Acc + Pers OR Acc + Refl)(*1 ABSTRACT OR RIEKTEDILLI BARRIER REAL-V LINK 0 Acc); #LINK NEGATE 0 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V OR NOT-HUMAN-OBJECT-V
	#$ Mátta-Norgga almennehiin dihtto ahte giliolbmot gitta 1700-lohkui dahke iežaset vuoigatvuođa almennehiidda erenoamážit gustojeaddjin Gonagasa eiseválddiide.

#!! * __SEMyouareNom__ Removes Gen AND Acc when 0 FAMILY or PROFESSION because you are Nom, but not if -1 Num and VFIN is LEAT or IV ''Oahpai go Sire sámegiela''
REMOVE:SEMyouareNom Gen OR Acc IF (*-1 VFIN BARRIER SV-BOUNDARY)(0 (N Nom) + FAMILY OR NON-FAMILY OR PROFESSION1)((*1 EOS BARRIER WORD - Acc OR TIME-ADV OR (VGen)) OR (*1 Acc - TEXT BARRIER NOT-NPMOD LINK 1 Loc OR Ill LINK 1 EOS)) (NEGATE *-1 NUM - NUMUNIT BARRIER NOT-Attr LINK *-1 LEAT-FIN-NOT-IMPRT OR IV OR TV BARRIER S-BOUNDARY)(NEGATE *-1 TRANS-V + FMAINV BARRIER NOT-NPMOD LINK NEGATE *2 Acc BARRIER SV-BOUNDARY)(NEGATE -1 SAPMI)(NEGATE *-1 TRANS-V + FMAINV BARRIER S-BOUNDARY LINK -1 Nom OR ("vai")) ; #(NEGATE *-1 NUM - NUMUNIT BARRIER NOT-Attr LINK
	#$ Oahpai go Sire sámegiela?
	#$ Oahpaigo Sire sámegiela universitehtas?
	#$ Do boahtá Lemet Niillas, min ođđa oahpaheaddji.
	#% Jávregáttis leat guokte olbmá.
	#% Doppe Ambram čuorvvi Hearrá nama.
	#% Giliviessu geassá 200 olbmo.
	#% Dovdágo Biret Turi Lene Antonsen?
	#Comment: If you get problems with Acc, concider making a separate rule for it.

#!! * __SEMyouareGen__ Removes Nom if movement verb to the left and illative to the right, because you are the modifier of Ill ''mannat Madame Tussaud kabinehttii''
REMOVE:SEMyouareGen Nom IF (*-1 MOVEMENT-V + FMAINV BARRIER SV-BOUNDARY)(0 Nom OR Gen LINK NOT 0 Acc)(1 Ill)(NEGATE 0 Prop LINK 1 Prop + Ill)(NEGATE 1 ("ealáhat") + Ill);
	#$ Odne áigo mannat Madame Tussaud kabinehttii.
	#$ Háliidetne geahččat dien nieidda, gii easká lea boahtán ránnjá vissui, eadni, dajai Tommi.
	#$ Árračavčča bođii skuvlladirektevra Gjermundsen guossái.
	#% 1980:s manai Anny Hámmárfestii vázzit buohccedivššárskuvlla.

#!! * __SEMnotNom__ Removes Nom if a Nom to the right followed by a transitive verb. 0 is animate and to the right is Ill. You are the modifier of Ill
REMOVE:SEMnotNom Nom IF (*-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY LINK *-1 Nom BARRIER S-BOUNDARY)(0 Acc OR Gen)(NEGATE *-1 MOVEMENT-V BARRIER SV-BOUNDARY)(1C Ill OR Acc)(NEGATE 0 Num) ;
	#$ Máttimus sápmelaččat eahpidedje dáža dutkiid eanet go davimus sápmelaččat.
	#$ 1961 manai Fokstad ealáhahkii.
	#$ Doppe Abram čuorvvui Hearrá nama.
	#% Finnmárkkuláhka ii nuppástuhte geavahan- ja oamastanvuoigatvuođaid Finnmárkku eatnamiidda (earret opmodagaid sirdima Statskog SF as Finnmárkkuopmodahkii).


#REMOVE:XXX Acc OR Nom (*-1 MOVEMENT-V BARRIER SV-BOUNDARY)(0 (N Gen))(*1 Ill BARRIER NOT-Attr)(NEGATE 0 ABSTR-ROUTE + Acc OR ("ádjit"))(NEGATE 0 Nom LINK *-1 MOVEMENT-V + (+FMAINV) BARRIER SV-BOUNDARY) ;
	#% 1980:s manai Anny Hámmarfestii vázzit buohccedivššárskuvlla.
	#% Muhtumat válljejedje vázzit beanagullama skuvlii nuppi guvlui.

#SELECT:SEMbody Gen (*-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY)(0 Prop OR Pron)(*1 BODY + Acc BARRIER NOT-Attr)(NEGATE 0 (Pron Dem) OR (Pron Refl))(NEGATE 1 Gen LINK 1 Po) ;



# Accusative in front of a NATION or "sápmi":

#!! * __SEMXxr2081__ Removes genitiv if NATION or POLITICAL-PLACE are to your right ''dilálašvuođaid sámi''
REMOVE:SEMXxr2081 Gen IF (0 Acc LINK NOT 0 NATION OR GENERAL-PLACE OR ORGANIZATION OR EDUCATION OR ANIMATE OR ("evangelium") OR (Prop Sem/Plc) OR ACR)(0 N LINK NOT *-1 Pr BARRIER NOT-NPMOD)(1 NATION OR POLITICAL-PLACE LINK NOT 0 ("sápmi" Pl));
	#$ Ráđđehus láhčá dilálašvuođaid sámi kultuvrra máŋggadáfotvuođa seailluheapmái.


# "Sámegiela hálddašanguovlu/hálddahusguovllu"
SELECT:SEMr2082 Gen IF (0 LANGUAGE + Acc OR ("giellanjuolggadus") + Acc OR ("giellaláhka") + Acc)(1 SAPMI-N-HEAD);
	#$ Sámiláhka meroštallá sámegiela hálddašanguovlun Kárášjoga, Guovdageainnu, Unjárgga, Deanu, Porsáŋggu ja Gáivuona gielddaid.

#SELECT:SEMr2083 Gen IF (0 ("sámiláhka"))(1 ("giellanjuolggadus"));

#!! * __SEMr2084__ Selects genitive for ''hálddašanguovllu suohkanat/gielddat''
SELECT:SEMr2084 Gen IF (0 ("hálddašanguovlu") OR ("hálddahusguovlu") LINK 0 Acc)(1 ("gielda") OR ("suohkan") OR ("fylka"));
	#$ Hálddašanguovllu suohkanat leat ovttasráđiid mearridan nu.

# Gen + MEDIA
#SELECT:SEMr2086 Gen IF (0 NATION LINK 0 Acc)(1 MEDIA);
	#$ Mediat Ruoŧa rádio, Oahpahusrádio ja Ruoŧa Televišuvdna, ožžot jagis 2002 ođđa sáddenlobiid.


#!! * __SEMXr2087__ Selects genitive if you are a Prop/Plc followed by "gielda" or "suohkan"
SELECT:SEMXr2087 Gen IF (0 (Prop Sem/Plc Acc))(*1 ("suohkan") OR ("gielda") BARRIER NOT-NPMOD LINK 0 Pl);
	#$

#!! * Selects genitive if you have "eana" or "guovu" immediately to your right
SELECT:SEMXr2087 (Sem/Plc Gen) IF (0 (Prop Acc))(1 ("eana") OR ("guovlu"));
#SELECT:SEMr2087 (Sem/Plc Gen) IF (0 (Prop Acc))(1 CC LINK 1 Gen LINK 1 ("eana"));
	#$ Sodoma ja Gomorra eatnamii geavvá geahppaseabbot duopmobeaivvi go dan gávpogii.

#!! * __SEMráđđi__ , tja
SELECT:SEMráđđi ("ráđđi" Gen) IF (-1 ("Davviriikkat"))(1 ("girjjálašvuođabálkkašupmi")) ;
	#$ Davviriikkaid ráđi girjjálašvuođabálkkašupmi. #Ritva comment: gávnnat juoga eará ráđđi-sátnái!

# "guovddášlihttu"
#SELECT:SEMr2088 Gen IF (0 ("gielda" Pl))(1 ("guovddášlihttu"));
	#$

LIST SIERRA-GUOVLU = (".*guovlu"r) "suomabealli" "norggabealli" "riddu" "ruoŧabealli" "ruoššabealli" ;


#!! * __SEMplcGen1__ Genitive in front/after of PLACE-ADV ''Finnmárkku máttabealde''
SELECT:SEMplcGen_a Gen IF (0 (Prop Sem/Plc) OR GEOGRAPHICAL-PLACE LINK 0 Acc)(1 PLACE-ADV);
SELECT:SEMplcGen_b Gen IF (0 (Prop Sem/Plc) OR GEOGRAPHICAL-PLACE LINK 0 Acc)(-1 PLACE-ADV);
	#$ vealgelogahallama mearkkašumi fylkkain Finnmárkku máttabealde
	#$ Nuppe dáfus ledje mihá eanebut geat bivde lagabus gátti.
	#Comment: I've added "lagabus" to the set PLACE-ADV, but please observe that this adv only can be positioned IN FRONT of the Sem/Plc-word (?bivde gátti lagabus). If a situations should emerge, concider dividing the PLACE-ADV to two (or more) sets for adv based on weather they can be placed in front or following the Sem/Plc-word, or both.


#!! * __SEMplcGen2__ removes Gen in front of a political-place or general-place ''bidjen hildu sadjásis''
REMOVE:SEMplcGen2 Gen IF (0 N LINK NOT 0 ABSTR-PLACE OR ACTIVITY OR HUMAN OR TOPIC OR PROCESS OR INDUSTRY OR INSTITUTION OR ORGANIZATION OR TIME OR Prop OR SIERRA-GUOVLU OR SAPMI OR LANGUAGE LINK NOT *-1 Pr BARRIER NOT-NPMOD)(1 GENERAL-PLACE + Loc OR POLITICAL-PLACE + Loc LINK NOT 0 ("biras" Pl Loc) OR ("sápmi"));
	#$ Mun oidnen dan golbmasa gávpešiljus.
	#$ Mun bidjen hildu sadjásis.
	#% Sii leat ságastallan daid sápmelaččaiguin geat dárbbašit dálvejagi ja geassejagi guohtuneatnamiid nuppi bealde riikaráji.

REMOVE:SEMplcGen2 Gen IF (0 ABSTR-TEXT OR TEXT LINK NOT *-1 Pr BARRIER NOT-NPMOD)(1 GENERAL-PLACE OR POLITICAL-PLACE LINK NOT 0 ("biras" Pl Loc) OR ("sápmi"));
	#$ Sámediggi sáhttá maid buktit cealkámušaid guovlluid dearvvašvuođafitnodagaid jahkedieđáhusain ja plánaárvalusain.

#!! * __SEMXr2079__ Removes genitive: because Accusative in front of MANNU ''guđii virggi skábmanánu 1. b.''
REMOVE:SEMXr2079 Gen IF (0 Acc LINK NOT 0 ("jahki") OR LAHKA OR ("Sámediggi"))(NEGATE *-1 Num OR Pr BARRIER NOT-NPMOD)(1 MANNU);
	#$ Carsten Smith guđii Sámi vuoigatvuođalávdegotti ovdaolbmo virggi skábmamánu 1. b. 1985.

# Genitive in front of ''virgi''
#REMOVE:SEMr2080 Acc IF (0 OFFICE OR PROFESSION)(1 ("virgi"));
	#$ Carsten Smith guđii Sámi vuoigatvuođalávdegotti ovdaolbmo virggi skábmamánu 1. b. 1985. #Ritva comment: dása gal ferte gávdnot eará čoavddus (gullá oktii bajit njuolggadusain).



#!! * __SEMxhab__ Selects accusative if COPULAS to the left of HAB-ACTOR ''lea min''
SELECT:SEMxhab Acc IF (0 HAB-ACTOR)(*-1 COPULAS BARRIER NOT-NPMOD LINK -1 (Pl Acc) LINK -1 ("mii" Sg Nom)) ;
	#$ Mii ávkkiid lea min doppe johtit?

#!! * __SEMxboaris__ Selects genitive
SELECT:SEMxboaris Gen IF (0 ("boaris" Pl Acc) LINK 1 SAPMI-N-HEAD OR SAPMI-PROP-HEAD) ;
	#$ Sii dolvo ieža áhku boarrásiid siidii. #Comment: modified, but still not sure we need this rule at all..

#!! * __EMeallimamuorra__ Selects genitive: eallima muorra
SELECT:SEMeallimamuorra Gen IF (0 ("eallin" Sg) LINK 1 ("muorra")) ;


#!! * __ACRGen__ Selects genitive: NRK Sápmi
SELECT:ACRGen (ACR Gen)(NOT 0 Attr)(1 (Prop Sem/Obj) OR (Prop Sem/Plc) OR (Prop Sem/Org)) ;
	#$ NSR Lulli-Sámi sámediggeáirras, NRK Sápmi

#!! * __ACRAttr__ Selects genitive: IL Nordlys
SELECT:ACRAttr (ACR Attr)(1 (Prop Sem/Obj) OR (Prop Sem/Plc) OR (Prop Sem/Org)) ;
	#$ IL Nordlys


# "heagga"
#SELECT:SEMr2090 Gen IF (0 (Acc))(1 ("heagga") LINK 0 Px);
	#$ Sii ealihit sin heakkaset vuodjimiin ja geasehemiin.

#REMOVE Acc IF (-1 HUPMAT)(0 HUMAN) ;
	#$ Mun human Tronda beatnaga birra.

# buo.
#SELECT:SEMr2091 Acc IF (*-1 ("buo") BARRIER NOT-NPMOD)(0 (N Gen));
	#$ , buo. sierra sámestáhta.

# ovttasráđiid
#REMOVE:SEMr2092 Gen IF (1 ("ovttasráđđi" Pl Gen));
	#$


#!! * __SEMXxr2093__ Selects accusative: if váldit to the left and mielde to the right: ''váldit mielde''
SELECT:SEMXxr2093 Acc IF (*-1 ("váldit") BARRIER NOT-NPMOD)(0 Gen)(1 ("mielde") OR ("fárrui"));
	#$ Sámediggi lea bivdán ráđđehusa váldit Sámedikki mielde árvoháhkanprográmmabargui.
	#$ Ii lean dábálaš váldit mánáid mielde rohkosvissui.

# "barggahit"
#SELECT:SEMr2094 Acc IF (*-1 ("barggahit") BARRIER NOT-ADV-PCLE)(0 HUMAN)(NEGATE 0 Gen LINK 1 HUMAN);
	#$

#!! * __SEMXr2096__ Removes genitive: because Accusative in front of an organization
REMOVE:SEMXr2096 Gen IF (NEGATE -1 Num)(0 Acc LINK 0 TEXT OR ABSTR-TEXT OR ABSTRACT)(1 ORGANIZATION);
    # We could do it the other way: IF (O Acc LINK NOT HUMAN OR POLITICAL-PLACE OR GENERAL-PLACE).
    # Or we could concider to widen the other part: (NEGATE 1 HUMAN-INDIVIDUAL OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR ANIMALS OR ABSTRACT)
	#$ Dat lea goappaš riikkaid eiseválddiid hálddus addit lobi boazoguohtunlávdegotti ja dárkkistangotti miellahttui.

#REMOVE:SEMr2097 Gen IF (0 N LINK NOT 0 NATION OR TIME)(NEGATE -1 Num)(1 ("sámediggi") OR ("Sámediggi"));
	#$ Sámedikkit nammadit lahtuid sámedikkiid gaskasaš ovttasbargoorgánii.

#Xx
#!! * __SEMGenORG__ selects Gen (modifier): in front of an organization ''Stáhta Oahpahuskantuvra''
SELECT:SEMGenORG Gen IF (0 NATION LINK 0 Acc)(*1 ORGANIZATION OR INSTITUTION OR EDUCATION OR INDUSTRY BARRIER NOT-NPMOD LINK NOT 0 ("skuvla"));
	#$ Artihkal maid dás almmuhat, lea logaldallan maid son doalai internáhttakonfereanssas maid Stáhta Oahpahuskantuvra Finnmárkkus lágidii Guovdageainnus 1999:s.
	#$ 80-logu beallemuttus ásahuvvojedje sámi organisašuvnnat.
	#$ Norgga ja Ruoŧa ráđđehusaide kommišuvdna lea jagi 1997 cuoŋománu rájis bargan doaimmaiguin mat dasa ledje biddjon ja ela odne, čoahkkimis Helssegis, loahpahan barggu ovttahienalaš evttohusain.
	#% 1970-1980 jagiid ásahedje maiddái sámi kulturguovddážiid dáid skuvllaid olis, ja maid stáhta ja fylkkagielda ruhtadedje. #Hits this, but shouldn't.


#REMOVE:SEMr2099 Gen IF (0 N LINK NOT 0 TIME OR ANIMATE OR NATIONAL-IDENTITY OR POLITICAL-PLACE)(NEGATE -1 Num)(1 ("servodat"));
	#$ Lea dan vuođul geatnegasvuohta ovddidit buot mánáid beroštumiid servodagas.
    # Should consider if we could put in (1 ORGANIZATION). Then we have to add at least Prop as 0.

# x
#!! * __SEMgen1__ removes Acc if buot, gait or buohkat in front of a genitive, followed by a plural noun ''buot Norlándda ohppiid''
REMOVE:SEMgen1 Acc IF (-1 BUOT OR ("buohkat"))(0 (Sg Gen) LINK *1 (N Pl) BARRIER REAL-V);
	#$ Son lea almmuhan eanemus girjjiid buot sámi čálliin.
	#$ Kantuvra meannuda buot Nordlándda, Romssa ja Finnmárkku ohppiid ja studeanttaid loatna- ja stipeandaohcamiid, ja buot velggolaččaid máksingeahpádusohcamiid, geat orrot seamma guovllus.


# "bargat" + object: X
#!! * __SEMgen2__ removes Acc if bargat or dihte are FMAINV or Inf and are found somewhere to the left of a Gen, which is followed by a noun ''bargame boazodoallolága ođastemiin''
REMOVE:SEMgen2 Acc IF (*-1 ("bargat") BARRIER SV-BOUNDARY OR ("dihte") LINK 0 FMAINV OR Inf)(NEGATE -1 Pr)(0 Gen LINK NOT 0 TEXT OR ("bargu") LINK *1 N BARRIER VFIN);
	#$ Leat maid bargame dálá boazodoallolága ođastemiin.
	#$ Dán leat mii bargan ovttas guoskevaš čearuid, boazoorohagaid, ja eiseválddiid áirasiiguin.
	#$ Almmatge lea jáhkkimis ahte boahtteáiggis bargojuvvo dáiguin jurdagiiguin, dannego kommišuvnna oaivila mielde eai boađe makkárge eará čovdosat nu bures buorrin buohkaide.
	#$ De son barggai guokte jagi buohccidivššárin Kolvereidas, Trøndelágas.

# * Removes genitive: if you are a TEXT and bargat is to the left ''bargat leavssuid''
#REMOVE:r2102 Gen IF (*-1 ("bargat") BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 Acc LINK 0 TEXT OR ("bargu") LINK NOT 1 N OR Po); # This is based on that the object will be "leavssuid, bargobihtáid, barggu"  elsewhere Gen.
	#$ Eahkedis mun bargen leavssuid. #Ritva comment: AccTV2 takes care of this.



#!! * __SEMXr2103__ Selects accusative: OASSI is usually accusative ''hálddaša stuora oasi''
SELECT:SEMXr2103 Acc IF (*-1 TRANS-V BARRIER SV-BOUNDARY)(NEGATE -1 Num)(0 OASSI LINK 0 Gen)(1 Gen LINK NOT 0 Der/NomAg);
	#$ Dát foanda hálddaša stuora oasi Boazodoallošiehtadusa ruđain.
	#$ Son vuolggahii muhtun bálvaleaddji lusa vai son oččošii oasis viidnegárddi šattuin.
	#Comments: Try to make a set of nouns which cannot be Gen in a AccGen string.
	# Possible Gen-construction: Nuppi oasi geavaheaddji ......

# Writing a TEXT:

#!! * __SEMXxr2104__ Selects accusative: if WRITING-ACTIVITY-V to the left and you are a TEXT ''čállá vaidaga''
SELECT:SEMXxr2104 Acc IF (*-1 WRITING-ACTIVITY-V BARRIER SV-BOUNDARY)(NEGATE -1 Num)(0 TEXT OR ABSTR-TEXT LINK 0 Acc)(NEGATE 1 CONCEPT OR TEXT OR ABSTR-TEXT OR LANGUAGE OR Po);
	#$ Sámi geavaheaddjit galggašedje sáhttit váidalit njálmmálaččat, man vuođul fylkkadoavttir de čállá váidaga, dárbbu mielde dulkka vehkiin.
    #Comment: CONCEPT should be a constraint, so that in "write a text's outline", "text" still gets Gen.

#!! * __SEMXxacc__Removes accusative: if WRITING-ACTIVITY-V to the left and a noun to the right ''čállit Norgga vásáhusaid''
REMOVE:SEMXxacc Acc IF (*-1 WRITING-ACTIVITY-V BARRIER SV-BOUNDARY)(NEGATE -1 Num)(NEGATE 0 TEXT OR ABSTR-TEXT OR LANGUAGE)(*1 N LINK 0 Gen OR Acc);
	#$ Dán oasis mii čállit Norgga ja Ruoŧa vásáhusaid ja juohkit daid osiide.


#!! * __SEMXxOrgRep__ Selects genitive: An organization´s representative ''Sámiráđi ovdaolmmoš''
SELECT:SEMXxOrgRep Gen IF (0 Acc LINK 0 ORGANIZATION OR INSTITUTION OR (Prop Sem/Org))(1 PROFESSION OR HUM-FUNCTION OR HUM-ATTR OR HUM-VERB OR OFFICE LINK NOT 0 Pl);
  	#$ Dán gáibideigga foandda hoavda Per Edvard Klemetsen ja Norgga Sámiráđi ovdaolmmoš Aslak Nils Sara gielddaministaris Arne Rettedalas geainna soai maŋŋebárgga deaivvadeigga Oslos.
  	#$ Jus áššebáhpirat leat váilevaččat, de galgá lávdegotti jođiheaddji addit sutnje, gii ovddida ášši, áigemeari divvut váilevašvuođaid.

# ášši
#! * __SEMr2106__ Gen if -1 NATIONAL-IDENTITY
#SELECT:SEMr2106 Gen IF (-1 NATIONAL-IDENTITY)(0 ("ášši"));
	#$

# fáktemuš
#!! * __SEMxr2107__ Acc if *-1 fáktemuš
SELECT:SEMxr2107 Acc IF (*-1 ("fáktemuš") BARRIER NOT-NPMOD)(0 (N Gen))(NEGATE 1 Acc OR Po);
	#$ Dain lea fáktemuš juohke balvva.
	#Comment:Subjeaktapredikatiiva objeakta @SPRED<OBJ. Gávdnošiigo dáidde oktasaš njuolggadus?

# egentlig laga for njuolggadus, men tester med et sett
REMOVE:SEMlahka-n Gen IF (0 LAHKA-N + Acc LINK 1 Gen LINK NOT 0 TEXT OR SATNI OR KLASS OR Der/NomAct)(*-1 TRANS-V BARRIER SV-BOUNDARY) ;
	#$ Finnmárkkuopmodat sáhttá addit dárkilet njuolggadusaid luondduvalljodagaid ávkkástallamii iežas eatnamiin, earret eará ferte olbmos lohpi dasa.

# SÁPMI


#!! * __SEMXxr2108__ Selects genitive if you are SAPMI with an Acc/Gen immediately to your left and a noun immediately to your right ''girji sámi áššiid (birra)''
SELECT:SEMXxr2108 Gen IF (-1 Acc OR Gen)(0 SAPMI + Sg)(1 N);
	#$ Čálán girjji sámi áššiid birra.

#SELECT:r2109 Gen IF (0 SAPMI + Sg)(*1C Acc BARRIER NOT-A); #Comment: Not needed. SEMXxr2112 takes care of this.
	#$ Dalle čujuhuvvui ahte lea dárbu nannet doaimmaid mat ovddidit sámi dutkama, fágaidgaskasaš ovttasbarggu ja sámi áššiid oahpahusa.
	#$ Ráđđehus deattuha sámegiela ja sámi kulturáddejumi gelbbolašvuođa loktema.

#SELECT:SEMr2110 Gen IF (0 SAPMI + Sg)(1C Gen)(2C Acc);
	#$ Ráđđehus deattuha sámegiela ja sámi kulturáddejumi gelbbolašvuođa loktema, ja sámi bargiid bálkáheami.
#! * Selects genitive (modifier): Sámi, suoma or ruoŧa  as modifier of sápmi-noun ''sámi álbmot''
#SELECT:SEMXxr2111 Gen IF (0 SAPMI LINK 1 SAPMI-N-HEAD OR SAPMI-PROP-HEAD); #Comment: Is this rule nessasary? SEMXxr2112 seems to take care of this.. Problem is that it has restrictions on verbs to the left. Why, is not clear.
	#$ Soai áiguba ovddastit sámi álbmoga dáhtu go Tromsa ohcá Olympia gilvvuid lágidit 2014:s.
	#$ Mii áigut oahpahit sámi álbmogii buot.

#!! * __SEMsapmiModifier__ Selects genitive (modifier): Sámi, suoma or ruoŧa  as modifier of noun ''sámi oahpahus''
SELECT:SEMsapmiModifier Gen IF (0 SAPMI OR SUOPMA)(*1 N BARRIER NOT-A); #Comment: Replaces SEMXxr2112. The difference is that it doesn't have the same restrictions on verbs. # OR NATIONAL-IDENTITY-NOT-SAPMI
SELECT:SEMsapmiModifier Gen IF (0 SAPMI OR SUOPMA LINK NOT *-1 COPULAS)(1 PrfPrc LINK 1 N); #Comment: Replaces SEMXxr2112. The difference is that it doesn't have the same restrictions on verbs. # OR NATIONAL-IDENTITY-NOT-SAPMI
	#$ Ja muđui dán čoahkkimis badjesápmelaččat dorjo eanet sámi hábmejuvvon oahpahusa.
	#$ Dat movt sámi oahpahus lea doaibman.
#! * Select genitive (modifier): Sámi, suoma or ruoŧa  as modifier of noun, but with restrictions on which verb can appear to the left of you.
	#% Seminarium heaittihuvvui, ja miššonearaid gevrejedje oahpahit sámiid dárogillii.
	#% Ja muđui dán čoahkkimis badjesápmelaččat dorjo eanet sámi hábmejuvvon oahpahusa.


#SELECT:SEMXxr2112 Gen IF (NEGATE *-1 ("máhttit") OR ("oahppat") OR ("oahpahit") OR ("juohkit") OR ("čállit") OR ("gallestallat") OR ("galledit") OR VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 SAPMI OR SUOPMA)(*1 N BARRIER NOT-A);
	#$ Dat movt sámi oahpahus lea doaibman.
	#% Mii áigut oahpahit sámi álbmogii buot. # Does not hit because of restrictions. Without the rule, this sentence gets an incorrect reading.

#! * Select genitive (modifier): Sámi, suoma or ruoŧa  as modifier of noun, but with restrictions on which verb can appear to the left of you.
#SELECT:SEMXr2113 Gen IF (NEGATE *-1 ("máhttit") OR ("oahppat") OR ("oahpahit") OR ("juohkit") OR ("čállit") OR ("gallestallat") OR ("galledit") OR VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 SAPMI OR SUOPMA)(1 (@CNP) LINK 1 SAPMI OR SUOPMA OR NATION LINK 0 Gen);
	#$

#!! * __SEMsapmiModifier2__ Select genitive (modifier): Sámi, suoma or ruoŧa on both sides of CNP as modifier of noun ''Suoma ja Ruošša soahti''
SELECT:SEMsapmiModifier2 Gen IF (0 SAPMI OR SUOPMA)(1 (@CNP) LINK 1 SAPMI OR SUOPMA OR NATION LINK 0 Gen); # Comment: Replaces SEMXxr2113. The difference is that it doesn't have the same restrictions on verbs.
	#$ 1700-logu álggus leai Suoma ja Ruošša gaskasaš soahti giksin.
	#$ Das leat mielde olbmot geain lea sihke suoma ja sámi gullevašvuohta.
	#$ Beasaimet váldit buoremus beliid Suoma ja Norgga mánáidgárdevieruin veahkkin hábmet pedagogihka.

#!! * __SEMdazaModifier__ Selects genitive (modifier): dáža, indiána, maya-indiána or romer  as modifier of noun ''dáža oahpahus''
SELECT:SEMdazaModifier Gen IF (0 ("dáža") OR ("dáčča") OR ("indiána") OR ("maya-#indiána") OR ("romer"))(*1 N BARRIER NOT-A)(NEGATE 1 (N Ess));
	#$ Mu oainnu mielde lei dáža servodat ollu verrivuođaid dahkan sámiid vuostái.
	#$ Ii leat álo leamaš nu álki heivehit dábálaš dáža oahpahusfáttá klássaide main leat sámegielat mánát.
	#% Vearrevuohta maid okkupašuvdnafápmustivrejeaddjin lei čájehan, almmostahtii dážaid hearrán unnitlogu ektui.

#SELECT:SEMr2114 Gen IF (0 SAPMI OR SUOPMA)(1 (@CNP))(2 ("eará") OR ("iežá"))(3 N);
	#$ Ulbmil lea čuovvulit sámilága áigumušaid ja vuoigatvuođaid sámegiela viidásut geavaheamis.
	#$

# LAHKA


#!! * __SEMXr2115__ Selects genitive (modifier) in front of a lahka-noun ''spábbačiekčanlága vuoigatvuohta''
SELECT:SEMXr2115 Gen IF (0 LAHKA)(*1 LAHKA-GEN-N OR LAHKA-N OR ("áigumuš") BARRIER NOT-Attr LINK 0 Acc);
	#$ Dálá Ruoŧabeali boazodoalloláhka 1971 rájes, čuovvu ovddit lága hámi dáfus.

#!! * __SEMXr2116__ Selects genitive (modifier) if you are LAHKA OR ORGANIZATION followed by mannu, day and numerals..
SELECT:SEMXr2116 Gen IF (0 LAHKA OR ORGANIZATION)(1 MANNU)(2 Ord OR CARDINALS)(3 ("beaivi") OR ("b"))(4 Num)(*5 N BARRIER NOT-NPMOD LINK NOT *1 FMAINV OR Inf BARRIER S-BOUNDARY);
	#$ Dás čujuha lávdegoddi muđui Sámedikki miessemánu 30. beaivvi 2000 doalloplána čuoggái 13.1.


# KLASS


#!! * __SEMtext__ (modifier) selects genitive (modifier) if you are a TEXT in front of KLASS ''doalloplána čuoggái''
SELECT:SEMtext Gen IF (0 TEXT OR ABSTR-TEXT LINK 0 Acc)(1 KLASS);
	#$ Dás čujuha lávdegoddi muđui Sámedikki miessemánu 30. beaivvi 2000 doalloplána čuoggái 13.1.


# GIELLA


#!! *  __SEMgiella1__ (modifier) selects Gen if you are a LANGUAGE in front of LESSON or SATNI ''sámegiela oahpahusa''
SELECT:SEMgiella1 Gen IF (0 LANGUAGE LINK 0 Acc)(*1 LESSON OR SATNI OR ("dilli") OR ("prošeakta") OR ("geavaheapmi") OR ("doaibmaplána") OR ("ovdáneapmi") OR ("sajádat") OR ("korrektur-#prográmma") OR ("dutkan#dilli") OR ("vuohki") BARRIER NOT-NPMOD LINK NOT 0 Nom OR Acc LINK NOT 1 ("ektui") OR ("olis") OR ("dihte"));
	#$ Muhtun mánát geat eai lean hárjánan sámegiela hállat, čehppo dađistaga sámegiela geavaheamis.
	#$ Mus ii leat dát áviisačálus, muhto mun ádden du vástádusas ahte muhtun lea čállán sámegiela oahpahusa birra Kárášjoga nuoraidskuvllas.
	#$ Ulbmil lea čuovvulit sámilága áigumušaid ja vuoigatvuođaid sámegiela viidásut geavaheamis.

#SELECT:r2119 Gen IF (*-1 TRANS-V BARRIER SV-BOUNDARY)(0 LANGUAGE LINK 0 Acc)(*1 LESSON OR ("dilli") OR ("prošeakta") OR ("geavaheapmi") OR ("doaibmaplána") OR ("ovdáneapmi") OR ("sajádat") OR ("korrektur-#prográmma") BARRIER NOT-NPMOD);
	#$

#!! * __SEMsamegiella__ selects Gen for LANGUAGE if *1 is LESSON
SELECT:SEMsamegiella Gen IF (0 LANGUAGE + Gen)(*1 LESSON OR ("analysa") BARRIER NOT-Attr)(NEGATE *1 LESSON LINK 1 ("ja"));
	#$ Gávpogis lei sámegiela oahpahus dušše ovtta skuvllas.
	#% Ráđđehus deattuha sámegiela ja sámi kulturáddejumi.

#SELECT:SEMr2120 Gen IF (0 LANGUAGE LINK 0 Acc)(1 SATNI);
	#$

#SELECT:SEMr2121 Gen IF (0 LANGUAGE LINK 0 Acc)(1 ("nubbi") OR ("vuosttaš") OR Ord OR ("a") OR ("b") OR ("c"))(2 ("giella") LINK NOT 0 Ess);
	#$

#!! * __SEMlang__ removes Gen if LANGUAGE is to the right, but not if you are ACTOR-ROLE and so on ''oahpponeavvuid sámegillii''
REMOVE:SEMlang Gen IF (0 N LINK NOT 0 ACTOR-ROLE OR ABSTR-PLACE OR TIME OR TEXT OR ABSTR-TEXT OR SETTLEMENT-PLACE OR EMOTION OR ACTIVITY)(1 LANGUAGE)(NEGATE -1 Ger);
	#$ Son lea maid leamaš mielde ráhkadeame oahpponeavvuid sámegillii ja lea jorgalan oahppogirjjiid sámegillii.
	#% Mun ohppen sin áddestalladettiin máttanorgga suopmana hállat.

#!! * __SEMlang2__ Gen if you are LANGUAGE with 1 N: You are only a modifier in a sentence with a TV-verb, if there is an Acc or Com between you, or if the Obj is topicalized ''ráhkadii sámegiela Áppesa''
SELECT:SEMlang2 Gen IF (0 LANGUAGE + Gen)(*1 N BARRIER NOT-A)(NEGATE *1 Sem/Time BARRIER NOT-Attr)(NEGATE *-1 TRANS-V CBARRIER Acc OR Com LINK NEGATE *-1 Acc BARRIER S-BOUNDARY)(NEGATE 1 COMMA LINK 1 Acc);
	#$ Báhpaeamit, Margrethe Wiig, gii lei gasku giellamoivvi, ráhkadii iežas sárgumiiguin sámegiela Áppesa, mii lei stuorra ávkin sámegiela oahpahusas.
	#$ Doaibmaplána lea bidjan buori vuođu ja leamaš mielde rievdadeame miellaguottuid sámegiela geavaheami ja oainnusin dahkama ektui.
	#% Moai hálidetne oahppat sámegiela universitehtas.
	#% Dát joavku galggašii beassat geavahit iežas giela dearvvašvuođabargiiguin.
	#Comment1: This rule was made so that SEMsapmiModifier would not be destoryed. It suffered from adding LANGUAGE to it (because then we needed the NEGATE).
	#Comment2: You are only a modifier in a sentence with a TV-verb, if there is an Acc or Com between you, or if the Obj is topicalized (as in: don't negate if you can't find an Acc between you but instead there is an Acc to the left of the TV-verb).


#!! * __SEMgiella2__ Gen if you are Pron followed by giella ''iežas giella''
SELECT:SEMgiella2 Gen IF (0 Refl OR Pers)(1 ("giella"));
	#$ Dát joavku galggašii beassat geavahit iežas giela dearvvašvuođabargiiguin.
	#$ Sii hállet iežaset giela.



# SÁMEDIGGI / STÁHTA


#REMOVE:SEMr2124 Gen IF (0 (N Acc) LINK NOT 0 ("Norga") OR NATION-NOT-NORGA OR HUMAN)(1 ("Sámediggi") OR ("stáhta") OR ("stáhtta") OR ("stáda"));
	#$ Departemeanttat buktet jahkásaš mearkkašumiideaset Sámedikki doaimmaid hárrái.

#SELECT:SEMr2126 Gen IF (0 ("Sámediggi"))(1 Num)(2 ("bušeahtta"));
	#$

#SELECT:SEMr2127 Gen IF (0 ("stáhta") OR ("stáhtta") OR ("stáda") LINK 0 Acc)(1 ("sápmi" Gen) OR ("Sápmi" Gen))(2 ("joatkkaskuvla"));
	#$

SELECT:SEMstahta Gen IF (-1 FYLKA LINK 0 Gen)(0 ("stáhta") OR ("stáhtta") OR ("stáda") LINK 0 Acc)(1 ("oahpahuskantuvra"));
	#$

#SELECT:SEMr2130 Gen IF (0 ("Sámediggi" Acc))(*1 SAMEDIGGI-GEN OR TEXT OR ABSTR-TEXT BARRIER NOT-NPMOD LINK NOT 0 Ess);
	#$ Sámediggi lea geigen jahkedieđáhusas departementii Sámedikki doaimmas 2005.

#!! * __SEMstahta1__ Gen if 0 stáhta 1 org etc.
SELECT:SEMstahta1 Gen IF (0 ("stáhta") OR ("stáhtta") OR ("stáda") OR ORGANIZATION LINK 0 Acc)(*1 STAHTA-GEN OR ORGANIZATION OR TEXT OR ABSTR-TEXT OR INSTITUTION OR RIEKTEDILLI BARRIER NOT-NPMOD OR COMMA OR CC OR (":") OR Dem LINK NOT 0 Ess);
	# TEXT and ABSTR-TEXT are perhaps too big.
	#$ Heiveheami ovddidit ovttas Sámedikkiin ja Davvi-Norgga spesiálapedagogalaš birrasiiguin, stáhta spesiálapedagogalaš doarjjavuogádagain ja Læringssenteret:iin.


# FYLKA

#!! * __SEMfylka1__ Gen if you are FYLKA followed by fylka ''Romssa fylkkasuohkan''
SELECT:SEMfylka1 Gen IF (0 FYLKA + Acc)(1 ("fylka") OR ("fylkkagielda") OR ("fylkkasuohkan") OR ("ámta"));
	#$ Romssa fylkkasuohkan lea dahkan Ráissá joatkkaskuvlla sáme- ja suomagieloahpahusa ovdavázzinskuvlan.
	#Comment: Isn't this actually taken care of by GenGYLKA3?

#!! * __SEMfylka2__ Gen if you are FYLKA, then "ja" to the right followed by FYLKA ''Finnmárkku ja Romssa fylkkagielddaide''
SELECT:SEMfylka2 Gen IF (0 FYLKA + Acc)(1 CC)(2 FYLKA + Gen);
	#$ Giellanjuolggadusat leat váikkuhan vaikkoba Finnmárkku ja Romssa fylkkagielddaide nu, ahte leat váldigoahtán ovdan dákkár áššiid.

#!! * __SEMfylka3__ Gen if FYLKA and some place or org to the right ''Finnmárkku ássiide''
SELECT:SEMfylka3 Gen IF (0 FYLKA + Acc)(*1 GEOGRAPHICAL-PLACE OR GEN-POLITICAL-PLACE OR HUMAN OR DILLI OR INDUSTRY OR RIEKTEDILLI OR ORGANIZATION BARRIER NOT-NPMODADVII); # NPNHAI
	#$ Prinsihppa ahte láhka ii lihkahala sajáiduvvan vuoigatvuođaid gusto maiddái dážaide, kvenaide ja eará Finnmárkku ássiide.
	#$ Álbmotrievttálaš ja historjjálaš ođđa riektegáldoosiid ferte árvvoštallat stáhta dálá geavada ja riikka lágaid ektui, erenoamážit Finnmárkku eatnamiid vuoigatvuođaid deasttas.


	# OTHER

#!! * __topGEN__ (modifier) selects Gen if sentence-intitial. To the right a perfect patrticiple that modifies nominative ''Stáhta nammadan láhtu''
SELECT:topGEN Gen IF (-1 BOS)(0 Gen LINK 1 (@>N) + PrfPrc LINK 1 Nom)(NEGATE 0 ABBR) ;
	#$ Stáhta nammadan láhttu ja sadjásašlahttu galgaba deavdit gáibádusaid, maid Lahtut ja sadjásašlahtut nammaduvvojit gitta njealji jagi rádjai háválassii.
	#$ Skuvllaviesu báhcán fiellut ja ávdnasat dolvouvvojedje Goalsevuohpái ja adnoje doppe skuvlii.
	#$ Plána celkojuvvon mihttomearri, juobe vuosttaš veršuvnnas, lei bajásgeassit ođđa sámebuolvvaid.
	#% Ollesdálveskuvlla ožžon easka go jagi maŋŋeil mannen Kárášjohkii vázzit Sámi álbmotallaskuvlla.

#!! !Gen and preposition/postposition


#!! * __GenAPP__ Selects genitive when a preposition to the left, or when a postposition to the right ''rastá riikarájiid''
SELECT:GenAPP Gen IF ((*-1C Pr BARRIER NOT-NPMOD) OR (*1C Po BARRIER WORD))(NEGATE 0 NUM + (Sg Acc) LINK -1 Pr LINK 2 Gen) ;
	#$ Beana goarjádii beavddi vuollái.
	#$ Seammás sáhttet riikat nannet luohttámuša ođđa konvenšuvdnii ja nu hukset buori ovttasbarggu rastá riikarájiid.
	#% Vuosttasnaššuvnnaid čoagganeapmi ovddasta badjel 300 indiánaservodaga miehtá Kanada ja gáibida dál iešstivrejumi Davvi-Amerihká buot eamiálbmogiidda.


#!! * __NomIfPo__ removes Nom if sentence initial, because it modifies Gen
REMOVE:NomIfPo Nom IF (-1 BOS)(1 Gen LINK 1 Po)(3 COPULAS + FMAINV LINK NOT *1 A BARRIER NOT-ADV-PCLE);
	#$ Pippi viesu bálddas lei nubbi gárdin ja nubbi viessu.
	#% Olmmái háma duohken hehtte suoládeami.


#!! * __GenPoCoordPunct__ Selects genitive for coordinated postpositions: with PUNKT to the left
SELECT:GenPoCoordPunct Gen IF (1 PUNCT-RIGHT LINK *1 Gen BARRIER NOT-NPMOD LINK *1C Po BARRIER WORD) ;
	#$

#!! * __GenPoCoord__ Selects genitive for coordinated postpositions ''ráŋggáštusa ja buhtadusa hárrái''
SELECT:GenPoCoord Gen IF (1 CC LINK *1C Gen BARRIER NOT-NPMOD LINK *1C Po BARRIER NP-BOUNDARY OR Nom)(NEGATE -1 Gen LINK 2 CC LINK 1 Gen LINK 1 Acc) ;
	#$ Ovdamearkan sáhttá namuhit ahte dábálaš mearrádusat ráŋggáštusa ja buhtadusaid hárrái bohtet prinsihpalaččat go dahkkojuvvojit dagut mat eai leat gullevaččat boazodollui.
	#$ Dárkkisteami maŋŋil gárte dan guovtti riikka boazodoallolágat measta áibbas ovttaláganat viidodaga ja sisdoalu dáfus.
	#% Lávdegoddi lea maiddái atnán áiggi ságastallat davviruoŧa čearuid ja Romssa boazoorohagaid gaskaneas váttisvuođaid birra.

#SELECT:GenPoCoord2 Gen
	#%Vuosttaš láđđasis njealját lađđasii gustojit seamma láhkai áššiide meahcceeatnamiid dahje vuoigatvuođaid meahcceeatnamiin luobaheami ja láigoheami birra.
	#$ Goalmmát oasis lea bargosajiid ja bargoeallima eavttuid birra, njealját oasis lea fidnooahpuid, giehtaduoji ja boaittobeali báikkiid ealáhusaid birra.

#!! * __GenGenPo__ (modifies pp-phrase) selects Gen in front of postposition-phrase ''álgojagiid soađi maŋŋá''
SELECT:GenGenPo Gen IF (1C Gen - Pron)(2C Po)(NEGATE -1 (Pron Refl))(NEGATE 0 Acc LINK *-1 TRANS-V BARRIER Acc OR SV-BOUNDARY)(NEGATE 0 Acc LINK *-1 (@CNP) BARRIER NOT-NPMOD LINK -1 Acc)(NEGATE 0 Adv OR PrfPrc OR VFIN OR Nom)(NEGATE 2 Px)(NEGATE 0 Acc LINK 1 (Pron Rel))(NEGATE 0 (Pron Dem) OR (A Ord) OR Sem/Time)(NEGATE 0 BODY + Acc LINK *-1 Prop + Gen OR Pron BARRIER NOT-Attr LINK *-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY);
	#$ Internáhtta ii sáhte leat ruovttu sajis, muhto álgojagiid soađi maŋŋá lei olbmuin gárži ruovttuin ja ovttahat biebmu.
	#$ Dat ii leat hábmejuvvon sámi boazodoalu beroštumiid mielde.
	#$ Muhto leimmet gal bivnnuhat gili nieiddaid gaskkas.
	#% Sii galget hábmet iežaset oaiviliid historjjá birra.
	#% Ledjen optimista ja dollen logaldallamiid ášši birra.

#!! * __GenORG__ (modifies Loc) selects Gen if you are MAIN-ORGANIZATION and to your right is Loc ''dearvvašvuođafitnodagaid jahkedieđáhusain''
SELECT:GenORG Gen IF (0 MAIN-ORGANIZATION + Gen)(1 SAMEDIGGI-GEN + Loc) ;
	#$ Sámediggi sáhttá maid buktit cealkámušaid guovlluid dearvvašvuođafitnodagaid jahkedieđáhusain ja plánaárvalusain.
	#$ Evttohus bođii suohkana politihkkariin.
	#$ Ledje stuora hástalusat oahpahusráđi barggus ja lei somá bargagoahtit Edela buori bargovuođu ala.

#!! * __SEMnom__ (modifies Nom) removes Acc if sentence boundary or adv to the left. To the right is Nom followed by a transitive verb and Acc''stálu beana njoallu háviid''
REMOVE:SEMnom Acc IF (-1 Adv OR BOC OR S-BOUNDARY)(1C (N Nom))(2 TRANS-V + FMAINV)(NEGATE 0 (Pron Dem))(*3 Acc BARRIER COMMA OR S-BOUNDARY);
	#$ Jus stálu beana njoallu stálu háviid, de hávit savvojit.
	#$ Mánáidgirjjiin muitaluvvo dávjá govt olbmuid mánát deivet nuppi máilmmi ássiid.
	#$ Boazodoallu gal lea dorvvastan suddjemiidda go dat oaidná movt eatnamat ja meahcci gáržžiduvvo go ruđa fápmu vuoitá.
	#$ Ehpet dii leat ieža sárdnumin, muhto Áhčádet Vuoigŋa sárdnu din bokte. #Comment: Didn't manage to salvage this.


#!! !Genitive in place adverbials ROUTE


#!! * __GenPlc__ Selects genitive if you are ROUTE, and there is a MOVEMENT-V to your left or right ''boahtiba dán geainnu''
SELECT:GenRoute Gen IF (*0 MOVEMENT-V BARRIER SV-BOUNDARY OR COMMA OR NP-HEAD-ACC - (Pron Dem))(0 ROUTE)(NEGATE -1 ("okta"))(NEGATE *-1 JOHTIT + IV BARRIER SV-BOUNDARY LINK *1 ROUTE BARRIER NOT-NPMOD) ;
	#$ Soai boahtiba dán geainnu.
	#$ Fertiimet vázzit bálgáid geasset ja Deanu mielde dálvet.
	#$ Mii vujiimet duon luotta.


#! * Selects accusative if you are ROUTE, and the verb čuovvut to the left.
SELECT:AccRoute ROUTE + Acc IF (*-1 ("čuovvut") BARRIER SV-BOUNDARY OR (N Acc) LINK 0 FMAINV OR Inf) ;
	#$ Son rávve olbmuid čuovvut skohtermáđii dárkilit go sáhttá leat suddi goappat beale luotta.

#! * __ruovttoluottaAdv__
SELECT:ruovttoluottaAdv ("ruovttoluotta" Adv)((0 Acc LINK -1 TRANS-V LINK 2 Acc) OR (NEGATE *0 TRANS-V)) ;
	#$ Eiseválddit gesse ruovttoluotta dán gáibádusa.

#!! !Adjectives take object
SELECT:oazzulat (N Acc) (-1 ("oažžul")) ;
	#$ Mánát leat oažžulat guliid.

#!! !Temporal adverbials: Choosing accusative or genitive TIME

#!! * __GenMannuOrdRight__ selects Gen if you are mannu and to your right is A Ord ''miessemánu 10.''
SELECT:GenMannuOrdRight Gen IF (0 MANNU LINK 1 Ord)(NEGATE 0 (Sg Gen) LINK -1 Num) ;
	#$ Mu riegádanbeaivi lea čakčamánu 3. b.
	#$ Juo cuoŋománu 10. beaivvi ija vuostá mátkkoštii Ruvdnaprinseassa Märtha badjel ráji Ruŧŧii oktan Ruvdnaprinsabára golmmain mánáin.

#!! * __GenMannuOrdLeft__ selects Gen if you are mannu, to your left is Ord and to your right is a numeral
SELECT:GenMannuOrdLeft Gen IF (-1 Ord)((0 MANNU LINK 1 Num) OR (0 ("beaivi") OR ("vahkku"))) ;
	#$ 23. cuoŋománu 1728 dovddus geaskkus lea ee. celkon ahte mearraguollebivdu lea luđolaš earret
	#$ Evaluerenraporta addojuvvui GOD:ii geassemánu 20. beaivvi 2000.
	#$ Geassemánu 8. beaivvi 2001 láhkaásahus nr 676 oktagaslaš plánaid birra dearvvašvuođalágaid vuođul lea addojuvvon láhkamearrádusaid láhkavuođuin.

#!! * __JahkeNumNom__ selects Nom if you are Num, to your left is beaivi, then ord/Num and then mannu ''borgemánu 1. b. 1891''
SELECT:NumNomMannu Nom IF (-3 MANNU)(-2 Ord OR Num)(-1 BEAIVI)(0 Num);
	#$ Sámedikki bargiidčoahkkimis Guovdageainnus juovlamánu 7. beaivvi 2001 mearridii...
	#$ Maiddái borgemánu 1. b. 1891 čuovvu njuolggadusain.
	#$ Juovlamánu 1. beaivi 1972 lei buorre beaivi.
	#$ Carsten Smith guđii Sámi vuoigatvuođalávdegotti ovdaolbmo virggi skábmamánu 1. beaivvi 1985.

#!! * __GenBoahtte__ selects Gen if you are time, to your left is boahtte, boahtit, čuovvovaš or ovddit
SELECT:GenBoahtte Gen IF (-1 ("boahtte") OR ("boahtit" PrsPrc) OR ("čuovvovaš" Attr) OR ("ovddit" Attr))(0 TIME)(NEGATE 0 Px) ;
	#$ Boađe boahtte vahku.
	#Comment: Assuming that the time-expression following those words will be in an idiomatic construction that implies a point of time rather than a period


#!! * __TIMEobs__ selects Gen if you are time, and to your right is an intransitive real-verb. No adverbials allowed to the right ''vuolggán bearjadaga''
SELECT:TIMEobs TIME + Gen IF (*-1 REAL-V BARRIER NOT-ADV-PCLE LINK NOT 0 TRANS-V OR ("orrut"))(NOT 1 ADVL)(NEGATE 0 VAHKKU-DUR + Acc)(NEGATE 0 Nom LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(NEGATE 0 Num) ;
	#$ Mun vuolggán bearjadaga ja boađán duorastaga.
	#$ Mánát leat olgun čierastallame juovlabasiid, ja sáhttet čierastit njuolgga stálu sehkkii.
	#$ Gursa bistá miessemánu beallemuddui.
	#$ Mun boađán bearjadaga.
	#% Mii leat sullii diimmu fávllis ovdalgo suhkat gáddái.
	#% Arkiivaávdnasat galget sáddejuvvot go leat sullii 25-30 jagi boarrásat.

#!! * __GenGuhte__ selects Gen if you are vahkku with guhte to your left ''guđe beaivvi''
SELECT:GenGuhte Gen IF (-1 ("guhte" Gen))(0 VAHKKU LINK 0 Acc);
	#$ Dieđátgo guđe beaivvi mis lea čoahkkin?

#!! * Nom_b_1 selects Nom if you are b/beaivi with a numeral/Ord to your left and a mannu to the left of that. To your right a finite verb čuovvut
SELECT:Nom_b_1 Nom IF (-2 Gen LINK 0 MANNU)(-1 Num OR Ord)(0 ("b"))(*1 VFIN + Sg3 BARRIER REAL-WORD-NOT-ABBR LINK 0 ("čuovvut"));
	## Maiddái borgemánu 1. b. 1891 čuovvu njuolggadusain.
	#Comment: Not in use?  b. gets now only Adv analysis

#!! * Nom_b_2 selects Nom if your are b with a numeral/Ord to your left and a mannu to the left of that. To your right copulas followd by beaivi in nom-case ''juovlamánu 1. b. 1972 lei buorre beaivi''
SELECT:Nom_b_2 Nom IF (-2 Gen LINK 0 MANNU)(-1 Num OR Ord)(0 ("b"))(*1 COPULAS + Sg3 BARRIER REAL-WORD-NOT-ABBR LINK *1 BEAIVI-REF + Nom);
	## Juovlamánu 1. b. 1972 lei buorre beaivi.
	#Comment: Not in use? b. gets now only Adv analysis

#!! * __Nom_b_3__ selects Nom if you are b/beaivi with Num/Ord to your left, with mannu to the left of that, with copulas even futer to the left and beaivi to the left of copulas
SELECT:Nom_b_3 Nom IF (-4 BEAIVI-REF + Nom)(-3 COPULAS + Sg3)(-2 Gen LINK 0 MANNU)(-1 Num OR Ord)(0 ("b"));
	## Mu riegádanbeaivi lea čakčamánu 3. b.
	#Comment: Not in use? b. gets now only Adv analysis

#!! * __aigiAcc__ Gen if 0 TIME 1 áigi
SELECT:aigiAcc TIME + Acc IF (NEGATE -1 Num)(1 ("áigi" Adv)) ;
	#$ Son bođii jagi áigi.


#!! * __GenBeaivi2__ selects Gen if you are beaivi with the end of the sentence or comma to your right. Restrictions to the left ''riegádanbeaivvi,''
SELECT:GenBeaivi2 Gen IF (NEGATE *-1 STRICT-TRANS-V OR DURATION-V BARRIER SV-BOUNDARY OR DURATION-A LINK NOT 0 ("leat"))(0 BEAIVI-REF)(1 EOS OR COMMA LINK NOT *1 REAL-V);
	#$ Mii leimmet doppe mu riegádanbeaivvi, čakčamánu 3. b.
	#$ Mun vuolggán ihttin, bearjadaga.
	#$ Mun láven basadanlanja bassat juohke bearjadaga.

#!! * __GenBeaivi3__ selects Gen if you are beaivi with the beginning of the sentence to your right ''Bearjadaga mii vuolgit''
SELECT:GenBeaivi3 Gen IF (-1 BOC OR NP-BOUNDARY)(0 BEAIVI-REF LINK NOT 0 ("beaivi"));
	#$ Bearjadaga mii vuolgit oaggut.
	#$ Mánnodagaid gehčče vel Sámi mánáid-TV ovttas mánáiguin.
	#$ Giellatekno álggahuvvui 1.8.2001 prošeaktan ráhkadit sámegiela grammatihkalaš analysa.

#!! * __GenBeaivi4__ selects Gen if you are beaivi with a NP-boundary to your right
SELECT:GenBeaivi4 Gen IF (-1 NP-BOUNDARY)(0 BEAIVI-REF - ("beaivi")); #Not in use?

#!! * __GenJuohke__ selects Gen if juohke or seamma to the left ''juohke dálvvi''
SELECT:GenJuohke Gen IF (-1 ("juohke") OR ("seammá") OR ("seamma"))(0 TIME);
	#$ Mun ledjen doppe juohke dálvvi.
	#$ Son lei doppe seamma dálvvi go don.
	#$ Juohke giđa sii lávejedje bassat dáhki ja seinniid.

#!! * __GenJahkiNum__ selects Gen if you are jahki num with a numeral to your right ''Skuvlajagi 1998-99''
SELECT:GenJahkiNum Gen IF (0 JAHKI-NUM)(1 Num);
	#$ Skuvlajagi 1998-99 rivde doarjjajuohkima eavttut.
	#$ Soaŋu-girji, mas leat noveallat, bođii jagi 1971.
	#$ Son vuittii Davviriikkaid Ráđi girjjálašvuođabálkkašumi jagi 1991 dán girjji ovddas.

#!! * __AigiModifier__ (modifier) selects Gen if aigi to the right ''konferánssa áiggi''
SELECT:AigiModifier Gen IF (NEGATE 0 REAL-V)(1 ("áigi"))(NEGATE 1 (Sg Loc) LINK 1 ("áigi" Sg Ill));
	#$ Čoahkkimis lea lávdegoddi árvalan konfereanssa áiggi, báikki ja fáttáid.
	#$ Soađi áigge duiskalaččat bolde eanaš viesuid Finnmárkkus ja Davvi-Romssas.
	#$ Organisašuvnna bláđđi man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.
	#$ Son riegádii gonagasa Herodesa áiggi.
	#$ Dán áiggi sii johtet biillaiguin, jos headjavissui lea guhkit mátki.

#!! * __GenHávvi__ selects Gen for hávvi if Acc somewhere to the right
SELECT:GenHávvi Gen IF (0 ("hávvi"))(*1C Acc BARRIER S-BOUNDARY);
	#$ Váilevaš vejolašvuohta doaimmahit kultuvrra ávnnaslaš beliid sáhttá muhtun háviid mearkkašit álbmotriekterihkkuma.


#!! * __GenHávvi2__ selects Gen for hávvi if a transitive verb cannot be found somewhere in the sentence
SELECT:GenHávvi2 Gen IF (0 ("hávvi"))(NEGATE *0 TRANS-V BARRIER SV-BOUNDARY);
	#$ Fágalaš sudjadeapmi daid háviid go doavttir lea dahkan biehttaleami.
	#$ Eará háviid ii leat hoahpu journálačállimiin.

#!! * __GenGeardi__ selects Gen if the beginning of the sentence to the left ''Eará háviid''
SELECT:GenGeardi Gen IF (*-1 BOC BARRIER NOT-NPMODADV)(0 ("áigi") OR ("bodda") OR ("geardi") OR ("hávvi"));
	#$ Eará gerddiid ii dáhpáhuvvan nu go álggus leimme jurddašan.
	#$ Ovddeš áiggiid čoakkalmasviesut ledje nuoraid áidna deaivvadansadji.

#!! * __GenRbeaivi__ (modifier) selects Gen if riegádanbeaivi to your right
SELECT:GenRbeaivi HAB-ACTOR + Gen IF (1 GEN-BEAIVI);
	#$ Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.
	#$ Mii leimmet doppe bearjadaga, mu riegádanbeaivvi.

#!! * __GenGeardi2__ selects Gen for geardi if Num Gen to the left
SELECT:GenGeardi2 Gen IF (-1 (Num Gen))(0 ("geardi"));
	## Galggat golmma geardde beaivvis borrat ovtta tableahta.

#!! * __GenTimePl__ selects Gen for Sem/Time + Pl if an attribute to the left ''lagamus beivviid''
SELECT:GenTimePl TIME + (Pl Gen) IF (-1 Attr);
	#$ NVE dieđiha maid, ahte dáid lagamus beivviid eat dárbbaš Finnmárkkus ballat dulvvis.
	#$ Duodji lei deaŧalaš sivva manne olbmot Gáivuonas birgejedje nu bures váttes jagiid vuosttaš ja nuppi máilbmesoađi gaskkas.

#Time-accusative:
#!! * __GenDURadj1__ selects Gen if a duration adverbial to the left
SELECT:GenDURadj1 Gen IF (NEGATE -1 DURATION-A)(0 ("čakčageassi") OR ("giđđadálvi") OR ("čakčadálvi") OR ("giđđageassi") LINK 0 Acc);
	#$ Unjárgga guvlui, muitaluvvo ahte olbmot lávejedje čakčageasi, go lájuiguin gerge, mannat Oardajávrái ja Oardavuonjoga bajágeahčái Báhcavuona suohkanii bivdit dálvuoli.
	#Comment: Duomma says that those compounds typically are in Genitive and not accusative case if they are a time adverbial.

#!! * __GenDURadj2__ removes Gen for Sem/Time, if duration adjective to the left ''olles dálvvi''
REMOVE:GenDURadj2 TIME + Gen IF (*-1 DURATION-A BARRIER NOT-NPMOD OR Num) ;
	#$ Jus oahppi dahje ohppiidjoavku jávká lobihemiid osiid diimmus dahje olles diimmu.
	#$ Bohccot šaddet gárddis jorrat guhkes beaivvi 20-30 buolašgrádas.
	#$ Doai leahppi áŋgirit lohkan sámegiela olles dálvvi.

#Time-accusative:
#!! * __GenDURNumPl__ duháhiid jagiid
REMOVE:GenDURNumPl Gen IF (*0 PLACE-V OR DURATION-V BARRIER SV-BOUNDARY)(0 (Num Pl) OR ("moanat" Pl) LINK *1 VAHKKU-DUR + (Pl Gen) BARRIER NOT-A LINK NOT 1 Ill OR TIME OR Po OR PREGEN LINK NEGATE 0 Nom LINK 1 EOS)(NOT *-1 Dem OR TIME-A OR Ord OR Indef OR PREGEN OR Pr BARRIER NOT-NPMOD)(NOT 1 ("biras" Pl Loc));
	#$ Sápmelaččaid historjá álbmogin lea duháhiid jagiid boaris, muhto dálá ássanguovlluin sápmelaččat eai leat ássan nu guhká.
	#$ Son lei moanaid jagiid lullisámi skuvllajođiheaddjin.

#!! * __GenDUR1__ removes Gen for VAHKKU-DUR if duration verb or place verb somewhere in the sentence. Restrictions. ''ádjánii beaivvi''
REMOVE:GenDUR1 Gen IF (*0 PLACE-V OR DURATION-V BARRIER SV-BOUNDARY)(0 VAHKKU-DUR + Acc - Pl LINK NOT 1 Ill OR TIME OR Po OR PREGEN LINK NEGATE 0 Nom LINK 1 EOS)((NOT *-1 Dem OR TIME-A OR Ord OR Indef OR PREGEN OR Pr OR Num BARRIER NOT-NPMOD) OR (-1 ("moanat")))(NOT 1 ("biras" Pl Loc) OR STRUCTURE) ;
	#$ Mii leat sullii diimmu fávllis ovdalgo suhkat gáddái.
	#$ Skiippaguras Kárášjohkii ádjánii bures beaivvi, muhto vuloseano ii ádjánan go beaivvi go dábálaš olu čáhci lei (Fixdal 1938: 64).
	#$ Mii oruimet doppe jagi.
	#$ Beaivvi mun čohkkájin doppe.
	#% Sápmelaččat ledje orron dáin guovlluin 1600-logu birrasiin.
	#% 1700-logu álggus leai Suoma ja Ruošša gaskasaš soahti giksin.

#!! * __GenDURNum__ vázzen guokte maŋimuš jagi doppe
REMOVE:NomDURNum Nom IF (*0 PLACE-V OR DURATION-V BARRIER SV-BOUNDARY)(0 (Sg Num) LINK *1 VAHKKU-DUR + (Sg Gen) BARRIER NOT-A LINK NOT 1 Ill OR TIME OR Po OR PREGEN LINK NEGATE 0 Nom LINK 1 EOS)(NOT *-1 Dem OR TIME-A OR Ord OR Indef OR PREGEN OR Pr BARRIER NOT-NPMOD)(NOT 1 ("biras" Pl Loc));
	#$ Go jagi maŋŋil álggahuvvui Kárášjoga gymnása, de fárrejin ruoktot ja vázzen guokte maŋimus jagi dáppe.
	#$ Niillas lea máŋga jagi bargan sámi áššiiguin, ja Álttá-Guovdageainnu riiddus son lei okta nealgudeddjiin Stuoradikki olggobealde.
	#$ 1982:s son roasmmohuvai bávkilahttima oktavuođas Álttádulvadeami vuostá, váldojuvvui gitta, muhto báhtarii Kanadai, ja orui doppe álgoálbmogiid luhtte moadde jagi.

#!! * __GenDUR2__ removes Gen for VAHKKU-DUR if the duration verb or place verb to the left is perfectum participle or infinitive with an auxiliary to the left
REMOVE:GenDUR2 Gen IF (*-1 PLACE-V OR DURATION-V BARRIER SV-BOUNDARY LINK 0 PrfPrc OR Inf LINK *-1 AUX OR Neg BARRIER SV-BOUNDARY)(0 VAHKKU-DUR + Acc LINK NOT *-1 Dem OR TIME-A OR Ord OR Num BARRIER NOT-NPMOD LINK NEGATE *1 VAHKKU-DUR + Acc LINK 1 Po)(NOT 1 ("biras" Pl Loc));

#!! * __NoTimeAcc__ removes Acc for time if POINT-IN-TIME-SPEC or Ord to the left ''vuosttas beaivvi''
REMOVE:NoTimeAcc TIME + Acc IF (-1 POINT-IN-TIME-SPEC OR Ord);
	#$ Eará beaivvi soaitá čuožžut ahte galgat láibut bihpporgáhkuid jna.
	#$ Vuosttas čoahkkinbeaivvi deattuhuvvojedje muhtun ráji dat čuolmmat ja hástalusat Skandinavias leat ovddabealde boranávddiid hálddašeami oktavuođas.
	#$ Čalmmehis stálu maid stohke dološ áiggi.
	#$ Vuos galgá liinna seaktit, de suohpput ja nuppi iđida fas geassit dan.

#!! * __NoTimeAccII__ removes Acc for time if POINT-IN-TIME verb to the left
REMOVE:r2060 TIME + Acc IF (*-1 POINT-IN-TIME-V BARRIER SV-BOUNDARY);
	#$ Bivdu, eandalii beatnagiin bivdin, sáhttá hehttet boazobarggu čohkken- ja johtináiggi.

#!! * __NoTimeAccIII__ removes Acc for time if POINT-IN-TIME verb to the left is infinitive or perfectum participle with an auxilliary or negation to the left
REMOVE:r2061 TIME + Acc IF (*-1 POINT-IN-TIME-V  BARRIER SV-BOUNDARY LINK 0 PrfPrc OR Inf LINK *-1 AUX OR Neg BARRIER SV-BOUNDARY);
	#$
    # Comment: Can still be Acc @ADVL if standing after a DURATION-A
    # Comment: That is why these rules have to stand after the DURATION-A rule

#!! * __AccBeaivi__ removes Acc for relative pronouns if followed by general beaivi ''guđe beaivvi''
REMOVE:AccBeaivi Acc IF (0 (Pron Rel) LINK 1 GEN-BEAIVI) ;
	#$ Dieđátgo guđe beaivvi mis lea čoahkkin?

#!! * __timeADVL__ selects Gen for time: when perfectum participle or infinitive to the left are time adverbial verbs or not time object verbs, to the left of this there shall be an auxiliary ''lean čoavdán cealkagiid maŋimuš áiggi''
SELECT:timeADVL1 TIME + Gen IF (*-1 TIME-ADVL-V OR N-TIMEOBJECT-V BARRIER SV-BOUNDARY LINK 0 (PrfPrc) OR (Inf) LINK *-1 AUX BARRIER SV-BOUNDARY) ;
	#$ Skuvllas mun lean čoavdán ollu cealkagiid maŋimuš áiggi.
	#$ Sápmelaččat ledje orron dáin guovlluin 1600-logu birrasiin.

SELECT:timeADVL3 TIME + Gen IF (*-1 TIME-ADVL-V OR N-TIMEOBJECT-V BARRIER SV-BOUNDARY)(NEGATE 0 Acc LINK *-1 CC BARRIER NOT-NPMOD LINK -1 Acc LINK *-1 STRICT-TRANS-V) ;
	#$ Jugaheadnu sutnje viinni maiddái dán ija, ja mana don gis ja veallá suinna.
	#% Girjjis leat boares govat ja ođđa lyrihkka mat čatnet oktii doloža ja dálá áiggi.

SELECT:timeADVL4 TIME + Gen IF (**-1 Acc BARRIER SV-BOUNDARY LINK -1 TRANS-V + FMAINV) ;
	#$ Ja don ii dahkan maidege dan jagi, muhto bođii fas nuppi jagi, ja de ledje sus guoimmit mielde.

SELECT:timeADVL2 TIME + Gen IF (*1 FMAINV BARRIER SV-BOUNDARY)(NEGATE 0 Nom LINK *1 FMAINV + COPULAS OR FMAINV + DURATION-V BARRIER SV-BOUNDARY)(NEGATE 1 MANNEL)  ;
	#$ Dán geasi áiggun bargat.
	#% Eahket gollá jođánit.

SELECT:timeADVLacc TIME + Acc IF (NOT *-1 Num BARRIER NOT-A)(1 MANNEL)  ;
	#$ Go jagi maŋŋil álggahuvvui Kárášjoga gymnása, de fárrejin ruoktot ja vázzen guokte maŋimus jagi dáppe.

SELECT:GenIndef Indef + Gen IF (1C TIME + Gen) ;

SELECT:GenMuitalit Gen (0 (Pers Acc) OR (Refl Acc) LINK -1 ("muitalit") LINK 2 Loc) ;

REMOVE:AccINF-V Acc (0 Gen LINK -1 INF-V)(1 N + ADVLCASE LINK 1 (IV Inf)) ;


	# Accusative sentence-finally
	# ---------------------------

	# Removed this one because it made trouble. The example function although.
	#REMOVE Gen IF (NEGATE *-1 Pr OR Num OR CC BARRIER NPNH)(0 N OR Pron)(NEGATE 0 TIME)
 #	(*1 EOC2 BARRIER NOT-ADV-PCLE) ;
	## Ferte čájehit várrugasvuođa go lea sáhka sihkkumis dakkár dieđuid.

#SELECT:TESTr2021 Acc IF (*-1 NP-HEAD-ACC BARRIER VFIN LINK *-1 TRANS-V BARRIER NOT-NPMOD)(*-1 NP-HEAD-NOM BARRIER NOT-ADV-PCLE)(0 Gen LINK NOT 0 Loc)(1 EOS);
	## Unna mánážat dulbmet eatni askki, stuora mánát fas váimmu.


 # CC in the preceding rule because of coordination after Pr. The following rule compensates for it.

#!! * __theAccusative___ selects Acc if you are a N or Pron with CC to your right, followed by Acc and a CLB or VFIN ''gápmagiid ja vuoddagiid, sii geavahedje''
SELECT:theAccusative Acc IF (NEGATE *-1 Pr OR Num BARRIER NOT-NPMOD)(0 NOTIME OR Pron)(NEGATE 0 Nom OR Loc OR Adv OR NOT-REAL-V OR (Prop Plc))  (*1 CC BARRIER NOT-ADV - ABBR LINK *1 Acc BARRIER NOT-NPMOD LINK *1 CLB OR VFIN OR Attr - ("gaskaneas") OR @CVP BARRIER NOT-ADV-PCLE LINK NOT *1 Gen BARRIER NOT-NPMOD)
(NEGATE -1 COMMA LINK -1 Gen)(NEGATE -1 Gen LINK -1 COMMA LINK -1 Gen);
	#$ Muhtun sámi biktasiid, nugo gápmagiid ja vuoddagiid, sii geavahedje guhkit, gitta 1960-logu rádjai.
	#$ Ii oktage goit hálit billistit iežas birgejumi vuođu, sihke eatnamiid ja elliid.
	#$ Gielddaid, fuođđo- ja guolástaneiseválddiid jna. ja guoskevaš geavaheddjiid servviid galgá gullat ovddalgihtii.


SELECT:TESTr2023 Acc IF (NEGATE *-1 Pr OR Num BARRIER NOT-NPMOD)(0 NOTIME OR Pron)(NEGATE 0 Nom OR Loc OR Adv OR NOT-REAL-V OR (Prop Plc))(1 COMMA LINK *1 Acc BARRIER NOT-NPMOD LINK 1 CC LINK *1 Acc BARRIER NOT-NPMOD LINK *1 CLB OR VFIN BARRIER NOT-ADV-PCLE LINK NOT *1 Gen BARRIER NOT-NPMOD)(NEGATE 1 COMMA LINK 1 Gen LINK 1 CC OR COMMA);
    ## Ráđđehus áigu ruhtadit dehálaš oktasašdoaimmaid, nugo dulkaoahpu ja diehtojuohkinbálvalusa.
    ## Sii bukte skeaŋkkaid mánnái: golliid, suovvasiid ja myrra.

SELECT:TESTr2024 Acc IF (NEGATE *-1 Pr OR Num BARRIER NP-BOUNDARY)(0 NOTIME OR Pron)(NEGATE 0 Nom OR Loc OR Adv OR NOT-REAL-V OR (Prop Plc))(1 CC LINK 1 Gen LINK 1 Acc LINK *1 CLB OR VFIN BARRIER NOT-ADV-PCLE LINK NOT *1 Gen BARRIER NOT-NPMOD)(NEGATE -1 COMMA LINK -1 Gen)(NEGATE -1 Gen LINK -1 COMMA LINK -1 Gen);
    ## Sámit soitet erenoamážit amašit norpmaid nugo guhkes váccahagaid ja oktasašvuođa váiluma.

#SELECT Acc IF (NEGATE *-1 Pr OR Num BARRIER NOT-NPMOD)(0 NOTIME OR Pron)(NEGATE 0 Nom OR Loc OR Adv OR NOT-REAL-V)(*1C CLB OR VFIN BARRIER NOT-ADV-PCLE LINK NOT *1 Gen BARRIER NOT-NPMOD)(NEGATE 2 ("ii") LINK 1 Gen LINK 1 N);
	## Jus divššohas roasmmohuvvá dahje oažžu duođalaš váttisvuođaid,...
	## Liggen dihtii viesuideaset, ferte ollusat muoraid boaldit.

#!! * __NotGenitive__ selects Acc if you are a N or Pron with punctuation marks to your right, followed by a noun-phrase boundary
REMOVE:NotGenitive Gen IF (NEGATE *-1 Pr OR Num BARRIER NOT-NPMOD)(0 NOTIME OR Pron)(*1 PUNCT BARRIER NOT-ADV-PCLE LINK 1 NP-BOUNDARY) ;
	#$ Jos sámegiela universitehta eksámen (vuođđofága dahje gaskafága) ii adnojuvvo bálkábajidanfágan, de mii evttohat ahet sáthta juolluda stipeanddaid oahpaheddjiide geat váldet dán eksámena.
	#$ Huitfeldt - fidnii munnje olles govvaalbuma maid su eadni lei govvidan Finnmárkku-mátkkis iežas vánhemiiguin 1901:s.

REMOVE:TESTr2026 Gen IF (*-2 TRANS-V BARRIER S-BOUNDARY OR Pr OR Acc)(NEGATE *-1 Pr OR CC OR (Num Nom) BARRIER NOT-NPMOD)(-1 Gen)(0 NOTIME OR Pron)(NEGATE 0 ALU)(*1 CLB BARRIER NOT-ADV-PCLE)(NEGATE 1 COMMA LINK 1 Gen LINK *1 Po OR (N Ill) OR (N Loc) BARRIER V)(NEGATE -1 Gen LINK -1 CC LINK -1 Gen);
	## ...ahte vuhtiiváldit divššohasa dárbbuid, beroštumiid ja riektesihkarvuođa dearvvašvuođabálvalusa ektui.
	#% Danne lea ráđđehussii deaŧalaš sápmelaččaid oassálastin dálkkádatáššiid bargui ja lea maid deaŧalaš ahte sámit ásahusaideaset bokte sáhttet leat mielde ásaheamen máhttovuođu luonddu, birrasa ja dálkkádatrievdamiid oktavuođas. # oktavuođas is N Loc
	#% Juos mearriduvvo meahcceeatnamiid nuppástuvvan geavaheapmi golmma ja dušše golmma stivrralahtu doarjagiin ja olles uhcitlohku vuođđudallá oainnus sámi kultuvrra, boazodoalu, meahcásteami, ealáhusdoaimmaheami ja servodateallima destii mii lea árvvoštallojuvvon Sámedikki válddahusaid vuođul, de sáhttá uhcitlohku ovtta oli stivrračoahkkima áigge gáibidit ášši Sámedikki gieđahallamii. # destii is N Ill

REMOVE:TESTr2027 Gen IF (*-1 TRANS-V)(0 ("dat" Acc))(1 CC OR CS);
	## Šloahtta dieđiha gait suohkaniidda gos kruvdnoprinsabárra manná guossis dan ahte sii geat sihtet govvet mannet dohko gos preassagovvejeaddjit leat.
 	# Difficult to make a border...
#!! ! Reflexive pronouns: acc or gen?

REMOVE:genFinally Gen (0 Acc LINK NOT 0 Nom LINK NOT -1 Pr OR Num)(NOT *-1 REAL-V)(*1 EOC OR CS BARRIER WORD) ;
	#$ Naba daid stuora dorskiid, gosa daid bidjet?
	#$ Muhtun sámi biktasiid, nugo gápmagiid ja vuoddagiid, sii geavahedje guhkit, gitta 1960-logu rádjai.

# Genitive between "nugo" and N

#!! * __NUGOr2159__ selects Gen between nugo and N ''nugo suorri dulkaoahpu''
SELECT:NUGOr2159 Gen IF (-1 ("nugo"))(1 N LINK NOT 0 PLACE);
	#$ Ráđđehus áigu ruhtadit dehálaš oktasašdoaimmaid, nugo suorggi dulkaoahpu ja diehtojuohkinbálvalusa.
	#$ Muhto sierraortnegiid, nugo lasi klássaid juogadandiimmuid, eai ožžon jos skuvllat eai atnán iežaset giellaseahkalasskuvlan ja čáhkadan sámi kulturoahpu.

# Genitive in comparison

SELECT:COMPAr2169 Gen IF (NEGATE *-1 TRANS-V BARRIER NOT-ADV-PCLE)(NEGATE 0 V)(1 (A Comp Ess))(NEGATE 1 V OR Adv)(NEGATE 2 go);
	#$ Mun jáhkán du iežan falibun.
  	#$ Sámi servodat dárbbaša dárbbašlaš veahki hukset min iežamet oahpahusvuogádaga guhkit áiggi boahtteáiggis. #Comment: Does not hit this, even if it should!


#!! * __AccIEScoord__ selects (Pron Refl Acc) Acc in front of "ja" to the left. To the right Loc or Ill ''elliideaset ja iežaset ealáhussii''
#SELECT:AccIEScoord (Pron Refl Acc)(-1 ("ja") LINK -1 Acc)(1 (N Loc) OR Ill)(NEGATE -1 ("ja") LINK -1 Acc LINK -1 Gen) ;
	#$ Sii ožžot geavahit eatnama ja gáttiid ja elliideaset ja iežaset ealáhussii.
	#Comment: Is this perhaps too narrow? Might wanna make one for objects in general. Seems like a good idea! Anyhow, for now: this rule only works when Acc already has been chosen (remove C to see this).

#!! * __GenIES__ (modifier) selects (Pron Refl Gen) if NON-FAMILY OR ("bellodat") OR SAMEDIGGI-GEN to the right ''iežaset mánáide''
SELECT:GenIES (Pron Refl Gen) IF (*1 NON-FAMILY OR ("bellodat") OR SAMEDIGGI-GEN BARRIER NOT-Attr)(NEGATE *-1 OPRED-V BARRIER NOT-ADV-PCLE) ;
	#$ Riektavuođa dihtii berre namahuvvot ahte cealkámušas maiddái addui vejolašvuohta ásahit sierra linnjáid váhnemiidda geat dáhttot iežaset mánáide eanet sámegiela oahpahusa geavaheami.
	#$ Dan mun muitalan dušše dutnje, iežan buoremus olmmái.

#!! * __AccIES__ (modifier) removes accusative object (Pron Refl Acc) if Ill or Loc to the right, but not if a transitive verb is found to the left
REMOVE:AccIES (Pron Refl Acc) IF (NEGATE *-1 TRANS-V BARRIER Acc OR S-BOUNDARY)(NOT -1 CC)(1 Ill OR (N Loc) OR Inf);
	#$ Sáhttágo Finnmárkkuopmodat bidjat dárkilet eavttuid bivdui, guolásteapmái ja lubmemii iežas eatnamiin?
	#% Sii ožžot geavahit eatnama ja gáttiid ja elliideaset ja iežaset ealáhussii.

#!! * __GenIESinf__ removes (Pron Refl Gen) if a transitive verb to the left and an Inf to the right
REMOVE:GenIESinf (Pron Refl Gen) IF (*-1 TRANS-V BARRIER Acc OR S-BOUNDARY)(1 Inf) ;
	#$ Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.
	#$ 	#$ IL Nordlysa beaivválaš jođiheaddji, Nils Peder Eriksen, lohká iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.

REMOVE:AccIESPo (Pron Refl Acc) IF (NEGATE *-1 TRANS-V + FMAINV BARRIER Acc OR S-BOUNDARY)(*1 (N Gen) BARRIER NOT-Attr LINK 1 Po)(NEGATE 0 Sem/Time) ;
	#$ Dieđálin eanet Oslo go iežan gili birra.
	#$ Mii leat fitnan su luhtte ja bivdán su muitalit iežas skuvlavázzima birra.


#!! !Accusative object


#!! * __AccActioEss__ Selects accusative: when a Strict transitive verb actio ess to the left, but not if there is an other Acc to the right followed by EOS
SELECT:AccActioEss Acc IF (*-1 STRICT-TRANS-V + (Actio Ess) BARRIER SV-BOUNDARY OR Acc)(NEGATE 1 Acc OR Gen LINK 1 EOS) ;
	#$ Lea maid deaŧalaš ahte sámit ásahusaideaset bokte sáhttet leat mielde ásaheamen máhttovuođu luonddu, birrasa ja dálkkádatrievdamiid oktavuođas.
	#Comment: "Steals" some sentences from AccTV1 and 2, but the alternative is to make those rules even longer than they already are..

#!! * __AccEss__ removes Acc when you are SAPMI-N-HEAD with an Ess to your right, but not if there is an Loc to the right of Ess ''dutkama duogážin''
REMOVE:AccEss Acc IF (0 SAPMI-N-HEAD LINK 1C (N Ess))(NEGATE 0 SAPMI-N-HEAD LINK 1 (N Ess) LINK 1 Loc)(NEGATE 1 (N Ess) LINK 1 FMAINV);
	#$ Son láve bargat áviissa váldodoaimmaheaddjin.
	#$ 2. máilmmesoađi maŋŋá fuobmágohte dutkit sosiáladarvinisttalaš osiid ja nálalaš mearkkaid mat ledje ovddeš politihka ja dutkama duogážin.
	#% Dán váilevašvuođa vigge buoridit nuvttá sámegielkurssa fállama bokte ja bálkálasáhusaiguin sidjiide geat máhtte geavahit giela veahkkin oahpahusas.

# Main rules are: AccTV1, r2271, AccTV2, AccTV3, topOBJ1, topOBJ2, AccInf

#!! *__topOBJPers__
REMOVE:topOBJPers Gen (0 Acc LINK 1 Pron LINK 1 TRANS-V)(-1 BOC) ;
	#$ Gáhkuid mii goikadit geađggi alde.

#!! *__AccVAbess__
SELECT:AccVAbess Acc (0 (N Gen) LINK 1 (TV VAbess))(NEGATE -1 Num) ;

#!! * __topOBJ1__ Selects accusative: when a Strict transitive verb to the right (topicalized object) ''beaskka geavahedje''
SELECT:topOBJ1 Acc IF
(*1 STRICT-TRANS-V BARRIER SV-BOUNDARY OR COMMA OR XGO OR Acc LINK NOT 0 FAUXV OR VAbess LINK NOT *1 Acc - Rel OR ("ahte") OR Inf OR VAbess OR VFIN BARRIER S-BOUNDARY OR COMMA)
(NEGATE 0 (Sg Nom) - Px LINK *-1 (V Sg3) BARRIER SV-BOUNDARY LINK NOT *-1C Nom BARRIER SV-BOUNDARY)
(NEGATE 0 (Sg Nom) - Px LINK *1 (V Sg3) BARRIER NOT-ADV-PCLE)
(NEGATE 0 (Sg Nom) - Px LINK -1 CC LINK -1 Nom LINK *3 (V Du3) OR (V Pl3) BARRIER NOT-ADV-PCLE)
(NEGATE -1 NUM)(NEGATE -1 PrfPrc LINK NOT *-1 REAL-COPULAS BARRIER SV-BOUNDARY)
(NEGATE 0 PrfPrc OR Adv OR VFIN OR Loc OR NUM LINK NOT 0 Px)(NEGATE 0 Gen LINK 1 Nom)(NEGATE 0 Gen LINK 1 (@>N) + PrfPrc LINK 1 Nom) ;
	# Comment: topOBJ1 looks only for the strict-TV: it doesn't care where FAUX is, it doesn't need a finite mainverb, and these things seem to be an advantage. TopOBJ2 is actually more strict in that manner.
	#$ Áigubeahtti go iežade dálu vuovdit?
	#$ Beaskka geavahedje gávtti badjel.
	#$ Vuovssá goddá boanda.
	#$ Son mu nama ipmirdii boastut.
	#$ Dat gal lea čalmmehis ášši dan čuoččuhit.
	#$ Duosttažan go dan dahkat?
	#$ In mun gal mánnásan diekkár oahpaheaddji sávaše.
	#% Áššiin meahcceeatnamiid nuppástuvvan geavaheami birra galget stáhtalaš, fylkkagielddalaš ja gielddalaš eiseváldit  árvvoštallat, mo nuppástupmi váikkuha sámi kultuvrii, sboazodollui, meahccásteapmái, ealáhusdoaimmaheapmái ja servodateallimii.

#!! * __topOBJ2__ Selects Acc when a transitive finite mainverb to the right (less strict) ''dan juohkehaš fuobmá''
SELECT:topOBJ2 Acc IF
(*1 TRANS-V + FMAINV BARRIER SV-BOUNDARY OR COMMA OR @CNP OR Po OR XGO OR Acc LINK NOT 0 FAUXV OR VAbess OR Ger LINK NOT *1C Acc OR ("ahte") OR VAbess OR VFIN BARRIER S-BOUNDARY OR COMMA)
(*1 TRANS-V + FMAINV BARRIER SV-BOUNDARY OR COMMA OR @CNP OR Po OR XGO OR Acc LINK NOT 0 FAUXV OR VAbess OR Ger LINK NEGATE *1 (N Acc) BARRIER S-BOUNDARY OR COMMA LINK NOT 0 Nom LINK 1 EOC OR Dem)
(NEGATE 0 (Sg Nom) LINK NOT 0 Px LINK *-1 (V Sg3) BARRIER SV-BOUNDARY)
(NEGATE -1 PrfPrc LINK NOT *-1 REAL-COPULAS BARRIER SV-BOUNDARY)
(NEGATE -1 NUM)
(NEGATE 0 PrfPrc OR Adv OR VFIN OR Loc OR NUM LINK NOT 0 Px)
(NOT *-1 (N Acc) OR (Pers Acc) OR (Rel Acc) BARRIER SV-BOUNDARY)
(NEGATE 0 Nom - Px)
(NEGATE 0 Gen LINK 1 (@>N) + PrfPrc LINK 1 Nom)
(NEGATE 0 (Pron Refl) LINK 1 (N Ess))
(NEGATE 0 (Pron Pers) - ("dat" Pron Pers) LINK 1 (N Nom) + FAMILY-ONLY-HUMAN)
(NEGATE *-1 ("leat") + FMAINV BARRIER SV-BOUNDARY OR COMMA)
(NEGATE 0 (Dem Gen) LINK 1C (N Gen))
(NEGATE *1 OPRED-V + FMAINV BARRIER SV-BOUNDARY LINK 1 Ess LINK 1 Inf)(NEGATE -1 go + CS LINK NOT -1 BOS)(NEGATE 1 Nom LINK 1 VFIN LINK 1 Gen LINK 1 Po) ;
#(NEGATE *1 (N Nom) BARRIER NOT-Attr)
	#$ Dan gal juohkehaš fuobmá dakkaviđe.
	#$ Gáhkuid mii goikadit geađggi nalde goardagis.
	#$ Gos daid lehpet goldán?
	#$ Sarridiid mii gal borašeimmet.
	#$ Daid heasttaid son lohká heivet buoremusat deike galbma guovlluide.
	#$ Nillá-mága gasttihišgođii.
	#% Maiddái su eadnái ja villjii son attii divrras skeaŋkkaid.
	#% Ja álbmotčoahkkimat dollojit gos báikki olbmot besset buktit oaiviliid.
	#% Ehpet dii leat ieža sárdnumin, muhto Áhčádet Vuoigŋa sárdnu din bokte.

SELECT:topOBJ2CC Acc IF
(*1 (N Ill) BARRIER NOT-NPMOD LINK 1 CC LINK *1 (N Ill) BARRIER NOT-NPMOD LINK 1 TRANS-V + FMAINV LINK NOT 0 FAUXV OR VAbess OR Ger LINK NOT *1C Acc OR ("ahte") OR VAbess OR VFIN BARRIER S-BOUNDARY OR COMMA) ;
	#$ Mávssu lahtuide ja sadjásašlahtuide máksá dat orgána, mii lea nammadan guoskevačča.


#!! * __topOBJ3__ Selects Acc. It is not depending on a transitive verb like topOBJ1 and 2, but selects Acc when Aux to the left, but only if there is no chanse of it beeing a Nom
SELECT:topOBJ3 Acc IF (*-1 BOC BARRIER NOT-NPMOD)(0 Acc OR Gen LINK NEGATE 0 Nom)(1 FAUXV)(NEGATE 0 Sem/Time OR TIME LINK NEGATE -1 ("olles"))
(NEGATE *-1 Num BARRIER NOT-A) ;
	#$ Dán filmma leat Kárášjoga nuorat oaidnán, eaige šat vuolgge sirkusii.
	#$ Min ii leat veara dohko vuolgit.
	#$ Mii doarjut Sámelávdegotti evttohusa oainnu ahte sámegiela berre sáhttit válljet fágan dan sadjái go duiskkagiela gymnásaráhkkaneaddji linnjás nuoraidskuvlla ovccát jagis.
	#$ Prošeavtta leaba Beate Mortensen Nesheim ja Svanhild Andersen čállán.
	#$ Dáid ii galgga suovvat bargat joavdelasaid.
	#$ Sajáiduvvan fágadoahpagiid sáhttá dieđusge geavahit.

#!! * __AccTV1__ Selects accusative: when a Strict transitive verb to the left (barrier exludes everything but: adv, N Ess , N Loc and Pcle). No Acc allowed to the left of the verb. No Acc allowed to the right of you, except pronouns and education (sentenceboundary and N Ess as barriers). Only numunit numerals are allowed to the left. You are not Acc if you are: time, ruote or Pron Indef. Neither if you are Pron Refl with Gen to your right followed by N Ess. Neither if you are Pron Refl with Gen to your right followed by Po. N Nom and Ger not allowed immediatly to your right. You are not Acc if you are a Nom cased Prop and the verb is some kind of verbalactivityverb and ahte or sentenceboundary is to the right. Vdic not allowed immediately to your left. If váldit is the verb, you are likely to be a Gen if Ill-body noun is found to the right. ''oste mielkki gávppis''
SELECT:AccTV1 Acc IF
(*-1C STRICT-TRANS-V BARRIER NOT-NPMODADV - (N Ess) - (N Loc) - (Pcle) - (A Ess) LINK NOT *-1C Acc BARRIER S-BOUNDARY OR CC)
(0 NP-HEAD-ACC LINK NEGATE *1 Acc - Pron BARRIER S-BOUNDARY OR (N Ess) OR Ill LINK NOT 1 Ger)
(NEGATE *-1 NUM BARRIER NOT-Attr LINK NOT 0 NUMUNIT)
(NEGATE 0 (Pron Indef) OR Adv)(NEGATE 0 (Pron Refl) LINK 1 (N Ess))(NEGATE 0 (Pron Refl) LINK 1 Gen LINK 1 Po)
(NEGATE 1 (N Nom) OR Ger)
(NEGATE 0 FIRSTNAME + Nom OR (Prop Sem/Sur) + Nom LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V BARRIER SV-BOUNDARY LINK *2 ("ahte") OR EOC)
(NEGATE -1 <vdic>)(NEGATE *-1 ("váldit") BARRIER NOT-NPMOD LINK 1 Gen LINK 1 Ill + BODY) ;#(NEGATE 0 Prop + Gen OR Pron + Gen LINK *1 BODY + Acc BARRIER S-BOUNDARY)  ;
	#$ Moai oste mielkki gávppis.
	#$ Šiehtadus sisttisdoallá dárkilis evttohusa movt guohtuneatnamiid sáhttá atnit ávkin goabbat bealde riikaráji.
	#$ Soai oastiba bileahta su filbmii.
	#$ Mii fertiimet doalvut su buohccivissui.
	#$ Sin bargun lei váldit fáŋgan gonagasa.
	#$ Lean mun gal oaidnán dola rievdamin čázi alde Deanus, muhto dat leamaš vissa gobmi.
	#% Dieđálin eanet Oslo go iežan gili birra. #Hits Oslo, but this is Gen+Po.
	#% Raporttas galgá gozihanlávdegoddi čilget gozihandoaimmas, árvvoštallat stivrra jahkemuitalusa ja dilálašvuođaid maid dat siskkilda, ja árvvoštallat mo dát láhka doaibmá ja leago sávahahtti dan nuppástuhttit. #Hits láhka


SELECT:AccSTV Acc IF (*-1 STRICT-TRANS-V BARRIER SV-BOUNDARY - (N Ess) - (N Loc) - (Pcle) - (A Ess) LINK NOT *-1 Acc BARRIER S-BOUNDARY)(1 Acc LINK 1 (Ger TV)) ;
	#$ Stivrra lahttu, gii eaktodáhtus dahje sahtedohko lea dagahan Finnmárkkuopmodahkii manahusa luohttámušdoaimma čađahettiin, lea geatnegas buhttet manahusa.


#!! * __PronNP__ (removes Acc): selects Gen for Pron Pers if Acc or Ill to the right, given that there is a secure object or that no transitive verb is found ''bija ruđa mu kontoi''
REMOVE:PronNP Acc IF (0 PPRON-NOT-DAT + Gen)(NEGATE 0* TRANS-V + FMAINV BARRIER Acc OR S-BOUNDARY)(*1 Acc OR Ill BARRIER NOT-Attr LINK NOT 1 Inf) ;
	#$ Oinniide go Mihkkala, munno nuoramus bártni?
	#$ Bija fal ruđa njuolgga mu kontui.
	#$ Min márkanii lea ráhkaduvvon ođđa skuvla.
	#$ Mun oainnán fierpmi, su merkka, ja dieđán ahte Spider-Man lea leamaš das.
	#$ Mun lean náitalan, ja mu eamit bargá girdišiljus.
	#$ Áhkku láve boahtit min siidii čakčat.


#!! * __r2206__ selects Gen when a finite verb to the left and Nom or Acc to the right ''lohkaba su girjji''
SELECT:r2206 Gen IF (*-1 VFIN BARRIER N OR (Pers Pron) OR CLB)(0 N OR Pron LINK NOT 0 ("buorre") OR VFIN)(*1 (N Nom) OR (N Acc) BARRIER NOT-NPMOD OR PrfPrc OR CC OR PUNCT OR ("ollis") LINK NOT 0 Prop OR Loc OR PrfPrc)(NEGATE 0 Num OR Dem OR VGen OR PrfPrc OR PrsPrc OR Indef OR Interr OR (Pron Rel)OR Adv OR Sem/Time OR Loc OR (Actio Ess) OR Nom)(NEGATE *-1 TRANS-V + FMAINV BARRIER NOT-NPMOD LINK *1 (Pron Refl Acc) BARRIER NOT-NPMOD LINK 1 Ess)(NEGATE 1 CLB OR Num)(NOT *1 Po OR V-TRANS BARRIER SV-BOUNDARY)(NEGATE *-1 PrfPrc + FMAINV BARRIER SV-BOUNDARY LINK *1 ANIMATE + Acc BARRIER NOT-NPMOD LINK 1 Gen);
	#$ Soai lohkaba su girjji eaŋgalasgillii.
	#$ De son ohcá stálu silbbaid.
	#$ Beana lea olbmo buoremus ustit.
	#$ Buvtte deike su pistovlla.
	#% Alimusriekti gávnnai dettetge doarjaga Čáhppesvuovdeášši bohtosii riikkaidgaskasaš álgoálbmot- ja unnitlohkorievttis.


#!! * __r2271__ Removes genitive when a transitive verb to the left and you (not if you are a pronoun) are followed by Ill/Loc/Com/Adv: ''doalvvui stálu meahccái''
REMOVE:r2271 Gen IF (*-1 TRANS-V - ("bargat") BARRIER SV-BOUNDARY OR Acc OR COMMA LINK NOT *-1C Acc BARRIER SV-BOUNDARY)(0 (N Acc) LINK NOT 0 (Pron))
(1 Ill OR Loc OR Com OR Adv LINK NOT *1 Acc BARRIER TRANS-V)(NEGATE 0 Refl)
(NEGATE *-1 Pr OR NUM BARRIER NP-BOUNDARY)
(NEGATE *-1 ("mii") BARRIER Acc OR S-BOUNDARY OR Po)
(NEGATE *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER S-BOUNDARY)
(NEGATE *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc)
(NOT *2 REAL-V BARRIER S-BOUNDARY);
	#$ De son doalvvui stálu meahccái.
	#$ Biera oahpaha matematihka munnje.
	#$ Duopmár bosádii vuoru johtui.
	#$ Máŋga gávpeolbmo lonuhedje fáhcaid gálvvuiguin.
	#% Ja dál mun goittotge lean riegádahttán bártni su boaresvuođa beivviin.
	#% Leat maid bargame dálá boazodoallolága ođastemiin.

#!! * __AccTV2__ Selects accusative: when a transitive verb to the left. No Acc allowed to the left in the sentence (sentenceboundary as a barrier). No Acc allowed to the right (barriers are CC, comma and sentenceboundary). Note that Gen to the right followed by a noun is allowed. You shall not be: route, time, Pron Dem. You are not Acc if you are: Gen-cased Pron or Animate with Ill immediately to your right. No Acc, Com, N Nom or Gerundium allowed immediately to your right. No Gen followed by Po allowed immediately to your right. A SG3-verb is only allowed to your left (barriers excluding everything except NP-heads and adverbs, PrfPrc is also a barrier) if there is a Nom left to the SG3-verb. No vdic allowed immediately to your left. You are not Acc if: you are a Nom-cased Prop, followed by ahte or EOS and the verb found to the left (SV-boundary) is some kind of verbalactivityverb or a humanagentverb.
SELECT:AccTV2 Acc IF (*-1C TRANS-V - Imprt BARRIER NOT-NPMODADV LINK NOT *-1 Acc - Num BARRIER S-BOUNDARY OR (N Ess) OR NEGFOC)
(0 NP-HEAD-ACC LINK NEGATE *1 Acc BARRIER CC OR COMMA OR SV-BOUNDARY LINK NOT 0 (Pron Gen) LINK *1 N BARRIER NOT-NPMOD OR COMMA)
(NEGATE *-1 NUM BARRIER NOT-Attr LINK NOT 0 NUMUNIT)
(NEGATE 0 (Pron Dem) LINK *1 N BARRIER NOT-Attr)(NEGATE 0 ANIMATE + Gen OR Pron + Gen LINK 1 Ill)(NEGATE 1 (N Acc) OR Com OR (N Nom) OR Ger)
(NEGATE 1 Gen LINK 1 Po)(NEGATE *-1C SG3-V BARRIER NOT-NPMODADV OR PrfPrc LINK NOT *-1 (N Nom) OR (Pron Nom) BARRIER S-BOUNDARY)
(NEGATE -1 <vdic>)
(NEGATE 0 FIRSTNAME + Nom OR (Prop Sem/Sur) + Nom LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V BARRIER SV-BOUNDARY LINK *2 ("ahte") OR EOC) ; #(NEGATE *-1 ILL-V + FMAINV BARRIER SV-BOUNDARY LINK *1 (N Gen) + SAPMI OR SUOPMA OR ("dáža") OR ("dáčča") LINK *1 Ill BARRIER NOT-A - ("ja") + @CNP)
# OR COMMA
	#$ De son ohcá stálu silbbaid.
	#$ Sámegillii galbet universitehta lea šaddan realitehta.
	#$ In máhte suomagiela, eaige nieiddat máhte nu bures eŋgelasgiela.
	#$ De son ohcá stálu silbbaid ja politiija golliid.
	#$ Fidniimet gillái náitalan nissona veahkkin, ja guokte vuosttamuš jagi vuoššaimet liema ja biđđosa.
	#$ Iežas ealu son doalai sierra iige luoitán daid mastat Labana sávzzaiguin ja gáiccaiguin.
	#% Dieđusge ledje sámisogalaččat Muosáin, ja velá dat olbmotge gulle dáža, sámi ja kveana seaguhussii.
	#% Mánáidgárddi ulbmilin lea nannet mánáid sámi iešdovddu ovddidettiin sámegiela geavahusa. #Make or fix a SEM-rule for this.

#!! * __AccTV3__ Selects accusative: when transitive verb to the left, if it doesn't find a barrier: comma, Num, real-v, Ess, s-boundary. Acc not allowed to the left of the verb. Not Acc if animate or Gen in front of Ill. Numerals the only Acc allowed to the right. Not Num, time route or adv. Not Com or Ger immediately to the right. Neither Po. Not Acc if sg3-verb to the left without a Nom to its left. Not Pron Dem followed by N, neither Pron Rel followed by time. No vdic immediately to your left. No Nom-cased Prop with some sort of verbal activity to its left is allowed..
SELECT:AccTV3 Acc IF (*-1 TRANS-V BARRIER COMMA OR Num OR REAL-V OR Ess OR S-BOUNDARY OR (N Acc) LINK NOT *-1 Acc BARRIER S-BOUNDARY)(NEGATE *-1C Acc BARRIER SV-BOUNDARY)(NEGATE 0 ANIMATE + Gen OR Pron + Gen LINK *1 Ill BARRIER NOT-NPMOD)
(NEGATE *1 Acc - Num BARRIER S-BOUNDARY OR COMMA OR @CVP)
(NEGATE 0 Num OR Adv)(NEGATE *1 Po BARRIER NP-BOUNDARY)(NEGATE 1 Com OR Ger)(NEGATE *-1C SG3-V BARRIER NOT-NPMODADV OR PrfPrc LINK NOT *-1 (Sg Nom) BARRIER S-BOUNDARY)(NEGATE 0 (Pron Dem) LINK 1 N)(NEGATE 0 (Pron Rel) LINK 1 Sem/Time)(NEGATE -1 <vdic>)(NEGATE 0 FIRSTNAME + Nom OR (Prop Sem/Sur) + Nom LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V BARRIER SV-BOUNDARY LINK *2 ("ahte") OR EOC)(NEGATE 0 ("jr") OR ("sr") LINK -1 Sem/Sur) ;
	#$ Biera oahpaha munnje matematihka.
	#$ Oastte min ja min eatnamiid láibbiin, de mii ja min eatnamat šaddet farao opmodahkan.
	#$ Son áiggui sáddet iežas bártni mu viežžat, son muitalii. #Comment: correct, but not't sure it's a good idea.. mu is obj of viežžat.
	#$ Nieiddat liikojedje guldalit sávaldatkonseartta ja mánáidbottu.
	#% Lávvui gullá maid uvssot.
	#% Dieđátgo guđe beaivvi mis lea čoahkkin?
	#% Mii fertet maiddái geahčadit makkár lassi guoimmuhemiid galgat fállat vai bohtet vel eanet olbmot boahtte jagi, loahpaha duhtavaš beaivválaš jođiheaddji Eriksen. #Comment: search for the vdic-rule.
	#% Seammás go skuvlla, sámi oahpahusráđi ja Sámi boazodoallohálddáhusa ovddasteaddjit háliidedje ahte sámegielat oahppit ožžot oahpahusa sámegillii, de dajai Eanandoallooahpahusráđi jođiheaddji Ola Nystø ahte LOR bealis leat álo geahččan boazodoalloskuvlla fágaskuvlan boazodoalus.


#!! * __OLDr2466__ Selects accusative: when transitive verb to the left, but not if the TV is FAUX OR LOC-V
SELECT:OLDr2466 Acc IF (*-1C TRANS-V - Imprt BARRIER SV-BOUNDARY OR NOT-NPMODADV OR XGO LINK NOT 0 FAUXV OR LOC-V LINK NOT *-1 Acc BARRIER SV-BOUNDARY OR (Neg Foc/ge))(NEGATE 0 (Sg Nom) LINK NOT 0 Px LINK *-1 (V Sg3) BARRIER SV-BOUNDARY)(NEGATE -1 PrfPrc LINK NOT *-1 REAL-COPULAS BARRIER SV-BOUNDARY)(NEGATE *-1 NUM BARRIER NOT-Attr LINK NOT 0 NUMUNIT)(NEGATE 0 PrfPrc OR Adv OR VFIN OR Loc LINK NOT 0 Px)(*1C NP-BOUNDARY OR S-BOUNDARY OR Sem/Time + Ill BARRIER WORD LINK NOT 0 Actio OR Inf);
	#$ Sus báhkkanii olles gorut go oinnii Lárina čuvges vuovttaid.



#SELECT:duration (DURATION-V objeakta sáhttá leat akk hámis!)

#!! * __AccInf__ Selects Acc if the verb to the left is TV + Inf (you are the obj of the Inf). Differs from the other rules by not beeing restricted by an Acc to the right ''hállat eatnigiela''
SELECT:AccInf Acc IF (*-1 TRANS-V + Inf OR (V TV PrfPrc) + (@<OBJ) BARRIER NOT-NPMODADV)
(0 NP-HEAD-ACC LINK NEGATE *1 Acc BARRIER CC OR COMMA OR SV-BOUNDARY LINK NOT 0 (Pron Gen) LINK *1 N BARRIER NOT-NPMOD)(NEGATE *-1 Num BARRIER NOT-Attr)(NEGATE 0 A)(NEGATE 0 ANIMATE + Gen OR Pron + Gen LINK 1 Ill)(NEGATE 1 (N Acc) OR Com LINK NEGATE 0 Com LINK 1 Com)(NEGATE 1 Gen LINK 1 Po)(NEGATE 0 (Pron Pers) LINK 1 (Pron Refl) LINK 1 Gen LINK 1 Po)(NEGATE 0 Adv) ; #(Pron Dem) OR
	#$ Sii eai ožžon lobi hállat iežaset eatnigiela, eai bottuinge dahje ovdal ja maŋŋá skuvllaáiggi.
	#$ Oidnen dárbbu geahččat lagabui buot boasttuvuođaid maid sápmelaččat šadde gierdat.
	#$ Logi minuvtta maŋŋel nagodii Sørøy Glimt čiekči Stig Martinsen dulbet bohtosa ovttain moalain.
	#$ Ovddemus galge nannet dárogieloahpahusa juoigidettiin vuosttaš ja nuppi jagi goabbat sadjái. #Should hit, but doesn't.
	#% 17. artihkkal sisttisdoallá mearrádusaid movt eananvuoigatvuođaid galgá juogadit sin iežaset mieldelahtuid hárrái.
	#% Daid ferte ferte árvvoštallat stáhta dálá geavada ja riikka lágaid ektui.
	#% Lávvardaga čájehuvvo ođđa dokumentárafilma maid leat gohčodan Smierru.

#!! * __AccCOP__ Selects Acc if copulas to the left and nominative to the left of COP ''gápmagat leat áhči''
REMOVE:AccCOP Acc IF (*-1 COPULAS + FMAINV BARRIER NOT-NPMOD OR CC LINK *-1 (N Nom) BARRIER NOT-ADV)(1 EOC OR COMMA OR NEGFOC)(NEGATE *-1 (N Nom) BARRIER S-BOUNDARY LINK -1 (Pron Rel))(0 (N Acc) LINK NEGATE *-1 Num BARRIER NOT-Attr)(NEGATE 0 Nom)(NEGATE 1 Nom OR Acc OR (N Ess) OR Loc)(NEGATE *1 FMAINV BARRIER COMMA OR S-BOUNDARY);
	#$ Diet gápmagat leat áhči.
	#$ Lihtti lea ravddaid dási.
	#Commet: (1 EOC OR COMMA OR NEGFOC) dárkkis dán oasi korpusis..


# GEN MODIFIERS INSIDE NP

#!! * __GenNP1__ Selects Gen for Pron Pers (modifier): if NP-BOUNDARY OR Acc (but not if the finite verb is TV) to the left and N to right
SELECT:GenNP1 (Pron Pers Gen) IF (-1 NP-BOUNDARY - TRANS-V OR Acc OR (Sem/Plc Loc))(*1 N BARRIER NOT-NPMOD)(NEGATE 0 Gen LINK 1 Po) ;
	#$ Min mánáin leat ollu stohkosat.
	#$ Dál lea du vuorru.
	#$ Gáivuonas su áhčis leat guokte oambeali ja guokte vilbeali.

#!! * __GenNP2__ Selects Gen for N (modifier): if CC "ja" immediately to your left and accusative to your right ''ja sámi jurddašanvuogi''
SELECT:GenNP2 (N Gen)  IF (-1 ("ja"))(1 (N Acc))(NEGATE 0 V OR ABBR OR Attr);
	#$ Miššonbarggu álggu rájes juo Leem háliidii oahppat sámegiela ja sámi jurddašanvuogi.
	#% Sii ožžot geavahit eatnama ja gáttiid ja elliideaset ja iežaset ealáhussii. # Deaivá dán vaikko eat hálit dan.

#!! * __GenNP3__ Selects Gen (modifier): if first one to right is Nom or Loc ''Norgga oaivegávpogis''
SELECT:GenNP3 Gen IF (*-1 BOS OR COMMA OR XGO BARRIER NOT-Attr)(0 Gen)(1 (N Loc) OR (N Nom))(NEGATE 0 (Pron Interr) OR (Pron Rel))(NEGATE 0 TITLE + Nom LINK 1 (Prop Nom))(NEGATE 0 (Pron Dem) + Acc)(NEGATE 0 (Sg Nom) LINK *1 SG3-V BARRIER VFIN OR Nom);
	#$ Ánde orru Oslos, Norgga oaivegávpogis.
	#$ Nieidda eadni ii sihtan vuolgit, muhto nisu hásttii.
	#$ Organisašuvnna bláđđi, man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.
	#$ Jovsset, su irgi, gii lei rehálaš olmmái ii ge hálidan heahppašuhttit su, áiggui earránit sus suoli.
	#% Čáhppesmurjjiid olbmot gazze milkkiin ovdal.

#!! * __GenNP4__ (modifier) selects Gen -1 BOS or COMMA, 1 Nom ''nissoniid bargu''
#SELECT:GenNP4 (N Gen) (*-1 BOS OR COMMA)(1 (N Nom))(NEGATE 0 Num)(NEGATE 1 Inf)(NEGATE 0 (Pron Pers)) ;
	#$ Galgágo buot bivttasráhkadeapmi leat nissoniid bargu?
	#% Mu oabbá lea bargan dán skuvlla rektorin.
	#% Sudno heasttat bivvet olgun vaikko leage buolaš. #Comment: This one is not meant for pronouns: they have GenNP1.

#!! * __GenNPCo__ (modifier) Selects Pron Pers Gen if Nom to the left of ja ''Mun ja mu ustibat''
SELECT:GenNPCo (Pron Pers Gen) IF (-1 ("ja") LINK -1 Nom) ;
	#$ Mun ja mu ustibat manaimet kinoi geahččat ođđaseamos Harry Potter-filmma.

#!! * __GenRefl__ (modifier) selects Gen in front of a noun in accusative or nominative case ''iežaset oiviliid''
SELECT:GenRefl Gen IF (0 (Pron Refl) + Gen)(*1 (N Acc) OR (N Nom) BARRIER NOT-Attr) ;
	#$ Atrihkkalat leat čálliid iežaset ovddasvástádus ja lohkkit berrejit daid lohkat kritihkkalaččat ja hábmet iežaset oaiviliid historjjá birra ja otná ja ihttá sámi skuvlla birra.
	#$ Lehpetgo iežadet diŋggaid dolvon?
	#$ Vaikko leatge sámeaktivista, de it dárbbat iežat čivgga dahkat givssidanoaffarin.



#!! * __AccAfterCC__ Select accusative: if genitiv to the left, and CC "ja" to the left of genitive ''eamiálbmot- ja globaliserenprošeavtta koordináhtor''
#SELECT:AccAfterCC (N Acc)  IF (-1 (N Gen) LINK -1 ("ja"))(0 (N Acc))(NEGATE *1 (N Acc) OR (N Nom) OR (N Ess) OR (N Loc) BARRIER NOT-Attr OR PUNCT-LEFT)(NEGATE 0 V)(NEGATE 1 Gen LINK 1 (N Loc))(NEGATE *1 Gen LINK 1 Po);
	#% Son oassásdoallá dál sámi historjjálaš girjebarggus ja lea máŋggafágalaš eamiálbmot- ja globaliserenprošeavtta koordináhtor. #Deaivá dán vaikko eat hálit dan.
	#% Lávdegoddi lea maiddái atnán áiggi ságastallat davviruoŧa čearuid ja Romssa boazoorohagaid gaskaneas váttisvuođaid birra. # Seamma.

# Alternatively this rule is not needed after removal of all Acc "KillAcc?", but for now it stays.
#SELECT:GenNP2 Gen (-1 NP-BOUNDARY)(*1 N BARRIER NOT-NPMOD)(NEGATE 0 Gen LINK 1 Po);
	#$ Soai lohkaba Harry Potter-girjji eaŋgalasgillii.
 # Jurddaš ná: Gii sáhttá oamastit gean/man?



#!! !Accusative in coordination

# All coordinated accusatives rely on a secure accusative somewhere in the s.

# COMMA

#!! * __CoAcc1__ Select accusative (coordinated acc) when NP inbetween commas ''guolleoivviid, dáraid, debbuid, buđeittaid, boares rásiid''
SELECT:CoAcc1 Acc IF (*-1 COMMA BARRIER NOT-NPMOD LINK -1C NP-HEAD-ACC)(1 COMMA OR CC OR ("jnv") OR ("jna")) ;
	#$ Doppe moai sáhtiime oastit gáfe, deaja dahje sávtta.
	#$ Liepmasii olbmot bidje guolleoivviid, dáraid, debbuid, buđeittaid, boares rásiid ja rohkajáffuid.
	#% Dat guoskkai Čohkkirasa, Wilhelmina, Suorssá ja Deartná gielddaid sápmelaččaide. #Hits this, but it shouldn't. Choose gen coordination first?
	#% Konvenšuvdna galgá ovddidit ja ovdanahttit riikkaid, čearuid ja boazoorohagaid ovttasbarggu.

# COORDINATOR LEFT

#!! * __CoAcc2__ Select accusative (coordinated acc): if coordinator to your left and accusative to the left of the coordinator ''deaja dahje sávtta''
SELECT:CoAcc2 Acc IF ((*-1 CRD BARRIER NOT-NPMODADV LINK -1C Acc) OR (*-1 CRD BARRIER NOT-NPMODADV LINK -1 Inf LINK -1C Acc))((1 NP-BOUNDARY OR COMMA OR Ill LINK NOT 0 Po) OR (1 Attr LINK NEGATE *1 (N Acc) BARRIER NOT-A))(NEGATE 0 Inf OR Rel)(NEGATE 0 Nom LINK *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 Dem LINK 1 PrfPrc LINK *1 N BARRIER NOT-NPMOD);
	#$ Doppe moai sáhtiime oastit gáfe, deaja dahje sávtta. #maŋit objeakta
	#$ Ja de sii bidje čázi duoldat ja juopmomielkki dasa, ja de dan juhke.
	#$ Gielddaid, fuođđo- ja guolástaneiseválddiid jna. ja guoskevaš geavaheddjiid servviid galgá gullat ovddalgihtii.

 # COORDINATOR RIGHT

#!! * __CoAcc3__ Selects Acc in front of "ja", if there is a secure Acc to the right ''semináraid ja diehtojuohkinčoahkimiid''
SELECT:CoAcc3 Acc IF (0 Acc LINK 1 CC LINK *1C Acc BARRIER NOT-NPMODADV)(NEGATE *1 FMAINV BARRIER S-BOUNDARY)(NEGATE -1 Acc LINK *1 Acc OR Gen LINK 1 ("ja") LINK 1 Acc LINK 1 Ill)(NEGATE *-1 NUM BARRIER NOT-NPMOD LINK 0 NUM) ;
	#$ Norgga-ruoŧa boazoguohtunkommišuvdna lea doallan moanaid semináraid ja diehtojuohkinčoahkkimiid guoskevaš eiseválddiiguin, organisašuvnnaiguin ja dutkiiguin.
	#$ Guovddáš lea ásahan alcces ovttasbargooktavuođaid ja fierpmádagaid relevánta fága- ja dutkanbirrasiiguin.
	#% Ii oktage fastivuođa ja giellása bálvaleaddji beasa sisa.
	#% De dat dagaha eahpesihkarvuođa skuvlla ja dan boahtteáiggi birra.


#!! * __CoAccJA__ Selects Acc when "ja" to the left and comma to the left of "ja" with a secure Acc to the left of comma ''sámegiela, ja heajos dárogiela.''
SELECT:CoAccJA Acc IF ((*-1 ("ja") BARRIER SV-BOUNDARY OR Nom OR FAUXV LINK -1 COMMA LINK -1C Acc) OR (*-1 ("ja") BARRIER SV-BOUNDARY OR Nom OR FAUXV LINK -1C Acc))(NEGATE 0 Num)(NEGATE 0 Gen LINK 1 Acc LINK 1 @CNP)(NEGATE 1 Gen LINK 1 Po)(NEGATE 1 Nom OR Acc OR Attr)(NEGATE *1 FMAINV BARRIER S-BOUNDARY OR COMMA)(NEGATE 1 (A Ess) LINK 1 Acc);
	#$ Mu givssidedje danin go hállen sámegiela, ja heajos dárogiela.
	#$ Gusa-, gálbbi- ja gáiccanáhkis mearragátti sámit ráhkadedje gápmagiid, ja sávzzanáhkis dorkkaid ja roavgguid.
	#% 1995 rájes son lea leamaš Sámi joatkkaskuvllas Kárášjogas, gos son lea oahpahan dearvvašvuođa- ja sosiálafága vuođđokurssa ja veahkkedivššár ja ambulánsa joatkkakurssa.

#!! * __CoAccJA2__ Selects Acc in front of Gen + Po if ja in front of Acc ''ja ruhtan sávzzaid ovddas''
SELECT:CoAccJA2 Acc IF (*-2 STRICT-TRANS-V BARRIER SV-BOUNDARY)(*-1 ("ja") BARRIER WORD - Acc LINK NEGATE -1 Gen)(0 Acc OR Gen)(*1 Gen BARRIER CC OR FMAINV LINK 1 Po LINK 1 EOS)(NEGATE 0 (A Ord))(NEGATE 0 HUMAN-INDIVIDUAL - HUMAN-LIKE OR Pron LINK *-2 HUMAN-INDIVIDUAL - HUMAN-LIKE + Acc OR Gen BARRIER SV-BOUNDARY)(NEGATE 1 Pr) ;
	#$ Mun attán heaggan, áhkkán ja ruhtan sávzzaid ovddas.
	#$ Olmmoš galgá čuovvut iežas guovllu gáktemálle, muhto sáhttá válljet liinni ja ivnni miela mielde.
	#$ Nu de mun fievrredin militearaseaŋgga ja bolstara burrerobi alde.
	#$ Dát dieđáhus joatká ulbmiliid ja bidjá eavttuid, strategiijaid ja váikkuhangaskaomiid nana guoddevaš boazodoalu váste.
	#% Váldde máná ja su eatni fárrui. #Shouldn't hit.
	#% Go bargen olles 37 jagi seamma skuvllas, de lean dieđusge vásihan ollu rievdadusaid, sihke skuvllaáiggi, oahpahusvugiid, oahpahusgiela ja ohppiid láhttema dáfus.

#SELECT:CoAccr2455 Acc IF (*-1 VFIN OR MOD-ASP OR COPULAS BARRIER NOT-NPMOD)(1 CC)(*2 NP-HEAD BARRIER NOT-NPMOD LINK 1 Inf);
	#$ Son ii muitán ahte lea ferten niesteseahka dahje lávkka guoddit.

#Missing rules for:

#$ Ulbmil čoahkkimiiguin lea oažžut buoret oktavuođa EU:i, ovddidit sámepolitihka luondduvalljodatgeavaheami, davviguovlluid ovdánahttinplánaid, Davviriikalaš sámekonvenšuvnna ja ruoššabeale sámiid dilálašvuođa riektadili ektui.




#!! !Intransitive verbs can sometimes be transitive

#!! * __IVasTV__ Selects accusative: if you are GEOGRAPHICAL-PLACE, ABSTR-ROUTE or EDUCATION and somewhere in the sentence is a intransitive verb acting as a transitive verb ''sii vázzet skuvlla''
SELECT:IVasTV Acc IF ((*-1 JOHTIT + IV BARRIER SV-BOUNDARY OR Pr LINK 0 IV) OR (*1 MOVEMENT-V + IV OR JOHTIT + IV BARRIER SV-BOUNDARY LINK NOT 0 ("mannat") OR ("vuolgit") OR ("boahtit")))(0 GEOGRAPHICAL-PLACE OR ROUTE OR EDUCATION)(NEGATE 1 Po OR ROOM) ; #(NEGATE -1 CC LINK *1 VFIN) ;
#SELECT:IVasTV EDUCATION + Acc IF (*0 ("vázzit") BARRIER SV-BOUNDARY)(NEGATE 1 Po OR ROOM) ;
	#$ Čuoigat vári.
	#$ Mun váccán vákkiid, vuvddiid ja váriid.
	#$ Fertiimet vázzit bálgáid geasset ja Deanu mielde dálvet.
	#$ Sii vázzet skuvlla.
	#$ Mun váccán skuvlla gávpogis.
	#$ Son váccii joatkkaskuvlla Girkonjárggas ja Kárášjogas ja buohccedivššárskuvlla Hámmárfeasttas, mas gearggai 1982:s.

	#% Mun váccán skuvlla šiljus. #Comment: This is not Acc, but Gen.



#!! * __IVisTrans__ Selects accusative: if you are ''spábba'' and somewhere is ''viehkat''
SELECT:IVisTrans Acc IF (0* ("viehkat") BARRIER S-BOUNDARY)(0 ("spábba")) ;
	#$ Son viehká spáppa.
	#$ Spáppa son viehká. #Comment: Iskka sáhttágo viiddidit.

#!! * __IVisTrans2__ Selects accusative: if you are SHOE or HUNT-ANIMAL or BOAZU and somewhere is ''vázzit''
SELECT:IVisTrans2 Acc IF (0* ("vázzit") BARRIER S-BOUNDARY)(0 SHOE OR HUNT-ANIMAL OR BOAZU) ;
	#$ Vázzit rievssaha.
	#$ Rievssaha vázzit.

#!! * __r1985__ Selects accusative
SELECT:r1985 Acc IF (*0 ("ceavzit") BARRIER S-BOUNDARY)(0 ("eksámen")) ;
	#$ Ikte mun cevzen eksámena nuoraidskuvllas. #Comment: too narrow.

#!! * __IVnohkkat__ Selects accusative
SELECT:IVnohkkat Acc IF (0* ("nohkkat") OR ("oađđit") OR ("oađašit") OR ("oađástallat") OR ("nohkastit") BARRIER S-BOUNDARY)(0 BEDCLOTHES) ;
	#$ Son oađđá beaskka.

#!! * __IVsahttit__ Selects accusative
SELECT:IVsahttit Acc IF (-1 ("sáhttit") LINK NOT *1 FMAINV OR Inf BARRIER S-BOUNDARY)(0 (Pron Pers) OR HUMAN LINK 0 Gen)(*1 Ill OR DOHKO BARRIER NOT-ADV);

#!! * __IVsahttit2__ Selects accusative
SELECT:IVsahttit2 Acc IF (0 (Pron Pers) OR HUMAN LINK 0 Gen)(*1 ("sáhttit") BARRIER S-BOUNDARY LINK NOT *1 FMAINV OR Inf BARRIER S-BOUNDARY)(*1 ("sáhttit") LINK *1 Ill OR DOHKO BARRIER NOT-ADV);
	#$ Gii sáhtii du dohko?
	#$ Gii du nu johtilit sáhtii dohko?



#!! !Accusative or genitive in front of ALU and in front of adjectives


#!! !Exceptional accusative attributes in front of ALU nouns.

#!! * __ALU__ Acc when Num and right is MEASURE LINK 1 ALU
SELECT:ALU Acc IF (0 Num LINK *1 MEASURE BARRIER NOT-A LINK 1 ALU);
	#$ Visti lei vihtta mehter alu.

#!! * __ALU2__ Acc when Num and not Adv, and 1 ALU
SELECT:ALU2 Acc IF (0 Num LINK NOT 0 Adv LINK 1 ALU);
	#$

#!! * __ALU3__ Acc for Num when right context Num ALU
SELECT:ALU3 Acc IF (0 Num LINK 1 Num LINK 1 ALU);
#Not-so-exceptional?

#!! * __NewGen__ (complement of numerals) selects Gen Sg when Num Sg to the left ''guhtta kilu''
SELECT:NewGen (Sg Gen) OR CURRENCY + Gen  IF (0 (N Acc) LINK *-1 NUM + Sg BARRIER NOT-A - (Pron Indef Attr) - Gen LINK 0 Nom OR Acc)(NEGATE -1 ("moadde") OR CURRENCY) ;
	#$ Juohke heasta borrá sullii guhtta kilu suinniid beaivái.
	#$ Dán skuvllas ledje jahkásaččat sullii 20 oahppi geat iešdáhtolaččat bohte deike, ja buohkat ledje lullisámit.
	#$ Do bohtet guokte olbmo.
	#$ Vuoiti oažžu 500 ru.
	#% Finnmárkku fylkkagielda lea jagi 1988 rájes ovddidan doaimmaid sámi álbmoga várás spesialistadearvvašvuođabálvalusas, ja ráhkadii jagi 1991 fylkkagieldda sámi dearvvašvuođa- ja sosiálaplána.
	#Comment: NewGen always pics genitiv for the noun that follows a Sg Num. There are two varieties, depending on whether preceded by TV (a transitive verb) = akk+gen or COPULAS = nom+gen.

#!! * __NewGenCo__ (coordinated complement of numerals) selects Gen if Num Acc + NewGen found to the left of "ja" ''máŋga dáhpáhusa ja digaštallama''
SELECT:NewGenCo (Sg Gen) OR CURRENCY + Gen  IF (0 (N Acc)) (-1 ("ja"))(-2C Gen)(*-3C NUM + (Acc Sg) BARRIER NOT-A - (Pron Indef Attr)) ;
	#$ Dákkár dáhpáhusat, ja máŋga iežá dáhpáhusa ja digaštallama dahke munnje persovnnalaččat váttisin dáid gažaldagaid mat bohciidedje Gáivuonas.
	#$ Dat mielddisbuktá ahte mánát ja ollesolbmot fertejit guođđit mánáidgárddi njeallje seainni ja šilju ja vuolgit mátkái. #Comment: What is the right analysis?
	#% Go duiskalaččat válde Finnmárkku vuolláseaset 1940 giđa ja geasi, de váldigohte sii skuvllaid ja internáhtaid soalddáhiid ásodahkan ja eará geavahussii.

#SELECT:XXr1993ja1997 Gen IF (-1 (Num Sg Acc))(0 MEASURE)(1 ALU OR A OR Pr OR ("dassái") OR ("dás") OR ("áigi" Adv)); #This is not needed because of NewGen!
	#$ Sus lea njeallje mehtara guhkkosaš biila.
	#$ Ádjá viessu lea guokte miilla bajábeal márkana.
	#$ Son bođii guokte jagi dás ovdal.

#!! * __ALU4__ selects Acc if you are Num and to your right Num Acc followed by MEASURE OR ALU/A ''guokte golbma mehtara alu''
SELECT:ALU4 Acc IF (0 NUM)(*1 (Num Acc) BARRIER NOT-COMMA LINK 1 MEASURE LINK 1 ALU OR A);
	#$ Guokte golbma mehtera guhku, ja guhkitge, sáhttá vuojahat leat.

#!! * __ALU5__ selects Gen if Num to the right, followed by Num, followed by ALU/A
SELECT:ALU5 Gen IF (1 (Num Sg Acc) LINK 1 (Num Sg Acc) LINK 1 MEASURE LINK 1 ALU OR A);
	#$

#Not in use.
#!! * __r1996__ selects Gen if part of comma separated row before MEASURE
#SELECT:r1996 Gen (1 COMMA LINK 1 (Num Sg Acc) LINK 1 COMMA LINK 1 (Num Sg Acc) LINK 1 MEASURE LINK 1 ALU OR A);
  	#$ Čuollun sáhttá leat dušše okta fierbmi gurgaluvvon, muhto dábálaččat lea čuollu guhkit, golbma, njeallja fierbmebaji guhku.

#Comment: NUM + ACC, no ALU/adjective here... Concider moving?
#!! * __NumTimeMannel__ Acc for Num before TIME MANNEL
SELECT:NumTimeMannel Acc IF (0 Num)(1 TIME)(2 MANNEL);
	#$ Logi minuvtta maŋŋel nagodii Sørøy Glimt čiekči Stig Martinsen dulbet bohtosa ovttain moalain.

#!! * __NumPageMannel__ Acc for Num before siiddu etc + MANNEL.
SELECT:NumPageMannel Acc IF (0 Num)(1 ("siidu") OR ("kapihtal"))(2 MANNEL);

#!! * __NumPageMannel2__ Acc for Num before ovdalis etc
SELECT:NumPageMannel2 Acc IF (0 Num)(1 ("siidu") OR ("kapihtal") LINK 2 ("maŋŋelis") OR ("ovdalis"));
	#$ Earát su luohkás ledje juo vissa njeallje siiddu su ovdalis matematihkka-girjjis.


#!! * __GenBoaris__ Gen in golbma ''jagi'' boaris
SELECT:GenBoaris Gen IF (-1 (Num Gen))(0 TIME + Sg)(1 ("boaris"));
	#$ Son lea viđa jagi boaris.
	#% Sápmelaččaid historjá álbmogin lea duháhiid jagiid boaris, muhto dálá ássanguovlluin sápmelaččat eai leat ássan nu guhká.
#SELECT:r2000 Gen IF (-1 (Num Acc))(0 TIME)(1 ("boaris"));
	#$ Mánná lea golbma jagi boaris.

#! * __ALU6__ selects Gen if 0 MEASURE and Num right
#!! ** Ritva comment: Find a rule for "viđa" aswell, this hits "mehter" as it should
SELECT:ALU6 Gen IF (-1 (Num Gen))(0 MEASURE + Sg)(1 A OR ALU);
	#$ Dohko manai viđa mehter govdosaš geaidnu.


#!! * __XXr2002__ Selects genitive if there is a numeral immediately to your left, and you are TIME: ''golbma jagi''
SELECT:XXr2002 Gen IF (-1 (Num Sg Acc))(0 TIME); #(1 ("maŋŋil"));
	#$ Dat dáhpáhuvai golbma jagi maŋŋil.

#!! * __XXr2003__ Gen if Num and then Acc, and it is somewhat fishy, says Ritva...
SELECT:XXr2003 Gen IF (0 Num)(1 (N Acc) LINK 1 ("velggolaš")); #Ritva comment:2003 and 2004 seems really fishy... How is it supposed to work?
	#$

#!! * __XXr2004__ Gen if -1 Num velggolaš
SELECT:XXr2004 Gen IF (-1 Num)(1 ("velggolaš"));
	#$ Ánde lea munnje vihttalot ruvnno velggolaš.

# Does not hit.
#!! * __XXr2005__ ovtta is Adv if 1 ALU (does not hit...)
SELECT:XXr2005 Adv IF (0 ("ovtta"))(1 ALU);
	#$ Dábálaččat leat joddut sullii ovtta guhku.



#!! !Numerals


#!! * __NumGenPo__ selects Gen for a numeral if a transitive verb to the left. To the right a Gen followed by a postposition ''vuovdán 163 000 ruvnnu ovddas''
SELECT:NumGenPo Gen IF (*-1 TRANS-V BARRIER S-BOUNDARY OR Pr LINK 0 FMAINV OR Inf)(0 NUM + Sg LINK NOT 0 NUMUNIT)(1 Gen LINK 1 Po) ;
	#$ Mii leat vuovdán oktiibuot 163 000 ruvnnu ovddas.

#!! * __NumMoney__ selects Gen if you are a numeral and immediately to your right is CURRENCY ''vihtta ruvnnu''
REMOVE:NumMoney Gen IF (0 Num)(1 CURRENCY);
	#$ 1500 ruvnno

#!! * __NumGitta__ selects Acc when you are a numeral with "gitta" immediately to your right followed by a numeral with acc-case ''180 gitta 200''
SELECT:NumGitta Acc IF (0 Num)(1 ("gitta"))(2 (Num Acc));
	#$ Dát ledje gáddenuohtit mat ledje 180 gitta 200 sala guhku, ja 15 gitta 20 sala čieŋu.

# MAIN RULES: NumAcc1, NumAcc2, GenIfNum

#!! * __NumAcc1__ selects Acc if you have a transitive verb to the left and you are a numeral followed by a noun  ''oste guokte mielkki''
SELECT:NumAcc1 Acc IF (*-1 TRANS-V BARRIER S-BOUNDARY OR Pr LINK 0 FMAINV OR Inf)(NEGATE -1 N LINK 2 VFIN)(NEGATE -1 CS OR ("jahki") OR GEASSET)(0 NUM LINK NOT 0 Adv OR NUMUNIT)(*1 (N Acc) OR (N Gen) BARRIER NOT-Attr)(NEGATE *1 Acc BARRIER S-BOUNDARY)(NEGATE *1 Ill OR Loc BARRIER NOT-A)(NEGATE 1 (N Gen) LINK 1 Po)(NEGATE *-1 CC BARRIER NOT-ADV LINK -1 Num + Sg)(NEGATE -1 BEAIVI + Gen)(NEGATE -1 MANNU LINK 1 (A Ord) LINK 1 (Adv ABBR));
	#$ Moai oste guokte mielkki gávppis.
	#$ Giliviessu geassá 200 olbmo.
	#$ De son barggai guokte jagi buohccidivššárin Kolvereidas, Trøndelágas.
	#% Sámediggi lea geigen jahkedieđáhusas departementii Sámedikki doaimmas 2005.
	#Comment: NumAcc1 is intended to work with NewGen: as in it is always followed by Gen.

#XX
#!! * __NumJahki__ removes Acc if you are a numeral and JAHKI-NUM is immediately to your left ''mávssii mannan jagi 43 ruvnnu''
REMOVE:NumJahki Acc IF (-1 JAHKI-NUM LINK NOT -1 (PrfPrc @>N) OR Attr OR Dem)(0 Num);
	#$ Son mávssii mannan jagi 43 ruvnnu kilos.
	#Comment: what's the difference between this one and XXr2006 (put it the other way: why wasn't XXr2006 chosen over this?)

#!! * __NomIfNum__ removes Acc if Gen to the right (because you are Nom). Transitive verb with an Acc to the right ''máŋga gávpeolbmá lonuhedje fáhcaid''
REMOVE:NomIfNum Acc IF (1 (N Gen))(2 NOT-FAUXV + TRANS-V)(*3 (N Acc) BARRIER NOT-NPMOD);
	#$ Máŋga gávpeolbmá lonuhedje fáhcaid gálvvuiguin.
	#Comment: The subject is a numeral.

#!! * __NumAcc2__ selects Acc for singular numerals if there is a transitive verb somewhere in the sentence and the numeral is followed by a noun ''logi báhkkoma'' OBS
SELECT:NumAcc2 Acc IF (0* NOT-FAUXV + TRANS-V BARRIER SV-BOUNDARY)(0 NUM + Sg LINK NOT 0 NUMUNIT)(*1 (N Acc) OR (N Gen) BARRIER NOT-Attr LINK NOT 0 Ill)(NEGATE *-1 COPULAS + FMAINV BARRIER SV-BOUNDARY)(NEGATE *-1 Der/Pass OR (Dem Nom) BARRIER NOT-ADV)(NEGATE *1 COPULAS + FMAINV BARRIER COMMA OR SV-BOUNDARY)(NEGATE -1 NUMMAR);
	#$ Sii máhtte áddekeahttá bajillohkat logi báhkkoma ja Áhččámet.
	#$ Áiggun dušše geahččat moadde filmma.
	#$ Dákkár dáhpáhusat, ja máŋga iežá dáhpáhusa ja digaštallama dahke munnje persovnnalaččat váttisin dáid gažaldagaid mat bohciidedje Gáivuonas.
	#% Oarje-Finnmárkkus leat dál registrerejuvvon 8.500 muohtaskohtera.
	#% Buot njeallje máná ledje náđđon eagi vuostá.
	#% Áirras nr. 10 Ole Henrik Magga oaččui permišuvnna.
	#Comment: Is intended to work with GenIfNum (or NewGen in some cases.)

# Missing rule for coordination:
#SELECT:NumCoord Acc (0 NUM + Sg LINK NOT 0 NUMUNIT)(1 ("ja") LINK *1 (Num Sg Acc) BARRIER WORD - Adv - A) ;
	#$ Juohke mánná oaččui eanemusat guokte ja eanemusat golbma jagi oahpahusa jagis.
	#Comment: Make it more general?


#!! * __GenIfNum__ (complement of numerals) selects Gen Sg if there is a Num Sg to your left ''guđa geardde''
SELECT:GenIfNum (Gen Sg) OR CURRENCY + Gen  IF (*-1 NUM + Sg BARRIER NOT-A LINK NOT 0 NUMUNIT)(0 (N Gen))(NEGATE 1 Po)(NEGATE -1 CC) ;
	#$ Mii finaimet doppe guđa geardde.
	#$ Sii geat máhtte dušše moadde luođi, ohppe máŋga ođđa luođi internáhtas orodettiin.
	#$ Viđa máná biktasat ledje jávkan.
	#$ De ohccu moadde girjji.
	#Comment: GenIfNum always pics genitiv for the noun that follows a Sg Num. There are two varieties, depending on whether preceded by TV (a transitive verb) = akk+gen or IV (intransitive verb) = gen+gen.

#!! * __NumAccCo__ (coordinated num) selects Acc if you are Num Sg and to your right: CC with a Num to the right ''guokte ja eanemusat golbma''
SELECT:NumAccCo Acc IF (0 NUM + Sg LINK NOT 0 NUMUNIT)(1 CC LINK *1C (Num Sg Acc) BARRIER NOT-ADV);
	#$ Juohke mánná oaččui guokte ja eanemusat golbma jagi oahpahusa jagis.

#!! * __NumAccIV__
SELECT:NumAccIV Acc (*-1 (N Nom) OR (N Ess) BARRIER FMAINV)(*-2 ("leat" V IV PrfPrc) + FMAINV BARRIER SV-BOUNDARY)(0 NUM + Sg LINK NOT 0 NUMUNIT)(1 N + Gen) ;
	#$ Rátnogođđin lea leamaš mearrasámi árbevierru goitge 1300 jagi.
	#OR CLB OR @CNP OR @CVP

#!! * __NumAge__ selects Acc for Sg numerals if a time unit to the right is followed by boaris ''vihtta jagi boaris''
SELECT:NumAge Acc (0 NUM + Sg LINK NOT 0 NUMUNIT)(1 TIME-UNIT + Gen LINK 1 ("boaris")) ;
	#$ Son lea vihtta jagi boaris.

# PLURAL

#!! * __NumAccPlRight__ selects Acc when transitive verb to the left. You are Num Pl and to your right is Acc ''goarui viđaid gápmagiid''
SELECT:NumAccPlRight Acc IF (*-1 TRANS-V BARRIER SV-BOUNDARY)(0 Num + Pl)(1 Acc) ;
	#$ Máret goarui viđaid gápmagiid.
	#$ Mun osten guvttiid buvssaid ja ovtta jáhka.
	#$ Mun osten dušše ovttaid sabehiid.
	#? Sin jahkásaš johtolagat sáhtte leat čuđiidge kilomehteriid, ja olle dálá Suomabeale vuovdeguovlluid dálveorohagain Jiekŋameara rittu geasseorohagaide.

#!! * __NumAccPlLeft__ selects Acc when tranistive verb to the right (same as the previous. Only differs in which direction the verb is found). ''galliid sabehiid don ostet''
SELECT:NumAccPlLeft Acc IF (*1 Acc BARRIER WORD)(*2 TRANS-V BARRIER SV-BOUNDARY)(0 Num + Pl) ;
	#$ Galliid sabehiid don ostet?
	#$ Galliid heajaid dii lehpet doallan?

#!! * __NumAccPlLeft__ selects Acc if you are N Acc Pl and to your left is Num Acc Pl ''galliid sabegiid''
SELECT:NumAccPl Acc IF (-1 (Num Acc Pl))(0 (N Acc Pl)) ;
	#$ Galliid sabehiid don ostet?
	#$ Sápmelaččaid historjá álbmogin lea duháhiid jagiid boaris, muhto dálá ássanguovlluin sápmelaččat eai leat ássan nu guhká.
	#$ Sin jahkásaš johtolagat sáhtte leat čuđiidge kilomehteriid, ja olle dálá Suomabeale vuovdeguovlluid dálveorohagain Jiekŋameara rittu geasseorohagaide.
	#$ Dat lea duháhiid jagiid boaris.
	#% Vaikko mánáin lei doarvái bargu skuvllas ja bihtáidbargamiin beaivvi mielde, de berre liikká dál maŋŋá fuomášuhttit ahte máŋggaid jagiid ii lean makkárge lágiduvvon asttuáigefálaldat mánáide.


# OKTA

#!! * __NumOkta__ Selects Acc if 0 okta followed by a noun. Transitive verb to the left ''oidnen ovtta nieidda''
SELECT:NumOkta Acc IF (*-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY)(0 ("okta" Num Sg))(1 N LINK NEGATE 0 Gen LINK 1C Acc) ;
	#$ Mun oidnen dušše ovtta nieidda.
	#$ Árvaluvvon juohkin dáhpáhuvvá dáinnalágiin ahte juohke suohkan beassá vuos árvalit ovtta evttohusa stipendii.
	#% Dan eatnamis oažžu ovtta gusa fuođđara.

# Genitive after quantifier

	#REMOVE Gen IF (*-1C (@>N) BARRIER NPNH)(NEGATE *1 N BARRIER NOT-NP);
#SELECT:QUANr2135 Gen IF (*-1C (Num Nom Sg) BARRIER NOT-NPMOD OR Gen OR CC OR PUNCT OR REAL-CLB OR Ord LINK NOT 0 OKTA OR Ord OR (Num A) LINK NOT -1 ("jahki"))(0 (@Num<))(NEGATE 0 V OR ("nr") OR ("nummar") OR ("s"));
	#(NEGATE -1C (@>N) LINK NOT *1 N BARRIER NOT-NP);
	## Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.

#SELECT:QUANr2136 Gen IF (-1 (@>N) LINK -1C Num LINK NOT 0 OKTA OR Ord OR (Num A) LINK NOT -1 ("jahki"))(0 (@Num<))(NEGATE 0 V OR ("nr") OR ("nummar") OR ("s"));


#SELECT:QUANr2137 Gen IF (*-1C Num BARRIER NOT-NPMOD OR Gen OR CC OR PUNCT OR REAL-CLB OR Ord LINK NOT 0 OKTA OR Ord OR (Num A) LINK -1 ("jahki") LINK -1 (PrfPrc @>N) OR (A @>N) OR (Dem @>N))(0 (@Num<))(NEGATE 0 V OR ("nr") OR ("nummar") OR ("s"));
	## Son mávssii mannan jagi 43 ruvnnu kilos.


#SELECT:QUANr2138 Gen IF (*-1 (Num Sg) BARRIER NOT-NPMOD OR Gen OR PUNCT OR REAL-CLB LINK NOT 0 OKTA OR Ord LINK *-1 REAL-COPULAS BARRIER NOT-ADV-PCLE)(NEGATE *1 TRANS-V BARRIER SV-BOUNDARY OR CC);
	## Joavkkus leat vihtta oahppi Kárášjogas.

#SELECT:QUANr2139 Gen IF (*-1 Num BARRIER NOT-NPMOD OR Gen OR PUNCT OR REAL-CLB LINK NOT 0 OKTA OR Ord LINK -1 CC OR CLB LINK *-1 (Num Nom) BARRIER NOT-NP)(NEGATE 0 Num LINK -1 CC OR CLB);
	## Joavkkus leat vihtta oahppi Kárášjogas, vihtta oahppi Muosáhis ja vihtta Bearalvágis.

#!! * __QUANgenCoord__ selects Gen for coordinated complement of a numeral
SELECT:QUANgenCoord Gen IF (-1 CC)(-2 RCmpnd)(*-3 NUM + Acc BARRIER NOT-NPMOD OR PUNCT)(NEGATE 0 NUM);
	#$ Máŋga sámi dearvvašvuođa- ja sosiálabargi vásihit dan.

#!! * __QUANgen1__ selects Gen if a numeral with Nom-case to the left and 3Pl-verb to the right
SELECT:QUANgen1 Gen IF (*-1 (Num Nom) BARRIER NOT-NP OR CLB)(NEGATE -1 CC)(*1 PL3-V BARRIER NOT-ADV-PCLE)(NEGATE 0 A);
	#Comment: Subject-numerals
	#$ Máŋga Romssa universitehta dutkanbirrasa leat ásahan ovttasbargguid...
	#$ Máŋga min sámegielat máná ledje beallegielagat.
	#% Midjiide ledje guokte smávva lohkanlanja maid ovdal min boahtima geavahedje oađđinlatnjan.

#!! * __QUANr2142__ selects Gen if a numeral to the left and genitive to the right. Transitive verb not allowed to the left.
#SELECT:QUANr2142 Gen IF (*-1 NUM BARRIER CC OR CLB OR NOT-NPMOD OR PUNCT OR ("ruvdnu") OR HEADOFPARTS LINK 0 Nom LINK NOT -1 ABBR OR CLB)(NEGATE *-1 (Num Acc) BARRIER NOT-NPMOD LINK *-1 Nom BARRIER S-BOUNDARY OR CC)(NEGATE *-1 TRANS-V BARRIER SV-BOUNDARY)(*1 Gen BARRIER NOT-NPMOD);
	#$ Máŋga Romssa universitehta dutkanbirrasa leat ásahan ovttasbargguid...
	#% Ráhkkásiiddán, allet vajáldahte ahte Hearrái lea okta beaivi dego duhát jagi ja duhát jagi dego okta beaivi.




#!! !Leftover accusatives

	# Acc, Gen or Nom; in front of Inf

#!! * __AccInf2__ Selects Acc if Inf immediately to the RIGHT ''guliid čoallut''
SELECT:AccInf2 Acc IF (*1C TRANS-V + Inf OR (V TV PrfPrc) BARRIER NOT-ADV-PCLE LINK NOT 0 (@>N))(NEGATE 0 Adv)(NEGATE 0 Prop + Nom)(NEGATE -1 Num)(NEGATE *-1 COPULAS + FMAINV BARRIER SV-BOUNDARY )(NEGATE *-2 Loc BARRIER Nom OR SV-BOUNDARY)(NEGATE -1 ("ja") + @CNP LINK -1 Nom) ;
	#$ Son čájeha midjiide mo galgat guliid čoallut.
	#$ Mun láven basadanlanja bassat juohke bearjadaga.
	#$ Organisašuvnna bláđđi man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.
	#? Gii lea cuigen din báhtarit boahttevaš moarreduomus?
	#? Guohtuma kvalitehta ii leat duššefal das gitta maid oaidná eatnamis, muhto gálgá vuhtiiváldit maiddái bohcco dárbbuid beassat earalágan eatnamiidda.
	#% Jagis 2007 álggii Giellatekno bargat interaktiivvalaš pedagogalaš prográmmaiguin.

#!! * __AccNomCOPconstr__ selects Acc in front of Inf; only if there is no chance for itself beeing Nom
SELECT:AccNomCOPconstr Acc IF (1 TRANS-V - FAUXV)(*-1 (N Nom) BARRIER NOT-NPMODADV LINK *-1 COPULAS + FMAINV BARRIER SV-BOUNDARY LINK *-1 HAB-ACTOR + Loc OR (N Loc) OR (N Nom) BARRIER BOC) ;
	#$ Divššohasas lea vuoigatvuohta iežas journála geahččat.


#!! * __AccTV4__ Selects Acc if transitive mainverb to the left. Lots of restrictions to the right
SELECT:AccTV4 Acc IF (*-1 TRANS-V + FMAINV - ("bargat") BARRIER NOT-NPMODADV LINK NOT 0 ("gullat") LINK NOT *-1 Acc BARRIER SV-BOUNDARY OR COMMA OR Ger)(NEGATE 0 Ord OR Adv OR Prop OR (N Prop) OR (Pron Dem) OR (Pron Indef))(NEGATE 0 Num + Sg LINK 1 Gen)(NEGATE 0 (Pron Refl) LINK 1 Ill)(NEGATE 0 (Pron Pers) LINK 1 (Pron Refl))(NEGATE 0 (Pron Refl) LINK 1 Com)(NEGATE 0 TITLE LINK 1 Prop)(NEGATE -1 Num LINK NOT 0 (Num Pl Acc))(NEGATE -1 (Pron Dem Nom))(NEGATE 1 Po OR Acc OR Ill)(NEGATE 1 Num OR Nom OR Ger)(NEGATE 0 Gen OR Nom LINK *-1 VERBAL-ACTIVITY-V OR HUMAN-AGENT-V BARRIER NOT-NPMOD);
	#$ Alimusriekti ii badjelgeahččange sápmelaččaid nu sakka go vuolit instánssa duopmu.
	#$ Silje lasiha 3-0 nuppi vuorus.
	#% Dáid doaibmabijuid viidáset ovdáneami, earret eará ođasmahttit sámegieloahppomateriála, sáhttá áimmahuššat universitehta bušeahttarámmaid siskkobealde.
	#% Leat maid bargame dálá boazodoallolága ođastemiin.
	#% Goalmmáda son guđii viesu seainni ovddabeallái čohkkát.


# Pron Rel

#!! * __AccPronRel__ Selects Pron Rel Acc when a secure Acc OR Nom to the left ''gáibidedje internáhttaskuvlla man''
SELECT:AccPronRel (Pron Rel Acc)  IF ((-1C Acc OR Nom) OR (-1 COMMA LINK -1C Acc OR Nom))(0 ("mii"))(NEGATE *2 ("leat") + FMAINV BARRIER SV-BOUNDARY)(NEGATE 1 Nom LINK *2 Der/Pass BARRIER SV-BOUNDARY) ;
	#$ Lullisámit gáibidedje internáhttaskuvlla man stáhta jođihii.
	#$ Oahppit lohke jitnosit čálabáikkiid maid ledje bajil oahppan.
	#% Son lea čállán mánáidgirjji man namma lea Mellet.
	#% Stáhta nammadan láhttu ja sadjásašláhttu galgaba deavdit gáibádusaid, maid Lahtut ja sadjásašláhtut nammaduvvojit gitta njealji jagi rádjai. #Odd sentence btw..

#!! * __AccPronRel2__ Selects Pron Rel Acc when somewhere in the sentence is a Nom (barrier is sv-boundary), but only if leat isn't the main verb. ''geaid eamiálbmogat''
SELECT:AccPronRel2 (Pron Rel Acc) IF (*1 Nom BARRIER NOT-NPMOD LINK *1 TRANS-V + FMAINV BARRIER SV-BOUNDARY OR Acc LINK NOT *1 Acc BARRIER S-BOUNDARY OR COMMA) ;
	#$ Genadutkit geaid eamiálbmogat miehtá máilmmi leat vuosttaldan garrasit, leat dál jođus Sápmái, ja sii ohcet sámi dievdduid genadutkamii.
	#$ Juohke gielddas ožžot olbmot skoviid maid sii galget deavdit ja sii jearahallet maiddái olbmuid, muhto Isaksen imašta kommišuvnna bargovugiid.
	#$ Eadni dal ieš čohkká ommangáttis ja boraha uhca nieiddažis gean son doallá askkistis.

#!! * __AccPronRel3__ Selects Acc if there is a Pron Rel Nom to the right. Obs: not hit nominatives, hence negations. ''eanu mii šealgá''
SELECT:AccPronRel3 Acc IF (0 (N Acc))(1C (Pron Rel) + Nom)(NEGATE *-1 BOS BARRIER NOT-NPMODADV)(NEGATE *-1 ("leat") + FMAINV BARRIER SV-BOUNDARY)(NEGATE 0 Prop)(NEGATE -1 Pr);
	#$ De eŋgel čájehii munnje eallima čázi eanu mii šealgá dego kristálla..

#!! * __AccActioLoc__ Selects Acc when transitive Actio Loc somewhere in the sentence ''guldeleames muitalusaid''
SELECT:AccActioLoc Acc IF (*0 (TV Actio Loc) BARRIER S-BOUNDARY OR COMMA OR TRANS-V + FMAINV OR Inf)(NEGATE 0 Num);
	#$ Son ii váiban goassege guldaleames áhčis muitalusaid.
	#$ Sii jáhkke ahte eatnigiela geavaheapmi hehtte oahppamis riikka váldogiela ja nu muđuige oahpahusa.
	#$ Lávdegoddi lea dasa lassin guorahallan báikkálaš riekteáddejumiid Finnmárkkus ja vuordimis áigu dán ilbmadit iežas boahtte čielggadeami mielddusin.

	# OTHER

#!! * __AccAhte__ Selects Acc when ahte is found to the right
SELECT:AccAhte (N Acc) IF (0 Acc)(1 ("ahte"))(NEGATE *-1 BOC BARRIER NOT-Attr)(NEGATE *-1 ("leat") + FMAINV BARRIER SV-BOUNDARY)(NEGATE 0 ANIMATE LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V OR NOT-HUMAN-OBJECT-V BARRIER SV-BOUNDARY)(NEGATE 0 MAIN-ORGANIZATION LINK *-1 ("háliidit") OR ("sihtat") BARRIER SV-BOUNDARY) ;
	#$ Jagi maŋŋil oaččuimet Skuvvanváris gažaldaga ahte áiggošeimmet go fállat skuvllaálgiide dakkár oahpu.
	#% De vástidii Otterbech ahte Norgga sámemišuvnna stivra ii hálidan dan.
	#% Seammás go skuvlla, sámi oahpahusráđi ja Sámi boazodoallohálddáhusa ovddasteaddjit háliidedje ahte sámegielat oahppit ožžot oahpahusa sámegillii, de dajai Eanandoallooahpahusráđi jođiheaddji Ola Nystø ahte LOR bealis leat álo geahččan boazodoalloskuvlla fágaskuvlan boazodoalus.

#!! * __AccAux__ Selects Acc if beginning of sentence to the right and aux, not leat,  is to the left. No Acc allowed to the left ''láđđi fertejetne oastit''
SELECT:AccAux Acc IF (*-1 BOS BARRIER NOT-NPMOD)(0 (N Gen) OR (N Acc))(1 FAUXV - ("leat") - Neg)(NEGATE 0 ACTOR OR PLACE OR Rel OR (N ACR))(NEGATE *2 (Der/PassL) OR (Der/PassS) BARRIER SV-BOUNDARY)(NEGATE *1 (N Acc) BARRIER S-BOUNDARY); #(NEGATE *1 ("ahte") BARRIER S-BOUNDARY);
	#$ Dan ášši ferte čilget nállevealaheami ektui sierra.
	#$ Láđđi fertejetne oastit, nu ahte láđđegákti lei divraseabbo. #Comment: should hit this, but doesn	t! Look into it.
	#% Divššohasbearráigeahčči galgá dahkat ortnega dovddusin.


#!! * __HabGenAdvl__ removes Acc; in a habitive adverbial construction with Gen, but only if there is no chans of 0 beeing Nom ''Dat lea áhči''
REMOVE:HabGenAdvl Acc IF (-2 Nom LINK NEGATE 0 (Pron Rel))(*-1 ("leat") + FMAINV BARRIER SV-BOUNDARY OR Adv LINK NOT *1 VFIN OR Nom BARRIER S-BOUNDARY)(0 ANIMATE + Acc OR Gen LINK NOT 0 Nom)(1 EOS)(NEGATE 0 Adv)(NEGATE 1 Gen LINK 1 Po)(NEGATE *-3 @CVP BARRIER S-BOUNDARY) ;
	#$ Dat lea áhči.
	#% Norgga sámegielat-dárogielat guovlluin skuvl aovddastii dušše ovtta kultuvrra, ja dat lei dáru.
	#Comment: Naba? Fanas, mii lea gáttis, lea mu.

#!! * __AccIll__ Selects Acc if a strict transitive verb is found to the left and Ill to your right. You are not allowed to be a possible modifier of ill: Pron, Px. ''buktán heasttaid meahccái''
SELECT:AccIll Acc IF (*-1 STRICT-TRANS-V + FMAINV BARRIER SV-BOUNDARY OR Acc)(1 Ill)(NEGATE *1 Acc BARRIER S-BOUNDARY OR COMMA)(NEGATE 0 Pron OR Px OR ABBR)(NEGATE -1 Gen)(NEGATE 0 FIRSTNAME + Nom OR (Prop Sem/Sur) + Nom LINK *-1 VERBAL-ACTIVITY-V OR DADJAT OR HUMAN-AGENT-V BARRIER SV-BOUNDARY LINK *2 ("ahte") OR EOC) ;
	#$ Leimmet buktán heasttaid meahccái, goivon ollu dávviriid eatnamii ja vuodján fatnasiid Detnui.
	#$ Mu váhnemat eaba nagodan doallat mánáid gymnásii ja dalle ii ožžon stipeandda iige loana dan dássái.
	#$ Mánáid orrun ja ovttastallan áhkuin ja ádjáin attii sudnuide eambo vejolašvuođaid giellaovdáneami ja vejolašvuođa oahppat eará doahpagiid sámegillii.
	#$ Lotnolasdiimmuin juhken olles joavkku guovtti oassái, 7. jahkeceahkki lei sierra.



 # GERUNDIUM

#!! * __Gerundium0__ selects Acc as the complement of Ger
SELECT:Gerundium0 Acc (*-1 (V TV Ger) BARRIER SV-BOUNDARY OR COMMA - @CNP)(0 Acc OR Gen)(NEGATE 0 (Pron Dem) LINK *1 N BARRIER NOT-Attr)(NEGATE 1 Acc);
	#% Sus mun jerren ráđi go mun iežan barggus deaivvadin sámegiel hálli olbmuiguin.
	#% Nu ožžot sámi beroštumit mat ovddastuvvojit stuorát deattu deaivvadettiin riikkalaš ja stuorraservodaga beroštumiiguin.

#!! * __Gerundium1__ Not Gen if no other object available for the preceding tv-verb
REMOVE:Gerundium1 Gen (*-1 TRANS-V + VFIN BARRIER SV-BOUNDARY OR Acc LINK NEGATE *-1 Acc BARRIER S-BOUNDARY)(0 Acc OR Gen)(1 Ger) ;
	#$ Ja mii loahpaheimmet dán oahpu doaladettiin suollemas ja duođalaš válggaid mas oahppit oassádallet.
	#$ Ja de son riegádahtii máná jođidettiin.

	#$ Dálveskuovat godde maiddái julggiid váccidettiin.
	#$ De mun ohppen sin áddestaladettiin máttanorgga suopmana hállat. Note! Questionable..
	#$ Mun gal ohppen vuođđofága logadettiinan bargodisipliinna ja diekkáriid, maid in goassege lean dárbbašan oahppat oahpaheaddji skuvllas. Note! Questionable..

#!! * __Gerundium2__ Acc in front of Ger, but not if it is not HAB-ACTOR/Pron Pers. No transitive verb allowed to the left, exept it it has an object of its own.
SELECT:Gerundium2 Acc (0 Acc OR Gen)(1 Ger)(NEGATE 0 HAB-ACTOR OR (Pron Pers))(NEGATE *-1 TRANS-V + FMAINV BARRIER Acc LINK NEGATE *-1 Acc BARRIER S-BOUNDARY)  ;
	#$ Konvenšuvdnaevttohusa ráhkadettiin delegašuvnnat leat váldán vuhtii Ruoŧa ođđa boazodoallolága.
	#$ Norgga delegašuvdna, várrepresideanttain, doalai sártni dievasčoahkkimii čoahkkima loahpahettiin, mas doarjjui WHO boahtteáiggi barggu dearvvasvuođain.
	#% Báhpa boradettiin biigá lei bassaladdame.

SELECT:GerundiumTEST Acc (*-1 TRANS-V + FMAINV BARRIER Acc LINK NEGATE *-1 Acc BARRIER S-BOUNDARY)(0 Acc OR Gen)(1 (V Ger TV) LINK NEGATE *1C Acc BARRIER S-BOUNDARY);
  #!! * __GerundiumTEST__ selects Gen for HAB-ACTOR and Pron Pers in front of Ger, but only if there is an Acc belonging to a transitive to the left
#SELECT:Gerundium3 Gen (*-1 Acc OR Gen BARRIER SV-BOUNDARY LINK *-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY)(0 HAB-ACTOR OR (Pron Pers) + Gen)(1 (V TV Ger)) ;
	#$ Son muitalii dan buot olbmuid guladettiin.


# Rules needed for:
	#$ Son muitalii dan buot olbmuid guladettiin.
	#$ Nu ožžot sámi beroštumit mat ovddastuvvojit stuorát deattu deaivvadettiin riikkalaš ja stuorraservodaga beroštumiiguin.
	#$ Vihtta bivdovuogi leat Deanus luosa bivddidettiin.





# Accusative or Nominative

#!! * __NomIfProp__ Removes Acc and Gen when you Prop because you are Nom. To the left is a sg3-verb. Should not hit Prop that are Sem/Plc.
REMOVE:NomIfProp Acc OR Gen IF (*-1 SG3-V BARRIER SV-BOUNDARY OR COMMA)
(NEGATE *-1 Acc OR (A Attr) BARRIER SG3-V LINK *-1 Nom BARRIER SG3-V LINK *-1 SG3-V)
(0 (N Nom) + Prop OR (N Acc) + Prop)(NEGATE 0 (Prop Sem/Plc))(NEGATE -1 Acc LINK *1 Ill BARRIER NOT-Attr) ;
	#$ Logi minuvtta maŋŋel nagodii Sørøy Glimt čiekči Stig Martinsen dulbet bohtosa ovttain moalain.
	#% Issát ásai Beer Lahai Roi birrasiin. <= Dárbbaša Sem/Plc-njuolggadusa, mii vállje Gen.
	#% Doppe Abram čuorvvui Hearrá nama. <= dát dárbbaša sierra njuolggadusa badjelis, mii vállje Gen.

#!! * __NomIfProp2__ Removes Acc and Nom when you are Prop Sem/Plc because you are Gen. To the left is a sg3-verb. To the right is a noun.
REMOVE:NomIfProp2 (Nom Sem/Plc) OR (Acc Sem/Plc) (*-1 SG3-V BARRIER SV-BOUNDARY)(NEGATE 0 FIRSTNAME OR (Prop Sem/Sur))(0 (N Nom) + (Prop Sem/Plc) OR (N Acc) + (Prop Sem/Plc))(*1 N BARRIER NOT-Attr) ;
	#$ Issát ásai Røros birrasiin.
	#$ Sidjiide lei maid liegga Ruija geasuheaddji guovlu.
	#$ Vuosttasnaššuvnnaid čoagganeapmi ovddasta badjel 300 indiánaservodaga miehtá Kanada ja gáibida dál iešstivrejumi Davvi-Amerihká buot eamiálbmogiidda.

SELECT:NomSentFin Nom (*-1 FMAINV + COPULAS BARRIER NOT-NPMOD)(0 Acc OR Gen LINK 1 EOC) ;
	#$ Organisašuvnna bláđđi man gohčodit Fokusin muitala ahte gonagas Olav su áiggistis lei bivdán beassat leat servodaga suodjaleaddji.

SELECT:jr_sr (ABBR Nom)(0 ("jr") OR ("sr") LINK -1 (Sem/Sur Nom)) ;

#!! !!Accusative before @COMP-CS<
#!! !!Accusative before some A
#!! !!Accusative sentence-finally

#!! !!Genitive

#Not in use?
#!! * __r2143__ The most frequent genitive rule: Gen when postpos immediately to the right:
SELECT:r2143 Gen IF (NEGATE 0 Adv OR PrfPrc OR (Num Nom))(1C Po)(NEGATE 1 Px OR ("eret"));
	#Comment: Iskka doallágo deaivása.




#!! !!Nominative and accusative


#REMOVE:NAr2285 Gen IF (NEGATE *-1 Pr OR NUM BARRIER NOT-NPMOD)(NEGATE 0 TIME)(1 Inf LINK 0 V-TRANS-ACT)(1C REAL-V);
	## Vuoigatvuohta journála geahččat.

#REMOVE:NAIfWeWantAcc Gen (0 Acc LINK NOT 0 TIME OR ROUTE LINK 1 Neg LINK *1 TRANS-V BARRIER SV-BOUNDARY);
	## Dáid guohtuneatnamiid ii sáhte dáhkidit sin atnui.

SELECT:NAr2266 Nom IF (-1 BOC)(0 N)(1 Rel)(2 VFIN LINK *1 COMMA BARRIER S-BOUNDARY LINK 1 VFIN LINK *1 NP-HEAD-ACC BARRIER NOT-NPMOD);
	## Ja Ipmil guhte suokkarda váimmuid, diehtá maid Vuoigŋa hálida, dasgo Vuoigŋa rohkadallá bassi olbmuid ovddas Ipmila dáhtu mielde.

#!! *__NomIFInitialThenSg3__ selects Nom if -1 BOS and 1 oblique / Sg3 lookalike. Works in fragments.
SELECT:NANomIFInitialThenSg3 Nom IF (-1 BOS)(0 (N Acc))(1 ILL-V OR LOC-V OR COM-V LINK 0 SG3-V);
    ## Liná liikui sárgut ja tennjet. (Liná)

#REMOVE:NAr2267 Gen IF (*-1 BOC BARRIER NOT-NPMOD OR Pl)(0 NP-HEAD-SG LINK 0 Gen)(NEGATE 0 TIME OR ROUTE)(1 (Pron Pers))(*1 VFIN);
#REMOVE:NAr2268 Gen IF (*-1 BOC BARRIER NOT-NPMOD OR Sg)(0 NP-HEAD-PL LINK 0 Gen)(NEGATE 0 TIME OR ROUTE)(1 (Pron Pers))(*1 VFIN);
	## Daid mun sáltejin.
	## ...man mii dan áigge dovdat gonagasriika Norgan.
	## Daid heasttaid son lohká heivet buoremusat deike galbma guovlluide.

SELECT:NAAccEllipsis1 Acc (-1 Nom LINK -1 STRICT-TRANS-V LINK NOT *-1 Acc)(1 CS);

SELECT:NAAccEllipsis2 Acc (-1 Nom LINK *-1 CS BARRIER NOT-NPMOD LINK -1 Acc)(0 Gen)(1 EOS);
	## Ovdal buorida Ipmil dálkkiidis go neavrres olmmoš dábiidis.

#REMOVE:NAr2269 Gen IF (-1 BOC)(1 Adv)(NEGATE 1 N OR A);
    	## Dieđuid maiddái ii dárbbaš addit go...

#REMOVE:NAr2271 Gen IF (*-1 TRANS-V BARRIER SV-BOUNDARY OR Acc OR COMMA LINK NOT *-1 Acc BARRIER SV-BOUNDARY)(NEGATE *-1 Pr OR NUM BARRIER NP-BOUNDARY)(NEGATE -1 ("mii"))(NEGATE *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER S-BOUNDARY)(NEGATE 0 TIME OR ROUTE OR Refl)(1 Ill OR Loc OR Com OR Adv LINK NOT *1 Acc BARRIER TRANS-V)(NOT *2 REAL-V BARRIER S-BOUNDARY)(NEGATE *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc);
	## Dalle Jesus moaitigođii gávpogiid gos son lei dahkan eanas fámolaš daguidis.

#REMOVE:NAr2272 Gen IF (*-1 TRANS-V BARRIER SV-BOUNDARY OR Acc LINK NOT *-1 Acc BARRIER NOT-ADV-PCLE)(NEGATE *-1 Pr OR NUM BARRIER NP-BOUNDARY)(NEGATE -1 ("mii"))(NEGATE *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER S-BOUNDARY)(NEGATE 0 TIME OR ROUTE)(1 Ill OR Loc OR Com OR Adv LINK *1 Gen BARRIER REAL-V LINK 1 Ill OR Loc OR Com)(NEGATE *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc);
	## Kripos lea dutkan jápminášši vuonas veahkkin NuortaFinnmárkku politiijaide.

#REMOVE:NAr2273 Gen IF (-1 (Dem Acc) LINK *-1 TRANS-V BARRIER SV-BOUNDARY OR Acc)(NEGATE *-1 Pr OR NUM BARRIER NP-BOUNDARY)(NEGATE -1 ("mii"))(NEGATE *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER S-BOUNDARY)(NEGATE 0 TIME OR ROUTE)(1 Ill OR Loc OR Com OR Adv LINK NOT *1 Acc BARRIER TRANS-V)(NEGATE *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc);
	## Dalle Jesus moaitigođii daid gávpogiid gos son lei dahkan eanas fámolaš daguidis.

#REMOVE:NAr2274 Gen IF (*-1 go BARRIER Acc LINK -1 TRANS-V)(0 Acc)(1 REAL-CLB);
	## Eai lean váldán go niestebohcco.
    ## Son gádju min eret min vašálaččain. # It shouldn´t hit this one.

#REMOVE:NAr2275 Gen IF (*-1 V-TRANS-ACT BARRIER SV-BOUNDARY)(*-1 Acc BARRIER V-TRANS-ACT LINK 1 CC)(NEGATE *-1 Pr OR NUM BARRIER NP-BOUNDARY)(NEGATE -1 ("mii"))(NEGATE *-1 CC BARRIER NOT-NP LINK *-1 GASKAL BARRIER S-BOUNDARY)(NEGATE 0 TIME OR ROUTE)(1 Ill OR Loc OR Com LINK NOT *1 Acc BARRIER TRANS-V)(NEGATE *-1 HEADOFPARTS LINK 1 Gen LINK 1 Loc);
	## Mun vuovddán gusa ja áhči beatnaga oahpaheaddjái.

#REMOVE:NAr2276 Gen IF (NEGATE *-1 Pr OR NUM BARRIER NOT-NPMOD)(0 N)(1 Dem);
	## Jahkásaš bušeahttarámma dán prošeavtta doaimmaide gártá oktiibuot 70.000 ruvnno.

#REMOVE:NAr2277 Gen IF (NEGATE *-1 Pr OR NUM BARRIER NOT-NPMOD)(1 SP-BEAIVI);
	## Sii bivdet luosa mánnodaga.

#REMOVE:NAr2278 Gen IF (*-1 OPRED-V BARRIER NOT-NPMOD LINK NOT 0 N OR Der/Pass LINK NOT *-1 NP-HEAD-ACC BARRIER SV-BOUNDARY)(1 Ess);
	## Don galggat gohčodit su Jesusin.

#REMOVE:NAr2279 Gen IF (*-1 TRANS-V BARRIER S-BOUNDARY OR Acc)(1 ("§"));
	## Vuoigatvuohta oažžut dearvvašvuođaveahki § 2-2.
 # This rule would work fine if we could make an earlier rule select Gen
 # for all occurrences of compounds in -lága in front of§.

#REMOVE:NAr2280 Gen IF (NEGATE *-1 Pr BARRIER NP-BOUNDARY)(0 (Prop Nom))(1 CLB)(NEGATE 2 (Prop Gen));
	## Gažaldat áirasis Egil Olli, Bargiidbellodaga sámediggejoavku.

#!! * __r2281__ marginal
#REMOVE:NAr2281 Gen IF (*-1 VFIN BARRIER NOT-ADV-PCLE)(0 N)(1 CC)(2 (Pron Sg3 Gen));
	## Váldde máná ja su eatni fárrui.

#REMOVE:NAr2283 Gen IF (NEGATE 0 Dem)(*1 ("hávvi" N Sg Gen) BARRIER NOT-NPMOD);
	## ..geasa dearvvaávuođabálvalus addá dahje fallá veahki juohke áidna háve...

#REMOVE:NAr2284 Gen IF (NEGATE *-1 Pr OR NUM BARRIER NOT-NPMOD)(NEGATE 0 TIME OR Num)(1 CLB)(2 CC);
	## Dat váikkuha ollu dasa, mo olbmot vásihit bálválusaid, ja oažžu sin dovdat stuorát oadjebasvuođa.

REMOVE:NAr2288 Nom IF (*-1 Acc BARRIER SV-BOUNDARY LINK *-1 ACC-INF-V BARRIER SV-BOUNDARY OR PUNCT)(NEGATE *-1 COPULAS BARRIER NOT-ADV-PCLE)(NEGATE *-1 (Interr Acc) BARRIER Acc);
	## Hearrá, suova mu vuos mannat ruoktot hávdádit áhččán.

#SELECT:NAr2292 Gen IF (0 Acc)(1 COMMA LINK 1 (N Gen) LINK 1 COMMA OR CC LINK 1C (N Gen));
    ## 80-logu beallemuttus ásahuvvojedje sámi organisašuvnnat (doaktáriid, buohccedivššáriid, sosiálabargiid ja sosionomaid sierra searvvit).

#REMOVE:NAr2290 Acc IF (NEGATE *-1 TRANS-V BARRIER SV-BOUNDARY)(NEGATE *-1 CS BARRIER V LINK 1 @COMP-CS<)(NEGATE *-1 (":") BARRIER TRANS-V)(NEGATE *-1 OLLU BARRIER NOT-NPMOD)(NEGATE 0 TIME OR Dem OR Num)(NEGATE 0 NUM LINK *1 TIME OR MEASURE BARRIER NOT-A)(NEGATE 0 ("dat") LINK 1 CS)(NEGATE *1 TRANS-V BARRIER S-BOUNDARY)(NEGATE 0 ABSTR-ROUTE)(NEGATE 1 V-TRANS-ACT + Loc)(NEGATE 0 MEASURE)(NEGATE 1 EOS OR COMMA)(NEGATE 1 (Pron Rel));
	## Sámedikkis lea leamaš čoahkkin 14.03.01.
	## Leago dudno mielas suohtas go sáhttá rabas julggiid vázzit?



#!! !!Nominative


	# Nominative
	# ==========

	# Miscellaneous rules
	# Vocatives, subjects of sentence fragments
	# Nominative in titles and sentence fragments
	# Nominative after "go", "dego", "dugo" and "nugo"
	# Nominative subjects
		# Preverbal subjects
		# Postverbal subjects
	# Nominative predicatives
	# Nominative as objects in existential clauses
	# Nominative in coordination and apposition
	# Nominative in parallell constructions
	# Not nominative

	# Miscellaneous rules
	# --------------------
SELECT:ND Nom IF (0 Coll + Loc LINK *0 Du3) ;
	## Bođežeaba go Ánne-guovttos ovdal gaskaija?

SELECT:NDr2300 Nom IF (-1 Gen)(NEGATE -1 (Pron Dem Gen))(0 N-SG-NOM LINK NOT 0 Loc)(NEGATE 0 PrfPrc OR Ill OR Po)(1 SG3-V);
	## Du ášši lea dehalaš.
	## Dan ášši ferte dutkat.
 # HMM, brave.. - but it seems to work!
 # Note that the tag is selected, the set is in the condition.

SELECT:NDr2302 Nom IF (-2 Num)(-1 ("ruvdno"));  # 0-3
	## Láđđi máksá 70 ruvnno mehtar.

SELECT:NDr2304 (Num Sg Loc) IF (-1 ("álgit") OR ("boahtit") OR ("vuolgit") OR ("diibmu") OR ("biellu"))(1 EOC); # 0-3
	## Filbma álgá čiežas.


SELECT:NDr2305 (Coll Nom) IF (-1 (Pers Nom))(-1 Pl1 OR Pl2 OR Pl3); # 0-2
	## De mii golmmas leansmánniin manaimet geahččat.

SELECT:NDr2306 (N Nom) IF (*-1 ("okta" Nom) OR ("nubbi" Nom) BARRIER NOT-NPMOD LINK NOT 0 Foc/ge);
	## Málli vuššojuvvui okta lihtter.


	# Nominative if two names in a row
SELECT:NDr2308 Nom IF (*-1 PROP BARRIER REAL-WORD-NOT-ABBR)(0 PROP)(NEGATE 0 Gen LINK 1 N)(*1 SG3-V BARRIER VFIN);
	## Birasgáhttenminister Siri Bjerke dajai čielgasit ahte...

	# Vocatives, subjects of sentence fragments
	# - - - - - - - - - - - - - - - - - - - - -
SELECT:NDr2309 Nom IF (*-1 BOS OR (":") BARRIER NOT-A)(0 Prop)(1 COMMA);
	## Biera, du sabehiid!
	## Jovsset, Dávveda Bárdni!

SELECT:NDr2310 Nom IF (NEGATE -2 HUMAN-INDIVIDUAL)(-1 COMMA)(0 Prop)(1 EOS);
	## Boađe deike, Duommá!

SELECT:NDr2311 (Sg Nom) IF (*-1 MUN OR DON OR (Pron Sg3) BARRIER NOT-A LINK *-1 BOS OR (":") BARRIER NOT-A-CC)(1 EOS); # 3-0
	## Son celkkii: Don jalla!

SELECT:NDr2312 (Pl Nom) IF (*-1 MII-PERS OR DII OR SII BARRIER NOT-NPMODADV LINK *-1 BOS OR CLB BARRIER NOT-A-CC)(1 EOS); # 0-2
	## Dii guoktilaččat!

SELECT:NDr2313 (A Pl Nom) IF (-2 MII-PERS OR DII OR SII LINK *-1 BOS OR CLB BARRIER NOT-A-CC)(-1 Adv OR Indef)(1 EOS); # 0-3
	## Dii uhccán oskkolaččat!

SELECT:NDr2314 Nom IF (*-1 BOS OR PUNCT BARRIER NOT-NPMOD)(1 COMMA)(2 Imprt);
	## Sáhtán, gáidda eret!

SELECT:NDr2315 Nom IF (-1 BOS OR PUNCT OR REAL-CLB)(1 COMMA);
	## Oahpaheaddji, mun áiggun čuovvut du.


	# Nominative in titles and sentence fragments
	# - - - - - - - - - - - - - - - - - - - - - -

	# A single word is nominative
SELECT:NDr2317 Nom IF (-1 BOS OR (":"))(NEGATE 0 VFIN)(0 N OR A OR Pron)(1 EOS);
	## Katja.
	## Bálvaleaddjit manne dalle isideaset lusa ja jerre: Hearrá!

	# A single word with a numeral in front of it is nominative
SELECT:NDr2318 Nom IF (-2 BOS)(-1 Num)(1 EOS);
	##

	# An NP head with a genitive modifier is nominative
SELECT:NDr2319 Nom IF (-2 BOS)(-1 Gen)(0C N)(1 EOS);
	##

	# A title is nominative if it has a Nom reading at all.
SELECT:NDr2320 Nom IF (0 TITLE LINK NOT 0 ABBR LINK 1 FIRSTNAME OR Sem/Sur);
	## Stuorra giitu maiddái professor Birger Stuevold Lassenii. # in this case the title of a person is meant, not a sentence fragment

	# An NP head with an Attr modifier is nominative.
SELECT:NDr2321 Nom IF (-2 BOS)(-1 Attr)(1 EOS);
	##

	# A Gen series is broken by an intermediate Prop

	# Nominative after "go", "dego", "dugo" and "nugo"
	# - - - - - - - - - - - - - - - - - - - - -
SELECT:NDr2324 Nom IF (*-1 go BARRIER NOT-NPMOD LINK NOT -1 VFIN)(*-1 COPULAS BARRIER NOT-AUX-V OR CS)(NEGATE 1 N OR A);
	## Jesus lea gievrrat go Sáhtán ja bahá vuoiŋŋat.

SELECT:NDr2325 Nom IF (*-1 go BARRIER NOT-NP LINK NOT -1 VFIN)(NEGATE 0 Acc OR VFIN)(1 EOC);
	## Ii leat almmustuvvan oktage stuorit go gásttašeaddji Johanas.

SELECT:NDr2326 Nom IF (*-1 XGO BARRIER NOT-NPMOD)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD)(*1 NP-BOUNDARY OR CLB BARRIER WORD);
	## Su ámadadju báittii dego beaivváš.

SELECT:NDr2327 Nom IF (-2 XGO)(-1 Gen OR PrsPrc)(1 NP-BOUNDARY);
	## ...johtá dego gillju ledjon ozadettiin muhtuma...

	## ...dego vuonccis čohkke čivggaidis soajáidis vuollái.

SELECT:NumNomgo (Num Nom) IF (-1 go LINK -1 Comp LINK NOT *-1 TRANS-V BARRIER NOT-ADV-PCLE) ;
	## ...go heaittihanáigemearri lea oanehat go vihtta jagi.
SELECT:NumAccgo (Num Acc) IF (-1 go LINK -1 Comp LINK *-1 TRANS-V BARRIER NOT-ADV-PCLE) ;

	# Nominative of "nummar" and "nummir" preceding a cardinal
	# - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

	# Nominative subjects
	# -------------------

	# Preverbal subjects
	# - - - - - - - - - -
	# The noun is very seldon, if it as at all.

SELECT:NDr2331 (N Nom) IF (*-1 (Dem Nom) BARRIER NOT-A)(NEGATE 0 ("leapma" Der/Dimin) OR Px);
	## Noađđespagáin leat dat geažit guokte golbma tumá guhku.
	## ...ja ahte dat dáhttu čielgasit berre doahttaluvvot.

SELECT:NDr2332 (Num Nom) IF (-1 (Dem Sg Nom));
	## Dát guokte ášši Siviilaáittardeaddji geavadis leat váldon...

SELECT:NDr2333 (Num Nom) IF (*1 TIME + Gen BARRIER NOT-A LINK *1 ("leat") BARRIER N LINK *1 Nom BARRIER NOT-NPMOD) ;
	## Vuosttaš logi minuvtta lei buorre áigodat Nordlysa ektui.

SELECT:NDr2334 Nom IF (*-1 BOC OR PUNCT BARRIER NOT-NPMODADV OR (Dem Acc))(0 Sg OR ABBR LINK NOT 0 Ord)(NEGATE 0 Adv OR Loc OR Po OR VFIN)(NEGATE 0 Ess LINK *1 Nom BARRIER NOT-NPMOD)(NEGATE 0 Ess LINK *1 OPRED-V BARRIER SV-BOUNDARY LINK *0 Nom BARRIER WORD)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD)((*1C SG3-V BARRIER NOT-ADV-PCLE) OR (1 (N Com) OR (N Loc) LINK *1C SG3-V BARRIER NOT-ADV-PCLE LINK NOT *1 (Sg Nom) BARRIER NOT-NPMOD));
	## ...ahte doaimma jođihangoddi vejolaččat mearrida...
	#$ Almmolaš sámi ságastallan lea vejolaš.

SELECT:NDr2335 Nom IF (*-1 BOC OR PUNCT BARRIER NOT-NPMODADV OR (Dem Acc))(0 Sg)(NEGATE 0 Adv OR Loc)(NEGATE 0 Ess)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD)(1 (Num Nom))(*2C SG3-V BARRIER NOT-ADV-PCLE);
	## NOU 1995:6 cealká earret eará nu.

#!! * __NDr2336__ selects (N Sg Nom) when 1 SG3-V
SELECT:NDr2336 (N Sg Nom) IF (NEGATE 0 Acc OR Po OR Loc)(1 SG3-V)(NEGATE 1 N)(NEGATE *2C Nom BARRIER S-BOUNDARY);
	#$ Doaibmaplána árvvoštallan čájeha maiddái dan.
	#% Dan ášši ferte čilget nállevealaheami ektui sierra. #Comment: Topicalised object.

SELECT:NDr2337 (N Sg Nom) IF (NEGATE 0 Acc)(1 ("oktan") LINK 1 Com)(3 SG3-V)(NEGATE 1 N)(NEGATE *4C Nom BARRIER S-BOUNDARY);
	## Ohcan oktan cealkámušaiguin sáddejuvvo boazodoallohálddahusa bokte Eanadoallodepartementii.

SELECT:NDr2338 (N Sg Nom) IF (NEGATE *-1 (N Sg Nom) BARRIER S-BOUNDARY OR CC OR CLB)(NEGATE 0 Loc)(1 (Neg Sg3));
	## Sámi dutkan ii leat dan dihte soaittáhagas válljejuvvon dutkansuorgi.

SELECT:NDr2339 Nom IF (*-1 BOC OR PUNCT BARRIER NOT-NPMODADV)(0 Pl)(NEGATE 0 Adv OR Ill OR VFIN)(*1 PL3-V BARRIER NOT-ADV-PCLE);
	## ...go beaktilis sivat bealuštit dan.


	## Ruovttus fárren sáhttá čuohcit olbmui sakka.
 # The idea here is that 0 is not accusative, since there is an ILL-V.
 # So we could alternativelyREMOVE:ND Acc. But the present rule seems to work.

SELECT:NDr2341 Nom IF (*-1 CS OR BOS OR CC OR CP BARRIER NP-HEAD - ("buot" Pron Indef Attr) OR VFIN)(0 NUM)(*1 PL3-V BARRIER SV-BOUNDARY)(NEGATE *1 Nom BARRIER S-BOUNDARY);
	## Guokte jahkásaš doavttergrádakurssa leat lágiduvvon.
	## Go máŋggas galget čállit seamma journálii, de...
 # In the last example, the reading N Sg Nom is chosen because of the reading
 # Num Sg Loc.
 	#$ Buot njeallje máná ledje náđđon eagi vuostá. #Comment: In order for the numeral to be the head in this sentence, I had to add - ("buot") to the BARRIER NP-HEAD which includes pronouns.

SELECT:NDr2342 Nom IF (*-1 CS OR BOS OR CC OR CP BARRIER NP-HEAD OR VFIN)(0 NUM)(*1 COPULAS BARRIER SV-BOUNDARY LINK 0 (Pl3) LINK NOT *1 PrfPrc OR Inf BARRIER SV-BOUNDARY);
	## Vihtta bivdovuogi leat Deanus luosa bivddidettiin.

SELECT:NDr2343 (Sg Nom) IF (*-1 CS OR BOS BARRIER NOT-ADV-PCLE)(NEGATE 0 Adv OR PrfPrc)(NEGATE 0 Qst)(*1 SG3-V OR DU3-V BARRIER NOT-NP OR Nom OR Ess OR SV-BOUNDARY LINK NOT *1 NP-HEAD-SG-NOM BARRIER NOT-NPMODADV)(NEGATE 0 Gen LINK *1 N BARRIER NOT-NPMOD)(NEGATE 0 Ess);
	## Mii vuordit ahte stáhta sámi vuoigatvuođaid barggu oktavuođas čuovvu...

SELECT:NDr2345 Nom IF (0 ("galle"))(*1 PL3-V BARRIER Nom OR VFIN);
	## ... ja albmada galle 'mearrasámi' ledje vearrováldit.

SELECT:NDr2346 Nom IF (0 Coll)(*1 PL3-V BARRIER NOT-ADV-PCLE);
	## Dakkár doaimmat, main máŋggas čállet dieđuid journálaide,...

	## Go logi jagi lea vássán maŋimus journálamerkesteami rájes,...

	## Man uhccán osku dus lea!

SELECT:NDr2350 Nom IF (0 Sg)(1 CC)(*2 (Sg Nom) BARRIER NOT-NPMOD LINK *1 DU3-V BARRIER NOT-ADV-PCLE);
	## Silas ja Timoteus báziiga Bereai.

SELECT:NDr2351 NP-HEAD-NOM IF (*-1 CC BARRIER NOT-NPMOD LINK -1 NP-HEAD-NOM)(1 PL3-V);
	## Árbevierru ja geavat sáhtášedje leat dulkonmomeanttat.

	## No examples in corpus?

SELECT:NDr2353 Adv IF (*-1 (N Pl Nom) BARRIER S-BOUNDARY OR COPULAS OR ("orrut"))(0 (A Pl Nom));
	## Eamiálbmotlaš áššit jogo systemáhtalaččat dubmejuvvojit menddo siivut...

SELECT:NDr2354 Adv IF (0 (A Pl Nom))(*1 NOT-AUX-V BARRIER S-BOUNDARY)(*1 (N Pl Nom) BARRIER S-BOUNDARY)(NEGATE 1 CC OR COMMA LINK *1 N OR A BARRIER NOT-NPMOD);
	## Nappo leat gažaldagas historjjálaččat háhkkojuvvon vuoigatvuođat.

SELECT:NDr2355 Adv IF (-1 BOC)(0 (A Pl Nom))(NEGATE 1 CLB)(NEGATE *1 DU3-V OR PL3-V BARRIER SV-BOUNDARY);
	## ...go cálálaččat lea registreren maid ja man ollu lea borran.
	## Reálapolitihkalaččat gehččojuvvon, de lea mihtilmas...

SELECT:NDr2357 (A Pl Nom) IF (*-1 BOC BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3)(NEGATE *1 NOT-AUX-V BARRIER S-BOUNDARY);
	## Nu maŋimuččat šaddet vuosttamužžan.

SELECT:NDr2358 (A Pl Nom) IF (*-1 BOC BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS BARRIER NOT-ADV-PCLE LINK 0 Du3)(NEGATE *1 NOT-AUX-V BARRIER S-BOUNDARY); # 0-4
	## ...ahte oasálaččat eaba leat dássásaččat.

SELECT:NDr2359 (A Pl Nom) IF (*-1 BOC BARRIER NOT-ADV-PCLE)(*1 Neg OR COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3 LINK *1 CC BARRIER S-BOUNDARY LINK *1 VFIN BARRIER S-BOUNDARY); # 8-0
	## Geafit gal leat álo din luhtte ja sidjiide sáhttibehtet dahkat buriid.


#
#The end of accusatives: removes all accusative leftovers


	# Postverbal subjects
	# - - - - - - - - - -

SELECT:NDr2360 Nom IF (*-1 VFIN BARRIER NOT-NPMOD OR Nom LINK NOT *-1 Nom BARRIER S-BOUNDARY)(*-1 VFIN BARRIER NOT-NPMOD OR Nom LINK *-1 NP-HEAD-ACC BARRIER S-BOUNDARY)(0 N LINK NOT 0 PrfPrc OR VFIN)(NEGATE *1 Nom BARRIER S-BOUNDARY LINK NOT 0 Po);
 	## Dán áiddi huksii stáhta.
 	##$ Liná liikui sárgut ja tennjet. (sárgut)
 	#% Sin bargun lei váldit fáŋgan Gonagasa.

SELECT:NDr2361 (N Sg Nom) IF (*-1 SG3-V BARRIER NOT-NPMODADVIIP OR CP LINK NOT 0 (Imprt Sg2) LINK *-1 BOC BARRIER Nom LINK NOT 0 Nom)(NEGATE -1 ("buorre" Acc))(NEGATE 0 VFIN OR PrfPrc OR Indef OR Gen OR Ill OR Loc OR (Actio Ess) OR Po OR (A Nom))(NEGATE 1 ("ahte"))(NEGATE 0 Loc LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD)(NEGATE 0 PrsPrc LINK 1 (N Sg Nom))(NEGATE 0 Attr LINK 1 N);
    	## Guovddážis dán barggus lea Sámi dutkamiid guovddáš.
    	## Riikkaid galgá Piera oaidnit.

	## Erenoamášit guoská dát dakkár áššiide, mat...
	## Ovdabargguin ii boađe ovdan eará, go ahte...

	## Eahpitkeahttá lea ávkin sámi beroštumiide dat go guovllut...

SELECT:NDr2364 (Sg Nom) IF (*-1 (Imprt Sg3) BARRIER NOT-NP)(NEGATE 0 Acc OR Ess);
	## Nu báitos din čuovggas ge olbmuide.

	##

SELECT:NDr2366 Nom IF (*-1 Der/Pass BARRIER CC OR NOT-NPMODADV)(NEGATE *-1 Nom BARRIER S-BOUNDARY)(NEGATE -1 (Der/PassS PrfPrc) OR (Der/PassL PrfPrc) LINK NOT *-1 REAL-COPULAS BARRIER SV-BOUNDARY OR PUNCT)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD)(NOT 0 Attr);
	## ...ahte lea ráhkaduvvon diehtojuohkinmateriála.
	#$

SELECT:NDr2367 Nom IF (NEGATE *-3 Nom BARRIER S-BOUNDARY)(-2 Der/Pass)(-1 (Der/PassL PrfPrc) OR (Der/PassS PrfPrc));
	## Váldonjuolggadussan galgá geavahuvvot čohkkejuvvon journála...

SELECT:NDr2368 (N Pl Nom) IF (*-1 PL3-V + IV BARRIER NOT-NPMODADVIIP LINK *-1 BOC BARRIER Nom)(NEGATE 0 PrfPrc OR Inf);
	## 1995:s gárvánedje rádiobuvttadeapmái lanjat.

SELECT:NDr2369 (Pl3 Nom) IF (*-1 PL3-V BARRIER NP-HEAD-NOM LINK *-1 BOS OR CS OR CLB BARRIER NP-HEAD-NOM)(NOT 1 NP-HEAD-NOM);
	## No hits?

SELECT:NDr2370 (Num Nom) IF (*-1 ILL-V BARRIER NOT-NPMODADV LINK 0 (Pl3) LINK *-1 Ill BARRIER NOT-ADV-PCLE);
	## Eanetlohku, masa gulle Riektejoavkku 6 eará lahtu,...

SELECT:NDr2372 (Pron Pl Nom) IF (*-1 PL3-V BARRIER NP-HEAD-NOM OR SV-BOUNDARY LINK *-1 BOC BARRIER NP-HEAD-NOM);
	## Eará osiid giddodagaid dáfus, leat dát dávjá dilit maid...
	## Sáhttetgo earát gáibidit buhtadasa boahtá das makkár beroštumiin lea...

SELECT:NDr2373 Nom IF (*-1C PL3-V BARRIER NOT-ADV-PCLE OR MO LINK *-1 BOC BARRIER Nom)(0 Coll OR NUM LINK NOT 0 Adv);
	## Mus báhce moadde girjji logakeahttá.
	## Go sotnabeaivi šattai, de čuoigaledje máŋggas várrái.
	#% De ohcot moadde girjji.
	#% Arkiivaávdnasat galget sáddejuvvot go leat sullii 25-30 jagi boarrásat. #Comment: Why is this marked?

	## ...de bohte muhtun viisát nuortan Jerusalemii.

SELECT:NDr2375 Nom IF (*-1 COMMA BARRIER NOT-NPMOD LINK NOT *-1 MAIN-V BARRIER S-BOUNDARY)(NEGATE *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 Adv OR ("leat" PrfPrc) OR Imprt OR Po OR VFIN)(NEGATE 0 Actio LINK 1 ("veara")) ;#(NEGATE 0 PrfPrc LINK *-1 MOVT LINK -1 ("nu") LINK -1 ("leat"));
	#$ Dá dat leat, 350 ruvnno.
	#% Eará guovlluin gos leat vel unnit sámit go Finnmárkkus, leamaš dákkár ákkain vel stuorát deaddu almmolaš ságastallamis.  # It should not take the latter one.

SELECT:NDr2376 Nom IF (*-1 VERBAL-ACTIVITY-V BARRIER NOT-NPMOD LINK -1 COMMA LINK *-1 VFIN)(NEGATE 1 Nom);
	## Don leat jallu, dadjá Tomi.
	## Don leat jallu, dadjá Tomi skihpár.
# !! * __PostVNom__ selects Nom if a Sg 3p verb to the left with no Nom to the left of it

SELECT:PostVNom (N Sg Nom)  IF (*-1C SG3-V BARRIER NOT-NPMODADV - Qst OR PrfPrc LINK NOT *-1 (Sg Nom) OR (Pron Sg3) BARRIER S-BOUNDARY)(NEGATE 0 Gen LINK *1 N BARRIER NOT-NPMOD)(NEGATE 0 Loc LINK *1 (Sg Nom) BARRIER VFIN)(NEGATE 0 Po OR Loc)(NEGATE 0 Attr LINK 1 N);
	#$ Lávvui gullá maid uvssot.
	#% Bovnnas lei viessu, ja viesus lei nuorra nisu mánnáváivvis.
	#% Son lea njulgen čielggi, ja lea nuorra ja searra.
	#% Son liiko guollái.

SELECT:PostVNomCop (N Sg Nom)  IF (*-1C COPULAS + Sg3 BARRIER NOT-NPMODADV - Qst OR PrfPrc LINK NOT *-1 (Sg Nom) OR (Pron Sg3) BARRIER S-BOUNDARY)(0 Nom LINK 1 (A Sg Nom));
	#$ Lea go Čáhppes Bearral buorre dutnje?


	# Nominative predicatives
	# -----------------------

#REMOVE:NDr2377 A IF (NEGATE *-1 COPULAS OR OPRED-V BARRIER NOT-AUX-V)(0 N)(NEGATE 0 ("veara"))(1 CLB)(NEGATE 2 A OR PrsPrc);
	## Ulbmil lea ovddidit guovddáža eamiálbmotdutkamiid guovddážin.
 # This rule has to follow identification of subject.

SELECT:NDr2378 (Sg Nom) IF (*-1 ("leat") BARRIER SV-BOUNDARY OR PUNCT OR NP-HEAD-NOM LINK 0 Sg3)(0 NP-HEAD OR (A Nom))(NEGATE 0 PrfPrc OR Der/NomAg OR (Prop Loc) OR Ess)(NEGATE 0 N-PO)(NEGATE 0 Loc LINK *-1 Nom BARRIER S-BOUNDARY OR PUNCT)(NEGATE 0 A LINK 0 Ill)(NEGATE 0 Adv LINK 1 Pr OR VGen)(1C NP-BOUNDARY);
	## Dasgo dus boahtá oaivámuš gii lea mu álbmoga Israela báimman.
	## Dat gal lei beare unnán.

SELECT:ND Nom IF (*-1 ("leat") BARRIER NOT-NPMOD LINK *-1 HUMAN OR Pers)(0 HUMAN LINK 1 PLACE OR Sem/Plc) ;

SELECT:NDr2379 (Sg Nom) IF (*-1 COPULAS BARRIER CC OR NOT-NPMODADV OR (Sg Nom))(*-1 SG3-V BARRIER NOT-AUX-V)(NEGATE 0 Loc OR Ess OR Adv)(NEGATE 0 N-PO)(*1C NP-BOUNDARY BARRIER WORD);
#SELECT:NDr2379 (Sg Nom) IF (*-1 COPULAS BARRIER CC OR NOT-NPMODADV OR (Sg Nom))(*-1 SG3-V BARRIER NOT-AUX-V)(NEGATE 0 Loc OR Ess OR Adv)(NEGATE 0 N-PO)((*1C NP-BOUNDARY OR COMMA BARRIER WORD) OR (*1 Sem/Time BARRIER NOT-NPMOD LINK *1 NP-BOUNDARY OR COMMA));
	## Go šattai eahket, de...

SELECT:NDr2380 (Pl Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY OR PUNCT OR NP-HEAD-NOM LINK 0 PL-V OR DU-V)(0 A OR NP-HEAD LINK NOT *1 TRANS-V BARRIER SV-BOUNDARY)(NEGATE 0 Inf); #(1C NP-BOUNDARY OR (N Pl Nom));
	## Vuollel 7% Finnmárkku eatnamiin leat vuovddit.
	## Olmmošlassáneami geažil šadde eanebut geat galge juogadit buriid.
	## Ledje go hilbadat mánát?
	#$ Muhto leimme gal bivnnuhat gili nieiddaid gaskkas.

SELECT:NDr2381 (Pl Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY OR PUNCT OR NP-HEAD-NOM LINK 0 Pl3)(0 A OR NP-HEAD)(NEGATE 0 Inf)(1 CLB)(2 NOT-NP);
	## ...main leat mielde dušše priváhta oasálaččat, ii leat...

SELECT:NDr2382 (Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3)(NOT 0 Inf)(1 BOC OR COMMA);
	## ...seammás go nuppe dáfus leat veháš várrugasat, ja dadjet ahte...

SELECT:NDr2383 Nom IF (*-1 COPULAS BARRIER NOT-NPMODADV OR PrfPrc LINK *-1 MUN BARRIER NOT-ADV-PCLE)(*-1 SG1-V BARRIER NOT-AUX-V)(NEGATE 0 PrfPrc OR (Actio Ess))(NEGATE 0 Loc LINK *-1 Nom BARRIER S-BOUNDARY OR PUNCT)(NEGATE 0 Ess)(NEGATE 1 N OR A)(NEGATE *1 PrfPrc BARRIER SV-BOUNDARY); # 0-3
	## Vaikko mun lean friddja in ge lean geange vuollásaš,...

SELECT:NDr2384 Nom IF (*-1 COPULAS BARRIER NOT-NPMODADV OR PrfPrc LINK *-1 DON BARRIER NOT-ADV-PCLE)(*-1 SG2-V BARRIER NOT-AUX-V)(NEGATE 0 PrfPrc)(NEGATE 0 Loc LINK *-1 Nom BARRIER S-BOUNDARY OR PUNCT)(NEGATE 1 N)(NEGATE *1 PrfPrc BARRIER SV-BOUNDARY); # 0-3
	## Mii diehtit ahte don leat Israela oahpaheaddji.

SELECT:NDr2385 Nom IF (*-1 COPULAS BARRIER SV-BOUNDARY)(NEGATE *-1 NP-HEAD-NOM BARRIER NOT-NPMODADV)(NEGATE 0 REAL-V OR Adv OR Loc OR Ill OR Ess)(*1 S-BOUNDARY BARRIER NP-HEAD OR (A Nom));
	## ...maidda lágas eai leat sierra njuolggadusat, muhto...

SELECT:NDr2386 Nom IF (-2 Nom)(-1 COPULAS)(0 N OR A)(NEGATE 0 PrfPrc OR Ess OR (Prop Loc))(1 EOS OR ("go"));
	## ...ahte dat leat earalágan go eará joavkkut.
 # Hits here because "earalágan" does not agree?

SELECT:NDr2387 Nom IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 Coll);
	## Stobus leigga guovttis.
	## Mii leimmet golmmas.

	# Nominative as objects in existential clauses
	# --------------------------------------------
SELECT:NDSgr2388 Nom IF (0 Sg LINK *-1 COPULAS + Sg3 BARRIER NOT-NPMODADV OR Dem - Nom LINK *-1 NORM-LOC BARRIER N OR SV-BOUNDARY)(NEGATE 0 Adv OR Ess OR PrfPrc)(NEGATE *1 Nom BARRIER NOT-NPMOD)(NEGATE 0 Attr LINK 1 N);
	## Ii dus leat gávdni masage.

SELECT:NDPlr2388 Nom IF (0 Pl OR NUM LINK *-1 COPULAS + Pl3 BARRIER NOT-NPMODADV OR Dem - Nom LINK *-1 NORM-LOC BARRIER N OR SV-BOUNDARY)(NEGATE 0 Adv OR Ess OR PrfPrc)(NEGATE *1 Nom BARRIER NOT-NPMOD);
	## Mus leat golbma máná.


SELECT:NDr2389 Nom IF (*-1 COPULAS BARRIER NOT-NPMODADV OR Nom LINK *-1 Loc BARRIER N OR A OR SV-BOUNDARY)(NEGATE 0 PrfPrc)(1 NP-BOUNDARY)(NEGATE 0 Attr OR PrsPrc LINK *1 N BARRIER NOT-A-CC)(NEGATE 0 Adv LINK *1 Nom BARRIER S-BOUNDARY);
	## Dat bulle ja goldne go dain ii lean ruohtas.

SELECT:NDr2390 Nom IF (*-1 COPULAS BARRIER Inf OR PrfPrc OR SV-BOUNDARY OR Nom)(*-1 PL3-V BARRIER SV-BOUNDARY LINK *-1 Loc BARRIER NOT-ADV-PCLE)(0 NUM);
	## Davviriikkain leat eahpitkeahttá guokte álbmoga maid atnit álgoálbmogin.

SELECT:NDr2391 Nom IF (*-1 Loc BARRIER SV-BOUNDARY OR Nom LINK *-1 COPULAS BARRIER SV-BOUNDARY OR Nom LINK NOT *-1 Nom BARRIER SV-BOUNDARY)(NEGATE 0 PrfPrc)(NEGATE 0 Adv LINK *1 Nom BARRIER S-BOUNDARY)(NEGATE 0 Attr LINK *1 N BARRIER NOT-NPMOD)(NEGATE 0 VFIN LINK -1 CLB)(1 NP-BOUNDARY);
	## Ovdal go kristtalašvuohta bođii Sápmái lei noaiddis erenoamáš
	## rolla oaidnilin ja dálkkodeaddjin.

#"REMOVE:NDr2392 Acc IF (*-1 COPULAS BARRIER PrfPrc OR Inf OR (Actio Ess) OR SV-BOUNDARY)(NEGATE 0 TIME OR MEASURE)(1 CS OR EOS);
	## Dain áššiin lea Ruonáeatnama ruovttustivrras ráđđeaddi váldi.

#"REMOVE:NDr2393 Nom IF (NEGATE 0 Dem)(1C (N Nom))(NEGATE 1 PUNCT OR Num OR Interr)(NEGATE *-1 COPULAS BARRIER SV-BOUNDARY)(NEGATE 0 ("son"))(NEGATE *1 COPULAS BARRIER SV-BOUNDARY LINK NOT *1 NOT-AUX-V BARRIER S-BOUNDARY)(NEGATE -1 Gen LINK 1 Actio LINK 1 N);
	## Divššohasjournála láhkaásahus.
	## ...maid buot journála geavaheaddji dearvvašvuođabargit áddejit.

#"SELECT:NDr2394 (Pl Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY OR go OR Nom LINK 0 Pl3)(1 EOS);
	## Váikkuhusat divššohasa lagamuččaide leat mearkkašahtti stuorrát.
#"SELECT:NDr2395 (Pl Nom) IF (*-1 COPULAS BARRIER NOT-NPMODADV OR PrfPrc)(*-1 DU-PL-V BARRIER NOT-AUX-V OR CS)(*-1 PPRON-DU-PL BARRIER S-BOUNDARY LINK NOT *-1 (Pl Nom) BARRIER NOT-ADV-PCLE)(NOT *1 TRANS-V BARRIER SV-BOUNDARY);
	## Ehpet go dii leat ollu mávssolaččabut go dat?

SELECT:NDr2396 (Pl Nom) IF (*-1 COPULAS BARRIER NOT-NPMODADV OR PrfPrc)(*-1 DU-PL-V BARRIER NOT-AUX-V OR CS)(NEGATE 0 Inf)(*-1 (N Sg Nom) BARRIER S-BOUNDARY LINK *-1 CC BARRIER NOT-NPMOD LINK -1 (N Sg Nom));
	## Anne ja Biret leaba goappašagat nu jallat.

SELECT:NDr2397 Nom IF (*-1 COPULAS OR (IV) BARRIER NOT-ADV-PCLE)(0 NUM)(1 ("ruvdno"));
	## Doaibmabiju jahkásaš bušeahttarámma lea: 380.000 ruvnno.

	# Nominative in coordination and apposition
	# -----------------------------------------

#"REMOVE:NDr2398 Nom IF (*-1 VFIN LINK NOT 0 Pl)(0 Loc LINK 1 CRD LINK *1 Nom LINK NOT *1 VFIN);
  	## Plána lei ahte galggai čađahit johtaluslohkamiid diimmá jagi skuhtervuodjimis ja bievlavuodjimis.

SELECT:NDr2399 Nom IF (*-1 CRD BARRIER NOT-NPMODADV LINK -1 Nom LINK NOT 0 Acc LINK NEGATE *-1 NOT-AUX-V BARRIER NOT-NPMODADV)(NEGATE 0 VFIN)(1C NP-BOUNDARY);
	## Divššohas dahje su ovddasteaddji gii oaivvilda ahte...

SELECT:NDr2400 Nom IF (-2C HEAD-N-NOM)(-1 CRD)(NEGATE 0 RCmpnd OR Der/NomAg OR Adv)(NEGATE 0 Attr OR Gen LINK 1 (N Nom))(NEGATE 0 PrfPrc LINK -1 CC LINK *-1 PrfPrc BARRIER REAL-V)(NEGATE 0 VFIN);
	## Eamiálbmogat ja riekti - muhtun prinsihpalaš vuolggasajit.

SELECT:NDr2401 Nom IF (-2 (Pron Nom))(-1 CRD);
	## Jesus Kristus ieš ja Ipmil.

SELECT:NDr2402 Nom IF (-1 CRD LINK *-1C (Num Nom) BARRIER NOT-N-A)(0 Num);
	## Journála divodeapmi ja sihkkun.
	## Joavkkus leat guokte Riikaoasselávdegotti ovddasteaddji ja 2 Sámedikkis.

SELECT:NDr2403 Nom IF (-2C Nom)(-1 CRD)(1 Gen LINK *1 Po BARRIER NOT-NPMOD)(NEGATE *1 Nom BARRIER Po);
	## Dearvvašvuođaásahusas mearriduvvo divvun dahje sihkkun dearvvašvuođabargiidlága § 44 mielde.

SELECT:NDr3529 Nom IF (*-1 TITLE BARRIER NOT-NPMOD LINK 0 ADVLCASE)(0 Prop);
	## Son logai dan maiddái professoriidda Asbjørn Kjønstad ja Kristian Andenæs.
		## Son logai dan maiddái professoriidda Kjønstad ja Andenæs.


#SELECT:NDr2404 Nom IF (*-1 CRD BARRIER NOT-NPMOD LINK *-1 Nom BARRIER WORD LINK NOT 0 PrfPrc)(1 CS OR EOS OR (PUNCT RIGHT))(NEGATE 0 Inf LINK -1 CC LINK -1 Inf);
	## ...Jáhkoba ja Jovssesa eadni, ja Salome.

#SELECT:NDr2405 Nom IF (NEGATE 0 PrfPrc LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(1 CRD)(*2C Nom BARRIER W-NOT-ACC-GEN LINK NOT 0 Rel LINK 1 NP-BOUNDARY OR CLB OR PUNCT-LEFT OR PUNCT-RIGHT);
	## 3) Oččodeapmi, diehtojuohkin ja ásahusovttasbargu.
	## Molssaevttolaš dálkkasdiehtaga našunála dutkanguovddáš ja Sámi dearvvašvuođadutkanguovddáš (goappašat fágabirrasat gullet Romssa universitehta vuollái) berrejit sáhttit oassálastit dán bargui.

SELECT:NDr2406 Nom IF (1 COMMA LINK *1 Nom BARRIER NOT-NP LINK 1 CC LINK *1 Nom BARRIER NOT-NP LINK *1 PL3-V BARRIER NOT-ADV-PCLE);
	## Sámedikki hálddahusa nannen, dearvvašvuođa- ja eallindiliiskkadeami
	## ruhtadeapmi ja Sámi dearvvašvuođadutkanguovddáža doaimmaid viiddideapmi
	## váldojit ovdan.

SELECT:NDr2407 Nom IF (NEGATE 0 Loc)(1 COMMA)(*2 NP-HEAD-NOM BARRIER NOT-NPMOD LINK 1 CC LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD LINK 1 CC OR COMMA OR NP-BOUNDARY);
	## ...dikšu, dearvvašvuođabisuheaddji, veajuidahtti dahje dikšun-
	## ja fuolahanulbmilat.

SELECT:NDr2408 Nom IF (1 COMMA)(*2C NP-HEAD-NOM BARRIER NOT-NPMOD LINK 1 COMMA LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD);
	## Romssa universitehtas lea sámi dutkan, gelbbolašvuođahuksen,
	## oahpahus ja eará gaskkustepmi leamaš...

SELECT:NDr2409 Nom IF (1 ("/"))(*2C Nom BARRIER NOT-NPMOD);
#"SELECT:NDr2410 Nom IF (-2C Nom)(-1 ("/"));
	## Vuoroheapmi lea: isit/eamit,...
	## Lea ráhkaduvvon diehtojuohkinmateriála/oččodanmateriála.

SELECT:NDr2411 Nom IF (*-1 COMMA BARRIER VFIN LINK -1C NP-HEAD-NOM)(1 COMMA)(*2 NP-HEAD-NOM BARRIER NOT-NPMOD);
	## ..persovdnarievtti dábálaš láhkavuogádagat, bearašriekti, árberiekti,...

SELECT:NDr2412 Nom IF (NEGATE 0 REAL-V OR (A Attr))(1 COMMA)(*2 NP-HEAD-NOM BARRIER NOT-NPMOD OR CC OR COMMA LINK 1 EOS OR S-BOUNDARY)(NEGATE 0 Acc LINK 1 COMMA LINK *2 NP-HEAD-ACC BARRIER NOT-NPMOD);
	## Jáhkobii riegádii Jovsset, Márjjá boadnjá.

SELECT:NDr2413 Nom IF (-2C Nom)(-1 COMMA)(1 EOC)(NEGATE 0 Adv LINK 1 CS)(NEGATE 0 VFIN); # 3-0
	## Olusat dadjet munnje dan beaivvi: Hearrá, Hearrá!

SELECT:NDr2414 Nom IF (-2C Nom)(-1 COMMA)(0 N)(NEGATE 0 Loc)(1 Inf); # 0-3
	## Muhto ruovttustivra ii leat ožžon mearridanválddi dakkár áššiid hárrái go stáhtavuođđoásahus, stáhtaborgárdoaba, pássa- ja visumváldi, váldi mearridit stáhtasymbolaid,...

SELECT:NDr2415 Nom IF (*-1 (V Pl3) BARRIER NOT-NPMOD LINK NOT *-1 (Pl Nom) OR (Pl3 Nom) BARRIER S-BOUNDARY)(1 CC)(*2 Nom BARRIER NOT-NPMOD);
	## ...maid liikká fertejedje leansmánni ja guokte albmá čujuhit.

SELECT:NDr2416 Nom IF (*-1 COMMA BARRIER NOT-NPMODADV LINK -1C Nom)(NEGATE 0 VFIN)(1 CC);
	## ...geat leat divššohasa lagašolbmot, fuolaheaddji dahje veahkefuolaheddji.

SELECT:NDr2417 Nom IF (1 CC)(2C (Pron Nom))(3 (Pron Nom));
	## Gonagas Agrippa ja dii buohkat geat lehpet dás minguin!

SELECT:NDr2418 Nom IF (1 CC)(2C (N Nom))(2 HEAD-N);
	## Jos dát leažžá dušše olbmuid dáhttu ja dahku,...

SELECT:NDr2420 Nom IF (-2 Nom)(-1 NEGFOC);
	## Eai ovdabarggut, teoria iige geavat doarjjo klausula áddejumi.

SELECT:NDr2421 Nom IF (-1 PUNCT-LEFT)(0 KLASS LINK NOT 0C ADVLCASE)(*1 PUNCT-RIGHT BARRIER Po OR TV OR N);
  	## Sámediggi sáhttá maid buktit cealkámušaid guovlluid dearvvašvuođafitnodagaid jahkedieđáhusain ja plánaárvalusain (kap. 3.4).
	## (čuokkis 10)

	# Nominative in parallell constructions
	# -------------------------------------

SELECT:NDr2422 Nom IF (*-1 BOS LINK *1 Gen LINK 1C NP-HEAD-NOM LINK *1 TV)(-1 Gen)(0 N LINK NOT 0 REAL-V LINK *1 TV);
	## Dearvvašvuođabargiilága § 10, mii gieđahallá dieđuid addima pasienttaide, ja pasientavuoigatvuođalága § 1-3, mii gieđahallá vuoigatvuođa informerejuvvon miehtamii, leat ovdamearkkat dakkár vuoigatvuođain.

#!! * __NDr2422__ selects Nom if it finds a Nom to the left of CC and to the left of a verb. No verb allowed to the right ''eamit barggai vuođđoskuvllas ja isit fas gymnásas''
SELECT:NDr2422 Nom IF (-1 CC LINK *-1 REAL-V LINK -1 Nom)(NOT 0 REAL-V OR Adv)(NOT *1 REAL-V BARRIER S-BOUNDARY)(NOT 0 (Num Sg Acc) LINK 1 (N Gen));
	#$ Nubbi goallostuvvo giehtahearggi lávžái ja goalmmát fas dan nuppi lávžái.
	#$ Bjørn Aarseth lei oahpaheaddji ja rektor Kárášjogas 1949-73.
	#$ Okta lei Hámmarfeastta gávpeguovddážis ja golbma eará fas Álttá-Dálbmeluovtta báhpasuohkanis.
	#$ Eamit barggai vuođđoskuvllas ja isit fas gymnásas.
	#% Juohke mánná oaččui guokte ja eanemusat golbma mánu oahpahusa jahkái.


	# Not nominative
	# --------------
REMOVE:NDr2424 Nom IF (*-1 NOT-AUX-V BARRIER S-BOUNDARY OR PUNCT LINK *-1 (Pron Nom) BARRIER S-BOUNDARY OR PUNCT OR COMMA)(NEGATE *-1 PrsPrc BARRIER NOT-NPMOD)(NEGATE 0 Adv)(NEGATE 0 Nom LINK *1 VFIN BARRIER S-BOUNDARY)(NEGATE -1 ("diibmu"));
	## ...vai ollašuvašii dat mii lea celkojuvvon profehta Jesaja bokte.
	#$ - Maid don dainna dieđuin, vástidii Liná ja áiggui vázzát viidáseappot.
	#% Danne son oaččui nama Peres. Comment: We want Nom on this!


REMOVE:NDr2425 Nom IF (*-1 VFIN BARRIER NOT-ADV-PCLE LINK NOT 0 COPULAS OR FAUXV LINK *-1C Nom BARRIER NOT-ADV-PCLE)(*1 NOT-AUX-V BARRIER S-BOUNDARY);
	## ...jus sierranas sivat eai eará cealkke.
	## Dii galgabehtet vuđolaččat bargat.

REMOVE:NDr2426 Nom - Actio IF (*-1 BOC BARRIER NOT-NP)(NEGATE 1 Prop)(*1C (N Nom) BARRIER CC OR CLB OR NOT-NPMOD OR PUNCT LINK *1 VFIN BARRIER NOT-NP)(NEGATE 1 (Interr Nom))(NEGATE -1 Gen LINK 1 ABBR);
	## Romssa universitehta vuosttaš čielgadeapmi sámi dutkama birra dahkkui...

REMOVE:NDr2427 Nom IF (*-1 COPULAS BARRIER NOT-NPMODADV LINK *-1 Loc BARRIER NOT-ADV-PCLE)(*1 Nom BARRIER S-BOUNDARY OR NOT-NPMOD OR PUNCT);
	## Sámedikkiin lea ráđđeaddi váldi buot sámi beroštumiide guoskevaš áššiin.

REMOVE:ND Nom IF (0 HUMAN + Gen)(*1 BODY BARRIER NOT-NPMOD) ;
    ## Moarsi oaivvis lea ruvdnu mas heaŋgájit guhkes hearvabáttit.

#"REMOVE:ND Nom IF (0 Acc LINK 0 (@-FSUBJ>))(*-1 TRANS-V BARRIER NOT-ADV-PCLE LINK NOT 0 AUX LINK NEGATE 0 Imprt LINK -1 BOS);
	## Suovai Liná vuoiŋŋastit go váibbai.


SELECT:nomHnoun Nom (NOT 0 Sem/Date)(*-1 BOS OR (":") BARRIER WORD)(1 (":") OR EOS) ;
	#$ Fáksa: 22242786


#!! !!Accusative

#!! !!Illative

#!! !!Comitative rules

#!! !NP internal disambiguation of Com

#!! * __PlSg-W__ removes Pl when SG-WORD
REMOVE:PlSg-W Pl IF (0 SG-WORD LINK NOT 0 VFIN)(NEGATE *-1 Num OR Ord BARRIER NOT-NPMOD)(*-1 BOS LINK NOT *1 PLURALIZER);
	#$ Go funet lea guolli goddomin, de fitná olmmoš buođu luhtte dušše oktii jándoris savdnjileamen joddu amas dat dierrut, suollemasat dainna doaivvuin ahte nago de lea boađistan.
	#$ Buoremus sámepolitihkka ožžojuvvo áigálii sámi veahkadaga ja ráđđehusa gaskaneas ipmárdusain.


#!! * __LocPlur__ removes Sg when PLURALIZER or OASSI
REMOVE:LocPlur (Sg Com)  IF (*-1 PLURALIZER OR OASSI BARRIER NOT-NPMOD OR Num)(0 (Pl Loc) - SG-WORD );
	#$ Jus ulbmilat politihkain galget šaddat duohtan fertejit visot náššuvnnalaš unnitálbmogiid dárbbut ja beroštumit váldojuvvot vuhtii juohke oasis riikkas ja buot dásiin.
REMOVE:LocPlur (Sg Com)  IF (*-1 OASSI BARRIER S-BOUNDARY OR Loc)(0 (Pl Loc) - SG-WORD );
	#$ Osiin gelbbolašvuođaguovddážiidda juolluduvvon ruđain sáhttá maid ruhtadit sadjásašgoluid hospiterema oktavuođas dahje lassigoluid, mat Kárášjogas / Leavnnjas šaddet joatkkaoahpus.

#!! * __Locgoabbat__ selects Pl Loc after goabbat Foc/ge
SELECT:Locgoabbat (Pl Loc)  IF (-1 ("goabbat" Foc/ge))(0 (Sg Com)) ;
	#$ Goabbatge joavkkuin lonuhit čikčiid juohke viđát minuvttas.

#!! * __NumCom__ selects Num Com: guvttiin nieiddain if not plural-noun like: guvttiin heajain
SELECT:NumCom (Num Com)  IF (*1 Com BARRIER NOT-A LINK NOT 0 PL-N + Pl);
	#$ Juhán vulggii viđain nieiddain.

#!! * __ComDemNum1__ selects N Com if there is a Dem or Num or buorre + Com to the left: Exception for plural-nouns
SELECT:ComDemNum1 (Sg Com) IF (0 (Pl Loc) LINK NOT 0 PL-N)(*-1 DAT-PRON + (Dem Com) OR (Num Com) OR ("buorre" Com) OR ("nubbi" Com) BARRIER NOT-A);
	#$ Romssa guovllubuohcceviessu lea bargamin dáinna áššiin.
	#$ Dainna sániin dadjat, de mearkkaša dat ahte ...
	#$ Ovttain sániin dadjat, de mearkkaša dat ahte ...
	#$ Mo sáhttá mottiin sániin dadjat...

#!! * __Comburiin__ selects N Com if there is a safe N Com to the right: buriin vugiin
SELECT:Comburiin (Sg Com) IF (0 (Pl Loc))(1C Com);
  	## Áššebealit galget juohke jagi šiehtadallat ekonomalaš doaimmaid, fágalaš sosiála áššiid ja eará gažaldagaid ja doaimmaid mat buriin vugiin ovdánahttet boazodoalu.


#!! * __ComCOM-A__ selects Sg Com after COM-A
SELECT:ComCOM-A (Sg Com) IF (*-1 COM-A BARRIER Com OR SV-BOUNDARY)(0 (Pl Loc));
	#$ Ja son lei doppe oppa joavkkuinis.
	#$ Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.


#!! * __Comduhtavas__ selects Sg Com after duhtavaš
#SELECT:Comduhtavas (Sg Com) (*-1 ("duhtavaš") BARRIER NOT-NPMOD)(0 (Pl Loc));
	#$ Sii lohket iežaset leat oalle duhtavaččat dán jagáš básárdoaluin.

#!! * __ComComAdv1__ selects Com after COM-ADV or juohke
SELECT:ComComAdv2 Com IF (*-1 COM-ADV OR ("juohke") BARRIER (N Sg));
	#$ Ii lean dárbu dábálaš eanavuovdimii, oktan opmodatregistreremiin.


#!! !Disambiguation based upon verb valency

#!! * __LocLocVL1, LocLocVR__ select Pl Loc if there is a LOC-V
SELECT:LocLocVL1 (Pl Loc)  IF (*-1 LOC-V BARRIER SV-BOUNDARY OR COM-HEAD LINK NOT 0 Adv)(NEGATE *-1 CC BARRIER S-BOUNDARY LINK -1 LOC-V)(NEGATE 0 Num OR Pron LINK *1C (N Com) BARRIER NOT-NPMOD)(0 Com OR Px);
	#$ Juohke dáhpáhusas ferte áibbas boastut leat beroškeahttá sámiid riekteáddejumiin.

SELECT:LocLocVR (Pl Loc) IF  (NEGATE 0 DAT-PRON OR Actio)(NEGATE *1C Com BARRIER NOT-NPMOD)(NEGATE 0 (Coll Nom) LINK 1 (Sg Com))(NEGATE 0 (Sg Com) LINK -1 (Coll Nom) OR ("buorre" Com))(*1 LOC-V BARRIER SV-BOUNDARY);
	#$ Maiddái sámi fágabirrasiin ja ásahusain sáhttá jearrat ráđiid.

#!! * __LLocAccLocVL__ select Pl Loc if there is a ACC-LOC-V
SELECT:LocAccLocVL (Pl Loc) IF  (*-1 ACC-LOC-V BARRIER NOT-NP);
	# Removed "viežžat" from ACC-LOC-V, because it can take Com: "Mun vižžen su biillain."

#!! !Disambiguation of Com depending on Adv or certain verb or N

#!! * __ComComAdv1__ selects Com for ACTOR OR ACTOR-ROLE after og before COM-ADV
SELECT:ComComAdv1 (Sg Com) IF (0 (Pl Loc) LINK 0 ACTOR OR ACTOR-ROLE)((-1 COM-ADV) OR (*1 COM-ADV BARRIER NOT-ADV));
	#$ Leahppi go fárrolaga ádjáineatte?
	#$ Son fitná doppe juohke skuvllain.

#!! * __Comboahtit__ selects riika Com when boahtit: boahtit riikkainis, which is a special construction
SELECT:Comboahtit (Sg Com) IF (0 ("riika") + Px LINK *0 ("boahtit") BARRIER SV-BOUNDARY) ;
	#$ Muhtumat eai muosát jápmima ovdal go oidnet Olbmobártni boahtimin riikkainis.

#!! * __ComComplPl-N__ selects Sg Com for HUMAN, ORGANIZATION, INSTITUTION, STATE, EVENT-TOOL-ACTIVITY, láhka when there is a COM-COMPL-N to the left or right
SELECT:ComComplPl-N (Sg Com)  IF (*0 COM-COMPL-N BARRIER SV-BOUNDARY)(0 HUMAN OR ORGANIZATION OR INSTITUTION OR STATE OR EVENT-TOOL-ACTIVITY OR ("láhka") OR Pron);
  	## Dearvvašvuođabargiid doaibma adnojuvvo priváhtan, vaikke vel dearvvašvuođabargiin lea fástadoavtteršiehtadus dahje eará šiehtadus almmolašvuođain, muhto eai leat dattetge virggis almmolaš ásahusas.
	#$ Das lei ee. oktavuohta čakčamánu 7. b. 1854 lágain.
	#$ Várrepresideanta lea maid 05.01.00 leamaš čoahkkimis Sosiála- ja dearvvasvuođadepartemeanttain čilgen dihte Sámedikki juolludemiid sámi dearvvasvuođa- ja sosiálaplána čuovvuleamis .
	#$ Vuojadansearvi váldá din fitnodagain oktavuođa vahkku 20:s, gullat mo dii sáhttibehtet veahkehit.

#!! * __Comoktavuohta__ selects Sg Com when oktavuohta is to the left or right
SELECT:Comoktavuohta Com IF (*0 ("oktavuohta") BARRIER S-BOUNDARY OR COPULAS OR N);
	#$ Vuoigatvuohta oktavuođa váldit divššohasbearráigeahččiin.

#!! * __ComDU-NR__ selects Sg Com after Pers dualis: moai áhčiin, munno vieljain
SELECT:ComDU-NR (Sg Com)  IF ((-1 $$DU-NR + Pers LINK *1 Com BARRIER NOT-A LINK *1 $$DU-NR + V BARRIER SV-BOUNDARY OR CC) OR (-1 (Pers Gen) + DU-NR LINK 1 N LINK 1 N));
	#$ Moai áhčiin manaime dohko.
	#$ Dat lea munno vieljain dállu.

#!! * __ComHumanOrg__ selects HUMAN Sg Com after HUMAN, ORGANIZATION, INSTITUTION
SELECT:ComHumanOrg (Sg Com) IF (*-1 HUMAN OR ORGANIZATION OR INSTITUTION BARRIER NOT-NPMOD LINK 0 Nom LINK NOT 0 LOC-HEAD OR A)(0 (Pl Loc) LINK 0 HUMAN)(*1 VFIN BARRIER S-BOUNDARY);
	#$ Norgga delegašuvdna, várrepresideanttain, doalai sártni dievasčoahkkimii čoahkkima loahpahettiin, mas doarjjui WHO boahtteáiggi barggu dearvvasvuođain.

#!! !Animate nouns
#!! * __ComAnimate__ selecst Sg Com if there is an animate to the left, and the noun itself is not a ABSTR-TEXT,  TEXT,  PLACE,  INDUSTRY,  EDUCATION,  INSTITUTION,  ANIMATE
SELECT:ComAnimateL (Sg Com)  IF (*-1 ANIMATE BARRIER NOT-NPMOD LINK 0 Nom OR Gen)(0 (Pl Loc) LINK NOT 0 ABSTR-TEXT OR TEXT OR PLACE OR INDUSTRY OR EDUCATION OR INSTITUTION OR ANIMATE)(1 CLB OR EOS);
	#$ Sii vástidedje: Johanasa gásttain.
  	## 1996:s rájes lea Romssa universitehtas leamaš Sámi dutkamiid guovddáža bokte formaliserejuvvon ovttasbargu St. Petersburga Hertzen universitehta Davviálbmogiid fakultehtain.
    #Does it really need (1 CLB OR EOS)?
    #should not hit:  	#$ Juohke dáhpáhusas ferte áibbas boastut leat beroškeahttá sámiid riekteáddejumiin.

#!! * __ComProp__ selecst Prop Sg Com for person names. Exception for habitive constructions.
SELECT:ComProp (Prop Sg Com) IF (0C Prop LINK 0 FIRSTNAME OR Sem/Sur)(NEGATE 0 Loc LINK 1 COPULAS LINK *1 Nom BARRIER NOT-NPMODADV) ;
	#$ Nystø mielas leaba soai Lene Hanseniin rievttes olbmot ovddastit sámi álbmoga dáhtu Tromssa ohcamušas.
	#$ Moai Birehiin barggašeimme mánáidgárddis.



#!! !HAB-ACTOR in habitive-constructions

#!! * __LocHab1, LocHab2__ select Pl when HAB-ACTOR
SELECT:LocHab1 (Pl Loc) IF (0 (Sg Com) + HAB-ACTOR) ((*1 LEAT-FIN-NOT-IMPRT BARRIER SV-BOUNDARY LINK *1 NP-HEAD-NOM BARRIER NOT-NPMOD) OR (*-1 go + Qst LINK -1 AUX LINK *1 HAB-ACTOR LINK 1 ("leat" Inf)))(NEGATE *0 COM-V BARRIER SV-BOUNDARY) ;
	#$ Divššohasain lea vuoigatvuohta dearvvašvuođaveahkkái dan áigodagas.
	#$ Searvegottiin lei dál ráfi miehtá Judea ja Galilea ja Samaria.
	#$ Galgá go báikkálaš olbmuin leat vuosttašvuoigatvuohta luondduriggodagaide?

SELECT:LocHab2 (Pl Loc)  IF (*-1 (Pron Rel) BARRIER NOT-NPMOD LINK -1 NP-HEAD-NOM)(0 (Sg Com) LINK 0 HAB-ACTOR)(*1 LEAT-FIN-NOT-IMPRT BARRIER SV-BOUNDARY LINK NOT *1 PRC-NOT-LEAT);
	#$ Orru čielga vuohki: Dat friddjavuohta mii guođoheddjiin dássážii lei leamaš, gáržžiduvvui dađistaga.

#!! !Disambiguation based upon verb valency

#!! !COM-V
#!! * __ComVR, ComVL__ select Com when COM-V
SELECT:ComVR (Sg Com)  IF (*-1 COM-V + REAL-V BARRIER SV-BOUNDARY)(NEGATE 0 PLACE OR SITUATION);
	#$ Dieđut galget meannuduvvot várrugasvuođain.
	#$ Nissonat barget gávppiin. #should not hit this one
	#$ Sámediggi lea váldán badjelasas váldoovddasvástádusa sihkkarastimis doaibmadoarjaga Nuortasámi museai (vrd. čoahkkimiin Njávdámis 05.06.01).
	#$ Sii čuvvot sámilága mearrádusaid, veardit siskkáldas goziheami geatnegasvuođain.

SELECT:ComVL (Sg Com)  IF (*-1 COM-V + REAL-V BARRIER SV-BOUNDARY)(0 POLITICAL-PLACE OR EDUCATION OR INSTITUTION OR INDUSTRY OR ORGANIZATION) ;
	#$ Sii áigot šiehtadit ođđasit skuvllain.

SELECT:ComVX (Sg Com)  IF (*1 COM-V + REAL-V BARRIER SV-BOUNDARY)(NEGATE *1 COPULAS BARRIER REAL-V LINK *1 COM-V + Inf BARRIER NOT-ADV-PCLE) ;

#!! * __ComVOktiiL, ComVOktiiR__ select Com when OKTII-V
SELECT:ComVOktiiL (Sg Com) IF  (*-1 ("oktii") BARRIER NOT-NPMODADV LINK *-1 OKTII-V OR COM-V BARRIER NOT-ADV-PCLE); # (0 N BARRIER Loc);
	#$ Mii heive bures oktii ránes datneláiggiin?
  # we have to think about a barrier, another Loc to the right for example

SELECT:ComVOktiiR- (Sg Com)  IF (1 OKTII-V LINK 1 ("oktii") LINK NEGATE *1 Com BARRIER S-BOUNDARY);
	#% Porsáŋggu loguin leat buot klássaid submi biddjon oktii.

#!! !tools (concrete and abstract)

#!! * __ComTool1, ComTool2, ComToolCoord__ select Com TOOL when ACTIVITY-V, MOVEMENT-V, PLACE-V-V
SELECT:ComTool1 (Sg Com) + TOOL  IF (*-1 ACTIVITY-V OR MOVEMENT-V OR PLACE-V BARRIER SV-BOUNDARY) ;
	#$ Dan moai ráidniime skárbbaniin.

SELECT:ComTool2 (Sg Com) + TOOL IF  (*1 ACTIVITY-V OR MOVEMENT-V OR PLACE-V BARRIER NOT-ADV);
	#$ Mon in leat dugo Sivvar - mii láve bissuin ja beatnagiin mannat dugo livččii dáčča.

SELECT:ComToolCoord (Sg Com) + TOOL  IF (1 CC LINK *1 Com BARRIER NOT-NPMODADV)(*1 ACTIVITY-V OR MOVEMENT-V);
	#$ Mon in leat dugo Sivvar - mii láve bissuin ja beatnagiin mannat dugo livččii dáčča.

#!! * __ComHuman__ selects Com ABSTR-TOOL OR SATNI when HUMAN-AGENT-V - does it function?
SELECT:ComHuman- (Sg Com)  IF (*-1 NP-HEAD-NOM + HUMAN BARRIER S-BOUNDARY)(0 ABSTR-TOOL OR SATNI)(*1 HUMAN-AGENT-V);

#!! !BODY as an instrument

#!! * __ComHuman__ selects Com BODY when VERBAL-ACTIVITY-V
SELECT:ComBodyVerbalV- (Sg Com) + BODY  IF (*-1 VERBAL-ACTIVITY-V BARRIER VFIN OR LOC-HEAD)(0 (Pl Loc));
	#$ Son rávvii buohkaid bissut Hearrá luhtte duođalaš váimmuin.
  # Idea: Body as an instrument to perform verbal activity.

#!! * __ComHuman__ selects Com HUMAN when VERBAL-ACTIVITY-V
SELECT:ComHumanVerbalV (Sg Com) IF (0 HUMAN OR ("media")) ((*-1 VERBAL-ACTIVITY-V BARRIER VFIN OR LOC-HEAD) OR (1 VERBAL-ACTIVITY-V))(0 (Pl Loc));

#!! * Abstract-entity-com-verbs

SELECT:ComAbstract (Sg Com)  IF (*0 ABSTR-ENTITY-COM-V BARRIER SV-BOUNDARY)(0 (Pl Loc) LINK 0 ABSTR-ENTITY OR ACTOR);
	#$ Mii leat lihkostuvvan bures prošeavttain.

#!! * Only-place-loc-verb
SELECT:ComOnlyPlaceV (Sg Com) IF (0 (Pl Loc) LINK NOT 0 PLACE OR ORGANIZATION OR HUMAN OR MATERIAL)(*0 ONLY-PLACE-LOC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf);
	#$ ...go sápmelaččat duođas álge eanadoaluin bargat.
    # It can be ambiguity with ORGANIZATION.


#!! !Dynamic-verbs

#!! * __LocdynamicVR, LocdynamicVL__ select Pl Loc if there is a DYNAMIC-V and the noun itself is not a TOOL, ABSTR-TOOL, WRITING-TOOL, CONCEPT, HUMAN, VEHICLE, buorre, Der/NomAc

SELECT:LocdynamicVR (Pl Loc) IF  (*-1 DYNAMIC-V - ABSTR-ENTITY-COM-V OR ("dahkat") + Der/Pass BARRIER SV-BOUNDARY OR COM-V OR COM-HEAD LINK 0 FMAINV OR Inf)(0 (Sg Com) LINK NOT 0 TOOL OR ABSTR-TOOL OR WRITING-TOOL OR CONCEPT OR SATNI OR ANIMATE OR VEHICLE OR BODY OR ("buorre") OR Der/NomAct);
	#$ Son beastá álbmogis sin suttuin.
	#$ Ikte moai finaime golmmain mállásiin.
	#$ Min mielas goit lea veara jurddašit nissongova birra mii boahtá ovdan jearahallamiin.
	#$ Lávdde luhtte juiget olbmot olles čoddagiin.
	#$ Terje hárve hupmá mediain, muhto son áinnas hupmá sápmelašvuođas birra.
	#$ Son ii daja njuolga rievttes namain.
	#$ Dan geažil lea dehálaš ávžžuhit bargiid sámástit ráđđádallamiin.
	#$ Sii máinnašuvvojedje vearrologahallamiin earáid maŋŋá.
	#$ Dálvet golggadettiin lea guovttegeardán skoađas birra gietkama, vuohččan lea sisti birra, mii gal adnojuvvo geasset ja dálvet, muhto dálvet adnojuvvo nubbi skoađas nalde, mii lea dahkkojuvvon gápmasiin.
	#$ Oahppit bohte dábálaččat vuođđoealáhusain.
 #ABSTR-ENTITY-COM-V

SELECT:LocdynamicVL (Pl Loc) IF (0 (Sg Com) LINK NOT 0 TOOL OR ABSTR-TOOL OR WRITING-TOOL OR CONCEPT OR HUMAN OR VEHICLE OR BODY)(*1 DYNAMIC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf);
	#$ Sámi guovlluin ožžo Sámeparlameantta Suoma beallái jagi 1976.
  	#$ Mánáidgirjjiin muitaluvvo dávjá govt olbmuid mánát deivet nuppi máilmmi ássiid.
	#$ Guovlluid dearvvašvuođafitnodagain gáibiduvvojit dakkár rutiinnat.
	#$ Gielddat berrejit dearvvašvuođastašuvnnain deattuhit mánáid ja nuoraid váttisvuođaid ovddalgihtii eastadeami ja psykososiálabarggu mánáid ja nuoraid váras.
	#$ Dálá sámiid máidnasiin ja muitalusain muitaluvvo dološ oskui gullan hálddiid birra .
	#$ Muhto mii diehtit ahte fágagirjjit leat divrasat ja diibmá jagi bušeahtain ii sáhttán investeret.


	#$ Muhto Ipmil lea seammá sániin vurken dáláš almmiid ja eatnama duopmobeaivvi dola várás, go ipmilmeahttumat duššaduvvojit. #skal ikke treffe guvssiin
  	#$ Ja ovdal go háhppehin basttiin goivestit eret de Ánddar guvssiin bođii. #skal ikke treffe guvssiin



#!! !Event-tool-actio
#!! Most actio can be both tool and event.


#!! !PLACE-V

#!! * __LocFurniture__ select Pl Loc FURNITURE if there is a PLACE-V
SELECT:LocFurniture (Pl Loc)  IF (*-1 PLACE-V BARRIER SV-BOUNDARY)(0 FURNITURE);

#!! * __ComPlaceV__ select Com ANIMATE, CONCEPT, TOOL, ABSTR-TOOL, EVENT-TOOL-ACTIVITY if there is a PLACE-V
SELECT:ComPlaceV (Sg Com)  IF (*0 PLACE-V BARRIER SV-BOUNDARY)(0 (Pl Loc) LINK 0 ANIMATE OR CONCEPT OR TOOL OR ABSTR-TOOL OR EVENT-TOOL-ACTIVITY);
	#$ Soai leigga čohkkámin fatnasis áhčiineaskka čiktimin firpmiid.
	 #$ ..gohčodedje maid earáid go sámesogat olbmuid, geat orro sámesiiddain.
	 #$ Gávpotbussiin olbmot dávjá fertejit čuožžut.

#!! * __LocInstitution__ select Loc INSTITUTION if there is a ABSTR-PLACE-V
SELECT:LocInstitution (Pl Loc)  IF (*-1 ABSTR-PLACE-V BARRIER SV-BOUNDARY)(0 INSTITUTION);
	#$ Go sápmelaččat ohcet fuolahanbálkká lagaš olbmuideaset dikšumii, de galgá gielda juolludeamis váldit vuhtii, váilugo bálvalusain giella ja kulturgelbbolašvuohta.

#!! * __LocPlaceIndustry__ select Loc GEOGRAPHICAL-PLACE if there is a INDUSTRY to the right
SELECT:LocPlaceIndustry (Pl Loc)  IF (*-1 INDUSTRY BARRIER NOT-NPMOD)(0 GEOGRAPHICAL-PLACE);
	#$ Guollebivdu jogain ja jávrriin.

SELECT:LocSourceVR- (Pl Loc) IF (0 HUMAN OR ORGANIZATION)(*1 SOURCE-V BARRIER SV-BOUNDARY LINK NOT 0 Adv)(NEGATE *-1 CC BARRIER S-BOUNDARY LINK -1 LOC-V);

SELECT:LocSourceVL (Pl Loc) IF  (*-1 SOURCE-V BARRIER SV-BOUNDARY LINK NOT 0 Adv)(NEGATE *-1 CC BARRIER S-BOUNDARY LINK -1 LOC-V)(0 HUMAN OR ORGANIZATION);
SELECT:LocHumanAgVR (Pl Loc)  IF (*-1 NP-HEAD-NOM LINK 0 HUMAN OR ORGANIZATION OR (Pron Rel))(0 ORGANIZATION OR HUMAN OR EVENT LINK 0 (Sg Com) LINK NOT -1 PRON-DU)(*1 HUMAN-AGENT-V - Actio LINK NOT *1 N BARRIER S-BOUNDARY OR COMMA);

#!! * __LocHumanAgVL__ XXX This one was commented out (cf. 0 .. LINK ... BARRIER). Note that this rule did not affect the test result
#SELECT:LocHumanAgVL (Pl Loc) IF  (*-1 HUMAN-AGENT-V - Actio LINK NOT 1 N)(0 ORGANIZATION OR EVENT LINK 0 (Sg Com) BARRIER S-BOUNDARY OR COMMA LINK NOT -1 PRON-DU);
	 #$ Departemeanta áigu ovttasbargat Sámedikkiin ja movttiidahttit gielddaid ovddidit doaimmaid, mat nannejit sámi mánáid ja nuoraid identitehta ja oktiigullevašvuođa sámi servodagain. #servodagain
  # This rules assumes that abstract activities are performed with a tool but not with the help of others in comitative case. The set of abstract-activity verbs might have to be restricted to those verbs that do have these features.

#!! * __LocHuman-agentV__ XXX This one was commented out (cf. 0 .. LINK ... BARRIER). Note that this rule did not affect the test result
#SELECT:LocHuman-agentV (Pl Loc) IF (0 ORGANIZATION OR HUMAN OR EVENT LINK 0 (Sg Com) BARRIER S-BOUNDARY OR COMMA LINK NOT -1 PRON-DU)(*1 HUMAN-AGENT-V - Actio LINK NOT 1 N);

#!! !STATE-V

SELECT:StateV (Pl Loc) IF (*0 STATE-V BARRIER SV-BOUNDARY)(0 (Sg Com) LINK 0 STATE OR OTHER-INDUSTRY);
	#$ Olbmot geat ellet náitoslágan dahje guimmešvuođalágan gaskavuođain.
	#$ Buolvvaid gaskkas leat stuora erohusat, vuorrasat leat eallán vuođđoealáhusain ja dujiin.

#!!	!ILLNESS-V

#!!	!Periphrastic constructions

SELECT:ComActivityV (Sg Com) IF  (*-1 ACTIVITY-A BARRIER NOT-NPMOD LINK *-1 REAL-COPULAS BARRIER S-BOUNDARY OR REAL-V)(0 ANIMATE OR TOOL);
	#$ Mun ledjen hilbat mánáin.

#!!	!Idiomatic expressions

SELECT:ComOaivil (Sg Com)  IF (*-1 ("oaivil" Loc) BARRIER NOT-NPMOD)(0 TEXT OR ABSTR-TEXT OR ACTOR-ROLE);
	#$ Jus lea sáhka 12 jagi deavdán mánás, gii ii leat ovtta oaivilis mearrádusain, gozihanlávdegoddi galgá gieđahallat mearrádusa.

SELECT:ComUlbmil (Sg Com) IF (*-1 ("ulbmil" Nom) BARRIER NOT-NPMOD)(0 TEXT OR ABSTR-TEXT OR ("prošeakta"));
	#$ Eará guovddáš ulbmil prošeavttain lea fuomášahttit dan máhttodárbbu maid riikkaidgaskasaš ja sisriikkalaš birasulbmilat bohciidahttet.

#!!	!Verbal-activity-verbs

SELECT:ComVerbalactV- (Sg Com) + SATNI IF  (*0 VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 (Pl Loc));


SELECT:LocVerbalactV (Pl Loc) IF  (*-1 VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 Com LINK 0 EVENT OR EVENT-TOOL-ACTIVITY);

SELECT:LocText (Pl Loc) IF  (NEGATE *-1 COM-HEAD BARRIER NOT-NPMOD)(0 TEXT OR ABSTR-TEXT)(*0 VERBAL-ACTIVITY-V BARRIER NOT-NPMOD LINK 0 Der/Pass );

SELECT:LocWeather- (Pl Loc)  IF (*-1 PERCEPTION-V OR VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY)(0 WEATHER-ENTITY OR SITUATION);

#!! !Movement-verbs

SELECT:ComboahtitR (Sg Com) IF (*-1 ANIMATE OR PPRON-NOM-NOT-DAT BARRIER S-BOUNDARY LINK *1 ("boahtit") LINK 0 FMAINV OR Inf)(0 (Pl Loc) LINK NOT 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY);
	#$ Son bođii biillain.
	#$ Son bođii politihkkariin.
SELECT:ComboahtitV- (Sg Com) IF (0 (Pl Loc) LINK NOT 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY)(*1 ANIMATE OR PPRON-NOM-NOT-DAT BARRIER NOT-NPMOD LINK *1 ("boahtit") BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf);

SELECT:ComMovemVL (Sg Com)  IF (*-1 MOVEMENT-V BARRIER SV-BOUNDARY OR ("diehtu") LINK 0 FMAINV OR Inf LINK NOT 0 ("boahtit"))(0 (Pl Loc) LINK NOT 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY);
	#$ Máhtte vujii biillain.
	#% Sáhttá viežžat dieđuid divššohasa oapmahaččain.

SELECT:ComMovemVR (Sg Com) IF (0 (Pl Loc) LINK NOT 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY)(*1 MOVEMENT-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf LINK NOT 0 ("boahtit")) ;

SELECT:LocMovemVL (Pl Loc) IF (*-1 ANIMATE OR PPRON-NOM-NOT-DAT BARRIER S-BOUNDARY LINK *-1 MOVEMENT-V LINK 0 FMAINV OR Inf)(0 (Pl Loc) LINK 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY);
SELECT:LocMovemVR- (Pl Loc) IF (0 (Pl Loc) LINK 0 PLACE OR ABSTR-PLACE OR EVENT-TOOL-ACTIVITY)(*1 ANIMATE OR PPRON-NOM-NOT-DAT BARRIER NOT-NPMOD LINK *1 MOVEMENT-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf LINK *1 VEHICLE OR ANIMATE BARRIER S-BOUNDARY);
	#$ Suovva bođii biillain.	 #rule doesn't hit this one

SELECT:LocboahtitVL (Pl Loc)  IF (*-1 NP-HEAD-NOM BARRIER S-BOUNDARY LINK 0 CONCEPT LINK 1 ("boahtit"))(0 (Pl Loc) LINK NOT 0 TOOL OR ABSTR-TOOL);
	#$ Evttohus bođii politihkkariin.
SELECT:LocConcept (Pl Loc) IF (0 (Pl Loc) LINK NOT 0 TOOL OR ABSTR-TOOL)(*1 NP-HEAD-NOM BARRIER NOT-NPMOD LINK 0 CONCEPT);
	#$ Evttohus bođii suohkana politihkkariin.


#!! !The super-set Dynamic-verb according to choose (Pl Loc) or (Sg Com)

#!! The idea is that the superset DYNAMIC-V are not connected to TOOL, ABSTR-TOOL or CONCEPT in (Pl Loc). This is the "minste felles multiplum". The sub-sets are different, f.i. many of them (but not all) are not connected to HUMAN in (Pl Loc), one is not connected to ABSTR-ENTITY and ACTOR in (Pl Loc). We work with negation so the rules don´t destroy analysis because of insufficent sets.

#!! First the general-rules for selecting (Sg Com), then the more special rules for selecting (Sg Com), and then we selct (Pl Loc) for the rest of them under # Another round of locative rules.


#!! * __ComDynV__ Dynamic-verbs selects Com when TOOL, ABSTR-TOOL, WRITING-TOOL, CONCEPT, EVENT-TOOL-ACTIVITY
SELECT:ComDynV (Sg Com) IF (*0 DYNAMIC-V BARRIER S-BOUNDARY LINK 0 FMAINV OR Inf)(0 (Pl Loc) LINK 0 TOOL OR ABSTR-TOOL OR WRITING-TOOL OR CONCEPT OR EVENT-TOOL-ACTIVITY);
	#$ Geahččoeiseválddit galget bearráigeahčuin váikkuhit dasa ahte dearvvašvuođa- ja sosiálabálvalusain váldet vuhtii geavaheaddjiid vuoigatvuođa oažžut áddehahtti informašuvnna ja vuoigatvuođa informerejuvvon miehtamii, ja fágalaččat dohkálaš doaimmaid giellagáibádusaid.
	#$ Gittabivdosiin ii leat lohpi bivdit deanu eambbo go beallái.
    # "Minste felles multiplum" for the dynamic verbs. The same rules come the other way round after the comitative-chapter - after we have picked out the special cases.


#!! * Dynamic-verb selects Com when HUMAN, but not for HUMAN-SOURCE-VEHICLE-V
#SELECT:ComDynamisV (Sg Com) + HUMAN (*0 DYNAMIC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf LINK NOT 0 HUMAN-SOURCE-VEHICLE-V)(0 (Pl Loc) LINK NOT *-1 Num - Com BARRIER NOT-NPMOD);
    # "Minste felles multiplum" for the from-human-verbs. The same rules come the other way round after the comitative-chapter - after we have picked out the special cases.


#!! * Body-activity-verb selects Com when BODY, for BODY-ACTIVITY-V or VERBAL-ACTIVITY-V
SELECT:ComBody (Sg Com) + BODY  IF (*0 BODY-ACTIVITY-V OR VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 (Pl Loc));
	#$ Muhto ieža eai hálit daid lihkastahttit suorbmagežiin ge.
    # They belong to the dynamic-verbs, but they can also have BODY as a tool.

#!! * ComVeh
SELECT:ComVeh- (Sg Com) + VEHICLE  IF ((*0 VEHICLE-TOOL-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf) OR (-1 (".*mátki"r)))(0 (Pl Loc));
	#$ Sii bukte girjjiid biillain.
	#$ Go eanet geainnuid ráhkadedje, de sáhtiimet ordnet ruovttumátkkiid bussiin eanaš ohppiide.



#!! !HUMAN-LOC-V
SELECT:LOCsatni- (Pl Loc) IF (0 (Sg Com) + SATNI)(*1 HUMAN-LOC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf LINK *0 WORD-PARTS OR SINGLE-SOUND BARRIER SV-BOUNDARY LINK 0 Acc OR Nom );
	#$ Váldonjuolggadus lea ahte báikenamain dábálaččat eat ane goallossázu.
	#$ Movt báikenamain geavahuvvo goallossáhcu čilgejuvvo 2.1.5 čuoggás.

SELECT:LOCwordparts- (Pl Loc) IF  (*0 HUMAN-LOC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf LINK *1 WORD-PARTS OR SINGLE-SOUND BARRIER SV-BOUNDARY LINK 0 Acc OR Nom )(0 (Sg Com) LINK 0 CONCEPT OR HUMAN OR SATNI);
	#$ Atnit stuora bustáva lágaid namain:
	#$ Gravismearka adnojuvvo sámegielas dušše muhtun vierronamain.
    # Have to preceede the next Sg Com rules because they are an exception to it with respect to SATNI

#SELECT:ComHumanLocV (Sg Com) IF (0 (Pl Loc) LINK 0 CONCEPT OR HUMAN OR SATNI)(*0 HUMAN-LOC-V BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf);
	#$ Sámediggeráđđi šálloša ja imaštallá go departementa ii namut sámi skuvlla sániin ge skuvlla IKT-áŋgiruššanplánain.
	#% Ollu ásahusat geavahit dan sin doaimmain.


#!!	!COPULAS
SELECT:ComCop (Sg Com)  IF (*-1 ("šiega") OR ("bahá") OR ("siivui") OR ("buorre") BARRIER SV-BOUNDARY LINK 1 COPULAS LINK 0 PrfPrc OR Inf OR ConNeg OR VFIN)(0 (Pl Loc) LINK 0 ANIMATE OR CONCEPT LINK *-1 A - Ord - Attr BARRIER NP-BOUNDARY);
#SELECT:ComCop (Sg Com)(0 (Pl Loc) LINK 0 ANIMATE OR CONCEPT LINK *-1 A BARRIER NP-BOUNDARY)(*1 COPULAS LINK 0 Inf OR ConNeg OR VFIN BARRIER S-BOUNDARY LINK NEGATE *1 REAL-V LINK NEGATE 0 VFIN LINK NOT 0 COPULAS); #This one is not good, it hits Hab
 	#$ Go dii ehpet leat šiega leamaš ealibiin.
	#$ Go dii lehpet šiega leamaš ealibiin.
	#$ Go dii lehpet šiega ealibiin.
	#$ Son lea šaddan beakkán su joavkkuinis.


SELECT:LocCop (Pl Loc) IF  (*-1 COPULAS BARRIER SV-BOUNDARY OR NP-HEAD-NOM OR COM-HEAD LINK NOT *-1 Loc BARRIER SV-BOUNDARY OR CC)(0 (Sg Com) LINK 0 EVENT OR PLACE OR ORGANIZATION);
	#$ Beassášmárkaniin fitne sullii 9000 guossi ja 3500 guossi ledje čakčamárkaniin.
	#$ Skuvlajagis 2001/2002 leat oktiibuot 469 oahppi suohkana vuođđoskuvllain.

#!! * bivvat - we don't need this any more
#SELECT:Combivvat (Sg Com)  IF (*0 ("bivvat") BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 (Pl Loc) LINK 0 CLOTHES OR BEDCLOTHES OR MATERIAL);
	#$ Beaskkain gal bivvá buolašin.

#!! * ealihit
SELECT:ComealihitL (Sg Com) IF  (-1 NP-HEAD-ACC LINK *-1 ("ealihit") BARRIER W-NOT-ACC-GEN)(0 (Pl Loc));
SELECT:ComealihitR- (Sg Com) IF (0 (Der/NomAct Pl Loc))(1 NP-HEAD-NOM LINK 1 ("ealihit"));
	#$ Sii ealihit sin heakkaset vuodjimiin ja geasehemiin.

#!! * ipmirdit / áddet
SELECT:Comipmirdit (Sg Com) IF (0 (Pl Loc Foc/ge))(1 ("ipmirdit") OR ("áddet") OR ("fáhtet"));
	#$ In vel čilgemiinge ipmirdan.
	# It should be possible to claim that 0 has to be Der/NomAct , but the tags are not there because of lexical baseform:

#!! * ruhtadit
SELECT:Comcruhta1- Com IF (0 ("ruhta" Com) LINK NOT *-1 HEADOFPARTS BARRIER S-BOUNDARY)(*1 ("ruhtadit"));
SELECT:Comruhta2- Com IF (0 HEADOFPARTS LINK 0 Com LINK *1 ("ruhta" Loc) BARRIER S-BOUNDARY)(*1 ("ruhtadit"));
  	#$ Osiin gelbbolašvuođaguovddážiidda juolluduvvon ruđain sáhttá maid ruhtadit sadjásašgoluid hospiterema oktavuođas dahje lassigoluid, mat Kárášjogas / Leavnnjas šaddet joatkkaoahpus.


#!! * suokkardit and čielggadit
SELECT:Comsuohkkardit- (Sg Com) IF  (*0 ("suokkardit") OR ("čielggadit") BARRIER SV-BOUNDARY LINK 0 FMAINV OR Inf)(0 (Pl Loc) LINK 0 ACTOR);
  	##$SDD áigu suokkardit Sámedikkiin, sáhttágo dán plána doaimmaid čađaheami delegeret Helse Nordii.


#!! * vástidit
SELECT:Locvastidit1- (Pl Loc) IF (*-1 ("vástidit") BARRIER NOT-NPMOD LINK 0 FMAINV OR Inf LINK *1 Ill BARRIER NOT-NPMOD)(0 (Sg Com));
SELECT:Locvastidit2 (Pl Loc) IF (*-1 ("vástidit") BARRIER NOT-NPMOD LINK 0 FMAINV OR Inf)(0 (Sg Com) LINK *1 Ill BARRIER NOT-NPMOD);
  	## SDD lea nammadan guovlluid dearvvašvuođafitnodagaid stivrraid, mat vástidit doaimmaineaset dušše stáhtii, mii daid eaiggáduššá.

#!! * Coordination
SELECT:ComCoord Com IF (*-1 CRD2 BARRIER NOT-NPMOD LINK *-1C Com BARRIER WORD);
	#$ Das ráhkaduvvo vuojahat gátti guvlui nu galliin čuolddain (dahje oar-julggiin) go lea vuogas.
	#$ Bargiidbellodaga Per Nils Saari beasai suohkanstivrii ja válljii ovttasbargat olgešbellodagain, gurutbellodagain ja johttisámi listtuin vai ožžot eanetlogu suohkanstivrras.



SECTION

# Accusatives

SELECT:miiPlAcc (N Pl Acc) OR (A Pl Acc) IF (-1 ("mii") OR ("gii") OR ("mihkkege") OR ("miige"))(1 NP-BOUNDARY);
	#$ Maid ipmašiid doppe dagat?
	#$ Buot álbmogiid hearvásvuođat ja riggodagat buktojuvvojit gávpogii , muhto ii mihkkege buhtismeahttumiid beasa sisa.

#!! * AccTV1NoC was Eckhard's late version of AccTV1 without C. We will look at this.
SELECT:AccTV1NoC Acc IF (*-1 STRICT-TRANS-V BARRIER NOT-NPMODADV LINK NOT *-1 Acc BARRIER S-BOUNDARY OR CC)(0 NP-HEAD-ACC LINK NOT *1 Acc - Pron BARRIER S-BOUNDARY)(NEGATE *-1 NUM BARRIER NOT-Attr LINK NOT 0 NUMUNIT)(NEGATE 0 TIME)(NEGATE 0 ANIMATE + Gen OR Pron + Gen LINK *1 Ill BARRIER NOT-NPMOD)(NEGATE 0 (Pron Refl) LINK 1 (N Ess))(NEGATE 0 (Pron Refl) LINK 1 Gen LINK 1 Po)(NEGATE 1 (N Nom) OR Ger)(NEGATE -1 <vdic>) ;

#!! * __AccEOS__ is The Dangerous Rule: it is the last rule before removing all leftover Acc. It only selects Acc if Nom is not an option, dont change this btw, and the end of the sentence is the next one to the right
SELECT:AccEOS Acc IF (0 Acc OR Gen LINK NOT 0 Nom)((1 EOS LINK *-1 TRANS-V) OR (1 Ger LINK NOT -1 HUMAN))(NEGATE 0 TIME OR Sem/Time OR Adv OR ("bassi" A))(NEGATE -1 Pr)(NEGATE -1 Num LINK *-1 TRANS-V + FMAINV BARRIER SV-BOUNDARY) ;
	#Comment: Please dont "star" the 1 EOS or else this rule looses its point.
	#$ Unná mánážat dulbmet eatni askki, stuora mánát fas váimmu.
	#$ Nu ožžot sámi beroštumit mat ovddastuvvojit stuorát deattu deaivvadettiin riikkalaš ja stuorraservodaga beroštumiiguin.
	#% Dat čohkkáiga oktii njuniid. <== Make a rule for this in the "acc/gen-section".
	#% Mánná viehkalii olggos álás julggiid. <== Note that both are IV-verbs.

#!! * __genRel__ removes genitive if Rel to your right ''ožžot olbmot skoviid maid''
REMOVE:genRel Gen (0 Acc - Sem/Time LINK *1C Rel OR @CVP BARRIER WORD)(NEGATE *-1 (Num Nom) OR (Num Acc) BARRIER NOT-NPMOD)(NEGATE *-1 Pr BARRIER NOT-A)  ;
	#$ Dan maid dalle dárbbaša fievrridit, ferte olmmoš noađuhit.
	#$ Juohke gielddas ožžot olbmot skoviid maid sii galget deavdit ja sii jearahallet maiddái olbmuid, muhto Isaksen imašta kommišuvnna bargovugiid

#!! * __genAcc__ selects Acc
SELECT:genAcc Acc (-1 BOC)(0 Nom)(1 Rel LINK *1 FMAINV LINK *1 TRANS-V BARRIER Nom LINK NOT *1 Acc) ;
	#$

#!! * __genNom__ removes Acc
REMOVE:genNom Acc (-1 BOC)(0 Nom)((1 Rel LINK *1 FMAINV LINK NOT *1 TRANS-V) OR (1 Rel LINK *1 COMMA LINK *1 COPULAS BARRIER Nom));
	#$ Gávpot mii lea vári alde, ii sáhte leat čihkosis.

#!! * __DemAcc__ selects Den Acc after the last acc-disambiguation of nouns
SELECT:DemAcc (Dem Acc)(0 Gen LINK 1C (N Acc)) ;
	#$

#!! * __KillAcc__ Removes Acc if you are Gen
REMOVE:KillAcc Acc IF (0 Gen) ;
	#$ Mii leat geardumiin juo álgán, nággestii unna vieljaš.



SECTION


#!! !Locative and comitative - Disambiguation based upon coordination
	# ================================================================
 # The idea is to let the other rules work first, and then let the coordination follow suit.

SELECT:LocCoord6- Loc IF (*-1C Loc LINK *1 Rel BARRIER Com OR S-BOUNDARY)(0 Com LINK *-1 CRD2 BARRIER NOT-NPMOD);

SELECT:ComCoord1- (Sg Com) IF (*-1 CRD2 BARRIER NOT-NPMOD LINK -1 Com LINK NOT 0 Loc);
	## Ii lean dárbu dábálaš eanavuovdimii, oktan opmodatregistreremiin ja čuoldimiin.

SELECT:ComCoord2 (Sg Com) IF (1 CRD2)(*2 Com BARRIER NOT-NPMOD LINK NOT 0 Loc);
	## Fága galgá ovddidit ovttasbarggus Sámedikkiin ja sámi dearvvašvuođa- ja sosiálabargiiguin.

#!! !And then we remove the remaining Sg Com analysis

REMOVE:KillComSg (Sg Com) IF (0 (Pl Loc)) ;
	#$ Giellakantuvrras ii leat formála váldi álggahit gielladoaimmaid etáhtain, muhto veahkehit.


#!! !!Essive

SELECT:ESSr2806 ESS-ADVL-N + Ess IF (NOT *-1 OPRED-V BARRIER S-BOUNDARY) ;
	#$ Seavdnjadin ii oainne gosa hearggit mannet.
	#$ Bassin ii galggaše kantuvrras čohkkát.
	#$ Beaskkain gal bivvá buolašin.

SELECT:ESS2807 Ess IF (-1C Nom)(0 Nom);
	#$ Gáibádussan lea ahte olbmos lea sámegiella ruovttogiellan. #no need
	#% Earáid hárrái čálii skuvladirektevra Kvammen ahte sii leat miehtemielalaččat ođđa áigái.

SELECT:ESSOpredV Ess IF (*-1 Acc BARRIER NOT-NPMOD LINK *-1 OPRED-V BARRIER NOT-NPMODADV)(NEGATE 0 ("oktan"));
	#$ Máret anii Máhte jallan.
	#$ Sii dahke iežaset oađđin.

SELECT:ESS2809 Ess - Actio IF (*-1 OPRED-V BARRIER NOT-A-ADV LINK *-1 Acc BARRIER SV-BOUNDARY);
	#$ Duon mun gohčodan juoigamin.

#SELECT:r2810 Ess IF (0 (N Ess) OR (A Ess))(1 CC)(*2C Ess BARRIER NOT-NP);
	## Ovdal go kristtalašvuohta bođii Sápmái lei noaiddis erenoamáš rolla oaidnilin ja dálkkodeaddjin.

SELECT:ESS2811 Ess IF (-1 ("bissut"))(0 Loc);

# Essive, not Actio Nom:
SELECT:ESS2812 Ess IF (0 (Actio Nom))(1C NP-HEAD-NOM);
	## Nuorran son bođii dohko.	 #no need

#!! !!Late case rules (after other case rules have worked).


#!! !!!VERBS PART 2, Section #22
# Disambiguating verbs - part 2

SECTION

#Lexical disambiguation of nouns

REMOVE ("vuorru" G3) (-1 Gen - Sem/Time) ;

#!! !!Finite or not

#!! !Finite
SELECT:r2906 SG2-V IF (*-2C SG2-V BARRIER SV-BOUNDARY OR PUNCT)(-1 Rel)(NEGATE -1 (Pl Nom));
 	#$ Don leat garra olmmái gii láddjet doppe gosa it leat gilván.

SELECT:r2820 VFIN IF (-1 NP-HEAD-NOM LINK NOT *-1 VFIN OR Acc)(0 PrsPrc LINK 0 TRANS-V)(*1 NP-HEAD-ACC BARRIER VFIN);
 	#$ Jus stálu beana njoallu stálu háviid, de hávit sávvojit.

SELECT:r2821 VFIN IF (*-1 BOC BARRIER REAL-V)(0 INF-V LINK NOT 0 Imprt)(NEGATE 0 ("manne") LINK *-1 BOS BARRIER NOT-CC)(*1 Inf BARRIER SV-BOUNDARY OR VFIN);
 	#$ Áigugo fas skuvlii álgit?
 	#$ Sáhttit maid namahit ahte ...
 # Looks brave but works! OBS! Should not take: Mánná sáhtášii mannat olggos.

SELECT:r2822 VFIN IF (NEGATE *-1C VFIN BARRIER S-BOUNDARY)(-1 Loc)(NEGATE -1 (Interr Sg))(0 LOC-V);
 	#$ Nuppi lađđasis čuovvu, ahte...


#!! !Not Finite

REMOVE:r2823 VFIN IF (NEGATE -2 BOS OR CLB)(-1C Inf)(NEGATE 0 Qst);
 	#$ Lága juksanmearrin lea váikkuhit sihkkarastit álbmogii
 	#$ ovttadássásaš dearvvašvuođaveahki.
 # Neg conditions because of:
 	#$ Suhpodit daddjojuvvo dat go dušše nođiid váldá olmmoš eret...
 	#$ Mearrideaddjin dasa ferte danin leat addojuvvogo...

REMOVE:r2824 VFIN IF (*-1 Nom BARRIER NOT-ADV-PCLE LINK *-1C VFIN BARRIER NOT-ADV-PCLE OR CP);
 	#$ Lea dárbu nannet doaimmaid mat ovddidit sámi dutkama.

#REMOVE:r2825 VFIN IF (*-1 PrfPrc BARRIER HEAD-N OR (Pron Nom) OR SV-BOUNDARY LINK *-1 REAL-COPULAS BARRIER NOT-ADV-PCLE);
  	#$ Sin badjel geat orrot jápmima eatnamis ja suoivanis, lea ihtán čuovggas.
 	#$ Gii lea cuigen din báhtarit boahttevaš moarreduomus?

REMOVE:r2827 VFIN IF (NEGATE *-1 Rel OR CS OR COMMA BARRIER S-BOUNDARY)(*-1C VFIN BARRIER S-BOUNDARY OR PUNCT LINK NOT *-1 Interr BARRIER NOT-NP)(*1 S-BOUNDARY OR CC OR EOS BARRIER NP-HEAD-NOM OR NP-HEAD-ACC);
 	#$ Muhto sáđuid son boaldá čáskatmeahttun dolas.

REMOVE:r2828 VFIN - Qst IF (NEGATE 0 Imprt LINK *-1 BOS BARRIER NOT-ADV-PCLE)(NEGATE *-1 Rel BARRIER REAL-V)(NOT 0 N)(*1C VFIN BARRIER S-BOUNDARY OR PUNCT OR XGO OR ("galle" Num) LINK NOT 0 Qst)(NEGATE *1 Neg BARRIER VFIN)(NEGATE *1 VFIN BARRIER S-BOUNDARY LINK 1 go);
 	#$ Sámegillii galbet universitehta lea šaddan realitehta.
 	#$ Áldu mii dálvvi miehtá njamaha miesi gohčoduvvo čearpmateadnin.

REMOVE:r2829 VFIN IF (0 Loc)(*1 Neg BARRIER NOT-ADV-PCLE LINK *1 PrfPrc OR ConNeg BARRIER SV-BOUNDARY);
 	#$ Lágas ii duššaduvvo oktage bustávaš dahje čuoggáš, ovdal go...
 	#$ Máhttájeaddjit orro jávohaga eai ge dalle muitalan geasage maid ledje oaidnán.

REMOVE:r2832 ConNeg IF (*-1 PrfPrc BARRIER NOT-ADV-PCLE);
 	#$ Go geavahus ii duođaštuvvon leat álggahuvvon ovdal go...
#SELECT:r2833 (@+FMAINV) IF (*-1 CS BARRIER S-BOUNDARY)(0C (Actio @>N) OR (PrfPrc @>N) OR (Gen @>N) OR (@+FMAINV))(*1 NP-HEAD-ACC)(NEGATE *1 VFIN BARRIER S-BOUNDARY);
 	#$ Go gulan olbmuid sámásteame, de lea dego doložii máhccat, go mánnán gullen áhči,  eagi ja Ristiinna-imi humadeame.

SELECT:r2834 VFIN IF (-1 BOS)(1 Adv)(2 (":"));
SELECT:r2835 VFIN IF (-1 BOS)(1 EOS);
SELECT:r2836 VFIN IF (-1 BOS)(1 ("go") LINK NOT *1 VFIN BARRIER S-BOUNDARY);
 	#$ Ozan ain:

	#!! !Indicative Negative
	# -------------------
 # Unique are Sg1, Sg3, Du1, Du2, Du3, Pl1, Pl2, Pl3
 # Sg2 "it" = ABBR.

	# Select Neg Sg2 instead of ABBR
REMOVE:r2837 ABBR IF (*-1 ConNeg OR PrfPrc)(0 Neg);  	#$ Dáppe don leamaš it.
REMOVE:r2838 ABBR IF (0 Neg)(*1 ConNeg OR PrfPrc);  	#$ Don it leat dál.


	#!! !Infinitive
	#    ----------

#!! * __InfComplToN__ Inf when -1 N
SELECT:r2840 Inf IF (-1C (Sg Nom) LINK NOT 0 Num LINK NEGATE *-1 CC BARRIER NOT-NPMOD LINK *-1 Nom BARRIER VFIN)(NEGATE 0 PL3-V LINK *1 NP-HEAD-PL-NOM BARRIER NOT-NPMODADV);
 	#$ Mánáin lea vuoigatvuohta oažžut dearvvašvuođaiskkadeami.
 	#$ Mus ii leat báljo dilli vuolgit.
#fk7559

SELECT:r2841 Inf IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(0 VFIN);
 	#$ Gean vuorru lea dál bálkestit birccu?
#fk2080

SELECT:Inf_ICL Inf IF (-1 Acc LINK NOT 0 Rel OR VFIN OR Adv OR Nom LINK *-1 VFIN BARRIER Acc OR S-BOUNDARY)(*1 ADVLCASE OR Acc BARRIER NOT-NPMOD);
 	#$ Son vuolggahii bálvaleaddjiidis rávkat bovdejuvvon gussiid heajaide.
 	#$ ...leago dieđiheamis doarvái vuođđu ášši váldit meannudeapmái.
 	#$ Vaikko ii loga makkárge revolušuvnna vuordit dásseárvopolitihkas iežas gielddas maŋŋel dán kurssa, de deattuha ahte buot dáhpáhuvvet proseassa čađa.


SELECT:r2845 Inf IF (-1 Nom LINK *-1 Loc BARRIER NOT-A-ADV LINK *-1 COPULAS BARRIER NOT-PCLE)(*1 Acc BARRIER NOT-A-ADV);
 	#$ Leatgo geavaheaddjis váttisvuođat buktit áššiid ovdan?

SELECT:r2846 Inf IF (-1 COMMA LINK *-1 (Pron Rel) LINK *-1 INF-V BARRIER REAL-V);
 	#$ Dalle fertejit buotlágan ávnnaslaš eavttut maid dárbbaša vai duođalaččat galgá sáhttit kultuvrras dikšut, gullat dása.

SELECT:r2847 Inf IF (-1 COMMA LINK *-1 (Pron Rel) LINK *-1 (Pron Rel) LINK *-1 INF-V BARRIER REAL-V);
 	#$ Eai galgga sii geat gullet dakkáraš minoritehtaide ovttas eará láhttuiguin mat gullet sin jovkui, manahit vuoigatvuođaideaset dikšut iežaset kultuvra, "mihkkege" Pron Indef Sg Acc ja geavahit iežaset oskku, dahje geavahit  iežaset giela.

SELECT:r2848 Inf IF (NEGATE *-1 (Pl Nom) BARRIER NOT-ADV-PCLE)(1 LEAT-FIN-NOT-IMPRT)(NEGATE 1 Qst);
 	#$ Dáidda heŋgejuvvojit gurpasat, ja maid dál de guottehit lea áigumin.

SELECT:r2849 Inf IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (A Sg Nom) BARRIER NOT-ADV-PCLE);
 	#$ Dárbbašlaš lea dikšut jávrriid ovdal go lea ekonomalaččat veara bivdit.

SELECT:r2850 Inf IF (*-1C NP-HEAD-NOM + Sg BARRIER REAL-V OR go)(1 PUNCT OR EOS);
 	#$ Áigemearri váidalusa buktit.
 	#% In dieđe man buorit dat leat.

REMOVE:r2851 Inf IF (NEGATE *-1 VFIN BARRIER S-BOUNDARY)(0 ("leat"))(*1 PrfPrc BARRIER REAL-V);
 	#$ De leat máksán.

	# Coordinated infinitives
	# - - - - - - - - - - - -
SELECT:r2852 Inf IF (*-1 COMMA BARRIER REAL-V OR MO LINK -1C Inf)(NEGATE 0 (Neg Imprt))(NEGATE *-1 (Pl Nom) BARRIER S-BOUNDARY)(NEGATE *1 (Pl Nom) BARRIER S-BOUNDARY);
 	#$ Mun in leat boahtán gomihit, muhto ollašuhttit.
 	#$ Son galgá stuorrut, mun fas uhccut.

SELECT:r2853 Inf IF (*-1 CC BARRIER SV-BOUNDARY LINK *-1 Inf BARRIER WORD);
 	#$ Hearrá Ipmilat galggat gudnejahttit, ja áivvestassii su bálvalit.

SELECT:r2854 Inf IF (*-2C Inf BARRIER REAL-V)(-1 CC);
 	#$ Duođalaš nana jáhku geažil lea divššohasas vuoigatvuohta biehttalit vuostáváldimis vara dahje varabuktagiid dahje biehttalit botkemis nealgudeami mii lea jođus.

SELECT:r2855 Inf IF (*-3 Interr LINK *-1 Inf BARRIER REAL-V)(-2 COMMA)(-1 CC)(NEGATE *1 Nom BARRIER NOT-NPMODADV);
 	#$ Divššohasbearráigeahččis lea vuoigatvuohta buktit oaiviliiddis dilálašvuođaid birra mat gullet bearráigeahčči bargoviidodahkii, ja evttohit konkrehtalaš buoridandoaimmaid.

	# Infinitives
	# - - - - - -
REMOVE Inf (0 VFIN + Qst) ;

SELECT:r2856 Inf IF (*-1 BOS BARRIER NOT-ADV)(NEGATE 0 Imprt)(NEGATE 0 VFIN LINK *1 Nom BARRIER NOT-NPMOD);
#fk841 (kor mange treff på freecorpus)

SELECT:r2857 Inf IF (-2 ABBR)(-1 (PUNCT RIGHT));
#fk60

SELECT:r2858 Inf IF (-1 N)(0 V)(*1 Rel BARRIER NOT-NPMODADVII LINK NOT 0 Adv);
#fk797

SELECT Inf IF (-1 Nom LINK *-1 CC BARRIER NOT-NPMOD LINK -1 Nom LINK *-1 COPULAS BARRIER NOT-NPMODADV) ;
 	#$ Jagi 2005 Sámediggi álggahii Divvuma, mii lei prošeakta ja bargojoavku ráhkadit sámegielaid sátnedivvunprográmmaid.
#fk194

REMOVE:NomAgwhenInf  Actor  (0 Inf) ;


	# Infinitives in lists
	# - - - - - - - - - -

SELECT:r2859 Inf IF (-2 VFIN)(-1 (":" CLB));
 	#$ Ulbmilat leat: eastadit soađi, nannet ráfi.

SELECT:r2860 Inf IF (*-2 Inf BARRIER VFIN LINK -1 (":" CLB) LINK -1 VFIN)(-1 ("\;" CLB));
 	#$ Ulbmilat leat: eastadit soađi; nannet ráfi.

SELECT:r2861 Inf IF (*-2 Inf BARRIER VFIN LINK -1 ("\;" CLB) OR ("," CLB) LINK *-1 Inf BARRIER VFIN LINK -1 (":" CLB) LINK -1 VFIN)(-1 ("\;" CLB) OR ("," CLB));
 	#$ Ulbmilat leat : eastadit soađi; nannet ráfi; lohkat girjiid.
	#      L-1 L-1 L*-1    L-1 *L-2   -1  0

SELECT:r2862 Inf IF (*-2 Inf BARRIER VFIN LINK -1 ("\;" CLB) OR ("," CLB) LINK *-1 Inf BARRIER VFIN LINK -1 ("\;" CLB) OR ("," CLB) LINK *-1 Inf BARRIER VFIN LINK -1 (":" CLB) LINK -1 VFIN)(-1 ("\;" CLB) OR ("," CLB));
 	#$ Ulbmilat leat : eastadit soađi; nannet ráfi;  lohkat girjiid , borrat márffi.
	 #      L-1 L-1 L*-1    L-1 *L-2   L-1  L* -1     -1  0

	# Infinitive in sentence fragments
	# - - - - - - - - - - - - - - - -

SELECT:r2863 Inf IF (-1C Acc LINK *-1 BOS BARRIER NOT-NPMOD)(1 EOS);
 	#$ Ruđa juogadit.

SELECT:r2864 Inf IF (-1C Ill LINK *-1 BOS BARRIER NOT-NPMOD)(0 ILL-V)(1 EOS);
 	#$ Mánáidgárdái hárjánit.

SELECT:r2865 Inf IF (-1 BOS)(*1C Acc BARRIER NOT-NPMOD LINK 1 EOS);
 	#$ Ruđa juogadit.

SELECT:r2866 Inf IF (-1 BOS)(0 ILL-V)(*1C Ill BARRIER NOT-NPMOD LINK 1 EOS);
 	#$ Hárjánit mánáidgárdái.

SELECT:r2867 Inf IF (-1 BOS)(0 LOC-V)(*1C Loc BARRIER NOT-NPMOD LINK 1 EOS);

SELECT:r2868 Inf IF (-1C Loc LINK *-1 BOS BARRIER NOT-NPMOD)(0 LOC-V)(1 EOS);



#!! !!Indicative or imperative

#!! !!Verbs according to person and number

#!! !Sg1 - First person singular


#!! * __InitialLeanRule__ selects lean when no VFIN to the left
SELECT:InitialLeanRule_r2900 Sg1 IF (NEGATE *-1 VFIN BARRIER S-BOUNDARY OR PUNCT)(0 LEAN);
	## Lean čiegas velohaddamen láhttis skuvlalávka oaivve vuolde.
	## Jesus celkkii: "Oanehis áiggi lean velá din luhtte."
# The function is in reality to remove Ess of Lea. Perhaps restrict to that.

#!! * __Sg1WhenAloneVfin__ selects Sg1 when no other VFIN or PrfPrc
SELECT:Sg1WhenAloneVfin_r2901 SG1-V IF (NEGATE *-1 VFIN OR PrfPrc BARRIER S-BOUNDARY)(NEGATE 0 Der/Pass OR N)(NEGATE 0 (N Nom) LINK 0 LEX-N)(NEGATE *1 VFIN BARRIER S-BOUNDARY)(NEGATE 0 PrfPrc LINK -1 CC LINK -1 PrfPrc);
	## Oađidettiin gullen olbmo boahtimin.

# Trond 29.4. Next to still to evaluate:

SELECT:r2903 SG1-V IF (*-1 CC OR COMMA BARRIER REAL-V LINK *-1C SG1-V BARRIER REAL-V)(NEGATE 0 Der/Pass);
	#$  Bargen  doppe  beannot  jagi  sullii  bealle  virggis  ,  oahpahin  eanaš  matematihka  ja  luonddufága  .

REMOVE:r2905 SG1-V IF (0 PrfPrc OR Actio)(*1C VFIN BARRIER S-BOUNDARY);
	## Dálá fiinnuhan riektejurddašanvuogi mielde ii eahpiduvvo ahte dán livččii galgan dahkat.


#!! !Sg2 - Second person singular

 # Sg2 = Inf (even syllables, copula)
 # Note the homonymy with derived verbs (jearat = jearadit + ...
 # Prt Sg2 = Prs Pl3 = Imprt Pl2 (even syll)  vulget
 # Prt Sg2 = Prs Pl3 = Prs Pl1 = Inf (odd syll)  muitalit
 # Prt Sg2 = Prs Pl3         (contracted) fertejit

SELECT:r2907_Pl3 (Prt Pl3) IF (*0 Pl)(*1 PAST-MARKER BARRIER SV-BOUNDARY);

#!! * --r2907__Sg2__ Prt Sg2 if ikte etc.
SELECT:r2907_Sg2 (Prt Sg2) IF (NOT *0 Nom + Pl)(*1 PAST-MARKER BARRIER SV-BOUNDARY);
	## Vulget go ikte?


SELECT:r2908 SG2-V IF (*-1 CC BARRIER VFIN LINK *-1C SG2-V BARRIER SVF-BOUNDARY);
	## Dasgo sániidat mielde don dahkkojuvvot friddjan, ja sániidat mielde dubmejuvvot.
#fk 291

SELECT:r2909 SG2-V IF (*-1 CC BARRIER NOT-ADV LINK *-1 CP LINK *-1C SG2-V BARRIER NOT-ADV-PCLE);
	## Don oainnát mo olbmot bahkkejit du birra, ja de jearat vel...
#fk 16

SELECT:r2910 (V Sg2) IF (*-2C (V Sg2) BARRIER VFIN)(-1 CS)(NEGATE *1 PxPl3);
	## Don it beasa doppe olggos ovdal go leat máksán.
#fk 498

REMOVE:CopNotPl ("leat" Pl1) OR ("leat" Pl3) IF (*1 (Sg Nom) OR ("okto") BARRIER NOT-ADV-PCLE LINK NOT 0 NUM LINK NOT 1 CC OR COMMA);
	#$ Leatgo árgi bajándálkkis?
	#$ Leat go árgi bajándálkkis?
	#$ Leat go okto áigume herskkostallat?
#fk 3152

REMOVE:r2912 (Prs Pl1) IF (-2 BOS)(-1 ("jus") OR ("jos") OR ("juos") OR ("joas"))(0 (Prs Sg2))(*1 ("de") BARRIER (Sg2 Nom));
	## Jus leat buohcamin, de fertet...
#fk 135

REMOVE:r2913 (V Sg2) IF (1 CC)(2 VFIN)(NEGATE 2 Sg2);
	## Buođđomuoraid maid ráhkadit olbmot gárvvisin dassá go deatnu leahkkasa, čuolddaid čohket ja ráhkadit oar-julggiid.

REMOVE:r2914 (V Sg2) IF (0 COPULAS)(*1 (Pl Nom) BARRIER NOT-ADV-PCLE);
	## Leatgo doppe olu gávppit?

REMOVE:r2915 (V Sg2) IF (*-1 (Num Nom) BARRIER NOT-NPMOD);
	## Gitta guhtta hearggi sáhttet leat ovtta ráiddus.

SELECT:r2916 (V Sg2) IF (0 Pl1 OR Pl3)(*1C (V Sg2) OR PxSg2 BARRIER VFIN OR (":"));
	## Jus dulvvi niegadat, de boađát gullat unohis šlárvaságaid.



#!! !Sg3 - Third person singular
#!! Infinitive and clausal subject

#!! Rules that look backwards for a subject across a relative clause:

#!! Rules that look backwards for a subject across a subordinate clause (CP boundary):

#!! Extension possibilities: Coordination

#!! Son oaidná du ja mu ovdal go boahtit...

#!! Coordinated Sg3 verbs

#!! Not (V Sg3)

#!! !Du1 - First person dual

 # Prs Du1 = Prt Pl3 (even syllable verbs); also similar to Prs Sg3 of other verbs.

#!! * __MunJaDonDu__ selects Du1 if Mon V ja don V de V-Du2
SELECT:MunJaDonDu (V Du1) IF (*-1 CC OR ("de") BARRIER VFIN LINK *-1C (V Sg2) BARRIER VFIN LINK *-1C (V Sg1) BARRIER VFIN);

#!! * __DonJaMunDu__ selects Du1 if Don V ja mun V de V-Du2
SELECT:DonJaMunDu (V Du1) IF (*-1 CC OR ("de") BARRIER VFIN LINK *-1C (V Sg1) BARRIER VFIN LINK *-1C (V Sg2) BARRIER VFIN);
	## Jus lean váiban ja don maid leat váiban, de manne nohkkat hui árrat.

#!! The previous two rules look marginal.

#!! * __DuNotPrtIfToday__ selects Du1 over Prt in the context of a present-marker.
SELECT:DuNotPrtIfToday_r2948 (Prs Du1) IF (0 Prt)(*1 PRESENT-MARKER BARRIER S-BOUNDARY);
	## Gergego vuolgit ihttin?
# This rule selects Prs over Prt. Better would be REMOVE Prt?

#!! * __Du1IfDu1__ selects Du1 with a left context Du1 ... ja ...
SELECT:Du1IfDu1_r2947 (V Du1) IF (*-1 CC BARRIER VFIN LINK *-1C (V Du1) BARRIER VFIN);
	## Moai bohte su lusa ja orrugohte su luhtte.


#!! * __NoDu1__ removes Du1 if no MOAI or Du1 around.
REMOVE:NoDu1_r2949 (V Du1) IF (NEGATE *-1 MOAI OR Du1)(NEGATE 0 Imprt LINK *-1 BOC BARRIER NOT-ADV)(NEGATE *1 MOAI);
	## De bohte ruđat.
	## Nieiddat jerre.


#!! !Du2 - Second person dual

#!! !Rules for leahppi = ("leahppi" N Sg Nom)

#!! !Du3 - Third person dual

#!! !Pl1 - First person plural

#!! !Pl2 - Second person plural

#!! !Pl3 - Third person plural


#!! * __Pl3IfPlSubj__ Pl3 if Pl noun to the left
SELECT:Pl3IfPlSubj PL3-V IF (*-1  NP-HEAD-PL-NOM BARRIER NOT-ADV-PCLE  LINK NOT 0 VFIN) ; # r2974
	## Muhto dorskit gal leat varrasat.
	## Divššohasaid birra geat leat uminddegin dahkkon.
	## Eatnagat bohtet nuortan ja oarjin.
	## Geafit gal leat álo din luhtte.

#!! * __Pl3IfPlSubj__ Pl3 if safe plural (incl pron) to the left
SELECT:r2975 PL3-V IF (*-1C NP-HEAD-PL-NOM BARRIER (V PrfPrc) OR PPRON-NOT-SII OR (N Sg Nom) OR CS OR CC)(NEGATE *-1 (V Pl3) BARRIER SV-BOUNDARY OR PUNCT);
	## Makkár vuoigatvuođat dain leat riikavuložin?

SELECT:r2976 PL3-V IF (*1C NP-HEAD-PL-NOM BARRIER PPRON-NOT-SII OR S-BOUNDARY);



# After everything has been done, we go for the third person.

#!! * __allSg2leat__ removes Sg2 if leat Prs Pl3
REMOVE:allSg2leat Sg2 IF (0 ("leat" Prs Pl3));

#!! * __allPl1__ removes Pl1 if Pl3
REMOVE:allPl1 Pl1 IF (0 (Prs Pl3) LINK NOT 0 Imprt);

#!! * __allPrtSg2__ removes PrtSg2 if PrsPl3
REMOVE:allPrtSg2 (Prt Sg2) IF (0 (Prs Pl3));


#!! !!Rules for a special infinitive construction
#!! !!More finite verbs
#!! !!Passive
#!! !!Infinitive
#!! !!Present Participle
#!! !!Actio/Perfect Participle


 # Actio = Prs Sg1 = PrfPrc (odd-syll verbs, contracted verbs)
 # Perfect Participle is also treated in "Disambiguating verbs - part 1".
 # The order of rules is of some importance here.
 # We do not SELECT Actio, we remove PrfPrc instead, since there might be a
 # non-derived N reading alongside Actio.


SELECT:ZRitvaTestLeksicalisation PrfPrc IF (*-1 BOC BARRIER NOT-NPMOD)(NEGATE 1 COMMA LINK 1 (@CNP) OR (@CVP))(*1 N BARRIER NOT-NPMOD LINK *1 VFIN BARRIER NOT-ADV-PCLE)(NOT O LEX-N + (Sg Nom))(NEGATE 0 (Actio Nom) LINK 1 (N Nom) LINK -2 HAB-ACTOR + Gen);
# Dan sajis go N, de geavhan LEX-N. Der/NomAct lea maid N.
REMOVE:ZRitvaTestChooseNoun PrfPrc IF (0 (Der/NomAct N Sg Nom) LINK *1 SG3-V BARRIER VFIN) ;
# Lene: dás váilu ovdamearkacealkka. Ii galggašii deaivat: Váiban mielain jurddašallá ahte vare son livččii girdisáhpán.

#SELECT:r3041 PrfPrc IF (*-1 BOC BARRIER NOT-NPMOD)(NEGATE 1 COMMA LINK 1 (@CNP) OR (@CVP))(*1 N BARRIER NOT-NPMOD LINK *1 VFIN BARRIER NOT-ADV-PCLE);
  	## Sajáiduvvan fágadoahpagiid sáhttá dieđusge geavahit.

SELECT:ZPrfPrcMannat PrfPrc IF (0 ("mannat") LINK 1 ("čađa"));

SELECT:Zr3045 PrfPrc IF (*-1 LEAT-FIN-NOT-IMPRT BARRIER SVF-BOUNDARY OR PrfPrc)(NEGATE 0 Ess)(NEGATE 0 Actio LINK -1 PrsPrc)(NEGATE 0 Actio LINK 1 COMMA OR CC LINK 1 Actio)(NEGATE *1 PrfPrc BARRIER S-BOUNDARY) ;
	## Dasto lea Sámediggi ovttasráđiid Finnmárkku ja Tromssa fylkamanniiguin doallan fágaseminára.

SELECT:Zr3047 PrfPrc IF (-1 CC LINK *-1 PrfPrc BARRIER REAL-V) ;
	## Boazodoallolágat leat dievasmahttán ja dahkan dárbbašmeahttun...

REMOVE:Zr3049 PrfPrc IF (*-1 Gen BARRIER NOT-NPMOD LINK -1 BOC) ;
	## Divššohasjournálaid geahččan dálkkasgeahččalemiid oktavuođas...

REMOVE:Zr3050 PrfPrc IF (NEGATE *-1 VFIN)(NEGATE 0 Der/Pass OR ("leat" PrfPrc))(NEGATE *1 VFIN BARRIER S-BOUNDARY) ;
	## Miehtan nuoraid ovddas geain ii leat miehtangealbu.
	## Doppe leamaš sámiin stuora deaddu. # It should not take the latter one.

REMOVE:Zr3051 PrfPrc IF (*-1 PrfPrc OR ConNeg BARRIER NOT-NPMODADV LINK NOT 0 ("leat")) ;
	## Ii ávžžuhuvvo dieđuid geahččan sivas olbmuid geat čužžot divššohasa lahka.


	# Coordinated actios
	# - - - - - - - - - -

REMOVE:Zr3057 PrfPrc IF (NEGATE -1 LEAT-FIN-NOT-IMPRT LINK *-1 CP BARRIER VFIN)(1 CLB LINK *1 N BARRIER NOT-NPMOD LINK *1 CC BARRIER VFIN LINK *1 N BARRIER NOT-NPMOD LINK NOT 0 Loc) ;
	## Sámedikki hálddahusa nannen, dearvvašvuođa- ja eallindiliiskkadeami ruhtadeapmi ja Sámi dearvvašvuođadutkanguovddáža doaimmaid viiddideapmi váldojit ovdan 2002 bušeahta oktavuođas.




#!! !!Actio
#!! !!Selecting some more finite verbs

#REMOVE:InfifVFIN Inf (0 VFIN LINK NOT *-1 AUX) ;

#!! !!Lexical disambiguation of verbs

	# arvit vs arvát vs arvat
	# ------------------------
IFF:Arvit ("arvit") IF (0 ("arvát") OR ("arvat"))(NEGATE *0 HUMAN LINK 0 SUBJ) ;

	# ballat vs. ballát
	# -----------------
REMOVE:LexSel ("ballát") IF (0 ("ballat")) ;
	# One can always (?) understand it as "ballat" if it is homonomy.

	# bassit vs. bassat
	# -----------------
IFF:LexSel ("bassit") IF (0 ("bassat") LINK *0 Sem/Food + Acc) ;


	# báhcit vs báhccat
	# -----------------
REMOVE:LexSel ("báhccat") IF (0 ("báhcit" Imprt)) ;


	# beassat vs. besset
	# ------------------
	# beassat = 'get out of; get into' etc., besset 'strip off bark'

REMOVE:LexSel ("besset") IF (0 ("beassat"))(*1 (V Actio Loc) BARRIER SV-BOUNDARY) ;
	## Mun bessen vuolgimis.

REMOVE:LexSel ("biestit") IF (0 ("bistit") LINK *1 TIME BARRIER SV-BOUNDARY) ;

REMOVE:LexSel ("bivvát") IF (0 ("bivvat")) ;


	# boahtit vs. bohtat
	# ------------------
	# boahtit 'come', bohtat 'swell'

	# These verbs have 5 homonym forms, Prs Du1/Pl3 bohte, Prt Sg1 bohten,
	# Prs Pl3/Prt Sg2 bohtet, Imprt Sg1 bohton, Imprt Pl1 bohtot
	# In almost all cases, the correct choice will be "boahtit", the
	# challenge is to avoid removing the cases where it might be "bohtat"

LIST BOHTAT-NOUN = "bálká" "áiteuksa" "bunci" "dáigi" "fanas" "lássa" "láse" "luŋka" "stohpouksa" "uksa" "viesso#uksa" "vanca" ;


REMOVE:LexSel ("bohtat" Sg1) IF (0 ("boahtit")) ; # Will hit 'bohten' and 'bohton'.
REMOVE:LexSel ("bohtat" Sg2) IF (0 ("boahtit")) ; # Will hit 'bohtet'.
	## Go mun bohten du vissui,...
	## Journálat álmmolaš doaimmas bohtet arkiivalága vuollái.

REMOVE:LexSel ("bohtat" Du1) IF (0 ("boahtit")) ; # Will hit 'bohte'.
REMOVE:LexSel ("bohtat" Pl1) IF (0 ("boahtit")) ; # Will hit 'bohtot'.

REMOVE:LexSel ("bohtat" Pl3) IF (0 ("boahtit"))(*1 ("son" Pl3 Nom) BARRIER SV-BOUNDARY) ;
REMOVE:LexSel ("bohtat" Pl3) IF (*-1 ("son" Pl3 Nom) BARRIER NOT-ADV-PCLE)(0 ("boahtit")) ;

SELECT:LexSel ("boahtit") IF (0 ("bohtat"))(NEGATE *1 (Pron Pers) OR BOHTAT-NOUN BARRIER SV-BOUNDARY OR Neg OR NP-HEAD-NOM LINK 0 Nom)(NEGATE *-1 (Pron Pers) OR BOHTAT-NOUN BARRIER NOT-ADV-PCLE LINK 0 Nom)(NEGATE *-1 (Pron Rel) OR MO LINK *-1 BOHTAT-NOUN BARRIER SV-BOUNDARY LINK 0 Nom) ;
	# Only if there is a BOHTAT-NOUN Nom or "dat" Nom, we leave the possibility of "bohtat". But also in those cases, it cuold be "boahtit".

 	# borgit vs borgát
 	# ----------------
REMOVE:LexSel ("borgát") IF (0 ("borgit")) ;
	# One can always (?) understand it as "borgit" if it is homonomy.


 	# buollit vs buollát
REMOVE:LexSel ("buollát") IF (0 ("buollit")) ;
	# One can always (?) understand it as "buollit" if it is homonomy.


	# caggat vs.caggát
	# ----------------
	# caggat = 'tighten; keep open; hold back', caggát = 'be wide open'

REMOVE:LexSel ("caggát") IF (0 ("caggat"))(*1 NP-HEAD-ACC BARRIER NOT-NPMODADV) ;
	## Olmmoš caggá čuolu rávdnjái fávlli guvlui.

	# čuohcat vs. čuohcit
	# -------------------
	# Prs Sg3 čuohcá.
	# Prs Pl3 čuhcet.

	# čuoigat vs čuoigát
	# ------------------
REMOVE:LexSel ("čuoigát") IF (0 ("čuoigat")) ;
	# One can always (?) understand it as "čuoigat" if it is homonomy.

	# čuollat vs čuollát
	# ------------------
REMOVE:LexSel ("čuollát") IF (0 ("čuollat") LINK *0 Acc BARRIER NOT-NPMODADV LINK NOT 0 Sem/Time OR Sem/Route LINK NEGATE 0 Num LINK 1 Sem/Time) ;
	#$ Go Niillas olle vuovdái ja čuollá ovtta muora, de son oaidná ahte ii doppe nagot buot muoraid čuollat, go vuovdi lea nu stuoris ahte geahči ii oidno.

	# čuovgat vs čuovgát
REMOVE:LexSel ("čuovgát") IF (0 ("čuovgat")) ;
	# One can always (?) understand it as "čuovgat" if it is homonomy.

    # divvut vs. dievvat
    # ------------------
SELECT:LexSel ("divvut") IF (0 ("dievvat" Der/PassL)) ;

 	# doadjit vs. dodjit
 	# ------------------
SELECT:LexSel ("doadjit") IF (*0 TOOL OR BODY OR PLANT BARRIER SV-BOUNDARY) ;

	# duđđat vs. duhtat
	# -----------------
	# duđđat = 'mess', duhtat 'be satisfied'

REMOVE:LexSel ("duđđat") IF (0 ("duhtat"))(*1 Ill BARRIER NOT-NPMODADV) ;
 	## Mun duđan dien vástádussii.

REMOVE:LexSel ("duđđat") IF (0 ("duhtat"))(1 go)(*2 Ill BARRIER NOT-NPMODADV) ;
	## Duđat go mu čállimii?

	# diehtit vs. dihttot
	# -------------------
	# diehtit is TV and takes an animate subject.
	# dihttot (vise seg, forekomme) is Passive and intransitive

SELECT:LexSel ("dihttot") IF (*-0 ("sátni") OR ("vearba") OR ("substantiiva") LINK 0 (@SUBJ)) ;
	## Sátni dihtto buot oarjesámegielain. # Needed here is a set of speach act and POS words.

	# dovdat vs. dovdát
	# -----------------

	#REMOVE ("dovdat") IF (-2 ("go"))(-1 ("vuos"))(0 ("dovdát" Sg3)) ;
  	## Go vuos dovdá ovtta unnitlohkoálbmoga, lea álki fuobmát oahpes
  	## minstariid maiddái eará unnitloguid diliin.
  # ii. dat ii doaimma ok, leago dat buoret? lea go MII buoret? MII? ahh..ii leat buoret. dat ferte leat "dovdat", muhto sahttasii maid leat "dovdát", muhto goitge "dovdat" orru leahkime buoret: 1.Hvis man først blir kjent med x, så er det lettere, 2. hvis man først kjenner et x, så er det lettere, 1. doaibmá maid
  #maid doai jáhkabeahtti?
  # datgo lei evttohus?
  # vurddes:
  # L: doaibmágo du mielas dadjat "go vuos dovdá" - dovdát - bli kjent med??
  # D: JEPP
  # L: muhto ii "go vuos dovdá" - dovdát - kjenne igjen ?
  # D: DAT NAI
  # (Duomma)

REMOVE:LexSel ("dovdát") IF (0 ("dovdat")) ;
	# One can always (?) understand it as "dovdat" if it is homonomy.

	# eallit vs. eallát
	# -----------------
	# These verbs have 2 homonym forms: Ind Prs Sg3 eallá, PrfPrc eallán
	# eallát = 'to recover, become alive again', eallit = 'to live'

REMOVE:LexSel ("eallát") IF (0 ("eallit"))(*1 ("eallin")) ;
	## Son eallá suttolaš eallima.
REMOVE:LexSel ("eallát") IF (0 ("eallit"))(*1 Loc OR ("siste") OR VAbess) ;
    	## Mun dovddan muhtun olbmo guhte eallá Kristusis.
    	## Son eallá naitalkeahttá.
REMOVE:LexSel ("eallát") IF (0 ("eallit")) ;
	# One can always (?) understand it as "eallit" if it is homonomy.

#REMOVE:LexSel ("gallet") IF (0 ("galledit")) ;

	# gárrat vs. gárrut
	# -----------------
	# gárrat = 'bind sth to sth', gárrut = 'get sth done, +Inf'

#REMOVE:LexSel ("gárrat") IF (0 ("gárrut"))(*1 Inf BARRIER NOT-ADV-PCLE-NEG) ;
    	## Mii lávlluimet morašlávlagiid, ehpet ge dii gárron luoibmat.

    # geargat IV vs. gerget TV
    # ------------------
	# gerget takes Acc

IFF:LexSel ("gerget") IF (0 ("geargat"))(*1 Acc BARRIER NOT-NPMOD) ;
#REMOVE:LexSel ("gerget") IF (0 ("geargat"))(*1 Acc LINK 1 Inf BARRIER NOT-NPMOD) ;
#REMOVE:LexSel ("gerget") IF (0 ("geargat"))(*1 Acc LINK -1 Inf BARRIER NOT-NPMOD) ;

REMOVE:r3136 ("geavvat") IF (0 ("geavahit")) ;



	# geassit TV vs. geassut IV
REMOVE:LexSel ("geassut") IF (0 ("geassit") LINK *0 Acc OR ("vuorbi") BARRIER NOT-NPMODADV) ;
	#$ Nordlysbearaš unnui dušše moatti olbmui ja maŋimuš vuorbi lei gesson.


	# gieldit vs. gildit
	# ------------------
	# gieldit = 'deny sb sth', gildit = 'shine'
	# Here, we have a transitivity difference.
	# Homonym in the diphtong simplification cases: Prs Du1 gilde, Prs Pl3 gildet
	# all Prt forms, gilden, gildet, ..., all Pot forms gilddežan, ...,
	# some of the Imprt forms.

REMOVE:LexSel ("gildit") IF (0 ("gieldit"))(*1 Acc BARRIER VFIN) ;
    	## Jesus gilddii sudno garrasit:
    	## Muhto mađe eanet son gilddii, dađe eanet sii almmuhedje dan.
	# The second example is ambiguous.

SELECT:LexSel ("gieldit") IF (*1 (V Actio Loc) BARRIER SV-BOUNDARY) ;
	## Mun gilden Máhte boahtimis.

	# goaddat vs. goddit
	# ------------------
	# goaddat = 'stick to, tarrautua', goddit = 'kill'
	# Homonym in the diphtong simplification cases.

REMOVE:LexSel ("goaddat") IF (0 ("goddit"))(*1 Acc BARRIER VFIN) ;
    	## Ja sii goddet su.
REMOVE:LexSel ("goaddat") IF (NEGATE *-1 Ill BARRIER SV-BOUNDARY)(0 ("goddit") LINK NOT *1 Ill BARRIER NOT-ADV-PCLE) ;

	# goarrut vs. gorrat
SELECT:LexSel ("goarrut") IF (*-1 BOC LINK *1 CLOTHES OR SHOE BARRIER SV-BOUNDARY LINK 0 Nom OR Acc)(0 ("gorrat")) ;

	# goastit TV vs. goastat IV
REMOVE:LexSel ("goastit") IF (0 ("goastat") LINK NOT *0 Acc BARRIER NOT-NPMODADV LINK NOT 0 Sem/Time OR Sem/Route LINK NEGATE 0 Num LINK 1 Sem/Time) ;
	#$ Gal moai goste iehčaneame.

	# goddet vs. goddit
	# -----------------
	# goddet = 'catch wild reindeer', goddit = kill

REMOVE:LexSel ("goddet") IF (0 ("goddit"))(*0 Acc BARRIER NP-BOUNDARY) ;
	## Vuovdái godde dán guovžža.

	# gođđat vs. gođđit
	# -----------------
	# gođđat = 'spawn', gođđit = 'knit, weave'

REMOVE:LexSel ("gođđat") IF (0 ("gođđit"))(*1 Acc - Sem/Ani BARRIER VFIN) ;
	## Ovddemus dáida oastit árppuid ja gođđá firpmiid.
SELECT:LexSel ("gođđat") IF (0 ("gođđit"))(*1 FISH BARRIER VFIN) ;


#REMOVE:LexSel ("gohččut") IF (0 ("gohčodit")) ;

	# gullat vs. gullát vs. gullet
	# ----------------------------
	# gullat = 'hear; belong', gullát 'begin to hear', gullet 'fish'

SELECT:LexSel ("gullat") IF (0 ("gullát") LINK *0 SOUND OR ("sátni") BARRIER NOT-NPMOD) ;
    ## Mánná hárve gullá eatnigielas.

SELECT:LexSel ("gullat") IF (0 ("gullát") OR ("gullet"))(*1 Ill BARRIER SV-BOUNDARY LINK NOT 0 SOUND) ;
	## Dat gullá lunddolaččat dakkár áššiide go Sámedikkiid válganjuolggadusaide.

SELECT:LexSel ("gullat") IF (*-1 Ill BARRIER NOT-ADV-PCLE LINK NOT 0 SOUND)(0 ("gullát") OR ("gullet")) ;
	## Doaimma divššohasjournálaide gullá persovdnaregisttar.
	## Eanetlohku, masa gulle Riektejoavkku 6 eará lahtu,...

SELECT:LexSel ("gullat") IF (*1 ("ahte") BARRIER NOT-ADV-PCLE) ;
	## Mun gullen, ahte vulget ikte.

	# guođđit vs. guđđat
	# ------------------
	# guođđit = 'leave sth swh', guđđat = 'swarm'

REMOVE:LexSel ("guđđat") IF (0 ("guođđit"))(*1 Acc BARRIER VFIN) ;
    	## De sii guđđet dan viesu ja gávpoga.

#REMOVE:LexSel ("guorrat") IF (0 ("guorahallat")) ;

	# hálidit, háliidit vs. hálihit
	# - - - - - - - - - - - - - - -
REMOVE:LexSel ("hálihit") IF (0 ("hálidit") OR ("háliidit"))(*1 (@-FMAINV) OR Inf BARRIER SV-BOUNDARY) ;
  	## Son ii hálit dan dahkat.
REMOVE:LexSel ("hálihit") IF (0 ("hálidit")) ;

	# joavdat vs. joavdit
	# -------------------
SELECT:LexSel ("joavdat") IF (0 ("joavdit"))(*0 ("moalla" Ill) BARRIER SV-BOUNDARY) ;
	# juhkat vs. juohkit
	# ------------------
LIST JUOHKIN-ADV = ("hálddahuslaš" Adv) ("geográfa" Adv) ;

LIST JUOHKIN-SUBJ = "eise#váldi" ;

LIST JUOHKIN-PASS-SUBJ = "gávdnoštupmi" "eana" "čázádat" "vuoigatvuohta" ;


SELECT:LexSel ("juohkit") IF (0 ("juhkat"))(*0 Ill BARRIER Loc OR SV-BOUNDARY LINK NOT 0 Sem/Time) ;
SELECT:LexSel ("juohkit") IF (0 ("juhkat"))(*0 ("diehtu") OR TEXT OR ABSTR-TEXT OR CURRENCY BARRIER NOT-NPMOD) ;
SELECT:LexSel ("juohkit") IF (0 ("juhkat"))(*1 Ess BARRIER NOT-A-PCLE) ;
SELECT:LexSel ("juohkit") IF (0 ("juhkat"))(*1 ("mielde") OR ("ektui") OR ("gaskkas") OR ("ovddas") OR ("gaskka") BARRIER NOT-NPMOD) ;
SELECT:LexSel ("juohkit") IF (0 ("juhkat"))(*0 JUOHKIN-ADV BARRIER REAL-V) ;
SELECT:LexSel ("juohkit") IF (0 ("juhkat") LINK 0 Der/Pass LINK 0 (PrfPrc @>N)) ;
SELECT:LexSel ("juohkit") IF (*-1 JUOHKIN-PASS-SUBJ BARRIER Nom OR SV-BOUNDARY)(0 ("juhkat") LINK 0 Der/Pass) ;
SELECT:LexSel ("juohkit") IF (*-1 JUOHKIN-SUBJ BARRIER Nom OR SV-BOUNDARY LINK 0 (Pl Nom))(0 ("juhkat")) ;
	## Ruhta lea juhkkojuvvon máŋgga olbmui.
	## Eatnamat leat hálddahuslaččat juhkkojuvvon.
	## Eatnamat leat juo juhkkojuvvon.
	## Eiseválddit juhke eatnamiid.

SELECT:LexSel ("juhkat") IF (0 ("juohkit") LINK -1 CC LINK -1 ("borrat")) ;
SELECT:LexSel ("juhkat") IF (0 ("juohkit") LINK 1 CC LINK 1 ("borrat")) ;
	## Ovdal stuoradulvvi sii borre ja juhke.

SELECT:LexSel ("juhkat") IF (0 ("juohkit") LINK *0 LIQUID BARRIER Acc OR SV-BOUNDARY) ;
	## Sii juhket gáfe.

SELECT:LexSel ("juhkat") IF (0 ("juohkit") LINK 1 ("buohkat")) ;
 	## Juhket buohkat dás!
 	## Ja sii juhke buohkat das.

	# laskat IV vs. lasket TV
SELECT:LexSel ("laskat") IF (0 ("lasket") LINK NOT *0 Acc BARRIER NOT-NPMODADV) ;
	#$ Erenoamážit dáidet leat hástalusat vurdojuvvon temperatuvrra loktaneami dihte ja danne go lasket ekstrema dálkkit.


	# lohkat vs. loahkkit
SELECT:LexSel ("lohkat") IF (0 ("loahkkit") LINK 0 Der/Pass) ;

	# mannet vs. mannat
	# -----------------
REMOVE:LexSel ("mannet") IF (0 ("mannat") LINK 0 Sg1 OR Sg2) ;
REMOVE:LexSel ("mannet") IF (0 ("mannat"))(*0 Ill BARRIER S-BOUNDARY) ;
REMOVE:LexSel ("mannet") IF (0 ("mannat"))(*0 ("dohko") OR ("maŋos") OR ("ruovtto#luotta") OR ("ruoktot") OR ("bajás") OR ("vulos") OR ("bokte") BARRIER S-BOUNDARY) ;
REMOVE:LexSel ("mannet") IF (0 ("mannat"))(*0 Inf BARRIER NOT-ADV) ;

	# mannut vs. mannat
	# -----------------
REMOVE:LexSel ("mannut") IF (0 ("mannat" Imprt)) ;


	# máhccat vs. máhccut
	# ------------------- #
	# máhccat = 'return, retreat', máhccut = 'make folds in'

	# máhttit vs máhttát
	# ------------------
REMOVE:LexSel ("máhttát") IF (0 ("máhttit")) ;
	# One can always (?) understand it as "máhttit" if it is homonomy.

	# muitit vs muitát
	# ----------------
REMOVE:LexSel ("muitát") IF (0 ("muitit")) ;
	# One can always (?) understand it as "muitit" if it is homonomy.

	# oađđit vs oađđát
	# --------------- #
REMOVE:LexSel ("oađđát") IF (*0 (A* Adv) BARRIER REAL-V)(0 ("oađđit")) ;

	# ohcat vs oahcut
	# --------------- #
REMOVE:LexSel ("oahcut") IF (0 ("ohcat") LINK *1 Acc) ;
	## De ohcot moadde girjji!

    # oastit vs. ostet
    # ---------------- #
LIST OSTEN-NOUN = "náhkki" "siste#náhkki" "sisti" ;

SELECT:LexSel ("oastit") IF (0 ("ostet"))(*-1 BOS LINK NEGATE *1 OSTEN-NOUN LINK 0 Acc) ;

	# orrut vs. orrot
	# ---------------- #
	# orrot = 'remain somewhere (not come back as expected), settle down'

SELECT:LexSel ("orrut") IF (0 ("orrot"))(1 (Actio Ess)) ;

SELECT:LexSel ("orrot") IF (0 ("orrut"))(-1 Ill) ;
SELECT:LexSel ("orrot") IF (0 ("orrut"))(*1 Ill BARRIER NOT-ADV) ;
REMOVE:LexSel ("orrut" Der/PassS) IF (0 ("orrut"))(1 ("go")) ;
REMOVE:LexSel ("orrut" Der/PassS) IF (0 ("orrut"))(*1 (Actio Ess) BARRIER NOT-ADV) ;
REMOVE:LexSel ("orrot") IF (0 ("orrut"))(NEGATE 1 ("go"))(NEGATE *1 (Actio Ess) BARRIER NOT-ADV)(NEGATE *-1 Ill BARRIER NOT-ADV) ;
REMOVE:LexSel ("orrot") IF (0 ("orrut"))(NEGATE 1 ("go"))(NEGATE *1 (Actio Ess) BARRIER NOT-ADV)(NEGATE *1 Ill BARRIER NOT-ADV) ;
  	## Mii áigut dohko orrot idjii.
  	## Buot dilálašvuođat main Ipmil orro leamen jávkosis.
  	## Min Áigi jearrá orro go heivvolaš ahte son čállá bartnis birra.

	# rihkkut vs. riehkkat
	# ------------------- #
	# rihkkut = 'break, transgress, infringe, offens against, forfeit', riehkkat = 'work hard, make great efforts to..'

SELECT:LexSel ("rihkkut") IF (0 Der/Pass)(*-1 BOS LINK *1C LAHKA OR LAHKA-N) ;
  	## Láhka lea rihkkojuvvon.

# Not in use because of REMOVE Der1 IF (0 Der3 OR Der2)
#REMOVE:LexSel ("ráfáiduvvat") IF (0 ("ráfáiduhttit")) ;
	## ráfáiduhttot

SELECT:LexSel ("savvot") IF (*-1 ("hávvi" Nom) BARRIER S-BOUNDARY)(0 ("savvit")) ;

# Not in use because of Remove V-DER if Lex-v-rule.
#REMOVE:LexSel ("seahkut") IF (0 ("seaguhit")) ;

	# ruohttat IV vs ruhttet TV
	#---------------------
LIST PO-ILL = "ala" "lusa" "vulos" "bajás" "vuollái" ;

SELECT:LexSel ("ruhttet") IF (0 ("ruohttat") LINK *0 Acc BARRIER NOT-NPMODADV LINK NOT 0 Sem/Time OR Sem/Route LINK NEGATE 0 Num LINK 1 Sem/Time) ;
SELECT:LexSel ("ruohttat") IF (0 ("ruhttet") LINK *0 Ill BARRIER NOT-NPMODADV) ;


	# ruoidnat vs ruoidnát
	#---------------------
SELECT:LexSel ("ruoidnat") IF (0 ("ruoidnát")) ;

	# sihtat vs. sihtit : D ja L have agreed to remove "sihtit". It´s not in KN.
	# -----------------
REMOVE:LexSel ("sihtit") IF (0 ("sihtat")) ;


	# sirdit vs. sirdát
	# -----------------
REMOVE:LexSel ("sirdát") IF (0 ("sirdit") LINK *0 Ill OR ("dohko") OR ("deike") OR ABSTR-ROUTE BARRIER SV-BOUNDARY) ;
	## Son sirdá gitta Billavutnii.

REMOVE:LexSel ("sirdát") IF (0 ("sirdit")) ;
	# One can always (?) understand it as "sirdit" if it is homonomy.


	# vázzit vs. vázzát
	# -----------------
REMOVE:LexSel ("vázzát") IF (0 ("vázzit") LINK *0 Ill OR ("dohko") OR ("deike") OR ABSTR-ROUTE BARRIER SV-BOUNDARY) ;
	## Son vázzá gávpogii.

REMOVE:LexSel ("vázzát") IF (0 ("vázzit")) ;
	# One can always (?) understand it as "vázzit" if it is homonomy.


	# vuodjat vs. vuodjit
	# ------------------
LIST VUODJIN-NOUN = "čáhcecrossa" "dáhkádus" "FMX" "mohtorsearvi" "motocross" "vuodjinlohpi" ;


SELECT:LexSel ("vuodjit") IF (0 ("vuodjat"))(*0 VEHICLE BARRIER SV-BOUNDARY LINK 0 Com OR Nom OR Acc) ;
SELECT:LexSel ("vuodjit") IF (0 ("vuodjat"))(*1 CONCRETE-ROUTE OR ("bargu" Ill) OR ("ruoktot") OR ("siida" Ill) BARRIER VFIN) ;
SELECT:LexSel ("vuodjit") IF (0 ("vuodjat"))(*-1 BOS LINK *1 VUODJIN-NOUN) ;
SELECT:LexSel ("vuodjit") IF (0 ("vuodjat"))(*1 (Prop Plc Ill) BARRIER VFIN) ;
REMOVE:LexSel ("vuodjat") IF (0 ("vuojašit")) ;
#SELECT:LexSel VUODJIN-WORD IF (0 VUODJIN-WORD) ;
SELECT:LexSel ("vuodjat") IF (0 ("vuodjit"))(*0 WATER BARRIER SV-BOUNDARY LINK 0 Loc) ;
SELECT:LexSel ("vuodjat") IF (*-1 FISH BARRIER NOT-AUX-V LINK 0 Nom OR Ess) ;
	## Hálidatgo gullat go vudjen stuora sáhpana iežan sykkeliin?
	## Okta nubbi vuodjá tráktor máđiiráigge.
	## Ivdni lea deaŧalaš, muhto buot deaŧaleamos lea movt dat vuodjá čázis.
	## Sii vudjet bálgá mielde.
	## Sii vudjet Romsii.
	## Muhto min guovlluin eatnasat veajehin ja vudjet merrii maŋŋel go leat leamaš jagi jogas.

	# vuohttit vs. vuohttát
	# ---------------------
	# vuohttá is sg3 of both these verbs
REMOVE:LexSel ("vuohttát") IF (0 ("vuohttit")) ;
	# One can always (?) understand it as "vuohttit" if it is homonomy.


	# vuolgit vs. vulgot
	# ---------------------

REMOVE:LexSel ("vulgot") IF (0 ("vuolgit") LINK *1 Ill OR PO-ILL BARRIER NOT-NPMODADV)  ;
	#$ Vulgot gávpogii, ovdalgo girjerádju manná gitta.

	# vuordit vs. vuordát
	# -------------------
REMOVE:LexSel ("vuordát") IF (0 ("vuordit"))(*0 Sem/Time BARRIER S-BOUNDARY LINK 0 Acc OR Ill) ;
REMOVE:LexSel ("vuordát") IF (0 ("vuordit"))(1 Num)(2 Po) ;
	## Son vuordá diimmu.
	## Son vuordá gitta mánnodahkii.
	## Doppe ledje geainnu vuordán 1924 rájes.

REMOVE:LexSel ("vuordát") IF (0 ("vuordit")) ;
	# One can always (?) understand it as "vuordit" if it is homonomy.



#!! !!!NOMEN
REMOVE ("gáfe") (0 ("gáffe") - Nom) ;
SELECT (Num Nom)((-1 ("diibmu")) OR (-2 ("diibmu")))   ;
	#$ Diibmu lea vihtta.


#!! !!Case rules
#!! !!Other rules for nouns and pronouns

#!! !!Determiners
#!! !!Adverbs and adjectives
SELECT:genAdv Adv (0 A LINK 1 SYN-V)(-1 Acc) ;
	#$ Dihkkádeaddji rávve skohtervuddjiid várrugasat mátkkoštit, go dán jagi lea erenoamán heajos jiekŋa jogain.

#!! !!!NOUNS
REMOVE Prop (-1 BOC) ;
	#$ Girjjis leat boares govat.

REMOVE (Pl Nom) (1 VFIN + Sg3) ;
REMOVE (Sg Nom) (1 VFIN + Pl3) ;

REMOVE (A @HAB>) (0 N) ;
SELECT A (0 Attr LINK 0 N LINK 1 N) ;
	#$ Lea suohtas kurssas. <= her er ikke Attr analysen riktig

#!! * __derNEss__ removes DER-N if lexicalised essives (revise this) - flytter denne til slutten av fila
REMOVE:derNEss DER-N IF (0 DER-N LINK 0 Ess)(0 LEX-N LINK 0 Ess)(NEGATE 0 (NomAg Ess) OR (Der/NomAg Ess));
 	## Grammatihkkavuđot giellateknologalaš reaiddut teakstaanalyseremii ja giellaoahpahussii ja digitálalaš sátnegirjjit leat buot gielaide eaktun dasa ahte sáhttet ceavzit geavahangiellan ođđaáigásaš servodagas.
 	# Removes DER-N if O is also a lexical noun, and both nouns are essive.


SELECT Adv IF (-1 N LINK -1 FMAINV)(0 A LINK 1 EOC) ;
SELECT:ADV Adv IF (-1 FMAINV)(0 (A Comp) OR (A Pl) LINK *1 Acc - TIME BARRIER NOT-NPMOD LINK NEGATE 1 go + @CNP) ;
	#$ Norgga láhkaaddit leat áigá jo suokkardan vuđolaččat kvenaid eamiálbmotstáhtusa gažaldaga.

AFTER-SECTIONS


LIST TYP-A = "árat" "buorre" "čeahppi" ;

LIST A-PL-N = "bassi" "boaris" "buohcci" "duiskalaš" "duiska" "headju" "lagamuš" "lohkki" "njálggis" "norgalaš" "nuorra" "ovddasteaddji" "rikkis" "searra" "vašálaš" "velggolaš" ;
SELECT:NPlwhenAPl (N Pl) + $$CASE (0 A + $$CASE LINK NOT 0 TYP-A) ;

SELECT:NomCCNom Nom (-1 CC LINK -1C Nom) ;
SELECT:AttrwhenAdv Attr (0 Adv - ("njuolga") LINK 1 N)(NEGATE -1 ("man") OR REAL-V)(NEGATE 0 ("veaháš") OR ("unnán") LINK 1 Prop) ;
SELECT:PlNomwhenAdv (Pl Nom) (0 Adv)((*-1 (V Pl3) OR COPULAS + Inf BARRIER NOT-ADV-PCLE LINK NOT -1 (Nom Pl) OR (Nom Pl3)) OR (*1 (V Pl3) OR COPULAS + Inf BARRIER NOT-ADV-PCLE LINK NOT 1 (Nom Pl) OR (Nom Pl3)))(NEGATE -1 (Rel Pl))(NEGATE 0 ("eanebut")) ;

REMOVE:Focge Foc/ge (0 Adv) ;
SELECT:AdvwhenAPl Adv (0 (A Pl) OR (Dem Loc)) ;
SELECT:AdvwhenAPl Adv (0 ("árat") LINK 1 Dem) ;
REMOVE:AdvwhenRefl Adv + Px (0 Refl) ;

#!! * __cleanSemClass__ cleans up if a word has more semclasses. This is just a start.
SELECT:cleanSemClass (Sem/Plc) (0 Sem/Org OR Sem/Sur OR FIRSTNAME) ;
SELECT:cleanSemClass (Sem/Mal) (0 Sem/Sur) ;
SELECT:cleanSemClass (Sem/Time) (0 (Sem/Measr) OR (Sem/Org)) ;
SELECT:cleanSemClass (Sem/Ani) (0 Sem/Hum) ;
	#$ Nussir

#!! !!!VERBS
#REMOVE:InfifVFIN Inf (0 VFIN + FMAINV LINK NOT 0 SUBJ OR OBJ LINK NOT *-1 AUX - Inf OR PrfPrc)(*0 REAL-V BARRIER S-BOUNDARY) ;

SELECT:SSg2Qst (V Sg2)(0 (V Pl1))((0 Qst) OR (1 Qst)) ;
SELECT:SSg2Interr (V Sg2)(0 (V Pl1) LINK *-1 Interr) ;
	#$ Makkár musihka guldalat millosepmosit?

SELECT:SSg2Qst (V Sg2)(0 (V Pl1) LINK *-1 CS)(NEGATE *0 Pl1) ;
SELECT:SSg2Pot (V Sg2)(0 (V Pot Pl1)) ;
#SELECT:SSg2Pl1 (V Sg2)(0 (V Pl1) LINK NOT *0 Pl1) ;

SUBSTITUTE (A) (A <sme>) WORD ;
SUBSTITUTE (N) (N <sme>) WORD ;
SUBSTITUTE (Adv) (Adv <sme>) WORD ;
SUBSTITUTE (V) (V <sme>) WORD ;
SUBSTITUTE (Num) (Num <sme>) WORD ;
SUBSTITUTE (Interj) (Interj <sme>) WORD ;
SUBSTITUTE (Po) (Po <sme>) WORD ;
SUBSTITUTE (Pr) (Pr <sme>) WORD ;
SUBSTITUTE (Pron) (Pron <sme>) WORD ;
SUBSTITUTE (CC) (CC <sme>) WORD ;
SUBSTITUTE (CS) (CS <sme>) WORD ;
SUBSTITUTE (Pcle) (Pcle <sme>) WORD ;


	# Disambiguating the semantic tags
	# ================================

	# Disambiguating symbols and punctuation marks #
	# Post-syntactic morphological disambiguation - Cycle 5 #
	# Tidying up some loose ends #
	# Substituting some syntactic tags #
	#=====#
      END #
	#=====#





